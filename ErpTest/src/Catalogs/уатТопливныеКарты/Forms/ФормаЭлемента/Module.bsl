
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	об = РеквизитФормыВЗначение("Объект");
	////уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Об, Отказ, СтандартнаяОбработка, ЭтаФорма, ДопПараметрыОткрытие);
	ЗначениеВРеквизитФормы(Об,"Объект");
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.КомуВыдана) И Параметры.Свойство("КомуВыдана") Тогда
		Объект.КомуВыдана = Параметры.КомуВыдана;
	КонецЕсли;
	
	// УправлениеПредприятием.СлужебныеПодсистемы
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// Конец УправлениеПредприятием.СлужебныеПодсистемы
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	////уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Объект.Основная Тогда
		ДругаяКарта = ПолучитьДругуюОсновнуюКарту(Объект.Ссылка, Объект.КомуВыдана);
		Если ЗначениеЗаполнено(ДругаяКарта) Тогда
			Отказ = Истина;
			ПоказатьПредупреждение(Неопределено, "У """ + Объект.КомуВыдана + """ уже имеется основная топливная карта, выданная """
				+ Объект.КемВыдана + """!");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомуВыданаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Список = Новый СписокЗначений;
	Список.Добавить("ТС");
	Список.Добавить("Водитель");
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("КомуВыданаНачалоВыбораЗавершение", ЭтотОбъект,
		Новый Структура("Элемент", Элемент)), Список, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КомуВыданаНачалоВыбораЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	Перем СтандартнаяОбработка;
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "ТС" Тогда
		уатИнтерфейсВводаТС.НомерТСНачалоВыбора(ДополнительныеПараметры.Элемент, Объект.КомуВыдана, Новый Структура, СтандартнаяОбработка);
	Иначе
		уатЗащищенныеФункцииКлиент.ДиалогВыбораСотрудника(ДополнительныеПараметры.Элемент, Объект.КомуВыдана, Новый Структура, СтандартнаяОбработка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомуВыданаОткрытие(Элемент, СтандартнаяОбработка)
	Если ТипЗнч(Объект.КомуВыдана) = Тип("СправочникСсылка.Сотрудники") Тогда
		уатЗащищенныеФункцииКлиент.ОткрытьФормуСотрудника(Объект.КомуВыдана, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомуВыданаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если ТипЗнч(Объект.КомуВыдана) = Тип("СправочникСсылка.Сотрудники") Тогда
		уатИнтерфейсВводаСотрудников.СотрудникАвтоПодборТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомуВыданаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	Если ТипЗнч(Объект.КомуВыдана) = Тип("СправочникСсылка.Сотрудники") Тогда
		уатИнтерфейсВводаСотрудников.СотрудникОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КемВыданаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Список = Новый СписокЗначений;
	Список.Добавить("АЗС");
	Список.Добавить("Контрагенты");
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("КемВыданаНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент)), Список, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КемВыданаНачалоВыбораЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    Если ВыбранныйЭлемент.Значение = "АЗС" Тогда
        ОткрытьФорму("Справочник.уатАЗС.Форма.ФормаВыбора",,ДополнительныеПараметры.Элемент);
    Иначе
        ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбора",,ДополнительныеПараметры.Элемент);
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// УправлениеПредприятием.СлужебныеПодсистемы
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
КонецПроцедуры
// Конец УправлениеПредприятием.СлужебныеПодсистемы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьДругуюОсновнуюКарту(Ссылка, КомуВыдана)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатТопливныеКарты.Основная,
	|	уатТопливныеКарты.КомуВыдана,
	|	уатТопливныеКарты.КемВыдана,
	|	уатТопливныеКарты.Ссылка
	|ИЗ
	|	Справочник.уатТопливныеКарты КАК уатТопливныеКарты
	|ГДЕ
	|	уатТопливныеКарты.КомуВыдана = &КомуВыдана
	|	И уатТопливныеКарты.Основная
	|	И уатТопливныеКарты.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("КомуВыдана", КомуВыдана);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

#КонецОбласти
