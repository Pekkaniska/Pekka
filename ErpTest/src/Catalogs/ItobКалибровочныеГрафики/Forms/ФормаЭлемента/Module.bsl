
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ = Неопределено Или Параметры.Ключ.Пустая() Тогда
		// Создание нового объекта
		Объект.ДлинаБуфераСглаживания = 40;
		Объект.ПорогЗаправки = 10;
		Объект.ПорогСлива = 10;
		Объект.ОкноГраницыЗаправкиСлива = 3;
		
	КонецЕсли;
    ПроверитьСортировку = Истина;
	ЗакрываемФорму = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПроверитьСортировку Тогда
		// Проверка сортировки таблицы преобразования
		СортировкаПравильная = Истина;
		
		Для Счетчик = 1 По Объект.Показатели.Количество()-1 Цикл
			Если Объект.Показатели[Счетчик-1].Вход > Объект.Показатели[Счетчик].Вход Тогда
				СортировкаПравильная = Ложь;
				Прервать;
			
			КонецЕсли;	
		КонецЦикла;
		Если НЕ СортировкаПравильная Тогда
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("Объект", Объект);
			ДопПараметры.Вставить("ЭтаФорма", ЭтаФорма);
			ПоказатьВопрос(Новый ОписаниеОповещения("ОтветНаВопросСортировка", ItobОбщегоНазначенияКлиент, ДопПараметры),
				"Таблицу преобразования рекомендуется отсортировать по колонке ""Вход"" по возрастанию!
				|Выполнить сортировать сейчас?", РежимДиалогаВопрос.ДаНет, 0);
			ЗакрываемФорму = Ложь;
		КонецЕсли;
	КонецЕсли;
	ПроверитьСортировку = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПечатьГрафика(Команда)
	
	ТабДок = Новый ТабличныйДокумент;
	ПечатьГрафикаСервер(ТабДок);

	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.Защита = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.Показать();
	
КонецПроцедуры

&НаСервере
Процедура ПечатьГрафикаСервер(ТабДокумент)
	
	Макет = Справочники.ItobКалибровочныеГрафики.ПолучитьМакет("График");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьГрафик = Макет.ПолучитьОбласть("График");
	
	ОбластьШапка.Параметры.ЗаголовокОтчета = НСтр("ru = 'Калибровочный график'")+" "+Объект.Наименование;
	ТабДокумент.Вывести(ОбластьШапка);
	
	ТабДанных = Объект.Показатели.Выгрузить();
	
	ТекстовыйДок = Новый ТекстовыйДокумент;
	ТекстовыйДок.ДобавитьСтроку("#Series1");
	ТекстовыйДок.ДобавитьСтроку(""+Формат(ТабДанных.Количество(),"ЧРД=.; ЧН=0; ЧГ=0")+";float;float");	
	Для каждого СтрДанных Из ТабДанных Цикл
		ТекущаяСтрокаДанных = Формат(СтрДанных.Вход,"ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ=0")
			+";"+Формат(СтрДанных.Выход,"ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧН=0; ЧГ=0")+";"""
			+Формат(СтрДанных.Вход,"ЧЦ=15; ЧДЦ=0; ЧРД=.; ЧН=0; ЧГ=0")+"""";
		ТекстовыйДок.ДобавитьСтроку(ТекущаяСтрокаДанных);					
	
	КонецЦикла;
	ИмяВременногоФайла = КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор()) + ".txt";
	ТекстовыйДок.Записать(ИмяВременногоФайла, "windows-1251");
	
	ИмяФайлаКартинки = КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор()) + ".png";
	
	АдресСервераРендеринга = ПараметрыСеанса.ItobАдресCsmSvc.Получить("СерверАдрес") + 	":"
		+Формат(ПараметрыСеанса.ItobАдресCsmSvc.Получить("СерверПорт"),"ЧГ=0");
	ОшибкаЗапросаНаСервер = Ложь;
	
	Если ПустаяСтрока(АдресСервераРендеринга) Тогда
		ОшибкаЗапросаНаСервер = Истина;
		ОписаниеОшибки = НСтр("ru = 'В настройках системы не указан адрес сервиса CsmSvc!
						 |Воспользуйтесь мастером настройки службы CsmSvc.'");
		
	ИначеЕсли  НЕ ItobОперативныйМониторинг.ПроверитьДоступностьСервисаCsmSvc(АдресСервераРендеринга) Тогда
		ОшибкаЗапросаНаСервер = Истина;
		ОписаниеОшибки = НСтр("ru = 'Сервис CsmSvc не доступен!
						 |Воспользуйтесь мастером настройки службы CsmSvc.'");
		
	Иначе
		
		Если Прав(АдресСервераРендеринга,1) = "/" Тогда
			АдресСервераРендеринга = Сред(АдресСервераРендеринга,1,СтрДлина(АдресСервераРендеринга)-1);			
		КонецЕсли;
		
		Попытка
			ШиринаРисунка = Формат(Цел(ОбластьГрафик.Рисунки.ImageChart.Ширина*1.8),"ЧГ=0");
			ВысотаРисунка = Формат(Цел(ОбластьГрафик.Рисунки.ImageChart.Высота*1.8),"ЧГ=0");		
			
			Соединение = Новый HTTPСоединение(АдресСервераРендеринга);	
			Соединение.ОтправитьДляОбработки(ИмяВременногоФайла, "RenderChart?Width="
			+ШиринаРисунка+"&Height="+ВысотаРисунка, ИмяФайлаКартинки);	
			
			УдалитьФайлы(ИмяВременногоФайла);
			
			КартинкаГрафик = Новый Картинка(ИмяФайлаКартинки);
			
			ОбластьГрафик.Рисунки.ImageChart.Картинка = КартинкаГрафик;
			
			ТабДокумент.Вывести(ОбластьГрафик);
			
			УдалитьФайлы(ИмяФайлаКартинки);
			
		Исключение
			ОшибкаЗапросаНаСервер = Истина;
			ОписаниеОшибки = НСтр("ru = 'Ошибка рендеринга графика:'")+" "+ОписаниеОшибки();			
			
		КонецПопытки;
	
	КонецЕсли;
	
	Если ОшибкаЗапросаНаСервер Тогда
	
		ОбластьОшибка = Макет.ПолучитьОбласть("Ошибка");
		ОбластьОшибка.Параметры.ТекстОшибки = ОписаниеОшибки;
		ОбластьОшибка.Параметры.Расшифровка = Новый Структура("Действие","ОткрытьМастерНастройкиCsmSvc");
		ТабДокумент.Вывести(ОбластьОшибка);	
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Отказ = Отказ ИЛИ НЕ ЗакрываемФорму;
	ЗакрываемФорму = Истина
КонецПроцедуры

#КонецОбласти
