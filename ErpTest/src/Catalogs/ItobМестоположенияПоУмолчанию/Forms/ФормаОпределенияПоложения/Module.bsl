
&НаКлиенте
Перем мСчетчикДокументСформирован;  // Переменная содержит счетчик сформированных документов

#Область ОбработчикиСобытийФормы

// Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	мСчетчикДокументСформирован = 0;
	
	АтрибутыКарты = "";
	Если Широта <> 0 И Долгота <> 0 И Масштаб <> 0 Тогда
		
		АтрибутыКарты = 
			"firstZoom = "+Формат(Масштаб,"ЧГ=0")+";
			|firstLat = "+ItobОбщегоНазначенияКлиентСервер.ФорматироватьКоординаты(Широта)+";
			|firstLon = "+ItobОбщегоНазначенияКлиентСервер.ФорматироватьКоординаты(Долгота)+";";		
		
	КонецЕсли;
	
	ПолеHTML = ItobОперативныйМониторингКлиентСервер.ПолучитьАдресКарты(АтрибутыКарты);
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события "ДокументСформирован" поля HTML "ПолеHTML".
//
&НаКлиенте
Процедура ПолеHTMLДокументСформирован(Элемент)
	
	СисИнфо = Новый СистемнаяИнформация;
	
	Если мСчетчикДокументСформирован = 0
		И Найти(СисИнфо.ИнформацияПрограммыПросмотра, "Firefox") <> 0 Тогда
		
		ПодключитьОбработчикОжидания("ОбновитьПолеHTML", 0.1, Истина);
		
	КонецЕсли;
	
	мСчетчикДокументСформирован = мСчетчикДокументСформирован + 1;
		
КонецПроцедуры // ПолеHTMLДокументСформирован()

// Процедура - обработчик события "ПриНажатии" поля HTML "ПолеHTML".
//
&НаКлиенте
Процедура ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	ItobОперативныйМониторингКлиент.ОбработатьНажатиеНаПолеКарты(Элемент, ДанныеСобытия, СтандартнаяОбработка);	
	
КонецПроцедуры

// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.Широта  = ?(Параметры.Свойство("Широта"), Параметры.Широта, 0);
	ЭтаФорма.Долгота = ?(Параметры.Свойство("Долгота"),Параметры.Долгота,0);
	ЭтаФорма.Масштаб = ?(Параметры.Свойство("Масштаб"),Параметры.Масштаб,0);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура обновляет содержимое поля HTML
//
&НаКлиенте
Процедура ОбновитьПолеHTML()

	ВыполнитьСкрипт("location.reload(true);");

КонецПроцедуры

// Процедура - обработчик команды "УвеличитьКарту".
//
&НаКлиенте
Процедура УвеличитьКарту(Команда)
	
	ВыполнитьСкрипт("m_map.zoomIn();");	
	
КонецПроцедуры // УвеличитьКарту()

// Процедура - обработчик команды "УменьшитьКарту".
//
&НаКлиенте
Процедура УменьшитьКарту(Команда)
	
	ВыполнитьСкрипт("m_map.zoomOut();");		
	
КонецПроцедуры // УменьшитьКарту()

// Процедура - обработчик команды "КомандаОК".
//
&НаКлиенте
Процедура КомандаОК(Команда)
	
	Попытка
		
		ТекстСкрипта = "document.form.result.value = m_map.getZoom();";		
		ВыполнитьСкрипт(ТекстСкрипта);	
		Масштаб = Элементы.ПолеHTML.Документ.form.result.value;
		
		ТекстСкрипта = 
		    "var pt = m_map.getCenter();
			|pt.transform(m_map.getProjectionObject(), m_map.displayProjection);
			|document.form.result.value = pt.toShortString();
			|pt = null;";
		ВыполнитьСкрипт(ТекстСкрипта);
		СтрокаКоординат = Элементы.ПолеHTML.Документ.form.result.value;		
		СтрокаКоординат = СтрЗаменить(СтрокаКоординат,",",Символы.ПС);
		
		Долгота = Число(СокрЛП(СтрПолучитьСтроку(СтрокаКоординат,1)));
		Широта  = Число(СокрЛП(СтрПолучитьСтроку(СтрокаКоординат,2)));
		
	Исключение
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		
	КонецПопытки;	
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Масштаб", Масштаб);
	ДопПараметры.Вставить("Широта",  Широта);
	ДопПараметры.Вставить("Долгота", Долгота);
	ДопПараметры.Вставить("КодВозвратаДиалога", КодВозвратаДиалога.ОК);
	Закрыть(ДопПараметры);		
	
КонецПроцедуры // КомандаОК()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет скрипт на поле HTML, с учетом вида браузера.
//
&НаКлиенте
Процедура ВыполнитьСкрипт(ТекстСкрипта)
	
	ItobОперативныйМониторингКлиент.ВыполнитьСкриптНаПолеHTML(Элементы.ПолеHTML, ТекстСкрипта);		

КонецПроцедуры // ВыполнитьСкрипт()

#КонецОбласти
