#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройки использования серий для вида номенклатуры на складе (подразделении).
//
// Параметры:
//  ВидНоменклатуры		 - СправочникСсылка.ВидыНоменклатуры							 - вид номенклатуры, параметры серий которого которой нужно получить
//  СкладПодразделение	 - СправочникСсылка.Склады, СправочникСсылка.СтруктураПредприятия	 - склад или производственное подразделение
//  	для которого нужно получить настройки серий. Если значение не задано - возвращаются настройки, которые
//  	от склада не зависят.
// 
// Возвращаемое значение:
//  Структура - структура с ключами:
//  * ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - вид номенклатуры, параметры серий которого нужно получить
//  * ИспользоватьСерии - Булево - Истина, если серии для вида номенклатуры используются
//  * ИспользоватьНомерСерии - Булево - Истина, если номера серий для вида номенклатуры используются
//  * ИспользоватьСрокГодностиСерии - Булево - Истина, если используется срок годности серий для вида номенклатуры
//  * ИспользоватьКоличествоСерии - Булево - Истина, если количество серии указывается
//  * ЕдиницаИзмеренияСрокаГодности - ПеречислениеСсылка.ЕдиницыИзмеренияВремени - единица измерения срока
//																						годности серии
//  * ТочностьУказанияСрокаГодностиСерии - ПеречислениеСсылка.ТочностиУказанияСрокаГодности - точность указания срока
//                                                                                            годности серии
//  * ШаблонРабочегоНаименованияСерии - Строка - шаблон рабочего наименования серии
//  * ВладелецСерий - СправочникСсылка.ВидыНоменклатуры - владелец
//  * НастройкиСерийБерутсяИзДругогоВидаНоменклатуры - Булево - Истина, если настройки серий берутся из другого вида номенклатуры
//  * ШаблонЭтикеткиСерии - СправочникСсылка.ШаблоныЭтикетокИЦенников - ссылка на шаблон этикетки серии
//  * ИспользоватьRFIDМеткиСерии - Булево - Истина, если используются RFID-метки серий
//  * ИспользоватьНомерКИЗГИСМСерии - Булево - Истина, если учет серий вида номенклатуры ведется по номерам КИЗ (ГиСМ)
//  * ИспользоватьДатуПроизводстваСерии - Булево - Истина, если ведется учет серий вида номенклатуры
//													по датам производства
//  * ИспользоватьПроизводителяЕГАИССерии - Булево - Истина, если ведется учет серий вида номенклатуры
//														по производителям (ЕГАИС)
//  * ИспользоватьСправку2ЕГАИССерии - Булево - Истина, если ведется учет серий вида номенклатуры по справкам 2 (ЕГАИС)
//  * ИспользоватьПроизводителяВЕТИССерии - Булево - Истина, если ведется учет серий вида номенклатуры
//														по производителям (ВЕТИС)
//  * ИспользоватьИдентификаторПартииВЕТИССерии - Булево - Истина, если ведется учет серий вида номенклатуры
//														партиям (ВЕТИС)
//  * ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии - Булево - Истина, если ведется учет серий вида номенклатуры
//																по записям складского журнала (ВЕТИС)
//  * ФорматнаяСтрокаСрокаГодности - Строка - форматная строка срока годности
//  * ОбязательныеДопРеквизиты - ФиксированныйМассив - массив строк наименования доп реквизитов
//  * ПолитикаУчетаСерий - СправочникСсылка.ПолитикиУчетаСерий - ссылка на политику учета серий
//  * УказыватьПриПоступлении - Булево - Истина, если серии указываются при поступлении
//  * УказыватьПриОтгрузке - Булево - Истина, если серии указываются при отгрузке
//  * УказыватьПриПланированииОтгрузки - Булево - Истина, если серии указываются при планировании отгрузки
//  * УчетСерийПоFEFO - Булево - Истина, если серии учитываются по FEFO
//  * УказывыватьПоФактуОтбора - Булево - Истина, если серии учитываются по факту отбора
//  * УчитыватьОстаткиСерий - Булево - Истина, если учитываются остатки серий
//  * УчитыватьСебестоимостьПоСериям - Булево  - Истина, если учитывается себестоимость по сериям.
//
Функция НастройкиИспользованияСерий(ВидНоменклатуры, СкладПодразделение = Неопределено) Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ВидНоменклатуры");
	Результат.Вставить("ИспользоватьСерии");
	Результат.Вставить("ИспользоватьНомерСерии");
	Результат.Вставить("ИспользоватьСрокГодностиСерии");
	Результат.Вставить("ИспользоватьКоличествоСерии");
	Результат.Вставить("ЕдиницаИзмеренияСрокаГодности");
	Результат.Вставить("ТочностьУказанияСрокаГодностиСерии");
	Результат.Вставить("ШаблонРабочегоНаименованияСерии");
	Результат.Вставить("ВладелецСерий");
	Результат.Вставить("НастройкиСерийБерутсяИзДругогоВидаНоменклатуры");
	Результат.Вставить("ШаблонЭтикеткиСерии");
	Результат.Вставить("ИспользоватьRFIDМеткиСерии");
	Результат.Вставить("ИспользоватьНомерКИЗГИСМСерии");
	Результат.Вставить("ИспользоватьДатуПроизводстваСерии");
	Результат.Вставить("ИспользоватьПроизводителяЕГАИССерии");
	Результат.Вставить("ИспользоватьСправку2ЕГАИССерии");
	Результат.Вставить("ИспользоватьПроизводителяВЕТИССерии");
	Результат.Вставить("ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии");
	Результат.Вставить("ИспользоватьИдентификаторПартииВЕТИССерии");
	
	Результат.Вставить("ФорматнаяСтрокаСрокаГодности");
	Результат.Вставить("ОбязательныеДопРеквизиты");
	
	Результат.Вставить("ПолитикаУчетаСерий");
	Результат.Вставить("УказыватьПриПоступлении");
	Результат.Вставить("УказыватьПриОтгрузке");
	Результат.Вставить("УказыватьПриПланированииОтгрузки");
	Результат.Вставить("УчетСерийПоFEFO");
	Результат.Вставить("УказывыватьПоФактуОтбора");
	Результат.Вставить("УчитыватьОстаткиСерий");
	Результат.Вставить("УчитыватьСебестоимостьПоСериям");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
	|	ВидыНоменклатуры.ИспользоватьСерии КАК ИспользоватьСерии,
	|	ВидыНоменклатуры.ИспользоватьНомерСерии КАК ИспользоватьНомерСерии,
	|	ВидыНоменклатуры.ИспользоватьСрокГодностиСерии КАК ИспользоватьСрокГодностиСерии,
	|	ВидыНоменклатуры.ИспользоватьКоличествоСерии КАК ИспользоватьКоличествоСерии,
	|	ВидыНоменклатуры.ЕдиницаИзмеренияСрокаГодности КАК ЕдиницаИзмеренияСрокаГодности,
	|	ВидыНоменклатуры.ТочностьУказанияСрокаГодностиСерии КАК ТочностьУказанияСрокаГодностиСерии,
	|	ВидыНоменклатуры.ШаблонРабочегоНаименованияСерии КАК ШаблонРабочегоНаименованияСерии,
	|	ВидыНоменклатуры.ИспользоватьRFIDМеткиСерии КАК ИспользоватьRFIDМеткиСерии,
	|	ВидыНоменклатуры.ИспользоватьНомерКИЗГИСМСерии КАК ИспользоватьНомерКИЗГИСМСерии,
	|	ВидыНоменклатуры.ИспользоватьДатуПроизводстваСерии КАК ИспользоватьДатуПроизводстваСерии,
	|	ВидыНоменклатуры.ИспользоватьПроизводителяЕГАИССерии КАК ИспользоватьПроизводителяЕГАИССерии,
	|	ВидыНоменклатуры.ИспользоватьСправку2ЕГАИССерии КАК ИспользоватьСправку2ЕГАИССерии,
	|	ВидыНоменклатуры.ИспользоватьПроизводителяВЕТИССерии КАК ИспользоватьПроизводителяВЕТИССерии,
	|	ВидыНоменклатуры.ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии КАК ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии,
	|	ВидыНоменклатуры.ИспользоватьИдентификаторПартииВЕТИССерии КАК ИспользоватьИдентификаторПартииВЕТИССерии,
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры
	|			ТОГДА ВидыНоменклатуры.ВладелецСерий
	|		ИНАЧЕ ВидыНоменклатуры.Ссылка
	|	КОНЕЦ КАК ВладелецСерий,
	|	ВидыНоменклатуры.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры КАК НастройкиСерийБерутсяИзДругогоВидаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ВидыНоменклатуры.НастройкиСерийБерутсяИзДругогоВидаНоменклатуры
	|			ТОГДА ВидыНоменклатуры.ВладелецСерий.ШаблонЭтикеткиСерии
	|		ИНАЧЕ ВидыНоменклатуры.ШаблонЭтикеткиСерии
	|	КОНЕЦ КАК ШаблонЭтикеткиСерии,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий, ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)) КАК ПолитикаУчетаСерий,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПриемке, ЛОЖЬ) КАК УказыватьПриПоступлении,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузке, ЛОЖЬ) КАК УказыватьПриОтгрузке,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки, ЛОЖЬ) КАК УказыватьПриПланированииОтгрузки,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO, ЛОЖЬ) КАК УчетСерийПоFEFO,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора, ЛОЖЬ) КАК УказывыватьПоФактуОтбора,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий, ЛОЖЬ)
	//++ НЕ УТ
	|			И (ТЧПолитикиУчетаСерий.Склад ССЫЛКА Справочник.Склады ИЛИ &УправлениеПроизводством21)
	//-- НЕ УТ
	|		КАК УчитыватьОстаткиСерий,
	|	ЕСТЬNULL(ТЧПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям, ЛОЖЬ) 
	//++ НЕ УТ
	|			И (ТЧПолитикиУчетаСерий.Склад ССЫЛКА Справочник.Склады ИЛИ &УправлениеПроизводством21)
	//-- НЕ УТ
	|		КАК УчитыватьСебестоимостьПоСериям
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ТЧПолитикиУчетаСерий
	|		ПО ВидыНоменклатуры.Ссылка = ТЧПолитикиУчетаСерий.Ссылка
	|			И (ТЧПолитикиУчетаСерий.Склад = &СкладПодразделение)
	|ГДЕ
	|	ВидыНоменклатуры.Ссылка = &ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыНоменклатурыРеквизитыДляКонтроляСерий.Свойство КАК Свойство
	|ИЗ
	|	Справочник.ВидыНоменклатуры.РеквизитыДляКонтроляСерий КАК ВидыНоменклатурыРеквизитыДляКонтроляСерий
	|ГДЕ
	|	ВидыНоменклатурыРеквизитыДляКонтроляСерий.Ссылка = ВЫБОР
	|			КОГДА ВЫРАЗИТЬ(&ВидНоменклатуры КАК Справочник.ВидыНоменклатуры).НастройкиСерийБерутсяИзДругогоВидаНоменклатуры
	|				ТОГДА ВЫРАЗИТЬ(&ВидНоменклатуры КАК Справочник.ВидыНоменклатуры).ВладелецСерий
	|			ИНАЧЕ &ВидНоменклатуры
	|		КОНЕЦ
	|	И ВидыНоменклатурыРеквизитыДляКонтроляСерий.ЭтоДопРеквизит
	|	И ВидыНоменклатурыРеквизитыДляКонтроляСерий.ОбязателенДляЗаполнения";
	
	Запрос.УстановитьПараметр("ВидНоменклатуры", ВидНоменклатуры);
	Запрос.УстановитьПараметр("СкладПодразделение", СкладПодразделение);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("УправлениеПроизводством21", ИспользоватьПроизводство());
	//-- НЕ УТ
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыШапки = РезультатЗапроса[0].Выбрать();
	РеквизитыШапки.Следующий();
	
	ЗаполнитьЗначенияСвойств(Результат, РеквизитыШапки);
	Результат.ФорматнаяСтрокаСрокаГодности = ФорматнаяСтрокаСрокаГодности(Результат.ТочностьУказанияСрокаГодностиСерии);
	
	Результат.ОбязательныеДопРеквизиты = Новый ФиксированныйМассив(РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Свойство"));
	
	Возврат Результат;
	
КонецФункции

// Получает вид номенклатуры, если вид номенклатуры один в справочнике.
//
// Возвращаемое значение:
// 	СправочникСсылка.ВидНоменклатуры - найденный вид номенклатуры
// 	Неопределено - если видов номенаклатуры нет или видов номенклатуры больше одного.
//
Функция ВидНоменклатурыПоУмолчанию() Экспорт
	
	Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	НЕ ВидыНоменклатуры.ПометкаУдаления
	|	И НЕ ВидыНоменклатуры.ЭтоГруппа";
	
	Запрос = Новый Запрос(Текст);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		ВидНоменклатуры = Выборка.ВидНоменклатуры;
	Иначе
		ВидНоменклатуры = Неопределено;
	КонецЕсли;
	
	Возврат ВидНоменклатуры;
	
КонецФункции // ВидНоменклатуры()

// Возвращает имена блокруемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значание:
//	Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("ТипНоменклатуры; ТипНоменклатуры");
	Результат.Добавить("ИспользованиеХарактеристик");
	Результат.Добавить("ИспользоватьХарактеристики");
	Результат.Добавить("ИспользоватьСерии; ВариантЗаданияНастроекСерий, НастройкаСерийПоСкладам1, НастройкаСерийПоСкладам2, НастройкаСерийПоСкладам3, НастройкаСерийПоПозраделениям1, НастройкаСерийПоПозраделениям2, НастройкаСерийПоПозраделениям3");
	Результат.Добавить("ПолитикиУчетаСерий; ПолитикиУчетаСерийДобавитьСкладВПолитикиУчетаСерий,ПолитикиУчетаСерийДобавитьПодразделениеВПолитикиУчетаСерий");
	Результат.Добавить("НастройкаИспользованияСерий");
	Результат.Добавить("ТочностьУказанияСрокаГодностиСерии");
	Результат.Добавить("ВариантОформленияПродажи");
	Результат.Добавить("АлкогольнаяПродукция");
	Результат.Добавить("ВариантПредставленияНабораВПечатныхФормах");
	Результат.Добавить("ВариантРасчетаЦеныНабора;ГруппаЦенаНабора");
	Результат.Добавить("СодержитДрагоценныеМатериалы");
	Результат.Добавить("ВладелецХарактеристик");
	Результат.Добавить("ВладелецСерий");
	Результат.Добавить("НастройкиСерийБерутсяИзДругогоВидаНоменклатуры");
	Результат.Добавить("ТоварныеКатегорииОбщиеСДругимВидомНоменклатуры");
	Результат.Добавить("ВладелецТоварныхКатегорий; НастройкаТоварныхКатегорий");
	Результат.Добавить("ЕстьИндивидуальныеНастройкиПолитикиУчетаСерий; ЕстьИндивидуальныеНастройкиПолитикиУчетаСерий");
	
	Возврат Результат;
	
КонецФункции

// Возвращает форматную строку представления срока годности.
//
// Параметры:
//  ТочностьУказанияСрокаГодности	 - Перечисления.ТочностиУказанияСрокаГодности - точность указания срока годности вида номенклатуры.
// 
// Возвращаемое значение:
//  Строка - форматная строка срока годности вида номенклатуры.
//
Функция ФорматнаяСтрокаСрокаГодности(ТочностьУказанияСрокаГодности)Экспорт
	
	Если ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ФорматнаяСтрока = "ДФ='dd.MM.yy HH:00'";
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоМесяцев Тогда
		ФорматнаяСтрока = "ДФ=MM.yy";
	Иначе
		ФорматнаяСтрока = "ДФ=dd.MM.yy";
	КонецЕсли;
	
	Возврат ФорматнаяСтрока;
	
КонецФункции

// Возвращает предустановленные виды номенклатуры.
//
// Возвращаемое значение:
//	Массив, элементы - СправочникСсылка.ВидыНоменклатуры.
//
Функция ПолучитьПредустановленныеВидыНоменклатуры() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	МассивВидов = Новый Массив;
	
	ДобавитьПредустановленныйВидНоменклатуры(МассивВидов, Перечисления.ТипыНоменклатуры.Товар, НСтр("ru = 'Товар'"));
	ДобавитьПредустановленныйВидНоменклатуры(МассивВидов, Перечисления.ТипыНоменклатуры.Услуга, НСтр("ru = 'Услуга'"));
	
	Возврат МассивВидов;

КонецФункции 

// Добавляет в массив вид номенклатуры с указанным типом.
// Если вида номенклатуры с таким типов нет в ИБ, то он создается.
//
// Параметры:
//	МассивВидов - Массив                        - массив видов номенклатуры,
//	Тип         - Перечисления.ТипыНоменклатуры - тип номенклатуры,
//	Имя         - Строка                        - имя нового вида номенклатуры.
//
Процедура ДобавитьПредустановленныйВидНоменклатуры(МассивВидов, Тип, Имя) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	ВидыНоменклатуры.ТипНоменклатуры = &Тип
	|	И НЕ ВидыНоменклатуры.ПометкаУдаления";
						  
	Запрос.УстановитьПараметр("Тип", Тип);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		НовыйВид = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
		НовыйВид.Заполнить(Неопределено);
		НовыйВид.Наименование = Имя;
		НовыйВид.ТипНоменклатуры = Тип;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаНоменклатуры") Тогда
			НовыйВид.ГруппаДоступа = Справочники.ГруппыДоступаНоменклатуры.ПолучитьГруппуДоступаПоУмолчанию(НовыйВид);
		КонецЕсли;
		НовыйВид.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг;
		
		ЗаполнитьРеквизитыДляКонтроля(НовыйВид, "Номенклатура");
		
		НастройкаЕдИзмерения = НовыйВид.РеквизитыДляКонтроляНоменклатуры.Найти("ЕдиницаИзмерения");
		Если НастройкаЕдИзмерения <> Неопределено Тогда
			НастройкаЕдИзмерения.ОтображатьПриСоздании = Истина;
			НастройкаЕдИзмерения.ОбязателенДляЗаполнения = Истина;
		КонецЕсли;
		
		НовыйВид.ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
		НовыйВид.ЕдиницаДляОтчетов = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
		
		НовыйВид.Записать();
		
		МассивВидов.Добавить(НовыйВид.Ссылка);
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		МассивВидов.Добавить(Выборка.Ссылка);
		
	КонецЕсли;	

КонецПроцедуры 

Процедура СоздатьВидНоменклатуры(ДанныеЗаполнения, ВидНоменклатурыСсылка) Экспорт
	
	ВидНоменклатурыОбъект = Справочники.ВидыНоменклатуры.СоздатьЭлемент();
	
	ВидНоменклатурыОбъект.Заполнить(Неопределено);
	ВидНоменклатурыОбъект.УстановитьНовыйКод();
	
	ВидНоменклатурыОбъект.Наименование      = ДанныеЗаполнения.Наименование;
	ВидНоменклатурыОбъект.СтавкаНДС         = ДанныеЗаполнения.СтавкаНДС;
	ВидНоменклатурыОбъект.ТипНоменклатуры   = ДанныеЗаполнения.ТипНоменклатуры;
	ВидНоменклатурыОбъект.Описание          = ДанныеЗаполнения.Описание;
	ВидНоменклатурыОбъект.ЕдиницаИзмерения  = ДанныеЗаполнения.ЕдиницаИзмерения;
	ВидНоменклатурыОбъект.ЕдиницаДляОтчетов = ДанныеЗаполнения.ЕдиницаДляОтчетов;
	ВидНоменклатурыОбъект.Родитель          = ДанныеЗаполнения.Родитель;
	ВидНоменклатурыОбъект.ИспользоватьХарактеристики = ДанныеЗаполнения.ИспользоватьХарактеристики;
	ВидНоменклатурыОбъект.ИспользованиеХарактеристик = ДанныеЗаполнения.ИспользованиеХарактеристик;
	ВидНоменклатурыОбъект.ВариантОформленияПродажи 	 = ДанныеЗаполнения.ВариантОформленияПродажи;
	
	Если Справочники.ГруппыДоступаНоменклатуры.ИспользуютсяГруппыДоступа() Тогда
		ГруппаДоступа = Справочники.ГруппыДоступаНоменклатуры.ПолучитьГруппуДоступаПоУмолчанию(ВидНоменклатурыОбъект);
		ДанныеЗаполнения.Свойство("ГруппаДоступа", ГруппаДоступа);
		ВидНоменклатурыОбъект.ГруппаДоступа = ГруппаДоступа;
	КонецЕсли;
	
	ЗаполнитьРеквизитыДляКонтроля(ВидНоменклатурыОбъект, "Номенклатура");
	
	НастройкаЕдИзмерения = ВидНоменклатурыОбъект.РеквизитыДляКонтроляНоменклатуры.Найти("ЕдиницаИзмерения");
	Если НастройкаЕдИзмерения <> Неопределено Тогда
		НастройкаЕдИзмерения.ОтображатьПриСоздании = Истина;
		НастройкаЕдИзмерения.ОбязателенДляЗаполнения = Истина;
	КонецЕсли;
	
	ВидНоменклатурыОбъект.Записать();
	
	ВидНоменклатурыСсылка = ВидНоменклатурыОбъект.Ссылка;
	
КонецПроцедуры

// Возвращает значение единицы измерения срока годности по значению точности указания срока годности серии.
//
// Параметры:
//	ТочностьУказанияСрокаГодностиСерии - ПеречислениеСсылка.ТочностиУказанияСрокаГодности - Точность указания срока 
//																							годности серии вида номенклатуры.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ЕдиницыИзмеренияВремени - единица измерения срока годности по значению точности указания 
//												срока годности серии.
//
Функция ЕдиницаИзмеренияСрокаГодности(ТочностьУказанияСрокаГодности) Экспорт
	
	ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.ПустаяСсылка();
	
	Если ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоМесяцев Тогда
		ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Месяц;
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоДней Тогда
		ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Сутки;
	ИначеЕсли ТочностьУказанияСрокаГодности = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
		ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Час;
	КонецЕсли;
	
	Возврат ЕдиницаИзмеренияСрокаГодности;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ ИСТИНА
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЭтоГруппа ИЛИ
	|	ЗначениеРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	НоменклатураВызовСервера.ВидыНоменклатурыОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Вызывается после окончания загрузки данных из локальной версии
// в область данных сервиса или наоборот.
//
Процедура ПослеЗагрузкиДанныхИзДругойМодели() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыНоменклатуры.Ссылка
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	(ВидыНоменклатуры.ЗапретРедактированияНаименованияДляПечатиНоменклатуры
	|			ИЛИ ВидыНоменклатуры.ЗапретРедактированияНаименованияДляПечатиХарактеристики
	|			ИЛИ ВидыНоменклатуры.ЗапретРедактированияРабочегоНаименованияНоменклатуры
	|			ИЛИ ВидыНоменклатуры.ЗапретРедактированияРабочегоНаименованияХарактеристики)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		СправочникОбъект.ЗапретРедактированияНаименованияДляПечатиНоменклатуры   = Ложь;
		СправочникОбъект.ЗапретРедактированияНаименованияДляПечатиХарактеристики = Ложь;
		СправочникОбъект.ЗапретРедактированияРабочегоНаименованияНоменклатуры    = Ложь;
		СправочникОбъект.ЗапретРедактированияРабочегоНаименованияХарактеристики  = Ложь;
		
		СправочникОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("КонвертацияОКП",   Справочники.КлассификаторОКПД2.ТаблицаКонвертацииОКП_ОКПД2());
	Запрос.Параметры.Вставить("КонвертацияОКВЭД", Справочники.КлассификаторОКВЭД2.ТаблицаКонвертацииОКВЭД_ОКВЭД2());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонвертацияОКП.КодОКП,
	|	КонвертацияОКП.КодОКПД2
	|ПОМЕСТИТЬ КонвертацияОКП
	|ИЗ
	|	&КонвертацияОКП КАК КонвертацияОКП
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодОКП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонвертацияОКВЭД.КодОКВЭД,
	|	КонвертацияОКВЭД.КодОКВЭД2
	|ПОМЕСТИТЬ КонвертацияОКВЭД
	|ИЗ
	|	&КонвертацияОКВЭД КАК КонвертацияОКВЭД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодОКВЭД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ ВТСкладыПодразделения
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Подразделения.Ссылка КАК Склад
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК Подразделения
	|ГДЕ
	|	Подразделения.ПроизводственноеПодразделение
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДляОбработки.Ссылка КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПолитикиУчетаСерий.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТСкладыПодразделения КАК СкладыПодразделения
	|			ПО ПолитикиУчетаСерий.Склад = СкладыПодразделения.Склад
	|	ГДЕ
	|		СкладыПодразделения.Склад ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	ГДЕ
	|		ВидыНоменклатуры.ПолитикаУчетаСерий = ЗНАЧЕНИЕ(Справочник.ПолитикиУчетаСерий.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	ГДЕ
	|		ВидыНоменклатуры.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	ГДЕ
	|		ВидыНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|		И (ВидыНоменклатуры.УдалитьВариантОказанияУслуг <> ЗНАЧЕНИЕ(Перечисление.УдалитьВариантыОказанияУслуг.ПустаяСсылка)
	|			ИЛИ ВидыНоменклатуры.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка КАК Ссылка
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	ГДЕ
	|		ВидыНоменклатуры.ИспользоватьСрокГодностиСерии
	|		И ВидыНоменклатуры.ЕдиницаИзмеренияСрокаГодности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка
	|	ИЗ
	|		КонвертацияОКП КАК КонвертацияОКП
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбщероссийскийКлассификаторПродукции КАК ОбщероссийскийКлассификаторПродукции
	|				ПО КонвертацияОКП.КодОКП = ОбщероссийскийКлассификаторПродукции.Код
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|				ПО (ВидыНоменклатуры.КодОКП = ОбщероссийскийКлассификаторПродукции.Ссылка)
	|	ГДЕ
	|		ВидыНоменклатуры.КодОКПД2 = ЗНАЧЕНИЕ(Справочник.КлассификаторОКПД2.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка
	|	ИЗ
	|		КонвертацияОКВЭД КАК КонвертацияОКВЭД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторВидовЭкономическойДеятельности КАК КлассификаторВидовЭкономическойДеятельности
	|				ПО КонвертацияОКВЭД.КодОКВЭД = КлассификаторВидовЭкономическойДеятельности.Код
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|				ПО (ВидыНоменклатуры.КодОКВЭД = КлассификаторВидовЭкономическойДеятельности.Ссылка)
	|	ГДЕ
	|		ВидыНоменклатуры.КодОКВЭД2 = ЗНАЧЕНИЕ(Справочник.КлассификаторОКВЭД2.ПустаяСсылка)
	|	) КАК ДанныеДляОбработки";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта        = "Справочник.ВидыНоменклатуры";
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МенеджерВременныхТаблиц);
	
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КонвертацияОКП.КодОКП,
	|	КонвертацияОКП.КодОКПД2
	|ПОМЕСТИТЬ КонвертацияОКП
	|ИЗ
	|	&КонвертацияОКП КАК КонвертацияОКП
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодОКП,
	|	КодОКПД2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонвертацияОКВЭД.КодОКВЭД,
	|	КонвертацияОКВЭД.КодОКВЭД2
	|ПОМЕСТИТЬ КонвертацияОКВЭД
	|ИЗ
	|	&КонвертацияОКВЭД КАК КонвертацияОКВЭД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодОКВЭД,
	|	КодОКВЭД2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ ВТСкладыПодразделения
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Подразделения.Ссылка КАК Склад
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК Подразделения
	|ГДЕ
	|	Подразделения.ПроизводственноеПодразделение
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыДляОбработки.Ссылка КАК Ссылка,
	|	СкладыПодразделения.Склад  КАК Склад
	|ПОМЕСТИТЬ ВТПолнаяТаблицаСкладов
	|ИЗ
	|	ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСкладыПодразделения КАК СкладыПодразделения
	|		ПО ИСТИНА
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Данные.Ссылка              КАК Ссылка,
	|	Данные.Ссылка.ВерсияДанных КАК ВерсияДанных,
	|	Данные.Склад               КАК Склад,
	|	МАКСИМУМ(Данные.ЗаполнитьСтавкуНДС) 					КАК ЗаполнитьСтавкуНДС,
	|	МАКСИМУМ(Данные.ЗаполнитьОсобенностьУчета) 				КАК ЗаполнитьОсобенностьУчета,
	|	МАКСИМУМ(Данные.ЗаполнитьЕдиницуИзмеренияСрокаГодности) КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|	МАКСИМУМ(Данные.КодОКПД2) 								КАК КодОКПД2,
	|	МАКСИМУМ(Данные.КодОКВЭД2) 								КАК КодОКВЭД2
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПолнаяТаблицаСкладов.Ссылка             КАК Ссылка,
	|		ПолнаяТаблицаСкладов.Склад              КАК Склад,
	|		ЛОЖЬ                                    КАК ЗаполнитьСтавкуНДС,
	|		ЛОЖЬ                                    КАК ЗаполнитьОсобенностьУчета,
	|		ЛОЖЬ                                    КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		NULL									КАК КодОКПД2,
	|		NULL									КАК КодОКВЭД2
	|	ИЗ
	|		ВТПолнаяТаблицаСкладов КАК ПолнаяТаблицаСкладов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|				ПО ПолнаяТаблицаСкладов.Ссылка = ОбъектыДляОбработки.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ТЧПолитикиУчетаСерий
	|				ПО ПолнаяТаблицаСкладов.Ссылка = ТЧПолитикиУчетаСерий.Ссылка
	|				И ПолнаяТаблицаСкладов.Склад = ТЧПолитикиУчетаСерий.Склад
	|	ГДЕ
	|		ТЧПолитикиУчетаСерий.Склад ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОбъектыДляОбработки.Ссылка               КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ИСТИНА                                   КАК ЗаполнитьСтавкуНДС,
	|		ЛОЖЬ                                     КАК ЗаполнитьОсобенностьУчета,
	|		ЛОЖЬ                                     КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		NULL									 КАК КодОКПД2,
	|		NULL									 КАК КодОКВЭД2
	|	ИЗ
	|		ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|				ПО ОбъектыДляОбработки.Ссылка = ВидыНоменклатуры.Ссылка
	|	ГДЕ
	|		ВидыНоменклатуры.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОбъектыДляОбработки.Ссылка               КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ЛОЖЬ                                     КАК ЗаполнитьСтавкуНДС,
	|		ИСТИНА                                   КАК ЗаполнитьОсобенностьУчета,
	|		ЛОЖЬ                                     КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		NULL									 КАК КодОКПД2,
	|		NULL									 КАК КодОКВЭД2
	|	ИЗ
	|		ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|				ПО ОбъектыДляОбработки.Ссылка = ВидыНоменклатуры.Ссылка
	|	ГДЕ
	|		ВидыНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|		И (ВидыНоменклатуры.УдалитьВариантОказанияУслуг <> ЗНАЧЕНИЕ(Перечисление.УдалитьВариантыОказанияУслуг.ПустаяСсылка)
	|			ИЛИ ВидыНоменклатуры.ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОбъектыДляОбработки.Ссылка               КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ЛОЖЬ                                     КАК ЗаполнитьСтавкуНДС,
	|		ЛОЖЬ                                     КАК ЗаполнитьОсобенностьУчета,
	|		ИСТИНА                                   КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		NULL									 КАК КодОКПД2,
	|		NULL									 КАК КодОКВЭД2
	|	ИЗ
	|		ВТОбъектыДляОбработки КАК ОбъектыДляОбработки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|				ПО ОбъектыДляОбработки.Ссылка = ВидыНоменклатуры.Ссылка
	|	ГДЕ
	|		ВидыНоменклатуры.ИспользоватьСрокГодностиСерии
	|		И ВидыНоменклатуры.ЕдиницаИзмеренияСрокаГодности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка					 КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ЛОЖЬ                                     КАК ЗаполнитьСтавкуНДС,
	|		ЛОЖЬ                                     КАК ЗаполнитьОсобенностьУчета,
	|		ЛОЖЬ                                     КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		КлассификаторОКПД2.Ссылка				 КАК КодОКПД2,
	|		NULL									 КАК КодОКВЭД2
	|	ИЗ
	|		ВТОбъектыДляОбработки КАК ВТОбъектыДляОбработки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|			ПО ВТОбъектыДляОбработки.Ссылка = ВидыНоменклатуры.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонвертацияОКП КАК КонвертацияОКП
	|			ПО (КонвертацияОКП.КодОКП = ВидыНоменклатуры.КодОКП.Код)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОКПД2 КАК КлассификаторОКПД2
	|			ПО (КонвертацияОКП.КодОКПД2 = КлассификаторОКПД2.Код)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.Ссылка					 КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ЛОЖЬ                                     КАК ЗаполнитьСтавкуНДС,
	|		ЛОЖЬ                                     КАК ЗаполнитьОсобенностьУчета,
	|		ЛОЖЬ                                     КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		NULL									 КАК КодОКПД2,
	|		КлассификаторОКВЭД2.Ссылка				 КАК КодОКВЭД2
	|	ИЗ
	|		ВТОбъектыДляОбработки КАК ВТОбъектыДляОбработки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|			ПО ВТОбъектыДляОбработки.Ссылка = ВидыНоменклатуры.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонвертацияОКВЭД КАК КонвертацияОКВЭД
	|			ПО (КонвертацияОКВЭД.КодОКВЭД = ВидыНоменклатуры.КодОКВЭД.Код)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОКВЭД2 КАК КлассификаторОКВЭД2
	|			ПО (КонвертацияОКВЭД.КодОКВЭД2 = КлассификаторОКВЭД2.Код)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТОбъектыДляОбработки.Ссылка			 КАК Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ЛОЖЬ                                     КАК ЗаполнитьСтавкуНДС,
	|		ЛОЖЬ                                     КАК ЗаполнитьОсобенностьУчета,
	|		ЛОЖЬ                                     КАК ЗаполнитьЕдиницуИзмеренияСрокаГодности,
	|		NULL									 КАК КодОКПД2,
	|		NULL									 КАК КодОКВЭД2
	|	ИЗ
	|		ВТОбъектыДляОбработки КАК ВТОбъектыДляОбработки
	|	) КАК Данные
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Ссылка,
	|	Данные.Склад
	|
	|ИТОГИ ПО
	|	Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОбъектыДляОбработки", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.Параметры.Вставить("КонвертацияОКП",   Справочники.КлассификаторОКПД2.ТаблицаКонвертацииОКП_ОКПД2());
	Запрос.Параметры.Вставить("КонвертацияОКВЭД", Справочники.КлассификаторОКВЭД2.ТаблицаКонвертацииОКВЭД_ОКВЭД2());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			СправочникОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(
				Выборка.Ссылка,
				Выборка.ВерсияДанных,
				Параметры.Очередь);
			
			Если СправочникОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ОбъектИзменен = Ложь;
			
			Если Не ЗначениеЗаполнено(СправочникОбъект.ПолитикаУчетаСерий) Тогда
				СправочникОбъект.ПолитикаУчетаСерий = Справочники.ПолитикиУчетаСерий.СерииНеИспользуются;
				ОбъектИзменен = Истина;
			КонецЕсли;
			
			Если СправочникОбъект.СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
				СправочникОбъект.СтавкаНДС = Перечисления.СтавкиНДС.ПустаяСсылка();
				ОбъектИзменен = Истина;
			КонецЕсли;
			
			ВыборкаСкладов = Выборка.Выбрать();
			
			Пока ВыборкаСкладов.Следующий() Цикл
				
				Если ВыборкаСкладов.ЗаполнитьОсобенностьУчета Тогда
					Если СправочникОбъект.УдалитьВариантОказанияУслуг = Перечисления.УдалитьВариантыОказанияУслуг.Партнером Тогда
						СправочникОбъект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.Партнером;
					ИначеЕсли СправочникОбъект.УдалитьВариантОказанияУслуг = Перечисления.УдалитьВариантыОказанияУслуг.ОрганизациейПоАгентскойСхеме Тогда
						СправочникОбъект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ОрганизациейПоАгентскойСхеме;
					Иначе
						СправочникОбъект.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ОрганизациейПродавцом;
					КонецЕсли;
					
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если ВыборкаСкладов.ЗаполнитьЕдиницуИзмеренияСрокаГодности Тогда
					СправочникОбъект.ЕдиницаИзмеренияСрокаГодности = Справочники.ВидыНоменклатуры.ЕдиницаИзмеренияСрокаГодности(
																		СправочникОбъект.ТочностьУказанияСрокаГодностиСерии);
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаСкладов.КодОКВЭД2) Тогда
					СправочникОбъект.КодОКВЭД2 = ВыборкаСкладов.КодОКВЭД2;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаСкладов.КодОКПД2) Тогда
					СправочникОбъект.КодОКПД2 = ВыборкаСкладов.КодОКПД2;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаСкладов.Склад) Тогда
					НоваяСтрока = СправочникОбъект.ПолитикиУчетаСерий.Добавить();
					НоваяСтрока.Склад = ВыборкаСкладов.Склад;
					НоваяСтрока.ПолитикаУчетаСерий = Справочники.ПолитикиУчетаСерий.СерииНеИспользуются;
					
					ОбъектИзменен = Истина;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.Ссылка);
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДополнительныеСвойстваИНаборыСвойствКОбновлению(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаборыСвойств.НаборСвойств КАК НаборСвойств
	|ПОМЕСТИТЬ ВТНаборыСвойствНоменктатуры
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВидыНоменклатуры.НаборСвойств КАК НаборСвойств
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.НаборСвойствСерий
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыНоменклатуры.НаборСвойствХарактеристик
	|	ИЗ
	|		Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ОбщийНаборСвойствНоменклатуры
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ОбщийНаборСвойствХарактеристик
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ОбщийНаборСвойствСерий) КАК НаборыСвойств
	|ГДЕ
	|	НаборыСвойств.НаборСвойств <> ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.НаборСвойств В
	|			(ВЫБРАТЬ
	|				ВТНаборыСвойствНоменктатуры.НаборСвойств
	|			ИЗ
	|				ВТНаборыСвойствНоменктатуры КАК ВТНаборыСвойствНоменктатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НаборыСвойств.НаборСвойств КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ
	|		НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка КАК НаборСвойств
	|	ИЗ
	|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|			ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|				И (НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка = ДополнительныеРеквизитыИСведения.НаборСвойств
	|					ИЛИ ДополнительныеРеквизитыИСведения.НаборСвойств = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка))
	|	ГДЕ
	|		ДополнительныеРеквизитыИСведения.Ссылка ЕСТЬ NULL
	|		И НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТНаборыСвойствНоменктатуры.НаборСвойств
	|				ИЗ
	|					ВТНаборыСвойствНоменктатуры КАК ВТНаборыСвойствНоменктатуры)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка
	|	ИЗ
	|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|			ПО НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|				И (НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка = ДополнительныеРеквизитыИСведения.НаборСвойств
	|					ИЛИ ДополнительныеРеквизитыИСведения.НаборСвойств = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка))
	|	ГДЕ
	|		ДополнительныеРеквизитыИСведения.Ссылка ЕСТЬ NULL
	|		И НаборыДополнительныхРеквизитовИСведенийДополнительныеСведения.Ссылка В
	|				(ВЫБРАТЬ
	|					ВТНаборыСвойствНоменктатуры.НаборСвойств
	|				ИЗ
	|					ВТНаборыСвойствНоменктатуры КАК ВТНаборыСвойствНоменктатуры)) КАК НаборыСвойств";
	
	Запрос.УстановитьПараметр("ОбщийНаборСвойствНоменклатуры", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Номенклатура_Общие);
	Запрос.УстановитьПараметр("ОбщийНаборСвойствХарактеристик", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ХарактеристикиНоменклатуры_Общие);
	Запрос.УстановитьПараметр("ОбщийНаборСвойствСерий", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_СерииНоменклатуры_Общие);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Результаты[1].Выгрузить().ВыгрузитьКолонку("Ссылка"));
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Результаты[2].Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДополнительныеСвойстваИНаборыСвойств(Параметры) Экспорт
	
	// Свойств и наборов не может быть очень много, при этом свойсва и наборы нужно записывать связанно
	// Сделаем это все в одной транзакции
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	ДополнительныеПараметрыОбработки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметрыОбработки.ВыбиратьПорциями = Ложь;
	
	РезультатСозданияВТНаборов = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь,
	    "Справочник.НаборыДополнительныхРеквизитовИСведений",
		МенеджерВТ,
		ДополнительныеПараметрыОбработки);
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Свойство,
	|	ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение КАК ЭтоДополнительноеСведение,
	|	ДополнительныеРеквизитыИСведения.ПометкаУдаления КАК ПометкаУдаления,
	|	ДополнительныеРеквизитыИСведения.НаборСвойств КАК НаборСвойств
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	ДополнительныеРеквизитыИСведения.НаборСвойств В
	|			(ВЫБРАТЬ
	|				ВТНаборы.Ссылка
	|			ИЗ
	|				ВТДляОбработкиНаборыДополнительныхРеквизитовИСведений КАК ВТНаборы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НаборСвойств
	|ИТОГИ ПО
	|	НаборСвойств";
	
	ВыборкаПоНаборам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВыборкаСвойствДляОбработки = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь,
		"ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения");
	
	НачатьТранзакцию();
	
	Попытка
		ИдетОбработкаНаборов = Истина;
		Пока ВыборкаПоНаборам.Следующий() Цикл
			
			НаборОбъект = ВыборкаПоНаборам.НаборСвойств.ПолучитьОбъект();
			ОбъектИзменен = Ложь;
			
			Если НаборОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаПоНаборам.НаборСвойств);
				Продолжить;
			КонецЕсли;
			
			ВыборкаПоСвойствам = ВыборкаПоНаборам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоСвойствам.Следующий() Цикл
				
				Если ВыборкаПоСвойствам.ЭтоДополнительноеСведение Тогда
					ТЧНабора = НаборОбъект.ДополнительныеСведения;
				Иначе
					ТЧНабора = НаборОбъект.ДополнительныеРеквизиты;
				КонецЕсли;
				
				НайденныеСтроки = ТЧНабора.НайтиСтроки(Новый Структура("Свойство", ВыборкаПоСвойствам.Свойство));
				Если НайденныеСтроки.Количество() <> 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТЧНабора.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоСвойствам);
				
				ОбъектИзменен = Истина;
				
			КонецЦикла;
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НаборОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаПоНаборам.НаборСвойств);
			КонецЕсли;
			
		КонецЦикла;
		
		НаборыКОтметкеОбработки = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь,
			"Справочник.НаборыДополнительныхРеквизитовИСведений",
			ДополнительныеПараметрыОбработки);
		
		Пока НаборыКОтметкеОбработки.Следующий() Цикл
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборыКОтметкеОбработки.Ссылка);
		КонецЦикла;				
		
		ИдетОбработкаНаборов = Ложь;
		
		Пока ВыборкаСвойствДляОбработки.Следующий() Цикл
			
			СвойствоОбъект = ВыборкаСвойствДляОбработки.Ссылка.ПолучитьОбъект();
			
			Если СвойствоОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ВыборкаСвойствДляОбработки.Ссылка);
				Продолжить;
			КонецЕсли;
			
			СвойствоОбъект.НаборСвойств = Справочники.НаборыДополнительныхРеквизитовИСведений.ПустаяСсылка();
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(СвойствоОбъект);
			
		КонецЦикла;
			
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		Если ИдетОбработкаНаборов Тогда
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ВыборкаПоНаборам.НаборСвойств);
		Иначе
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ВыборкаСвойствДляОбработки.Ссылка);
		КонецЕсли;
			
	КонецПопытки;
	
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		"Справочник.НаборыДополнительныхРеквизитовИСведений")
		И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		"ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения");

КонецПроцедуры

Процедура ЗарегистрироватьКонстантуИспользованияОбщихДополнительныхРеквизитовКОбновлению(Параметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ОбновитьКонстантуИспользованияОбщихДополнительныхРеквизитов(Параметры) Экспорт
	
	Если Не Константы.ИспользоватьДополнительныеРеквизитыИСведения.Получить()
		Или (Константы.ИспользоватьОбщиеДополнительныеРеквизитыИСведения.Получить()
		И Константы.ИспользоватьОбщиеДополнительныеЗначения.Получить()) Тогда
		Параметры.ОбработкаЗавершена = Истина;
	Иначе
		Константы.ИспользоватьОбщиеДополнительныеРеквизитыИСведения.Установить(Истина);
		Константы.ИспользоватьОбщиеДополнительныеЗначения.Установить(Истина);
		Параметры.ОбработкаЗавершена = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ЭтоРеквизитСоставногоТипа(Реквизит)
	
	ТипЗначенияРеквизита = ТипЗнч(Реквизит);
	
	Если ТипЗначенияРеквизита = Тип("ФиксированнаяСтруктура") Тогда
		Возврат (Не Реквизит.Тип.Типы().Количество() < 2);
	ИначеЕсли ТипЗначенияРеквизита = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
		Возврат (Не Реквизит.ТипЗначения.Типы().Количество() < 2);
	Иначе
		ТекстИсключения = НСтр("ru = 'Неожиданный тип значения параметра Реквизит.'");
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
КонецФункции

// Проверяет возможность изменения настройки отображения реквизита.
//
//	Параметры:
//		Объект - СправочникОбъект.Номенклатура
//		ИмяРеквизита - Строка
//		ВладелецРеквизита - Строка
//		ЭтоДопРеквизит - Булево
//		ЗаполнятьОбязательно - Булево
//
//	Возвращаемое значение:
//		Булево - изменение настройки возможно.
//
Функция ДоступноИзменениеНастройкиОтображенияРеквизита(Объект, СтрокаРеквизита, ВладелецРеквизита) Экспорт
	
	ИмяРеквизита   = СтрокаРеквизита.ИмяРеквизита;
	ЭтоДопРеквизит = СтрокаРеквизита.ЭтоДопРеквизит;
	
	Если ЭтоДопРеквизит Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаРеквизита.Свойство,"ЗаполнятьОбязательно");
		ЗаполнятьОбязательно = Реквизиты.ЗаполнятьОбязательно;
	Иначе
		Если ВладелецРеквизита = "Номенклатура" Тогда
			
			ОписаниеРеквизита = Справочники.Номенклатура.РеквизитОписание(ИмяРеквизита,
																Объект.ТипНоменклатуры,
																Объект.ОсобенностьУчета,
																Объект.ИспользованиеХарактеристик,
																Объект.ИспользоватьСрокГодностиСерии);
			
			ЗаполнятьОбязательно = ОписаниеРеквизита.ОбязательныйДляЗаполнения;
			
		Иначе
			ЗаполнятьОбязательно = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ДоступноИзменениеНастройки = Не ЗаполнятьОбязательно;
	
	Если ЭтоОбязательноОтображаемыйРеквизит(Объект, ИмяРеквизита, ВладелецРеквизита, ЭтоДопРеквизит, ЗаполнятьОбязательно) Тогда
		ДоступноИзменениеНастройки = Ложь;
	КонецЕсли;
	
	Возврат ДоступноИзменениеНастройки
			И Не (СтрокаРеквизита.Уникален
					Или СтрокаРеквизита.ОбязателенДляЗаполнения);
	
КонецФункции

// Проверяет возможность изменения настройки обязательности заполнения реквизита.
//
//	Параметры:
//		Объект - СправочникОбъект.Номенклатура
//		ИмяРеквизита - Строка
//		ВладелецРеквизита - Строка
//		ЭтоДопРеквизит - Булево
//		ЗаполнятьОбязательно - Булево
//
//	Возвращаемое значение:
//		Булево - изменение настройки возможно.
//
Функция ДоступноИзменениеНастройкиОбязательностиЗаполненияРеквизита(Объект, СтрокаРеквизита, ВладелецРеквизита)
	
	ИмяРеквизита         = СтрокаРеквизита.ИмяРеквизита;
	ЭтоДопРеквизит       = СтрокаРеквизита.ЭтоДопРеквизит;
	
	Если ЭтоДопРеквизит Тогда
		ТекстИсключения = НСтр("ru = 'Функция не предназначена для использования с доп. реквизитами.'");
		ВызватьИсключение ТекстИсключения;
	Иначе
		Если ВладелецРеквизита = "Номенклатура" Тогда
			
			ОписаниеРеквизита = Справочники.Номенклатура.РеквизитОписание(ИмяРеквизита,
																		Объект.ТипНоменклатуры,
																		Объект.ОсобенностьУчета,
																		Объект.ИспользованиеХарактеристик,
																		Объект.ИспользоватьСрокГодностиСерии);
			
			ТипЗначения = ОписаниеРеквизита.Тип;
			
			ЗаполнятьОбязательно = ОписаниеРеквизита.ОбязательныйДляЗаполнения;
			
		ИначеЕсли ВладелецРеквизита = "ХарактеристикиНоменклатуры" Тогда
			
			ОписаниеРеквизита = Справочники.ХарактеристикиНоменклатуры.РеквизитОписание(ИмяРеквизита,
																						Объект.ТипНоменклатуры,
																						Объект.ОсобенностьУчета);
			
			ТипЗначения = ОписаниеРеквизита.Тип;
			
			ЗаполнятьОбязательно = ОписаниеРеквизита.ОбязательныйДляЗаполнения;
			
		Иначе
			ЗаполнятьОбязательно = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ДоступноИзменениеНастройки = НЕ ЗаполнятьОбязательно;
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	
	Возврат ДоступноИзменениеНастройки
		И ТипЗначения <> ОписаниеТиповБулево;
	
КонецФункции

// Добавляет реквизит в табличные части элемента справочника номенклатуры.
//
// Параметры:
//	Объект - СправочникОбъект.Номенклатура
//	Реквизит - Структура - см. Справочник.Номенклатура.ЗначениеСвойствРеквизита
//	ТаблицаРеквизитовДляКонтроляНоменклатуры - ТаблицаЗначений
//	ТаблицаРеквизитовОтбораНоменклатуры - ТаблицаЗначений
//
Процедура ДобавитьРеквизитДляКонтроляИОтбораНоменклатуры(Объект, Реквизит, ВладелецРеквизита) Экспорт
	
	Если Справочники.Номенклатура.НеотображаемыеВТаблицеНастроекРеквизиты().Найти(Реквизит.Имя) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НеиспользуемыеДляКонтроляРеквизиты = Справочники.Номенклатура.НеиспользуемыеДляКонтроляУникальностиРеквизиты();
	ТипыНеИспользуемыеДляКонтроля = Справочники.Номенклатура.НеиспользуемыеДляКонтроляЗаполненияИОтборовТипы();
			
	СвойстваДляЗаполнения =
		"Уникален, ОбязателенДляЗаполнения, ОтображатьПриСоздании, ДоступностьУникален,
		|ДоступностьОбязателенДляЗаполнения, ДоступностьОтображатьПриСоздании";
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	
	Если Не ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Реквизит, "Тип") Тогда
		ТипРеквизита = Новый ОписаниеТипов("Неопределено");
	Иначе
		ТипРеквизита = Реквизит.Тип;
	КонецЕсли;
	
	ЭтоТовар = (Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар);
	ЭтоМногооборотнаяТара = (Объект.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара);
	
	ПредставлениеРеквизита = Реквизит.Представление;
		
	ЭтоНеиспользуемыйДляКонтроляРеквизит = (НеиспользуемыеДляКонтроляРеквизиты.Найти(Реквизит.Имя) <> Неопределено);
	ЭтоНеиспользуемыйДляКонтроляТип = (ТипыНеИспользуемыеДляКонтроля.Найти(Реквизит.Имя) <> Неопределено);
	ЭтоРеквизитСоставногоТипа = ЭтоРеквизитСоставногоТипа(Реквизит);
	ЭтоОбязательныйДляЗаполненияРеквизит = Реквизит.ОбязательныйДляЗаполнения;
	ТипРеквизитаБулево = (ТипРеквизита = ОписаниеТиповБулево);
	
	ЭтоРеквизитРекомендуемыйДляКонтроляУникальности = Реквизит.РекомендуемыйДляКонтроляУникальности; 
	
	Отбор = Новый Структура("ИмяРеквизита,ЭтоДопРеквизит", Реквизит.Имя, Ложь);
	
	Если ВладелецРеквизита = "Номенклатура" Тогда
		ИмяТЧ = "РеквизитыДляКонтроляНоменклатуры";
	Иначе
		ИмяТЧ = "РеквизитыДляКонтроляХарактеристик";
	КонецЕсли;
		
	НайденныеСтроки = Объект[ИмяТЧ].НайтиСтроки(Отбор);	
	
	ЭтоНоваяСтрока = Ложь;
	Если НайденныеСтроки.Количество() = 0 Тогда
		СтрокаРеквизитовДляКонтроля = Объект[ИмяТЧ].Добавить();
		СтрокаРеквизитовДляКонтроля.ИмяРеквизита = Реквизит.Имя;
		ЭтоНоваяСтрока = Истина;
	Иначе
		СтрокаРеквизитовДляКонтроля = НайденныеСтроки[0];
	КонецЕсли;	
	
	СтрокаРеквизитовДляКонтроля.ПредставлениеРеквизита = ПредставлениеРеквизита;
	
	Если Не СтрокаРеквизитовДляКонтроля.ОбязателенДляЗаполнения Тогда
		СтрокаРеквизитовДляКонтроля.ОбязателенДляЗаполнения = ЭтоОбязательныйДляЗаполненияРеквизит;
	КонецЕсли;
	
	Если Не СтрокаРеквизитовДляКонтроля.ОтображатьПриСоздании Тогда
		СтрокаРеквизитовДляКонтроля.ОтображатьПриСоздании =
			ЭтоОбязательноОтображаемыйРеквизит(Объект, Реквизит, "Номенклатура", Ложь, ЭтоОбязательныйДляЗаполненияРеквизит);
	КонецЕсли;
	
	Если ЭтоРеквизитРекомендуемыйДляКонтроляУникальности
		И ЭтоНоваяСтрока Тогда
		СтрокаРеквизитовДляКонтроля.Уникален = Истина;
		СтрокаРеквизитовДляКонтроля.ОтображатьПриСоздании = Истина;
		СтрокаРеквизитовДляКонтроля.ДоступностьУникален = Истина;
	ИначеЕсли ЭтоНеиспользуемыйДляКонтроляРеквизит
		Или ЭтоНеиспользуемыйДляКонтроляТип Тогда
		СтрокаРеквизитовДляКонтроля.ДоступностьУникален = Ложь;
		СтрокаРеквизитовДляКонтроля.Уникален = Ложь;
	Иначе
		СтрокаРеквизитовДляКонтроля.ДоступностьУникален = Истина;
	КонецЕсли;
	
	СтрокаРеквизитовДляКонтроля.ДоступностьОбязателенДляЗаполнения = ДоступноИзменениеНастройкиОбязательностиЗаполненияРеквизита(Объект,
																												СтрокаРеквизитовДляКонтроля,
																												ВладелецРеквизита);

		
	СтрокаРеквизитовДляКонтроля.ДоступностьОтображатьПриСоздании   = ДоступноИзменениеНастройкиОтображенияРеквизита(Объект,
																													СтрокаРеквизитовДляКонтроля,
																													ВладелецРеквизита);
КонецПроцедуры

Функция ЭтоОбязательноОтображаемыйРеквизит(Объект, ИмяРеквизита, ВладелецРеквизита, ЭтоДопРеквизит, ЗаполнятьОбязательно)
	
	Если ЭтоДопРеквизит Тогда
		Возврат ЗаполнятьОбязательно;
	КонецЕсли;
	
	Если ЗаполнятьОбязательно Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ВладелецРеквизита = "Номенклатура" Тогда
		
		ОбязательноОтображаемый = Справочники.Номенклатура.РеквизитОписание(ИмяРеквизита,
																Объект.ТипНоменклатуры,
																Объект.ОсобенностьУчета,
																Объект.ИспользованиеХарактеристик,
																Объект.ИспользоватьСрокГодностиСерии).ОбязательныйДляОтображенияПриСоздании;
		
		Возврат ОбязательноОтображаемый;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ИменаРеквизитовДляФормыНастройкаСоставаРеквизитовСерии(Событие) Экспорт
	
	//Событие: СохранениеРезультатов,ОткрытиеФормыРедактирования
	
	ИменаРеквизитов = "ИспользоватьRFIDМеткиСерии,ИспользоватьНомерКИЗГИСМСерии,ИспользоватьНомерСерии,ИспользоватьСрокГодностиСерии,"
						+ "ТочностьУказанияСрокаГодностиСерии,НастройкаИспользованияСерий,АвтоматическиГенерироватьСерии,ИспользоватьДатуПроизводстваСерии,"
						+ "ИспользоватьПроизводителяЕГАИССерии,ИспользоватьСправку2ЕГАИССерии,"
						+ "ИспользоватьПроизводителяВЕТИССерии,ИспользоватьЗаписьСкладскогоЖурналаВЕТИССерии,ИспользоватьИдентификаторПартииВЕТИССерии";
	
	Если Событие = "ОткрытиеФормыРедактирования" Тогда
		ИменаРеквизитов = ИменаРеквизитов + ",КиЗГИСМ,ПродукцияМаркируемаяДляГИСМ,АлкогольнаяПродукция,"
							+ "ПодконтрольнаяПродукцияВЕТИС";
	КонецЕсли;
	
	Возврат ИменаРеквизитов;
	
КонецФункции

Процедура УдалитьРеквизитДляКонтроля(Объект, ИмяРеквизита, ВладелецРеквизита)
	
	Отбор = Новый Структура("ИмяРеквизита", ИмяРеквизита);
	КоллекцияРеквизитовДляКонтроля = КоллекцияРеквизитовДляКонтроля(Объект, ВладелецРеквизита);
	
	НайденныеСтроки = КоллекцияРеквизитовДляКонтроля.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		СтрокаТаблицыРеквизитов = НайденныеСтроки[0];
		КоллекцияРеквизитовДляКонтроля.Удалить(СтрокаТаблицыРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьРеквизитДляОтбора(Объект, ИмяРеквизита, ВладелецРеквизита)
	
	КоллекцияРеквизитовОтбора = КоллекцияРеквизитовОтбора(Объект, ВладелецРеквизита); 
	
	МассивСтрок = КоллекцияРеквизитовОтбора.НайтиСтроки(Новый Структура("ИмяРеквизита", ИмяРеквизита));
	
	Если МассивСтрок.Количество() > 0 Тогда
		СтрокаТаблицыРеквизитов = МассивСтрок[0];
		КоллекцияРеквизитовОтбора.Удалить(СтрокаТаблицыРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииФлагаУникален(Объект, ВладелецРеквизита, ИдентификаторСтрокиТаблицы) Экспорт
	
	КоллекцияРеквизитовДляКонтроля = Справочники.ВидыНоменклатуры.КоллекцияРеквизитовДляКонтроля(Объект, ВладелецРеквизита);
	Если ТипЗнч(Объект) = Тип("СправочникОбъект.ВидыНоменклатуры") Тогда
		ТекущиеДанные = КоллекцияРеквизитовДляКонтроля[ИдентификаторСтрокиТаблицы];
	Иначе	
		ТекущиеДанные = КоллекцияРеквизитовДляКонтроля.НайтиПоИдентификатору(ИдентификаторСтрокиТаблицы);
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	
	Если ВладелецРеквизита <> "СерииНоменклатуры"
		И ТекущиеДанные.Уникален Тогда
		ТекущиеДанные.ОбязателенДляЗаполнения = Истина;
		ТекущиеДанные.ОтображатьПриСоздании = Истина;
	КонецЕсли;
		
	// Если тип реквизита - Булево, то не включать контроль заполнения.
	Если ТекущиеДанные.ЭтоДопРеквизит Тогда
		
		ТипЗначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеДанные.Свойство, "ТипЗначения");
		
		Если ТипЗначения = ОписаниеТиповБулево Тогда
			ТекущиеДанные.ОбязателенДляЗаполнения = Ложь;
		КонецЕсли;
		
	Иначе
		
		Если ВладелецРеквизита = "Номенклатура" Тогда
			
			ТипРеквизита = Справочники.Номенклатура.РеквизитОписание(ТекущиеДанные.ИмяРеквизита,
																	Объект.ТипНоменклатуры,
																	Объект.ОсобенностьУчета,
																	Объект.ИспользованиеХарактеристик,
																	Объект.ИспользоватьСрокГодностиСерии).Тип;
			
			Если ТипРеквизита = ОписаниеТиповБулево Тогда
				ТекущиеДанные.ОбязателенДляЗаполнения = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВладелецРеквизита <> "СерииНоменклатуры" Тогда 
		ТекущиеДанные.ДоступностьОтображатьПриСоздании = Справочники.ВидыНоменклатуры.ДоступноИзменениеНастройкиОтображенияРеквизита(Объект,
																																	ТекущиеДанные,
																																	ВладелецРеквизита);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет заполнение таблицы настроек контроля реквизитов справочников Номенклатура и ХарактеристикиНоменклатуры.
//
Процедура ЗаполнитьРеквизитыДляКонтроля(Объект, ВладелецРеквизитов) Экспорт
	
	ЭтоРеквизитыХарактеристик = ВладелецРеквизитов = "ХарактеристикиНоменклатуры";
	
	Если ЭтоРеквизитыХарактеристик
		И Не Объект.ИспользоватьХарактеристики Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоРеквизитыХарактеристик Тогда
		ЗависимостьОтТипаНоменклатуры = ЗначениеНастроекПовтИсп.ВсеРеквизитыХарактеристикНоменклатуры(Объект.ТипНоменклатуры,
																									Объект.ОсобенностьУчета);
	Иначе
		ЗависимостьОтТипаНоменклатуры = ЗначениеНастроекПовтИсп.ВсеРеквизитыНоменклатуры(Объект.ТипНоменклатуры,
																						Объект.ОсобенностьУчета,
																						Объект.ИспользоватьХарактеристики,
																						Объект.ИспользоватьСрокГодностиСерии);
	КонецЕсли;
	
	Для Каждого НастройкаОтТипа Из ЗависимостьОтТипаНоменклатуры Цикл
		
		Если НастройкаОтТипа.Значение.Использование Тогда
			Справочники.ВидыНоменклатуры.ДобавитьРеквизитДляКонтроляИОтбораНоменклатуры(Объект,
																						НастройкаОтТипа.Значение,
																						ВладелецРеквизитов);
		Иначе
			УдалитьРеквизитДляКонтроля(Объект, НастройкаОтТипа.Ключ, ВладелецРеквизитов);
			УдалитьРеквизитДляОтбора(Объект, НастройкаОтТипа.Ключ, ВладелецРеквизитов);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭтоРеквизитыХарактеристик Тогда
		Объект.РеквизитыДляКонтроляХарактеристик.Сортировать("ПредставлениеРеквизита");
	Иначе
		Объект.РеквизитыДляКонтроляНоменклатуры.Сортировать("ПредставлениеРеквизита");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДопРеквизитыДляКонтроляИОтбора(Объект, НазначениеДопРеквизитов) Экспорт
	
	Если НазначениеДопРеквизитов = "ХарактеристикиНоменклатуры"
		И Не Объект.ИспользоватьХарактеристики Тогда
		Объект.РеквизитыБыстрогоОтбораХарактеристик.Очистить();
		Объект.РеквизитыДляКонтроляХарактеристик.Очистить();
		Возврат;
	ИначеЕсли НазначениеДопРеквизитов = "СерииНоменклатуры"
		И Не Объект.ИспользоватьСерии Тогда
		Объект.РеквизитыДляКонтроляСерий.Очистить();
		Возврат;
	КонецЕсли;
	
	ТипыНеИспользуемыеДляКонтроля = Справочники.Номенклатура.НеиспользуемыеДляКонтроляЗаполненияИОтборовТипы();
	
	СвойстваДляЗаполнения = "Уникален, ОбязателенДляЗаполнения, ОтображатьПриСоздании, ДоступностьУникален, ДоступностьОбязателенДляЗаполнения, ДоступностьОтображатьПриСоздании";
	
	Отбор = Новый Структура("Свойство, ЭтоДопРеквизит");
		
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	
	ВсеДопРеквизиты = Справочники.Номенклатура.ВсеДополнительныеРеквизиты(Объект.Ссылка, НазначениеДопРеквизитов);
	
	РеквизитыДляКонтроля    = КоллекцияРеквизитовДляКонтроля(Объект, НазначениеДопРеквизитов);
	РеквизитыБыстрогоОтбора = КоллекцияРеквизитовОтбора(Объект, НазначениеДопРеквизитов);
	
	УдаляемыеСтроки = Новый Массив;
	
	Для каждого ДопРеквизит Из РеквизитыДляКонтроля Цикл
		Если Не ДопРеквизит.ЭтоДопРеквизит Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отбор, ДопРеквизит);
		
		Если ВсеДопРеквизиты.НайтиСтроки(Отбор).Количество() = 0 Тогда
			УдаляемыеСтроки.Добавить(ДопРеквизит);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрМас Из УдаляемыеСтроки Цикл
		РеквизитыДляКонтроля.Удалить(СтрМас);
	КонецЦикла;
	
	Если РеквизитыБыстрогоОтбора <> Неопределено Тогда
		УдаляемыеСтроки = Новый Массив;
		
		Для каждого ДопРеквизит Из РеквизитыБыстрогоОтбора Цикл
			Если Не ДопРеквизит.ЭтоДопРеквизит Тогда
				Продолжить;
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Отбор, ДопРеквизит);
			
			Если ВсеДопРеквизиты.НайтиСтроки(Отбор).Количество() = 0 Тогда
				УдаляемыеСтроки.Добавить(ДопРеквизит);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрМас Из УдаляемыеСтроки Цикл
			РеквизитыБыстрогоОтбора.Удалить(СтрМас);
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого ДопРеквизит Из ВсеДопРеквизиты Цикл
		
		ТипЗначения = ДопРеквизит.Тип;
		ЭтоНеиспользуемыйДляКонтроляТип = (ТипыНеИспользуемыеДляКонтроля.Найти(ТипЗначения) <> Неопределено);
		ТипРеквизитаБулево = (ТипЗначения = ОписаниеТиповБулево);
		
		ЗаполнитьЗначенияСвойств(Отбор, ДопРеквизит);
		
		НайденныеСтроки = РеквизитыДляКонтроля.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			СтрокаРеквизитовДляКонтроля = НайденныеСтроки[0];
			СтрокаРеквизитовДляКонтроля.ПредставлениеРеквизита = ДопРеквизит.ПредставлениеРеквизита;	
			НоваяСтрока = Ложь;
		Иначе
			СтрокаРеквизитовДляКонтроля = РеквизитыДляКонтроля.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРеквизитовДляКонтроля, ДопРеквизит);
			НоваяСтрока = Истина;
		КонецЕсли;
		
		Если ДопРеквизит.ЗаполнятьОбязательно Тогда
			СтрокаРеквизитовДляКонтроля.ОбязателенДляЗаполнения = Истина;
			СтрокаРеквизитовДляКонтроля.ДоступностьОбязателенДляЗаполнения = Ложь;
			Если НазначениеДопРеквизитов <> "СерииНоменклатуры" Тогда
				СтрокаРеквизитовДляКонтроля.ОтображатьПриСоздании              = Истина;
			КонецЕсли;
		Иначе
			СтрокаРеквизитовДляКонтроля.ДоступностьОбязателенДляЗаполнения = Истина;
		КонецЕсли;
		
		Если НазначениеДопРеквизитов <> "СерииНоменклатуры" Тогда
			Если Не ЭтоНеиспользуемыйДляКонтроляТип Тогда
				СтрокаРеквизитовДляКонтроля.ДоступностьУникален = Истина;
			Иначе
				СтрокаРеквизитовДляКонтроля.Уникален = Ложь;
				СтрокаРеквизитовДляКонтроля.ДоступностьУникален = Ложь;
			КонецЕсли;
			
			СтрокаРеквизитовДляКонтроля.ДоступностьОтображатьПриСоздании = Не СтрокаРеквизитовДляКонтроля.ОбязателенДляЗаполнения
																		  И Не СтрокаРеквизитовДляКонтроля.Уникален;
			
		КонецЕсли;			
	КонецЦикла;
		
	Если РеквизитыБыстрогоОтбора <> Неопределено Тогда
		Отбор = Новый Структура("Свойство");
		
		Для Каждого ДопРеквизит Из ВсеДопРеквизиты Цикл
			ЗаполнитьЗначенияСвойств(Отбор, ДопРеквизит);
			ИзменяемыеСтроки = РеквизитыБыстрогоОтбора.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаРеквизитов Из ИзменяемыеСтроки Цикл
				ЗаполнитьЗначенияСвойств(СтрокаРеквизитов, ДопРеквизит, "ПредставлениеРеквизита");
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если РеквизитыДляКонтроля <> Неопределено Тогда
		РеквизитыДляКонтроля.Сортировать("ПредставлениеРеквизита");
	КонецЕсли;
	
КонецПроцедуры

Функция КоллекцияРеквизитовДляКонтроля(ТекущийОбъект, ТипВладельцаНабораЭлементов) Экспорт
	
	Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
		Возврат ТекущийОбъект.РеквизитыДляКонтроляНоменклатуры;
	ИначеЕсли ТипВладельцаНабораЭлементов = "ХарактеристикиНоменклатуры" Тогда
		Возврат ТекущийОбъект.РеквизитыДляКонтроляХарактеристик;
	ИначеЕсли ТипВладельцаНабораЭлементов = "СерииНоменклатуры" Тогда
		Возврат ТекущийОбъект.РеквизитыДляКонтроляСерий;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция КоллекцияРеквизитовОтбора(ТекущийОбъект, ТипВладельцаНабораЭлементов) Экспорт
	
	Если ТипВладельцаНабораЭлементов = "Номенклатура" Тогда
		Возврат ТекущийОбъект.РеквизитыБыстрогоОтбораНоменклатуры;
	ИначеЕсли ТипВладельцаНабораЭлементов = "ХарактеристикиНоменклатуры" Тогда
		Возврат ТекущийОбъект.РеквизитыБыстрогоОтбораХарактеристик;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ИспользоватьПроизводство() Экспорт
	
	ИспользоватьПроизводство = Ложь; //Для УТ
	
	//++ НЕ УТ
	ИспользоватьПроизводство = ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством"); //Для КАУП
	//-- НЕ УТ
	
	Возврат ИспользоватьПроизводство;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
