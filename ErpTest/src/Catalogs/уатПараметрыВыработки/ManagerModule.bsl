
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	мсвЗапрещенныеКВыбору = Новый Массив();
	
	Если Не Константы.уатИспользоватьDynafleet.Получить()
			И Не Константы.уатИспользоватьOmnicomm.Получить()
			И Не Константы.уатИспользоватьPositionReport.Получить()
			И Не Константы.уатИспользоватьАвтоГРАФ.Получить()
			И Не Константы.уатИспользоватьАвтотрекер.Получить()
			И Не Константы.уатИспользоватьСКАУТ.Получить()
			И Не Константы.уатИспользоватьЦСМ.Получить() Тогда 
		мсвЗапрещенныеКВыбору.Добавить(Справочники.уатПараметрыВыработки.ПробегОбщийПоДаннымGPS);
		мсвЗапрещенныеКВыбору.Добавить(Справочники.уатПараметрыВыработки.ВремяВРаботеПоДаннымGPS);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("ЗапрещенныеКВыбору", мсвЗапрещенныеКВыбору);
	Запрос.УстановитьПараметр("СтрокаПодбора", "" + Параметры.СтрокаПоиска + "%");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	уатПараметрыВыработки.Ссылка,
	|	уатПараметрыВыработки.Наименование
	|ИЗ
	|	Справочник.уатПараметрыВыработки КАК уатПараметрыВыработки
	|ГДЕ
	|	НЕ уатПараметрыВыработки.Ссылка В (&ЗапрещенныеКВыбору)
	|	И уатПараметрыВыработки.Наименование ПОДОБНО &СтрокаПодбора
	|	И %__ДопПараметры__%";
	
	ДопОтборы = "";
	Если Параметры.Свойство("Отбор") И Не Параметры.Отбор = Неопределено Тогда  
		Сч = 1;
		Для Каждого ТекОтбор Из Параметры.Отбор Цикл 
			ДопОтборы = ДопОтборы + ?(ДопОтборы="",""," И ") + "уатПараметрыВыработки." + ТекОтбор.Ключ 
				+ " = &Парам" + Формат(Сч, "ЧН=0; ЧГ=0");
			Запрос.УстановитьПараметр("Парам" + Формат(Сч, "ЧН=0; ЧГ=0"), ТекОтбор.Значение);
			Сч = Сч + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если ДопОтборы = "" Тогда 
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "%__ДопПараметры__%", "ИСТИНА");
	Иначе 
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "%__ДопПараметры__%", ДопОтборы);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеВыбора = Новый СписокЗначений();
	
	Пока Выборка.Следующий() Цикл 
		врПозицияОтсечения = СтрДлина(Параметры.СтрокаПоиска);
		тПредставление = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока(Лев(Выборка.Наименование, врПозицияОтсечения),, WebЦвета.Красный), 
			Новый ФорматированнаяСтрока(Прав(Выборка.Наименование, СтрДлина(Выборка.Наименование)-врПозицияОтсечения)));
		ДанныеВыбора.Добавить(Выборка.Ссылка, тПредставление);
	КонецЦикла;
	
КонецПроцедуры
