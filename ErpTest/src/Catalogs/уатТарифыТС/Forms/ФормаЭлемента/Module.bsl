////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура НастроитьКомпоновщикОтбора()
	СхемаКомпоновкиДанных = Справочники.уатТарифыТС.ПолучитьМакет("КомпоновкаДанныхОбластьДействия");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	КомпоновщикНастроекОтбора.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		ЗагрузитьНастройкиОтбораОбластиДействий();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиОтбораОбластиДействий()
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекНастройки = ТекОбъект.ОбластьДействия.Получить();
	Если ТипЗнч(ТекНастройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		КомпоновщикНастроекОтбора.ЗагрузитьНастройки(ТекНастройки);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтбораОбластиДействий(ТекОбъект)
	ТекОбъект.ОбластьДействия = Новый ХранилищеЗначения(КомпоновщикНастроекОтбора.Настройки);
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостью()
	
	Если Объект.ФиксированныйТариф Тогда
		Элементы.СложныйТариф.Видимость = Ложь;
		Элементы.МинимальнаяВыработка.Видимость = Ложь;
	Иначе
		Элементы.СложныйТариф.Видимость = Истина;
		Элементы.МинимальнаяВыработка.Видимость = Истина;
	КонецЕсли;	
	
	Если (НЕ Объект.СложныйТариф) ИЛИ Объект.ФиксированныйТариф Тогда
		Элементы.ГруппаГруппировки.Видимость = Ложь;
		Элементы.ГруппаТариф.Видимость = Истина;
	Иначе	
		Элементы.ГруппаГруппировки.Видимость = Истина;
		Элементы.ГруппаТариф.Видимость = Ложь;
		
		Элементы.ТарифыЗнакГруппировки1.Видимость = ЗначениеЗаполнено(Объект.ГруппировкаТарифа1);
		Элементы.ТарифыЗначениеГруппировки1.Видимость = ЗначениеЗаполнено(Объект.ГруппировкаТарифа1);
		Элементы.ТарифыЗначениеГруппировки1.Заголовок = Объект.ГруппировкаТарифа1;
		Элементы.ТарифыЗнакГруппировки2.Видимость = ЗначениеЗаполнено(Объект.ГруппировкаТарифа2);
		Элементы.ТарифыЗначениеГруппировки2.Видимость = ЗначениеЗаполнено(Объект.ГруппировкаТарифа2);
		Элементы.ТарифыЗначениеГруппировки2.Заголовок = Объект.ГруппировкаТарифа2;
		Элементы.ТарифыЗнакГруппировки3.Видимость = ЗначениеЗаполнено(Объект.ГруппировкаТарифа3);
		Элементы.ТарифыЗначениеГруппировки3.Видимость = ЗначениеЗаполнено(Объект.ГруппировкаТарифа3);
		Элементы.ТарифыЗначениеГруппировки3.Заголовок = Объект.ГруппировкаТарифа3;
		
		Если ЗначениеЗаполнено(Объект.ПараметрВыработки) Тогда
			Элементы.ТарифыТариф.Видимость = Истина;
			Элементы.ТарифыТариф.Заголовок = "Тариф (" + Объект.ПараметрВыработки + ")";
		Иначе	
			Элементы.ТарифыТариф.Видимость = Ложь;
		КонецЕсли;
		
		Если Элементы.ТарифыЗначениеГруппировки1.Видимость Тогда
			Если Объект.ГруппировкаТарифа1 = ПредопределенноеЗначение("Перечисление.уатГруппировкиТарифов.КлассГруза") Тогда
				Элементы.ТарифыЗначениеГруппировки1.Формат = "ЧДЦ = 0; ЧЦ = 2";
				Элементы.ТарифыЗначениеГруппировки1.ФорматРедактирования = "ЧДЦ = 0; ЧЦ = 2";
			Иначе
				Элементы.ТарифыЗначениеГруппировки1.Формат = "ЧДЦ = 3; ЧЦ = 10";
				Элементы.ТарифыЗначениеГруппировки1.ФорматРедактирования = "ЧДЦ = 3; ЧЦ = 10";
			КонецЕсли;
		КонецЕсли;
		Если Элементы.ТарифыЗначениеГруппировки2.Видимость Тогда
			Если Объект.ГруппировкаТарифа2 = ПредопределенноеЗначение("Перечисление.уатГруппировкиТарифов.КлассГруза") Тогда
				Элементы.ТарифыЗначениеГруппировки2.Формат = "ЧДЦ = 0; ЧЦ = 2";
				Элементы.ТарифыЗначениеГруппировки2.ФорматРедактирования = "ЧДЦ = 0; ЧЦ = 2";
			Иначе
				Элементы.ТарифыЗначениеГруппировки2.Формат = "ЧДЦ = 3; ЧЦ = 10";
				Элементы.ТарифыЗначениеГруппировки2.ФорматРедактирования = "ЧДЦ = 3; ЧЦ = 10";
			КонецЕсли;
		КонецЕсли;
		Если Элементы.ТарифыЗначениеГруппировки3.Видимость Тогда
			Если Объект.ГруппировкаТарифа3 = ПредопределенноеЗначение("Перечисление.уатГруппировкиТарифов.КлассГруза") Тогда
				Элементы.ТарифыЗначениеГруппировки3.Формат = "ЧДЦ = 0; ЧЦ = 2";
				Элементы.ТарифыЗначениеГруппировки3.ФорматРедактирования = "ЧДЦ = 0; ЧЦ = 2";
			Иначе
				Элементы.ТарифыЗначениеГруппировки3.Формат = "ЧДЦ = 3; ЧЦ = 10";
				Элементы.ТарифыЗначениеГруппировки3.ФорматРедактирования = "ЧДЦ = 3; ЧЦ = 10";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборНоменклатурыУслуги()
	
	ПарамОтбор = Новый ПараметрВыбора("Отбор.ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Услуга);
	мсвОтборы = Новый Массив();
	мсвОтборы.Добавить(ПарамОтбор);
	фиксМсвОтборы = Новый ФиксированныйМассив(мсвОтборы);
	
	Элементы.НоменклатураУслуги.ПараметрыВыбора = фиксМсвОтборы;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ  

&НаКлиенте
Процедура ФиксированныйТарифПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры

&НаКлиенте
Процедура СложныйТарифПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаТарифа1ПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаТарифа2ПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаТарифа3ПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры

&НаКлиенте
Процедура НастройкиОтбораПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПараметрВыработкиПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	об = РеквизитФормыВЗначение("Объект");
	//уатЗащищенныеФункцииСервер.уатСправочникФормаЭлементаПриСозданииНаСервере(Об, Отказ, СтандартнаяОбработка, ЭтаФорма, ДопПараметрыОткрытие);
	ЗначениеВРеквизитФормы(Об,"Объект");
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	НастроитьКомпоновщикОтбора();
	
	Если НЕ ПравоДоступа("Редактирование", Метаданные.Справочники.уатТарифыТС) Тогда
		Элементы.ГруппаСтраницаОбластьДействия.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	УстановитьОтборНоменклатурыУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//уатЗащищенныеФункцииКлиент.уатСправочникФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеВидимостью();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Для Каждого ТекОтбор Из КомпоновщикНастроекОтбора.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ТекОтбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Сообщить("Запрещено использование групп в области действия!", СтатусСообщения.Внимание);
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	СохранитьНастройкиОтбораОбластиДействий(ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзменениеТарифаТС");
КонецПроцедуры
