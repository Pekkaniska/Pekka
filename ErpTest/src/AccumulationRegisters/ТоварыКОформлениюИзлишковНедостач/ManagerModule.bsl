#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ТоварыКОформлениюИзлишковНедостач";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка.Склад КАК Склад,
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка.Дата КАК Дата,
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка КАК Ссылка,
	|	КорректировкаПоОрдеруНаТоварыТовары.Номенклатура КАК Номенклатура,
	|	КорректировкаПоОрдеруНаТоварыТовары.Характеристика КАК Характеристика,
	|	КорректировкаПоОрдеруНаТоварыТовары.Назначение КАК Назначение,
	|	ВЫБОР
	|		КОГДА КорректировкаПоОрдеруНаТоварыТовары.СтатусУказанияСерий В (4, 6, 8, 10, 14)
	|			ТОГДА КорректировкаПоОрдеруНаТоварыТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	КорректировкаПоОрдеруНаТоварыТовары.СтатусУказанияСерий = 14 КАК ЭтоСерияПоСебестоимости,
	|	ВЫБОР
	|		КОГДА КорректировкаПоОрдеруНаТоварыТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишекОставитьВЗонеОтгрузки)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек)
	|		ИНАЧЕ КорректировкаПоОрдеруНаТоварыТовары.ВидОперации
	|	КОНЕЦ КАК ВидОперации,
	|	СУММА(КорректировкаПоОрдеруНаТоварыТовары.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	Документ.КорректировкаПоОрдеруНаТовары.Товары КАК КорректировкаПоОрдеруНаТоварыТовары
	|ГДЕ
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка.Проведен
	|	И КорректировкаПоОрдеруНаТоварыТовары.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишекОставитьВЗонеОтгрузки))
	|	И ЕСТЬNULL(КорректировкаПоОрдеруНаТоварыТовары.Упаковка.ТипУпаковки, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка,
	|	КорректировкаПоОрдеруНаТоварыТовары.Характеристика,
	|	КорректировкаПоОрдеруНаТоварыТовары.Номенклатура,
	|	КорректировкаПоОрдеруНаТоварыТовары.ВидОперации,
	|	ВЫБОР
	|		КОГДА КорректировкаПоОрдеруНаТоварыТовары.СтатусУказанияСерий В (4, 6, 8, 10, 14)
	|			ТОГДА КорректировкаПоОрдеруНаТоварыТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	КорректировкаПоОрдеруНаТоварыТовары.СтатусУказанияСерий = 14,
	|	КорректировкаПоОрдеруНаТоварыТовары.Назначение,
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка.Склад,
	|	КорректировкаПоОрдеруНаТоварыТовары.Ссылка.Дата
	|
	|ИМЕЮЩИЕ
	|	СУММА(КорректировкаПоОрдеруНаТоварыТовары.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистраторыСОтклонениями.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВложенныйЗапрос.Регистратор КАК Регистратор,
	|		ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
	|		ВложенныйЗапрос.Период КАК Период,
	|		ВложенныйЗапрос.Склад КАК Склад,
	|		ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|		ВложенныйЗапрос.Характеристика КАК Характеристика,
	|		ВложенныйЗапрос.Назначение КАК Назначение,
	|		ВложенныйЗапрос.Серия КАК Серия,
	|		СУММА(ВложенныйЗапрос.КОформлениюАктов) КАК КОформлениюАктов
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаТовары.Ссылка КАК Регистратор,
	|			ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|			ТаблицаТовары.Дата КАК Период,
	|			ТаблицаТовары.Склад КАК Склад,
	|			ТаблицаТовары.Номенклатура КАК Номенклатура,
	|			ТаблицаТовары.Характеристика КАК Характеристика,
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|					ТОГДА ТаблицаТовары.Назначение
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			КОНЕЦ КАК Назначение,
	|			ВЫБОР
	|				КОГДА ТаблицаТовары.ЭтоСерияПоСебестоимости
	|					ТОГДА ТаблицаТовары.Серия
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			КОНЕЦ КАК Серия,
	|			ТаблицаТовары.Количество КАК КОформлениюАктов
	|		ИЗ
	|			ВТТовары КАК ТаблицаТовары
	|		ГДЕ
	|			ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаТовары.Ссылка,
	|			ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|			ТаблицаТовары.Дата,
	|			ТаблицаТовары.Склад,
	|			ТаблицаТовары.Номенклатура,
	|			ТаблицаТовары.Характеристика,
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|					ТОГДА ТаблицаТовары.Назначение
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			КОНЕЦ,
	|			ВЫБОР
	|				КОГДА ТаблицаТовары.ЭтоСерияПоСебестоимости
	|					ТОГДА ТаблицаТовары.Серия
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			КОНЕЦ,
	|			ТаблицаТовары.Количество
	|		ИЗ
	|			ВТТовары КАК ТаблицаТовары
	|		ГДЕ
	|			ТаблицаТовары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТоварыКОформлениюИзлишковНедостач.Регистратор,
	|			ТоварыКОформлениюИзлишковНедостач.ВидДвижения,
	|			ТоварыКОформлениюИзлишковНедостач.Период,
	|			ТоварыКОформлениюИзлишковНедостач.Склад,
	|			ТоварыКОформлениюИзлишковНедостач.Номенклатура,
	|			ТоварыКОформлениюИзлишковНедостач.Характеристика,
	|			ТоварыКОформлениюИзлишковНедостач.Назначение,
	|			ТоварыКОформлениюИзлишковНедостач.Серия,
	|			-ТоварыКОформлениюИзлишковНедостач.КОформлениюАктов
	|		ИЗ
	|			РегистрНакопления.ТоварыКОформлениюИзлишковНедостач КАК ТоварыКОформлениюИзлишковНедостач
	|		ГДЕ
	|			ТоварыКОформлениюИзлишковНедостач.Регистратор ССЫЛКА Документ.КорректировкаПоОрдеруНаТовары) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Регистратор,
	|		ВложенныйЗапрос.ВидДвижения,
	|		ВложенныйЗапрос.Период,
	|		ВложенныйЗапрос.Склад,
	|		ВложенныйЗапрос.Номенклатура,
	|		ВложенныйЗапрос.Характеристика,
	|		ВложенныйЗапрос.Назначение,
	|		ВложенныйЗапрос.Серия
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(ВложенныйЗапрос.КОформлениюАктов) <> 0) КАК РегистраторыСОтклонениями";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ТоварыКОформлениюИзлишковНедостач";
	
	ОбработкаЗавершена = Ложь;
	
	Регистраторы = Новый Массив;
	
	Регистраторы.Добавить("Документ.КорректировкаПоОрдеруНаТовары");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы, ПолноеИмяРегистра, Параметры.Очередь);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
	
#КонецЕсли