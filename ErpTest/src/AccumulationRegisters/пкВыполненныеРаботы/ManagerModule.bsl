
Процедура СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, тИсточник)
	
	НаборЗаписей  = тИсточник.Движения.пкВыполненныеРаботы;
	НаборЗаписей.Записывать = Истина;
	СписокСтатусов = Новый Массив;
	СписокСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.Открыт);
	СписокСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.ВРаботе);
	СписокСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.Выполнен);
	СписокСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.Закрыт);
	СписокСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.Отложен);
	
	Если СписокСтатусов.Найти(тИсточник.Статус) <> Неопределено Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	пкЗаказНарядРаботы.Ссылка КАК Регистратор,
		|	пкЗаказНарядРаботы.Ссылка КАК ЗаказНаряд,
		|	пкЗаказНарядРаботы.ДатаОкончания КАК Период,
		|	пкЗаказНарядРаботы.Номенклатура КАК Работа,
		|	пкЗаказНарядРаботы.Сотрудник,
		|	пкЗаказНарядРаботы.Количество,
		|	пкЗаказНарядРаботы.СуммаСНДС КАК Сумма,
		|	пкЗаказНарядРаботы.ДатаНачала,
		|	пкЗаказНарядРаботы.ДатаОкончания,
		|	пкЗаказНарядРаботы.Командировка,
		|	пкЗаказНарядРаботы.НочныеРаботы,
		|	пкЗаказНарядРаботы.ВыходныеДни
		|ИЗ
		|	Документ.пкЗаказНаряд.Работы КАК пкЗаказНарядРаботы
		|ГДЕ
		|	пкЗаказНарядРаботы.Ссылка = &Ссылка
		|	И пкЗаказНарядРаботы.ДатаОкончания > ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)");
		Запрос.УстановитьПараметр("Ссылка",тИсточник.Ссылка);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда 
			НаборЗаписей.Загрузить(Результат.Выгрузить());
		КонецЕсли;
			
	КонецЕсли;
	
	Попытка
		НаборЗаписей.Записать(Истина);
	Исключение
		Отказ = Истина;
		тСообщение = Новый СообщениеПользователю;
		тСообщение.Текст = ОписаниеОшибки();
		тСообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
Процедура СформироватьЗаписи(Отказ, РежимПроведения, тИсточник) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	
	Если ТипЗнч(тИсточник) = Тип("Массив") Тогда
		МассивИсточников = тИсточник;
	Иначе
		МассивИсточников.Добавить(тИсточник);
	КонецЕсли;
	
	Для Каждого ЭлементИсточник из МассивИсточников Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ДокументОбъект.пкЗаказНаряд") Тогда
			СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, ЭлементИсточник);	
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры
