
Функция ПолучитьТаблицуПризнанияВНУЛизинговыхПлатежей(Источник)
	
	Запрос = Новый Запрос;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПриобретениеУслугПоЛизингу") Тогда
		
		Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
		Запрос.УстановитьПараметр("Период", Источник.Дата);
		Запрос.УстановитьПараметр("Организация", Источник.Организация);
		Запрос.УстановитьПараметр("Договор", Источник.Договор);
		Запрос.УстановитьПараметр("СуммыДокументовВВалютеРегл", Источник.ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаСуммыДокументовВВалютеРегл);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СуммыРегл.ИдентификаторСтроки,
		|	СуммыРегл.СуммаНДСРегл,
		|	СуммыРегл.СуммаБезНДСРегл
		|ПОМЕСТИТЬ втСуммыРегл
		|ИЗ
		|	&СуммыДокументовВВалютеРегл КАК СуммыРегл
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	&Договор КАК ДоговорЛизинга,
//Рарус Владимир Подрезов 20.03.2017
//Нужно без НДС
//		|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл + Суммы.СуммаНДСРегл, Начисления.СуммаСНДС) КАК СуммаНУ
		|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл, Начисления.СуммаСНДС - Начисления.СуммаНДС) КАК СуммаНУ
//Рарус Владимир Подрезов Конец
		|ИЗ
		|	Документ.ПриобретениеУслугПоЛизингу.Начисления КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСуммыРегл КАК Суммы
		|		ПО (Суммы.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)
		|			И (Суммы.СуммаБезНДСРегл <> 0)
		|ГДЕ
		|	Начисления.Ссылка = &Ссылка";
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.АмортизацияОС") Тогда
		
		Запрос.УстановитьПараметр("ТаблицаАмортизации", Источник.ДополнительныеСвойства.НачисленнаяАмортизация);
		Запрос.УстановитьПараметр("Организация", Источник.Организация);
		Запрос.УстановитьПараметр("Период", Источник.Дата);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации) КАК Организация,
		|	ВЫРАЗИТЬ(ТаблицаАмортизации.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации) КАК ОбъектУчета,
		|	ВЫРАЗИТЬ(ТаблицаАмортизации.Подразделение КАК Справочник.СтруктураПредприятия) КАК Подразделение,
		|	ВЫРАЗИТЬ(ТаблицаАмортизации.СуммаНУ КАК ЧИСЛО) КАК СуммаНУ,
		|	ВЫРАЗИТЬ(ТаблицаАмортизации.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов) КАК СтатьяРасходов,
		|	ТаблицаАмортизации.АналитикаРасходов КАК АналитикаРасходов
		|ПОМЕСТИТЬ втТаблицаАмортизации
		|ИЗ
		|	&ТаблицаАмортизации КАК ТаблицаАмортизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаАмортизации.ОбъектУчета,
		|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ДоговорЛизинга
		|ПОМЕСТИТЬ втДоговорыЛизинга
		|ИЗ
		|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(&Период, ) КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаАмортизации КАК втТаблицаАмортизации
		|		ПО ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = втТаблицаАмортизации.ОбъектУчета
		|			И ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.Организация = втТаблицаАмортизации.Организация
		|			И (НЕ ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ДоговорЛизинга = ЗНАЧЕНИЕ(Справочник.ДоговорыЛизинга.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Организация КАК Организация,
		|	втТаблицаАмортизации.Подразделение,
		|	втТаблицаАмортизации.СуммаНУ,
		|	втТаблицаАмортизации.СтатьяРасходов,
		|	втТаблицаАмортизации.АналитикаРасходов,
		|	ЕСТЬNULL(втДоговорыЛизинга.ДоговорЛизинга, ЗНАЧЕНИЕ(Справочник.ДоговорыЛизинга.ПустаяСсылка)) КАК ДоговорЛизинга
		|ИЗ
		|	втТаблицаАмортизации КАК втТаблицаАмортизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДоговорыЛизинга КАК втДоговорыЛизинга
		|		ПО втТаблицаАмортизации.ОбъектУчета = втДоговорыЛизинга.ОбъектУчета";
		
	Иначе
		
		Возврат Новый ТаблицаЗначений;
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура СформироватьЗаписиПоДокументу(Источник, Отказ, РежимПроведения)

	Таблица = ПолучитьТаблицуПризнанияВНУЛизинговыхПлатежей(Источник);
	
	Если Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = Источник.Движения.пкПризнаниеВ_НУ_ЛизинговыхПлатежей;
	НаборЗаписей.Записывать = Истина;
	
	НаборЗаписей.Загрузить(Таблица);

    Попытка
        НаборЗаписей.Записать(Истина);
    Исключение
        Отказ = Истина;
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = ОписаниеОшибки();
        Сообщение.Сообщить();
    КонецПопытки;
	
КонецПроцедуры

Процедура СформироватьЗаписи(Источник, Отказ, РежимПроведения) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	
	Если ТипЗнч(Источник) = Тип("Массив") Тогда
		МассивИсточников = Источник;
	Иначе
		МассивИсточников.Добавить(Источник);
	КонецЕсли;
	
	Для Каждого ЭлементИсточник из МассивИсточников Цикл
		Если ТипЗнч(ЭлементИсточник) = Тип("ДокументОбъект.ПриобретениеУслугПоЛизингу") Тогда
			СформироватьЗаписиПоДокументу(ЭлементИсточник, Отказ, РежимПроведения);
		ИначеЕсли ТипЗнч(ЭлементИсточник) = Тип("ДокументОбъект.АмортизацияОС") Тогда
			СформироватьЗаписиПоДокументу(ЭлементИсточник, Отказ, РежимПроведения);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры
