#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Регистрирует данные для многопоточного обработчика обновления
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщиками";
	
	ПараметрыВыборки 						= Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров 	= ПолноеИмяРегистра;
	ПараметрыВыборки.ПолныеИменаОбъектов 	= ТипыДокументовКОбработке();
	ПараметрыВыборки.СпособВыборки 			= ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|			ПО Ключи.Ссылка = РасчетыСПоставщиками.АналитикаУчетаПоПартнерам
	|ГДЕ
	//Обработчик 2.4.2
	|	((РасчетыСПоставщиками.Сумма <> 0
	|			ИЛИ РасчетыСПоставщиками.КОплате <> 0)
	|	И РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводОстатков
	|	И РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.ЗаказПоставщику) НЕ В (&ТипыДокументов)
	|	И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.ЗаказПоставщику) НЕ В (ТИП(Справочник.ДоговорыКонтрагентов),ТИП(Справочник.ДоговорыМеждуОрганизациями))
	|	И РасчетыСПоставщиками.РасчетныйДокумент = Неопределено
	|	И РасчетыСПоставщиками.ЗаказПоставщику <> НЕОПРЕДЕЛЕНО)
	//Обработчик 2.4.6
	//Порядок расчетов
	|	ИЛИ (((РасчетыСПоставщиками.ПорядокОперации = """" 
	|			ИЛИ РасчетыСПоставщиками.ПорядокЗачетаПоДатеПлатежа = """")
	|		И (РасчетыСПоставщиками.Сумма <> 0 ИЛИ РасчетыСПоставщиками.КОплате <> 0 ИЛИ РасчетыСПоставщиками.КПоступлению <> 0)
	//Связанный документ Корректировки приобретения
	|	ИЛИ (РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
	|		И РасчетыСПоставщиками.СвязанныйДокумент = Неопределено)
	//Хозяйственная операция
	|	ИЛИ (РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|		И НЕ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаРегистров)
	//Пустые варианты оплаты
	|	ИЛИ (РасчетыСПоставщиками.КОплате <> 0
	|		И ВидДвижения = Значение(ВидДвиженияНакопления.Расход)
	|		И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) В (&ТипыДокументов)
	|		И РасчетыСПоставщиками.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыОплатыПоставщику.ПустаяСсылка))
	//Дата регистратора необходима для порядка расчетов
	|	ИЛИ РасчетыСПоставщиками.ДатаРегистратора = ДАТАВРЕМЯ(1, 1, 1)
	//ХО Удержания вознаграждения
	|	ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ОтчетКомитенту
	|		И РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И РасчетыСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеВознагражденияКомитентом)
	//Валюта документа 
	|	ИЛИ РасчетыСПоставщиками.ВалютаДокумента = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|		И НЕ РасчетыСПоставщиками.Регистратор Ссылка Документ.СписаниеЗадолженности
	//Дозаполнение закупка по заказу
	|	ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|		И ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ПриобретениеТоваровУслуг).ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
	|		И ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.ПриобретениеТоваровУслуг).ПоступлениеПоЗаказам
	|		И РасчетыСПоставщиками.ЗакупкаПоЗаказу = Неопределено
	|		И (РасчетыСПоставщиками.Сумма <> 0 ИЛИ РасчетыСПоставщиками.КОплате <> 0 ИЛИ РасчетыСПоставщиками.КПоступлению <> 0)
	//КорОбъектРасчетов
	|	ИЛИ (ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.ЗаказПоставщику) В (ТИП(Документ.СписаниеБезналичныхДенежныхСредств),
	|															ТИП(Документ.РасходныйКассовыйОрдер))
	|			ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.ЗаказПоставщику) = ТИП(Документ.ПервичныйДокумент)
	|				И ВЫРАЗИТЬ(РасчетыСПоставщиками.ЗаказПоставщику КАК Документ.ПервичныйДокумент).ТипПервичногоДокумента = 
	|					ЗНАЧЕНИЕ(Перечисление.ТипыПервичныхДокументов.ОплатаПоставщику))
	|			И РасчетыСПоставщиками.КорОбъектРасчетов = Неопределено
	|			И РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И РасчетыСПоставщиками.Сумма <> 0
	|			И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) НЕ В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),
	|															ТИП(Документ.ПриходныйКассовыйОрдер),
	|															ТИП(Документ.ОперацияПоПлатежнойКарте),
	|															ТИП(Документ.РасходныйКассовыйОрдер),
	|															ТИП(Документ.СписаниеБезналичныхДенежныхСредств),
	|															ТИП(Документ.СписаниеЗадолженности),
	|															ТИП(Документ.ВзаимозачетЗадолженности),
	|															ТИП(Документ.ВводОстатков))
	//2.4.6 Исправление НД в ПТиУ по нескольким заказам с разными НД
	|	ИЛИ РасчетыСПоставщиками.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|		И РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|		И Ключи.НаправлениеДеятельности <> ВЫРАЗИТЬ(РасчетыСПоставщиками.ЗаказПоставщику КАК Документ.ЗаказПоставщику).НаправлениеДеятельности
	//Перезаполенине Хоз операции для отражения возвратов по расшифровке платежа.
	|	ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) В (ТИП(Документ.ВозвратТоваровПоставщику), ТИП(Документ.ВозвратТоваровМеждуОрганизациями))
	|		И РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И РасчетыСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
	|)
	| И НЕ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ЗаявкаНаРасходованиеДенежныхСредств
	| И НЕ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаРегистров)
	|
	|");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ТипыДокументов", ПолучитьТипыДокументов());

	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, 
																Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"),
																ПолноеИмяРегистра);
	
КонецПроцедуры

Функция ТипыДокументовКОбработке()
	
	Возврат
		"Документ.ЗаказПоставщику,
		
		//++ НЕ УТ
		|Документ.ВыкупПринятыхНаХранениеТоваров,
		|Документ.ЗаказПереработчику,
		|Документ.ВыбытиеДенежныхДокументов,
		|Документ.ОтчетПереработчика,
		|Документ.ОтчетОператораСистемыПлатон,
		|Документ.ПоступлениеДенежныхДокументов,
		//-- НЕ УТ
		
		|Документ.ГрафикИсполненияДоговора,
		|Документ.АвансовыйОтчет,
		|Документ.ВводОстатков,
		|Документ.ВзаимозачетЗадолженности,
		|Документ.ВозвратТоваровМеждуОрганизациями,
		|Документ.ВозвратТоваровПоставщику,
		|Документ.ВыкупВозвратнойТарыУПоставщика,
		|Документ.КорректировкаПриобретения,
		|Документ.ОтчетКомиссионера,
		|Документ.ОтчетКомитенту,
		|Документ.ОтчетКомитентуОСписании,
		|Документ.ОтчетПоКомиссииМеждуОрганизациями,
		|Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании,
		|Документ.ПередачаТоваровМеждуОрганизациями,
		|Документ.ПоступлениеБезналичныхДенежныхСредств,
		|Документ.ПриобретениеТоваровУслуг,
		|Документ.ПриобретениеУслугПрочихАктивов,
		|Документ.ПриходныйКассовыйОрдер,
		|Документ.РасходныйКассовыйОрдер,
		|Документ.СписаниеБезналичныхДенежныхСредств,
		|Документ.СписаниеЗадолженности,
		|Документ.ТаможеннаяДекларацияИмпорт";
	
КонецФункции

Функция ПолучитьТипыДокументов()
	
	ТекстТиповДокументов = ""; 
	
	//++ НЕ УТКА
	ТекстТиповДокументов = ТекстТиповДокументов + "ТИП(Документ.ЗаказПереработчику), ";
	//-- НЕ УТКА
	
	ТекстТиповДокументов = ТекстТиповДокументов + "ТИП(Документ.ЗаказПоставщику)";
	
	Возврат ТекстТиповДокументов;
	
КонецФункции

// Обработчик обновления УТ 11.4.3
// Заполняет реквизит РасчетныйДокумент.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщиками";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазыУТ.ДополнительныеПараметрыПерезаписиДвиженийИзОчереди();
	ДополнительныеПараметры.НужнаДополнительнаяОбработкаЗаписей = Истина;
	ДополнительныеПараметры.ОбновляемыеДанные = Параметры.ОбновляемыеДанные;
	
	ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(Неопределено, ПолноеИмяРегистра, Параметры.Очередь, ДополнительныеПараметры);
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

// Регистрирует данные для обновления данных движений документа Корректировка регистров
Процедура ЗарегистрироватьДанныеКорректировокРегистровКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщиками";
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ
	//Обработчик 2.4.6
	//Порядок расчетов
	|	(РасчетыСПоставщиками.ПорядокОперации = """" 
	|	ИЛИ РасчетыСПоставщиками.ПорядокЗачетаПоДатеПлатежа = """"
	//Дата регистратора необходима для порядка расчетов
	|	ИЛИ РасчетыСПоставщиками.ДатаРегистратора = ДАТАВРЕМЯ(1, 1, 1))
	| И РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, 
																Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"),
																ПолноеИмяРегистра);
	
КонецПроцедуры

// Обработчик обновления УТ 11.4.6
// Заполняет реквизиты ПорядокОперации, ДатаРегистратора, ДатаПлатежа, ПорядокЗачетаПоДатеПлатежа 
// по движениям, сделанным документом .Корректировка регистров
//
Процедура ОбработатьДанныеКорректировокРегистровДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщиками";
	
	//Корректировки регистров.
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(Параметры.Очередь,
		"Документ.КорректировкаРегистров", ПолноеИмяРегистра);
	
	ТипДокумента = Тип("ДокументСсылка.КорректировкаРегистров");
	ВидТипДокумента = ОперативныеВзаиморасчетыСервер.ВидТипДокумента(ТипДокумента);
	
	Пока Выборка.Следующий() Цикл
		
		Регистратор = Выборка.Регистратор;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.КорректировкаРегистров");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
			
			РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Выборка.Регистратор,"Дата,Номер");
			
			Набор = РегистрыНакопления.РасчетыСПоставщиками.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Регистратор);
			Набор.Прочитать();
			
			ОбъектИзменен = Ложь;
			
			Для Каждого Запись Из Набор Цикл
				Если Не ЗначениеЗаполнено(Запись.ДатаРегистратора) Тогда
					Запись.ДатаРегистратора = РеквизитыДокумента.Дата;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Запись.ДатаПлатежа) И Запись.КОплате <> 0 Тогда
					Запись.ДатаПлатежа = РеквизитыДокумента.Дата;
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(Запись.ПорядокЗачетаПоДатеПлатежа) ИЛИ Не ЗначениеЗаполнено(Запись.ПорядокОперации) Тогда
					Если Запись.Сумма = 0 И Запись.КОплате <> 0 Тогда
						Запись.ПорядокОперации = ОперативныеВзаиморасчетыСервер.Порядок(Запись.ДатаРегистратора, РеквизитыДокумента.Номер, ВидТипДокумента);
						Запись.ПорядокЗачетаПоДатеПлатежа   = ОперативныеВзаиморасчетыСервер.Порядок(Запись.Период, РеквизитыДокумента.Номер, ВидТипДокумента);
					ИначеЕсли Запись.ВидДвижения = ВидДвиженияНакопления.Приход И Запись.Сумма > 0 
							ИЛИ Запись.ВидДвижения = ВидДвиженияНакопления.Расход И Запись.Сумма < 0 Тогда 
						Запись.ПорядокОперации = ОперативныеВзаиморасчетыСервер.Порядок(Запись.Период, РеквизитыДокумента.Номер, ВидТипДокумента);
						Запись.ПорядокЗачетаПоДатеПлатежа   = Запись.ПорядокОперации;
					Иначе
						Запись.ПорядокОперации = ОперативныеВзаиморасчетыСервер.Порядок(Запись.ДатаРегистратора, РеквизитыДокумента.Номер,,ТипДокумента, Запись.ВидДвижения);
						Запись.ПорядокЗачетаПоДатеПлатежа   = ОперативныеВзаиморасчетыСервер.Порядок(
							?(ЗначениеЗаполнено(Запись.ДатаПлатежа),Запись.ДатаПлатежа,Запись.ДатаРегистратора), РеквизитыДокумента.Номер,, ТипДокумента, Запись.ВидДвижения);
					КонецЕсли;
					ОбъектИзменен = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если НЕ ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Регистратор, ДополнительныеПараметры);
			Иначе
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать движения документа ""%1"" по причине:
					|%2'"),
				Регистратор,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				Регистратор.Метаданные(),
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли