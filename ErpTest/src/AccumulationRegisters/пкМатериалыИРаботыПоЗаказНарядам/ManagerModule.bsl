
Процедура СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, тИсточник)
	
	НаборЗаписей  = тИсточник.Движения.пкМатериалыИРаботыПоЗаказНарядам;
	НаборЗаписей.Записывать = Истина;
	
	Если ЗначениеЗаполнено(тИсточник.ДатаОкончанияФакт) Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	пкЗаказНарядРаботы.Ссылка КАК Регистратор,
		                      |	пкЗаказНарядРаботы.Ссылка КАК ЗаказНаряд,
		                      |	пкЗаказНарядРаботы.Ссылка.ДатаОкончанияФакт КАК Период,
		                      |	пкЗаказНарядРаботы.Номенклатура КАК РаботаМатериал,
		                      |	пкЗаказНарядРаботы.Количество,
		                      |	пкЗаказНарядРаботы.СуммаСНДС КАК Сумма,
		                      |	NULL КАК Характеристика,
		                      |	NULL КАК Серия,
		                      |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
		                      |ИЗ
		                      |	Документ.пкЗаказНаряд.Работы КАК пкЗаказНарядРаботы
		                      |ГДЕ
		                      |	пкЗаказНарядРаботы.Ссылка = &Ссылка
		                      |	И пкЗаказНарядРаботы.ПлатныйРемонт
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	пкЗаказНарядМатериалы.Ссылка,
		                      |	пкЗаказНарядМатериалы.Ссылка,
		                      |	пкЗаказНарядМатериалы.Ссылка.ДатаОкончанияФакт,
		                      |	пкЗаказНарядМатериалы.Номенклатура,
		                      |	пкЗаказНарядМатериалы.Количество,
		                      |	пкЗаказНарядМатериалы.СуммаСНДС,
		                      |	пкЗаказНарядМатериалы.Характеристика,
		                      |	пкЗаказНарядМатериалы.Серия,
		                      |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		                      |ИЗ
		                      |	Документ.пкЗаказНаряд.Материалы КАК пкЗаказНарядМатериалы
		                      |ГДЕ
		                      |	пкЗаказНарядМатериалы.Ссылка = &Ссылка
		                      |	И пкЗаказНарядМатериалы.ПлатныйРемонт");
		Запрос.УстановитьПараметр("Ссылка",тИсточник.Ссылка);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда 
			НаборЗаписей.Загрузить(Результат.Выгрузить());
		КонецЕсли;
			
	КонецЕсли;
	
	Попытка
		НаборЗаписей.Записать(Истина);
	Исключение
		Отказ = Истина;
		тСообщение = Новый СообщениеПользователю;
		тСообщение.Текст = ОписаниеОшибки();
		тСообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
Процедура СформироватьЗаписи(Отказ, РежимПроведения, тИсточник) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	
	Если ТипЗнч(тИсточник) = Тип("Массив") Тогда
		МассивИсточников = тИсточник;
	Иначе
		МассивИсточников.Добавить(тИсточник);
	КонецЕсли;
	
	Для Каждого ЭлементИсточник из МассивИсточников Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ДокументОбъект.пкЗаказНаряд") Тогда
			СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, ЭлементИсточник);	
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры
