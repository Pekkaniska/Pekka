#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ЦеныНоменклатурыПоставщиков";
	ИмяРегистра = "ЦеныНоменклатурыПоставщиков";
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПриобретениеТоваровУслуг.Ссылка КАК Ссылка 
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг
	|ГДЕ
	|	ПриобретениеТоваровУслуг.ЦенаВключаетНДС
	|	И ПриобретениеТоваровУслуг.РегистрироватьЦеныПоставщика
	|	И ПриобретениеТоваровУслуг.Проведен";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	//++ НЕ УТ
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПриемкаТоваровНаХранение.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриемкаТоваровНаХранение КАК ПриемкаТоваровНаХранение
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыПоставщиков КАК ЦеныНоменклатурыПоставщиков
	|		ПО ПриемкаТоваровНаХранение.Ссылка = ЦеныНоменклатурыПоставщиков.Регистратор
	|ГДЕ
	|	ЦеныНоменклатурыПоставщиков.Регистратор ЕСТЬ NULL
	|	И ПриемкаТоваровНаХранение.Проведен";
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	//-- НЕ УТ
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());;
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ПриобретениеТоваровУслуг");
	//++ НЕ УТ
	Регистраторы.Добавить("Документ.ПриемкаТоваровНаХранение");
	//-- НЕ УТ
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
		"РегистрСведений.ЦеныНоменклатурыПоставщиков",
		Параметры.Очередь);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли