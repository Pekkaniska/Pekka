
Процедура СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, тИсточник)
	
	НаборЗаписей  = тИсточник.Движения.пкПроведениеРемонтов;
	НаборЗаписей.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	пкЗаказНаряд.Ссылка,
		|	пкЗаказНаряд.Дата,
		|	пкЗаказНаряд.ДатаНачалаФакт,
		|	пкЗаказНаряд.ДатаОкончанияФакт,
		|	пкЗаказНаряд.Моточасы,
		|	пкЗаказНаряд.Техника,
		|	пкЗаказНаряд.ВидРемонта
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.пкЗаказНаряд КАК пкЗаказНаряд
		|ГДЕ
		|	пкЗаказНаряд.Ссылка = &Ссылка
		|	И пкЗаказНаряд.Статус В(&МассивСтатусов)
		|	И НЕ пкЗаказНаряд.ТехникаКлиента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТ.Ссылка
		|ПОМЕСТИТЬ ВТ2
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.пкЗаказНаряд.Работы КАК пкЗаказНарядРаботы
		|		ПО ВТ.Ссылка = пкЗаказНарядРаботы.Ссылка
		|ГДЕ
		|	пкЗаказНарядРаботы.ЗаданиеНаРемонт.ВидРемонта = &ВидРемонтаТО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТ.Ссылка
		|ПОМЕСТИТЬ ВТ3
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.пкЗаказНаряд.Работы КАК пкЗаказНарядРаботы
		|		ПО ВТ.Ссылка = пкЗаказНарядРаботы.Ссылка
		|ГДЕ
		|	пкЗаказНарядРаботы.ЗаданиеНаРемонт.ВидРемонта = &ВидРемонтаЧТО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТ.Ссылка
		|ПОМЕСТИТЬ ВТ4
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.пкЗаказНаряд.Работы КАК пкЗаказНарядРаботы
		|		ПО ВТ.Ссылка = пкЗаказНарядРаботы.Ссылка
		|ГДЕ
		|	пкЗаказНарядРаботы.ЗаданиеНаРемонт.ВидРемонта = &ВидРемонтаПТО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Ссылка КАК Регистратор,
		|	ВТ.Дата КАК Период,
		|	ВТ.ДатаНачалаФакт,
		|	ВТ.ДатаОкончанияФакт,
		|	ВТ.Моточасы,
		|	ВЫБОР
		|		КОГДА ВТ2.Ссылка ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЭтоТО,
		|	ВТ.Техника,
		|	ВЫБОР
		|		КОГДА ВТ4.Ссылка ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЭтоПТО,
		|	ВЫБОР
		|		КОГДА ВТ3.Ссылка ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЭтоЧТО,
		|	ВТ.ВидРемонта
		|ПОМЕСТИТЬ ВТ5
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ2 КАК ВТ2
		|		ПО ВТ.Ссылка = ВТ2.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ3 КАК ВТ3
		|		ПО ВТ.Ссылка = ВТ3.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ4 КАК ВТ4
		|		ПО ВТ.Ссылка = ВТ4.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ5.Регистратор,
		|	ВТ5.Период,
		|	ВТ5.ДатаНачалаФакт,
		|	ВТ5.ДатаОкончанияФакт,
		|	ВТ5.Моточасы,
		|	ВТ5.ЭтоТО,
		|	ВТ5.Техника,
		|	ВЫБОР
		|		КОГДА ВТ5.ЭтоПТО
		|			ТОГДА &ВидРемонтаПТО
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВТ5.ЭтоЧТО
		|					ТОГДА &ВидРемонтаЧТО
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ВТ5.ЭтоТО
		|							ТОГДА &ВидРемонтаТО
		|						ИНАЧЕ ВТ5.ВидРемонта
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК ВидРемонта
		|ИЗ
		|	ВТ5 КАК ВТ5";
		
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.Выполнен);
	МассивСтатусов.Добавить(Перечисления.пкСтатусыЗаказНарядов.Закрыт);
	
	//Запрос.УстановитьПараметр("ВидРемонта", Справочники.ВидыРемонтов.НайтиПоНаименованию("ТО",Истина));
	Запрос.УстановитьПараметр("ВидРемонтаТО", пкОбщийМодульСервер.ПолучитьВидРемонтаТО());
	Запрос.УстановитьПараметр("ВидРемонтаПТО", пкОбщийМодульСервер.ПолучитьВидРемонтаПТО());
	Запрос.УстановитьПараметр("ВидРемонтаЧТО", пкОбщийМодульСервер.ПолучитьВидРемонтаЧТО());
	Запрос.УстановитьПараметр("МассивСтатусов", МассивСтатусов);
	Запрос.УстановитьПараметр("Ссылка", тИсточник.Ссылка);
						  
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		НаборЗаписей.Загрузить(Результат.Выгрузить());
	КонецЕсли;
	
	Попытка
		НаборЗаписей.Записать(Истина);
	Исключение
		Отказ = Истина;
		тСообщение = Новый СообщениеПользователю;
		тСообщение.Текст = ОписаниеОшибки();
		тСообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
Процедура СформироватьЗаписи(Отказ, РежимПроведения, тИсточник) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	
	Если ТипЗнч(тИсточник) = Тип("Массив") Тогда
		МассивИсточников = тИсточник;
	Иначе
		МассивИсточников.Добавить(тИсточник);
	КонецЕсли;
	
	Для Каждого ЭлементИсточник из МассивИсточников Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ДокументОбъект.пкЗаказНаряд") Тогда
			СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, ЭлементИсточник);
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры
