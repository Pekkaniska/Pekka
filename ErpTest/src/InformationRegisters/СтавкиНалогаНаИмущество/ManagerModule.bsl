#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает парамтеры формы для выбора кода
//
// Параметры:
// 		НазваниеМакета - Строка - Имя макета
// 		ТекущийПериод - Дата - Дата, помещаемая в параметры формы как параметр "ТекущийПериод".
//
// Возвращаемое значение:
// 		Структура - Структура параметров общей формы "ФормаВыбораКода".
//
Функция ПолучитьПараметрыФормыВыбораДляКода(НазваниеМакета, ТекущийПериод) Экспорт
	
	КодыЛьгот = Новый ТаблицаЗначений;
	
	КодыЛьгот.Колонки.Добавить("Код");
	КодыЛьгот.Колонки.Добавить("Наименование");
	КодыЛьгот.Колонки.Добавить("КодЕдиницыИзмерения");
	
	Макет = ПолучитьМакет(НазваниеМакета);
	
	НазваниеОбласти = "";
	СписокОбластей = Новый СписокЗначений;
	ОпределитьПараметрыСпискаКодов(Макет, ТекущийПериод, НазваниеОбласти, СписокОбластей);
	
	ТекущаяОбласть = Макет.Области.Найти("Область" + НазваниеОбласти);
	
	Если НЕ (ТекущаяОбласть = Неопределено) Тогда	
		
		Для НомерСтр = ТекущаяОбласть.Верх По ТекущаяОбласть.Низ Цикл
			
			// Перебираем строки макета.
			КодПоказателя = СокрП(Макет.Область(НомерСтр, 1).Текст);
			Название = СокрП(Макет.Область(НомерСтр, 2).Текст);
			КодЕдиницыИзмерения = СокрП(Макет.Область(НомерСтр, 3).Текст);
			
			Если КодПоказателя = "###" Тогда
				Прервать;
			ИначеЕсли ПустаяСтрока(КодПоказателя) Тогда
				Продолжить;
			Иначе
				НоваяСтрока = КодыЛьгот.Добавить();
				НоваяСтрока.Код                 = КодПоказателя;
				НоваяСтрока.Наименование        = Название;
				НоваяСтрока.КодЕдиницыИзмерения = КодЕдиницыИзмерения;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("СписокКодов" , КодыЛьгот);
	Параметры.Вставить("СписокПериодовДействия", СписокОбластей);
	Параметры.Вставить("ТекущийПериод" , НазваниеОбласти);
	
	Возврат Параметры;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьПараметрыСпискаКодов(Макет, ТекущийПериод, НазваниеОбласти, СписокОбластей)
	
	Области = Макет.Области;
	Если Области.Количество() = 0 Тогда
		НазваниеОбласти = "";
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекОбласть Из Области Цикл
		СписокОбластей.Добавить(Прав(ТекОбласть.Имя,4));
	КонецЦикла;
	
	ТекущаяОбласть = СписокОбластей[0].Значение;
	Для Каждого ТекОбласть Из СписокОбластей Цикл
		Если Год(ТекущийПериод) < Число(ТекОбласть.Значение) Тогда
			Прервать;
		КонецЕсли;
		
		ТекущаяОбласть = ТекОбласть.Значение;
	КонецЦикла;
	
	НазваниеОбласти = ТекущаяОбласть;
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию() Экспорт
	
	ТекстЗапроса = 
	// Заполнение сниженной ставки.
	"ВЫБРАТЬ
	|	СтавкиНалогаНаИмущество.Период КАК Период,
	|	СтавкиНалогаНаИмущество.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмущество КАК СтавкиНалогаНаИмущество
	|ГДЕ
	|	СтавкиНалогаНаИмущество.УдалитьСниженнаяНалоговаяСтавка <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//  Добавление ставки с 2018 г.
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмущество.Организация КАК Организация,
	|	СтавкиНалогаНаИмущество.НалоговаяСтавка КАК НалоговаяСтавка,
	|	СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	СтавкиНалогаНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	СтавкиНалогаНаИмущество.СнижениеНалоговойСтавки КАК СнижениеНалоговойСтавки,
	|	СтавкиНалогаНаИмущество.УменьшениеСуммыНалогаВПроцентах КАК УменьшениеСуммыНалогаВПроцентах,
	|	СтавкиНалогаНаИмущество.ПроцентУменьшения КАК ПроцентУменьшения,
	|	СтавкиНалогаНаИмущество.ПриНаличии1ЛьготыЗаполнять1ЛистРаздела2 КАК ПриНаличии1ЛьготыЗаполнять1ЛистРаздела2,
	|	СтавкиНалогаНаИмущество.НалоговаяСтавкаДвижимоеИмущество КАК НалоговаяСтавкаДвижимоеИмущество,
	|	СтавкиНалогаНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
	|	СтавкиНалогаНаИмущество.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
	|	СтавкиНалогаНаИмущество.УдалитьСниженнаяНалоговаяСтавка КАК УдалитьСниженнаяНалоговаяСтавка
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(
	|			ДАТАВРЕМЯ(2017, 12, 31),
	|			НЕ Организация В
	|					(ВЫБРАТЬ
	|						СтавкиНалогаНаИмущество2018.Организация
	|					ИЗ
	|						РегистрСведений.СтавкиНалогаНаИмущество КАК СтавкиНалогаНаИмущество2018
	|					ГДЕ
	|						СтавкиНалогаНаИмущество2018.Период >= ДАТАВРЕМЯ(2018, 1, 1))) КАК СтавкиНалогаНаИмущество";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Результат = Запрос.ВыполнитьПакет();
	
	// Заполнение сниженной ставки.
	Выборка = Результат[0].Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмущество.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Прочитать();
		
		Для каждого ЗаписьРегистра Из НаборЗаписей Цикл
			Если ЗаписьРегистра.УдалитьСниженнаяНалоговаяСтавка <> 0 Тогда
				ЗаписьРегистра.НалоговаяСтавка = ЗаписьРегистра.УдалитьСниженнаяНалоговаяСтавка;
				ЗаписьРегистра.СнижениеНалоговойСтавки = Истина;
				ЗаписьРегистра.УдалитьСниженнаяНалоговаяСтавка = 0;
			КонецЕсли; 
		КонецЦикла; 
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
	КонецЦикла;
	
	// Добавление ставки с 2018 г.
	Выборка = Результат[1].Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.СтавкиНалогаНаИмущество.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить('201801010000');
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		
		ЗаписьРегистра = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьРегистра, Выборка);
		ЗаписьРегистра.Период = '201801010000';
		ЗаписьРегистра.НалоговаяСтавкаДвижимоеИмущество = 1.1;
		ЗаписьРегистра.ОсвобождениеОтНалогообложенияДвижимогоИмущества = Ложь;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли