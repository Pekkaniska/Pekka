#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
	
	ЗаписатьИзмененияВРегистры2_2();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.Состояние КАК Состояние,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ТаблицаПередЗаписью.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаПередЗаписью.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаПередЗаписью.СтатьяРасходовАмортизационнойПремии КАК СтатьяРасходовАмортизационнойПремии,
	|	ТаблицаПередЗаписью.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
	|	ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаПередЗаписью.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияРасходовАмортизационнойПремии КАК СпособОтраженияРасходовАмортизационнойПремии
	|ПОМЕСТИТЬ ПорядокУчетаОСБУПередЗаписью
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПорядокУчетаОСБУИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.СтатьяРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.АналитикаРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.СпособОтраженияРасходовАмортизационнойПремии
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходов <> ТаблицаПередЗаписью.СтатьяРасходов
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходов <> ТаблицаПередЗаписью.АналитикаРасходов
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.СтатьяРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.АналитикаРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходов <> ТаблицаПередЗаписью.СпособОтраженияРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.СпособОтраженияРасходовАмортизационнойПремии
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПередЗаписью.Организация       КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|ПОМЕСТИТЬ ПорядокУчетаОСБУ_НачислятьАмортизацию
	|ИЗ
	|	ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ ИЛИ ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПриЗаписи.Организация,
	|	ТаблицаПриЗаписи.ОсновноеСредство
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|ГДЕ
	|	ТаблицаПриЗаписи.Регистратор = &Регистратор
	|	И (ТаблицаПриЗаписи.НачислятьАмортизациюБУ
	|		ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаОСБУПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПорядокУчетаОСБУИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

Процедура ЗаписатьИзмененияВРегистры2_2()

	Если НЕ ЗначениеЗаполнено(Отбор.Регистратор.Значение)
		ИЛИ ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
		Возврат;
	КонецЕсли;
	
	НачислениеБУ = Неопределено;
	НачислениеНУ = Неопределено;
	СостоянияОСОрганизаций = Неопределено;
	СпособыОтраженияРасходов = Неопределено;
	
	ТипРегистратора = ТипЗнч(Отбор.Регистратор.Значение);
	
	Если ТипРегистратора <> Тип("ДокументСсылка.ВводОстатковВнеоборотныхАктивов2_4") 
		И ТипРегистратора <> Тип("ДокументСсылка.ПоступлениеАрендованныхОС") 
		И ТипРегистратора <> Тип("ДокументСсылка.ВыбытиеАрендованныхОС")
		И ТипРегистратора <> Тип("ДокументСсылка.ПоступлениеПредметовЛизинга") Тогда
		НачислениеБУ = РегистрыСведений.НачислениеАмортизацииОСБухгалтерскийУчет.СоздатьНаборЗаписей();
		НачислениеБУ.Отбор.Регистратор.Установить(Отбор.Регистратор.Значение);
		
		НачислениеНУ = РегистрыСведений.НачислениеАмортизацииОСНалоговыйУчет.СоздатьНаборЗаписей();
		НачислениеНУ.Отбор.Регистратор.Установить(Отбор.Регистратор.Значение);
	КонецЕсли; 
	
	Если ТипРегистратора = Тип("ДокументСсылка.ПринятиеКУчетуОС2_4") 
		ИЛИ ТипРегистратора = Тип("ДокументСсылка.ВозвратОСОтАрендатора2_4")
		ИЛИ ТипРегистратора = Тип("ДокументСсылка.ПередачаОСАрендатору2_4")
		ИЛИ ТипРегистратора = Тип("ДокументСсылка.ПодготовкаКПередачеОС2_4")
		ИЛИ ТипРегистратора = Тип("ДокументСсылка.СписаниеОС2_4")
		ИЛИ ТипРегистратора = Тип("ДокументСсылка.РазукомплектацияОС") Тогда
		СостоянияОСОрганизаций = РегистрыСведений.СостоянияОСОрганизаций.СоздатьНаборЗаписей();
		СостоянияОСОрганизаций.Отбор.Регистратор.Установить(Отбор.Регистратор.Значение);
	КонецЕсли; 
	
	Если ТипРегистратора <> Тип("ДокументСсылка.ВводОстатковВнеоборотныхАктивов2_4")
		И  ТипРегистратора <> Тип("ДокументСсылка.АмортизацияОС2_4")
		И ТипРегистратора <> Тип("ДокументСсылка.ПоступлениеАрендованныхОС")
		И ТипРегистратора <> Тип("ДокументСсылка.ВыбытиеАрендованныхОС")
		И ТипРегистратора <> Тип("ДокументСсылка.ПоступлениеПредметовЛизинга") Тогда
		СпособыОтраженияРасходов = РегистрыСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СоздатьНаборЗаписей();
		СпособыОтраженияРасходов.Отбор.Регистратор.Установить(Отбор.Регистратор.Значение);
	КонецЕсли; 
	
	ДатаНачалаУчета = Константы.ДатаНачалаУчетаВнеоборотныхАктивов2_4.Получить();
	
	Для каждого ДанныеСтроки Из ЭтотОбъект Цикл
		
		Если ДанныеСтроки.Период < ДатаНачалаУчета Тогда
			Продолжить;
		КонецЕсли;
		
		Если НачислениеБУ <> Неопределено Тогда
			НоваяЗапись = НачислениеБУ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеСтроки);
			НоваяЗапись.НачислятьАмортизацию = ДанныеСтроки.НачислятьАмортизациюБУ;
		КонецЕсли;
		
		Если НачислениеНУ <> Неопределено Тогда
			НоваяЗапись = НачислениеНУ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеСтроки);
			НоваяЗапись.НачислятьАмортизацию = ДанныеСтроки.НачислятьАмортизациюНУ;
		КонецЕсли;
		
		Если СостоянияОСОрганизаций <> Неопределено Тогда
			НоваяЗапись = СостоянияОСОрганизаций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеСтроки);
			НоваяЗапись.ДатаСостояния = ДанныеСтроки.Период;
		КонецЕсли;
			
		Если СпособыОтраженияРасходов <> Неопределено Тогда
			НоваяЗапись = СпособыОтраженияРасходов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеСтроки);
			НоваяЗапись.СпособОтраженияРасходовЗаданДокументом = ЗначениеЗаполнено(ДанныеСтроки.СпособОтраженияРасходов);
		КонецЕсли;
		
	КонецЦикла;
	
	Если НачислениеБУ <> Неопределено Тогда
		НачислениеБУ.Записать();
	КонецЕсли; 
	Если НачислениеНУ <> Неопределено Тогда
		НачислениеНУ.Записать();
	КонецЕсли; 
	Если СостоянияОСОрганизаций <> Неопределено Тогда
		СостоянияОСОрганизаций.Записать();
	КонецЕсли; 
	Если СпособыОтраженияРасходов <> Неопределено Тогда
		СпособыОтраженияРасходов.Записать();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

