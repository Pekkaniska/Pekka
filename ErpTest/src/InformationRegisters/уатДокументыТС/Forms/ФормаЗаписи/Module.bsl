////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ОбновитьНомерТС()
	НомерТС = уатОбщегоНазначения.уатПредставлениеТС(Запись.ТС, ТекОрганизация);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗагрузитьКартинку(Команда)
	ДиалогОткрытияФайлаФильтр =
	"Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
	+ "Все файлы (*.*)|*.*|"
	+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
	+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
	+ "Формат TIFF (*.tif)|*.tif|"
	+ "Формат GIF (*.gif)|*.gif|"
	+ "Формат PNG (*.png)|*.png|"
	+ "Формат icon (*.ico)|*.ico|"
	+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|"; // картинки
	
	ДопПараметры = Новый Структура("ДиалогОткрытияФайлаФильтр", ДиалогОткрытияФайлаФильтр);
	НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения(
		"ВыбратьФайлПослеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект, ДопПараметры));
		
КонецПроцедуры

// Продолжение процедуры ВыбратьФайл.
&НаКлиенте
Процедура ВыбратьФайлПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДопПараметры) Экспорт
	
	Если Не Подключено Тогда
		ОбработкаПродолжения = Новый ОписаниеОповещения("ВыбратьФайлПриНачалеПомещенияФайла", ЭтотОбъект, ДопПараметры);
		НачатьПомещениеФайла(ОбработкаПродолжения, , , , УникальныйИдентификатор);
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Заголовок = НСтр("ru = 'Выберите файл внешней обработки'");
	Диалог.Фильтр = ДопПараметры.ДиалогОткрытияФайлаФильтр;
	
	НачатьПомещениеФайлов(Новый ОписаниеОповещения(
		"ВыбратьФайлПослеПомещенияФайлов", ЭтотОбъект, ДопПараметры), , Диалог, Ложь, УникальныйИдентификатор);
	
КонецПроцедуры

// Продолжение процедуры ВыбратьФайл.
&НаКлиенте
Процедура ВыбратьФайлПослеПомещенияФайлов(ПомещенныеФайлы, ДопПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры.Вставить("Адрес", ПомещенныеФайлы[0].Хранение);
	ДопПараметры.Вставить("ИмяФайла", ПомещенныеФайлы[0].Имя);
	
	ВыбратьФайлПослеПомещенияФайла(ДопПараметры);
	
КонецПроцедуры

// Продолжение процедуры ВыбратьФайл.
&НаКлиенте
Процедура ВыбратьФайлПриНачалеПомещенияФайла(Результат, Адрес, ВыбранноеИмяФайла, ДопПараметры) Экспорт
	
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ДопПараметры.Вставить("Адрес", Адрес);
	ДопПараметры.Вставить("ИмяФайла", ВыбранноеИмяФайла);
	
	ВыбратьФайлПослеПомещенияФайла(ДопПараметры);
	
КонецПроцедуры

// Продолжение процедуры ВыбратьФайл.
&НаКлиенте
Процедура ВыбратьФайлПослеПомещенияФайла(ДопПараметры)
	ДопПараметры.Вставить("ОшибкаНаСервере", Новый Структура);
	ДопПараметры.Вставить("ДанныеПодписи",   Неопределено);
	
	ТекстОшибки = "";
	ДобавитьФайлНаСервере(ДопПараметры.Адрес, ДопПараметры.ИмяФайла, ТекстОшибки);
	Если ТекстОшибки <> "" Тогда
		Сообщить(ТекстОшибки, СтатусСообщения.Внимание);
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайлНаСервере(Адрес, ИмяФайла, ТекстОшибки = "")
	Картинка = Адрес;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКартинку(Команда)
	
	#Если ВебКлиент Тогда
		Попытка
			ПолучитьФайл(Картинка, "", Истина);
		Исключение
		КонецПопытки;
	#Иначе
		ДиалогОткрытияФайлаФильтр =
		"Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
		+ "Все файлы (*.*)|*.*|"
		+ "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
		+ "Формат JPEG (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
		+ "Формат TIFF (*.tif)|*.tif|"
		+ "Формат GIF (*.gif)|*.gif|"
		+ "Формат PNG (*.png)|*.png|"
		+ "Формат icon (*.ico)|*.ico|"
		+ "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|"; // картинки
		
		ПараметрыДиалога = Новый Структура("Режим, Фильтр, ИндексФильтра, Заголовок");
		ПараметрыДиалога.Режим  = РежимДиалогаВыбораФайла.Сохранение;
		ПараметрыДиалога.Фильтр = ДиалогОткрытияФайлаФильтр;
		ПараметрыДиалога.ИндексФильтра = 0;
		ПараметрыДиалога.Заголовок = "Выберите место сохранения";
		
		ДиалогСохраненияКартинки = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогСохраненияКартинки.Фильтр = ДиалогОткрытияФайлаФильтр;
		ДиалогСохраненияКартинки.Заголовок = "Выберите место сохранения";
		
		ДиалогСохраненияКартинки.Показать(Новый ОписаниеОповещения("ПослеВыбораФайлаКартинки", ЭтотОбъект));
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаКартинки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекКартинка = Новый Картинка(ПолучитьИзВременногоХранилища(Картинка));
	ТекКартинка.Записать(ВыбранныеФайлы[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКартинку(Команда)
	Картинка = "";
	Модифицированность = Истина;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

&НаКлиенте
Процедура НомерТСПриИзменении(Элемент)
	уатИнтерфейсВводаТС.НомерТСПриИзменении(НомерТС, Запись.ТС, ТекОрганизация);
	ОбновитьНомерТС();
КонецПроцедуры

&НаКлиенте
Процедура НомерТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтруктураОтбора = Новый Структура;
	Если ЗначениеЗаполнено(ТекОрганизация) Тогда
		СтруктураОтбора.Вставить("уатОрганизация", ТекОрганизация);
	КонецЕсли;
	уатИнтерфейсВводаТС.НомерТСНачалоВыбора(Элемент, Запись.ТС, СтруктураОтбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОчистка(Элемент, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСОчистка(Запись.ТС);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОткрытие(Элемент, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСОткрытие(Запись.ТС, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСОбработкаВыбора(НомерТС, Запись.ТС, ВыбранноеЗначение, СтандартнаяОбработка, ТекОрганизация);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НомерТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСАвтоПодборТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, ТекОрганизация);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, ТекОрганизация);
КонецПроцедуры

&НаКлиенте
Процедура КартинкаНажатие(Элемент, СтандартнаяОбработка)
	#Если ВебКлиент Тогда 
		СтандартнаяОбработка = Ложь;
		Парам = Новый Структура;
		Парам.Вставить("Картинка", Картинка);
		форма = ОткрытьФорму("РегистрСведений.уатДокументыТС.Форма.ФормаИзображенияВебКлиент",Парам,ЭтаФорма);
	#Иначе
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Картинка) Тогда
			Изображение = ПолучитьИзВременногоХранилища(Картинка);
			Если ТипЗнч(Изображение) = Тип("Картинка") Тогда 
				ПоказатьЗначение(,Изображение); 
			ИначеЕсли (ТипЗнч(Изображение) = Тип("ДвоичныеДанные")) или (ТипЗнч(Изображение) = Тип("Строка")) Тогда 
				Изображение = новый Картинка(Изображение ,Истина);
				ПоказатьЗначение(, Изображение);
			КонецЕсли;
		КонецЕсли;
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Запись.Комментарий");
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТекОрганизация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ПользователиКлиентСервер.ТекущийПользователь(), "ОсновнаяОрганизация");
	
	Если НЕ ЗначениеЗаполнено(Запись.ТС) И Параметры.Свойство("ТС") Тогда
		Запись.ТС = Параметры.ТС;
	КонецЕсли;
	
	ОбновитьНомерТС();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОбновитьНомерТС();
	Картинка = ПоместитьВоВременноеХранилище(ТекущийОбъект.Изображение.Получить(), УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЭтоАдресВременногоХранилища(Картинка) Тогда
		ТекущийОбъект.Изображение = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Картинка));
	Иначе
		ТекущийОбъект.Изображение = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьНомерТС();
КонецПроцедуры
