&НаСервере
Процедура ПроверитьДобавитьЗаписиДоступности()
	
	//Определяем список способов без записи для объекта
	ЗапросСпособы = Новый Запрос;
	ЗапросСпособы.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыСпособы.Объект КАК Объект,
	|	ОбъектыСпособы.Способ КАК Способ
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Объекты.Объект КАК Объект,
	|		Способы.Ссылка КАК Способ
	|	ИЗ
	|		(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			сНоменклатура.Ссылка КАК Объект
	|		ИЗ
	|			Справочник.Номенклатура КАК сНоменклатура
	|		ГДЕ
	|			сНоменклатура.пкЭтоТехника
	|			И НЕ сНоменклатура.ЭтоГруппа
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ТранспортныеСредства.Ссылка
	|		ИЗ
	|			Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			СМ_ОбъектыСтроительства.Ссылка
	|		ИЗ
	|			Справочник.СМ_ОбъектыСтроительства КАК СМ_ОбъектыСтроительства
	|		ГДЕ
	|			НЕ СМ_ОбъектыСтроительства.ЭтоГруппа) КАК Объекты
	|			ПОЛНОЕ СОЕДИНЕНИЕ Справочник.пкСпособыПогрузкиРазгрузкиТехники КАК Способы
	|			ПО (ИСТИНА)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Объекты.Объект,
	|		Способы.Ссылка) КАК ОбъектыСпособы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.пкДоступностьСпособовПогрузкиИРазгрузкиТехники КАК пкДоступностьСпособовПогрузкиИРазгрузкиТехники
	|		ПО ОбъектыСпособы.Способ = пкДоступностьСпособовПогрузкиИРазгрузкиТехники.СпособПогрузкиРазгрузки
	|			И ОбъектыСпособы.Объект = пкДоступностьСпособовПогрузкиИРазгрузкиТехники.Объект
	|ГДЕ
	|	пкДоступностьСпособовПогрузкиИРазгрузкиТехники.Доступен ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыСпособы.Объект,
	|	ОбъектыСпособы.Способ";
	ОбъектыСпособыБезЗаписей = ЗапросСпособы.Выполнить().Выгрузить();
	
	НаборЗаписей = РегистрыСведений.пкДоступностьСпособовПогрузкиИРазгрузкиТехники.СоздатьНаборЗаписей();
	
	Для Каждого текОбъектСпособ Из ОбъектыСпособыБезЗаписей Цикл
		//формируем записи для каждого объекта по текущему способу
		НаборЗаписей.Отбор.Объект.Установить(текОбъектСпособ.Объект, Истина);
		НаборЗаписей.Отбор.СпособПогрузкиРазгрузки.Установить(текОбъектСпособ.Способ, Истина);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Объект					= текОбъектСпособ.Объект;
		НоваяЗапись.СпособПогрузкиРазгрузки	= текОбъектСпособ.Способ;
		НоваяЗапись.Доступен				= Ложь;
		Попытка
			НаборЗаписей.Записать(Истина);
		Исключение
			тСообщение = Новый СообщениеПользователю;
			тСообщение.Текст = "Не удаось обновить записи доступности способов погрузки и разгрузки для Способа " + текОбъектСпособ.Способ + " для Объекта " + текОбъектСпособ.Объект + " " + ОписаниеОшибки();
			тСообщение.Сообщить();
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьСоотвествияОбъектовИСпособов(Команда)
	
	ПроверитьДобавитьЗаписиДоступности();
	
КонецПроцедуры
