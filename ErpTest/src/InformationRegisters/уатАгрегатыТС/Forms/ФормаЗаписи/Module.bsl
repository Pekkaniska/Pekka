
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("УзелОбъектаЭксплуатации")
			И ЗначениеЗаполнено(Параметры.УзелОбъектаЭксплуатации) Тогда 
		ЗаписьПоУзлу = ПолучитьЗаписьПоУзлу(Параметры.УзелОбъектаЭксплуатации);
		Если Не ЗаписьПоУзлу = Неопределено Тогда 
			ЗначениеВРеквизитФормы(ЗаписьПоУзлу, "Запись");
		КонецЕсли;
	КонецЕсли;
	
	уатЗащищенныеФункцииСервер.уатРегистрФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустой() И Параметры.Свойство("УзелОбъектаЭксплуатации") Тогда 
		Запись.УзелОбъектаЭксплуатации = Параметры.УзелОбъектаЭксплуатации;
	КонецЕсли;
	
	Если (Запись.ТипАгрегата = ПредопределенноеЗначение("Справочник.уатТипыАгрегатов.Шина")) 
			И (Не Запись.УзелОбъектаЭксплуатации.Пустая()) Тогда
		ОбщийПробег = ПолучитьПробегШины(Запись.УзелОбъектаЭксплуатации);
	КонецЕсли;
	
	УстановитьВидимостьИДоступностьЭлементовФормы();
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ИсторияУстановкиАгрегата, "УзелОбъектаЭксплуатации", Запись.УзелОбъектаЭксплуатации, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//уатЗащищенныеФункцииКлиент.уатРегистрФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Запись.ДатаПроизводстваШины <> Дата(1,1,1) Тогда
		ДатаПроизводстваШиныСтрока = Формат(НеделяГода(Запись.ДатаПроизводстваШины), "ЧЦ=2; ЧВН=") + Формат(Запись.ДатаПроизводстваШины, "ДФ=yy");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ИсторияУстановкиАгрегата, "УзелОбъектаЭксплуатации", Запись.УзелОбъектаЭксплуатации, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ИсторияУстановкиАгрегата, "УзелОбъектаЭксплуатации", Запись.УзелОбъектаЭксплуатации, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипАгрегатаПриИзменении(Элемент)
	
	УстановитьВидимостьИДоступностьЭлементовФормы();
	
	Если Запись.ТипАгрегата = ПредопределенноеЗначение("Справочник.уатТипыАгрегатов.Шина") Тогда
		Запись.ГоденДо            = 0;
		Запись.НачалоЭксплуатации = 0;
	Иначе
		Запись.ДатаПроизводстваШины = 0;
		Запись.СрокСлужбыШины       = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МодельАгрегатаПриИзменении(Элемент)
	
	ПриИзмененииМоделиНаСервере();
	УстановитьВидимостьИДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПроизводстваШиныСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДатаПроизводства = ?(ЗначениеЗаполнено(Запись.ДатаПроизводстваШины), Запись.ДатаПроизводстваШины, ТекущаяДата());
	Подсказка  = НСтр("en = 'The production date of the tire:'; ru = 'Дата производства шины:'");
	ЧастьДаты  = ЧастиДаты.Дата;
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораДатаПроизводстваШины", ЭтотОбъект);
	ПоказатьВводДаты(Оповещение, ДатаПроизводства, Подсказка, ЧастьДаты);
КонецПроцедуры

&НаКлиенте
Процедура ДатаПроизводстваШиныСтрокаПриИзменении(Элемент)
	ГодПроизводства    = СокрЛП(Прав(ДатаПроизводстваШиныСтрока, 2));
	НеделяПроизводства = СокрЛП(Лев(ДатаПроизводстваШиныСтрока, 2));
	
	Если ((НЕ ЗначениеЗаполнено(ГодПроизводства) ИЛИ ГодПроизводства = "0")
		И (НЕ ЗначениеЗаполнено(НеделяПроизводства) ИЛИ НеделяПроизводства = "0" ИЛИ НеделяПроизводства = "00")) Тогда
		Запись.ДатаПроизводстваШины = Дата(1,1,1);
		ДатаПроизводстваШиныСтрока  = "";
		Модифицированность = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ГодПроизводства) Тогда
		ГодПроизводства = Число(ГодПроизводства);
		ТекДата         = Число(Формат(ТекущаяДата(), "ДФ=yy"));
		ТекДатаГод      = Число(Лев(Формат(ТекущаяДата(), "ДФ=yyyy"), 2));
		ГодПроизводстваСтрока = Формат(ГодПроизводства, "ЧЦ=2; ЧВН=");
		Если НЕ ЗначениеЗаполнено(ГодПроизводстваСтрока) Тогда
			ГодПроизводстваСтрока = "00";
		КонецЕсли;
		Если ГодПроизводства < ТекДата + 10 Тогда
			ГодПроизводства = Строка(ТекДатаГод) + ГодПроизводстваСтрока;
		Иначе
			ГодПроизводства = Строка(ТекДатаГод -1) + ГодПроизводстваСтрока;
		КонецЕсли;
		ГодПроизводства = Число(ГодПроизводства);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НеделяПроизводства) 
		И НеделяПроизводства <> "0" И НеделяПроизводства <> "00" Тогда
		НеделяПроизводства = Число(НеделяПроизводства);
		мГод = ГодПроизводства;
		Если Не ЗначениеЗаполнено(ГодПроизводства) Тогда 
			мГод = Год(ТекущаяДата());
		КонецЕсли;
		Если НеделяПроизводства > НеделяГода(КонецГода(Дата(мГод,1,1))) Тогда
			НеделяПроизводства = НеделяГода(КонецГода(Дата(мГод,1,1)));
		КонецЕсли;
	Иначе
		НеделяПроизводства = 1;
	КонецЕсли;
	
	НеделяПроизводстваСтрока = Формат(НеделяПроизводства, "ЧЦ=2; ЧВН=");
	Запись.ДатаПроизводстваШины = ДатаПоНомеруНедели(НеделяПроизводства, ГодПроизводства);
	ДатаПроизводстваШиныСтрока  = НеделяПроизводстваСтрока + Формат(Запись.ДатаПроизводстваШины, "ДФ=yy");
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкаФормированияНаименованияАгрегатов(Команда)
	
	ПараметрыФормы = Новый Структура("ТипАгрегата", Запись.ТипАгрегата);
	
	ОткрытьФорму("ОбщаяФорма.уатНастройкаФормированияНаименованияАгрегатов", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименованиеВУзле(Команда)
	
	Если Не ЗначениеЗаполнено(Запись.УзелОбъектаЭксплуатации) Тогда 
		ТекстОшибки = НСтр("ru = 'Необходимо указать узел объекта эксплуатации';en='NTRS'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Запись.УзелОбъектаЭксплуатации");
		Возврат;
	КонецЕсли;
	
	Наименование = уатОбщегоНазначения.СформироватьНаименованиеАгрегата(Запись.УзелОбъектаЭксплуатации);
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьНаименованиеУзла(Запись.УзелОбъектаЭксплуатации, Наименование);
	
	ОповеститьОбИзменении(Запись.УзелОбъектаЭксплуатации);
	
	Если Не ВладелецФормы = Неопределено
			И ТипЗнч(ВладелецФормы) = Тип("УправляемаяФорма")
			И ЗначениеЗаполнено(ВладелецФормы.ИмяФормы)
			И ВладелецФормы.ИмяФормы = "Справочник.УзлыОбъектовЭксплуатации.Форма.ФормаЭлемента" Тогда 
		ВладелецФормы.Прочитать();
	КонецЕсли;
	
	ТекстПредупреждения = НСтр("ru = 'Наименование узла объекта эксплуатации обновлено успешно.';en='NTRS'");
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьЗаписьПоУзлу(УзелОбъектаЭксплуатации)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("УзелОбъектаЭксплуатации", УзелОбъектаЭксплуатации);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	уатАгрегатыТС.УзелОбъектаЭксплуатации,
	|	уатАгрегатыТС.УдалитьТипАгрегата,
	|	уатАгрегатыТС.УдалитьСерияНоменклатуры,
	|	уатАгрегатыТС.УдалитьНоменклатура
	|ИЗ
	|	РегистрСведений.уатАгрегатыТС КАК уатАгрегатыТС
	|ГДЕ
	|	уатАгрегатыТС.УзелОбъектаЭксплуатации = &УзелОбъектаЭксплуатации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		МенеджерЗаписи = РегистрыСведений.уатАгрегатыТС.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.УзелОбъектаЭксплуатации = Выборка.УзелОбъектаЭксплуатации;
		МенеджерЗаписи.УдалитьТипАгрегата = Выборка.УдалитьТипАгрегата;
		МенеджерЗаписи.УдалитьСерияНоменклатуры = Выборка.УдалитьСерияНоменклатуры;
		МенеджерЗаписи.УдалитьНоменклатура = Выборка.УдалитьНоменклатура;
		МенеджерЗаписи.Прочитать();
		
		Возврат МенеджерЗаписи;
		
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьЗаписьПоУзлу()

&НаСервере
Процедура УстановитьВидимостьИДоступностьЭлементовФормы()
	
	Если Запись.ТипАгрегата = ПредопределенноеЗначение("Справочник.уатТипыАгрегатов.Шина") Тогда
		Элементы.ОбщийПробег.Видимость           = Истина;
		Элементы.ГруппаЭксплуатацияШин.Видимость = Истина;
		Элементы.ГруппаЭксплуатация.Видимость    = Ложь;
		Элементы.ПараметрВыработки.Видимость     = Ложь;
		
	Иначе
		Элементы.ОбщийПробег.Видимость           = Ложь;
		Элементы.ГруппаЭксплуатацияШин.Видимость = Ложь;
		Элементы.ГруппаЭксплуатация.Видимость    = Истина;
		
		Если Запись.ТипАгрегата = ПредопределенноеЗначение("Справочник.уатТипыАгрегатов.Аккумулятор")
				Или Запись.ТипАгрегата = ПредопределенноеЗначение("Справочник.уатТипыАгрегатов.Аптечка") Тогда
			Элементы.ПараметрВыработки.Видимость = Ложь;
		Иначе
			Элементы.ПараметрВыработки.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПробегШины(Шина)
	
	Возврат "" + РегистрыСведений.уатАгрегатыТС.ПолучитьПробегШины(Шина) + " км";
	
КонецФункции

&НаСервере
Процедура ПриИзмененииМоделиНаСервере()
	
	Если ЗначениеЗаполнено(Запись.МодельАгрегата) Тогда
		Запись.ТипАгрегата = Запись.МодельАгрегата.ТипАгрегата;
		Если Запись.ТипАгрегата = ПредопределенноеЗначение("Справочник.уатТипыАгрегатов.Шина") Тогда
			Запись.СрокСлужбыШины = Запись.МодельАгрегата.СрокСлужбы;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьНаименованиеУзла(УзелОбъектаЭксплуатации, Наименование)
	
	СпрОб = УзелОбъектаЭксплуатации.ПолучитьОбъект();
	СпрОб.Наименование = Наименование;
	
	СпрОб.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораДатаПроизводстваШины(Дата, ДополнительныеПараметры) Экспорт
	
	Если НЕ Дата = Неопределено Тогда
		ГодПроизводства    = Формат(Дата, "ДФ=yy");
		НеделяПроизводства = НеделяГода(Дата);
		Запись.ДатаПроизводстваШины = Дата;
		НеделяПроизводства = Формат(НеделяПроизводства, "ЧЦ=2; ЧВН=");
		ДатаПроизводстваШиныСтрока = НеделяПроизводства + ГодПроизводства;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДатаПоНомеруНедели(НомерНедели, Год = "")
	
	Если НомерНедели = 1 Тогда
		Возврат Дата(?(Год = "", Год(ТекущаяДата()), Год),1,1)+(НомерНедели-НеделяГода(Дата(?(Год = "", Год(ТекущаяДата()), Год), 1, 1))) * 604800;
	Иначе
		Возврат НачалоНедели(Дата(?(Год = "", Год(ТекущаяДата()), Год),1,1)+(НомерНедели-НеделяГода(Дата(?(Год = "", Год(ТекущаяДата()), Год), 1, 1)))* 604800);
	КонецЕсли;
	
КонецФункции 
#КонецОбласти
