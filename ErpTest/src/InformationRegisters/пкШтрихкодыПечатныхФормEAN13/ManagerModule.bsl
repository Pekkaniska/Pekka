
Функция СформироватьШтрихкодEAN13() Экспорт
	
	ПрефиксУзлаШтрихкода = ПрефиксУзлаШтрихкода();
	
	Код = ПолучитьМаксимальныеЗначенияКодовШтучныхШтрихкодов();
	
	Если Код = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщенияНетСвободныхКодовШтучныхШтрихкодов());
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПолучитьШтрихкодПоКоду(Код, ПрефиксУзлаШтрихкода);
	
КонецФункции

// Возвращает текст сообщения о недостатке свободных кодов штучных штрихкодов
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Строка - Текст сообщения
//
Функция ТекстСообщенияНетСвободныхКодовШтучныхШтрихкодов() Экспорт
	Возврат НСтр("ru = 'Нет свободных кодов.
		               |Штрихкод не сформирован.'")	
КонецФункции


Функция ПрефиксУзлаШтрихкода() Экспорт
	
	Возврат Формат(Константы.ПрефиксШтрихкодовУзлаРИБ.Получить(),"ЧЦ=1; ЧН=; ЧВН=");
	
КонецФункции

Функция ПолучитьМаксимальныеЗначенияКодовШтучныхШтрихкодов() Экспорт
	
	ПрефиксУзла = Константы.ПрефиксШтрихкодовУзлаРИБ.Получить();
	
	Запрос = Новый Запрос;
	
	СтрокаПоиска = """" + Строка(ПрефиксУзла) + "____________" + """";
	
	Запрос.Текст = "ВЫБРАТЬ
		|	МАКСИМУМ(ПОДСТРОКА(ШтрихкодыПФ.ШтрихкодEAN13, 2, 11)) КАК КодСтрока
		|ИЗ
		|	РегистрСведений.пкШтрихкодыПечатныхФормEAN13 КАК ШтрихкодыПФ
		|ГДЕ
		|	ШтрихкодыПФ.ШтрихкодEAN13 ПОДОБНО " + СтрокаПоиска;
	
	Выборка = Запрос.Выполнить().Выбрать();
	ОписаниеТипаЧисла = ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 0);
	МаксимальныйКод = МаксимальныйКод();
	
	Если Выборка.Следующий() Тогда
		Код = ОписаниеТипаЧисла.ПривестиЗначение(Выборка.КодСтрока);
		Если Код < МаксимальныйКод Тогда
			Возврат Код + 1;
		Иначе
			Возврат Неопределено;
		КонецЕсли;	
	Иначе
		Возврат Неопределено;
	КонецЕсли;	

КонецФункции

// Функция возвращает максимальный код штучного товара
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Число
//
Функция МаксимальныйКод() Экспорт
	
	//Возврат 999999999;
	Возврат 99999999999;
	
КонецФункции

// Функция возвращает штрихкод, создаваемый из кода с префиксами и контрольным символом
//
// Параметры:
//  Код                  - Число
//  Диапазон             - Строка
//  ПрефиксУзлаШтрихкода - Строка
//
// Возвращаемое значение:
//  Строка
//
Функция ПолучитьШтрихкодПоКоду(Код, ПрефиксУзлаШтрихкода) Экспорт

	//Штрихкод = Строка(Диапазон) + ПрефиксУзлаШтрихкода + Формат(Код, "ЧЦ=9; ЧВН=; ЧГ=");
	Штрихкод = ПрефиксУзлаШтрихкода + Формат(Код, "ЧЦ=11; ЧВН=; ЧГ=");
	Штрихкод = Штрихкод + КонтрольныйСимволEAN(ШтрихКод, 13);

	Возврат Штрихкод;

КонецФункции

// Функция вычисляет контрольный символ кода EAN
//
// Параметры:
//  Штрихкод     - штрихкод (без контрольной цифры)
//  Тип          - тип штрихкода: 13 - EAN13, 8 - EAN8
//
// Возвращаемое значение:
//  Контрольный символ штрихкода
//
Функция КонтрольныйСимволEAN(ШтрихКод, Тип) Экспорт

	Четн   = 0;
	Нечетн = 0;

	КоличествоИтераций = ?(Тип = 13, 6, 4);

	Для Индекс = 1 По КоличествоИтераций Цикл
		Если (Тип = 8) и (Индекс = КоличествоИтераций) Тогда
		Иначе
			Четн   = Четн   + Сред(ШтрихКод, 2 * Индекс, 1);
		КонецЕсли;
		Нечетн = Нечетн + Сред(ШтрихКод, 2 * Индекс - 1, 1);
	КонецЦикла;

	Если Тип = 13 Тогда
		Четн = Четн * 3;
	Иначе
		Нечетн = Нечетн * 3;
	КонецЕсли;

	КонтЦифра = 10 - (Четн + Нечетн) % 10;

	Возврат ?(КонтЦифра = 10, "0", Строка(КонтЦифра));

КонецФункции
