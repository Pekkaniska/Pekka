Процедура СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, тИсточник)
	
	НаборЗаписей  = тИсточник.Движения.пкРаботыВЗаказНаряде;
	НаборЗаписей.Записывать = Истина;
	
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	пкЗаказНарядРаботы.Ссылка КАК Регистратор,
		                      |	пкЗаказНарядРаботы.ЗаданиеНаРемонт,
		                      |	пкЗаказНарядРаботы.ВидРемонта,
		                      |	пкЗаказНарядРаботы.Номенклатура КАК Работа,
		                      |	1 КАК Количество,
		                      |	пкЗаказНарядРаботы.Ссылка.Дата КАК Период
		                      |ИЗ
		                      |	Документ.пкЗаказНаряд.Работы КАК пкЗаказНарядРаботы
		                      |ГДЕ
		                      |	пкЗаказНарядРаботы.Ссылка = &Ссылка
		                      |	И пкЗаказНарядРаботы.ЗаданиеНаРемонт <> ЗНАЧЕНИЕ(Документ.пкЗаданиеНаРемонт.ПустаяСсылка)
		                      |
		                      |СГРУППИРОВАТЬ ПО
		                      |	пкЗаказНарядРаботы.Ссылка,
		                      |	пкЗаказНарядРаботы.ЗаданиеНаРемонт,
		                      |	пкЗаказНарядРаботы.ВидРемонта,
		                      |	пкЗаказНарядРаботы.Номенклатура,
		                      |	пкЗаказНарядРаботы.Ссылка.Дата");
	Запрос.УстановитьПараметр("Ссылка",тИсточник.Ссылка);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда 
		НаборЗаписей.Загрузить(Результат.Выгрузить());
	КонецЕсли;
	
	
	Попытка
		НаборЗаписей.Записать(Истина);
	Исключение
		Отказ = Истина;
		тСообщение = Новый СообщениеПользователю;
		тСообщение.Текст = ОписаниеОшибки();
		тСообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////
Процедура СформироватьЗаписи(Отказ, РежимПроведения, тИсточник) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивИсточников = Новый Массив;
	
	Если ТипЗнч(тИсточник) = Тип("Массив") Тогда
		МассивИсточников = тИсточник;
	Иначе
		МассивИсточников.Добавить(тИсточник);
	КонецЕсли;
	
	Для Каждого ЭлементИсточник из МассивИсточников Цикл
		
		Если ТипЗнч(ЭлементИсточник) = Тип("ДокументОбъект.пкЗаказНаряд") Тогда
			СформироватьЗаписиПоДокументуЗаказНаряд(Отказ, РежимПроведения, ЭлементИсточник);
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры
