
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриОткрытии()
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Значение = ItobВызовСервераПовтИсп.ДанныеТерминаловБратьИзБазыIMCS();
	Элементы.Группа1.Видимость = Не Значение;
	Элементы.Группа2.Видимость = Значение;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОчистку(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не указан период!'"));
		Возврат;
	
	КонецЕсли;
			
	НадписьСостояния = НСтр("ru = 'Сбор данных для очистки...'");
	
	ЭтаФорма.ОбновитьОтображениеДанных();
	
	КодыТрекеров = ПолучитьТрекерыДляОчистки();
	
	СостояниеПроцесса = 0;
	
	Счетчик = 0;
	
	 ВремяНачалаПроцесса = ItobОбщегоНазначенияСервер.ПолучитьТекущуюДату();
	 Надпись= "";
	    
	Для каждого ТекущийКод Из КодыТрекеров Цикл
		
		НадписьСостояния = НСтр("ru = 'Очистка данных по терминалу'")+" "+ТекущийКод+"...";
		
		ЭтаФорма.ОбновитьОтображениеДанных();
		
		ОчиститьДанныеПоТерминалу(ТекущийКод);
		
			
		Время = (ItobОбщегоНазначенияСервер.ПолучитьТекущуюДату()-ВремяНачалаПроцесса)/КодыТрекеров.Количество()/60; 
		
		Надпись = НСтр("ru = 'Осталось до конца'")+" "
				  +?(Цел(Время)=0,НСтр("ru = 'менее минуты'"),Цел(Время)+" минут");
		
		ЭтаФорма.ОбновитьОтображениеДанных();
		
		
		Счетчик = Счетчик+1;
		
		СостояниеПроцесса = Цел(100*Счетчик/КодыТрекеров.Количество());
		ЭтаФорма.ОбновитьОтображениеДанных();
		
		ОбработкаПрерыванияПользователя();
	КонецЦикла;
	
	СостояниеПроцесса = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьТрекерыДляОчистки()
	
	Если  ЗначениеЗаполнено(Объект.Терминал) Тогда
		МассивКодов = Новый Массив;
		МассивКодов.Добавить(Объект.Терминал.Код);
		
		Возврат МассивКодов;
	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Объект.Период);
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ItobДанныеТерминалов.Терминал.Код КАК КодТрекера
	               |ИЗ
	               |	РегистрСведений.ItobДанныеТерминалов КАК ItobДанныеТерминалов
	               |ГДЕ
	               |	ItobДанныеТерминалов.Период < &Период";
				   
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КодТрекера");

КонецФункции // ПолучитьТрекерыДляОчистки()


&НаСервере
Функция ОчиститьДанныеПоТерминалу(Код)
	
	ТекущийТерминал = Справочники.ItobТерминалы.НайтиПоКоду(Код);
	
	ОтборТерминал = Новый Структура("Терминал", ТекущийТерминал);

	ВыборкаЗаписейДанныеДатчиков = РегистрыСведений.ItobДанныеДатчиков.Выбрать(,Объект.Период,ОтборТерминал);
	Пока ВыборкаЗаписейДанныеДатчиков.Следующий() Цикл
		ВыборкаЗаписейДанныеДатчиков.ПолучитьМенеджерЗаписи().Удалить();
		
	КонецЦикла;
	
	ВыборкаЗаписейДанныеТерминалов = РегистрыСведений.ItobДанныеТерминалов.Выбрать(,Объект.Период,ОтборТерминал);
	Пока ВыборкаЗаписейДанныеТерминалов.Следующий() Цикл
		ВыборкаЗаписейДанныеТерминалов.ПолучитьМенеджерЗаписи().Удалить();
		
	КонецЦикла;
    
КонецФункции // ОчиститьДанныеПоТерминалу()

#КонецОбласти
