
Функция ПечатьМакет(АктивнаяСтрока, ВариантЗаполнения = 1) Экспорт
	Если ТипЗнч(АктивнаяСтрока)=Тип("Число") Тогда
		АктивнаяСтрока=ТаблицаСчетов[АктивнаяСтрока];
	КонецЕсли; 
	
	ТабДок = Новый ТабличныйДокумент;
	
	Если ВариантЗаполнения = 1 Тогда
		Макет = ПолучитьМакет("Макет");
	ИначеЕсли ВариантЗаполнения = 2 Тогда
		Макет = ПолучитьМакет("МакетВариант2");
	Иначе
		Макет = ПолучитьМакет("Макет");
	КонецЕсли;
	
	ОбластьШапка  = Макет.ПолучитьОбласть("Шапка");
    ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	Если ПодписиВПодвале Тогда
		ОбластьПодвал  = Макет.ПолучитьОбласть("ПодвалСПодписями");
	Иначе
		ОбластьПодвал  = Макет.ПолучитьОбласть("Подвал");
	КонецЕсли;
	
	Организация=АктивнаяСтрока.Счет.Организация;
	Контрагент=АктивнаяСтрока.Счет.Контрагент;
	
	ДанныеОрганизации=ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Организация, ТекущаяДата());
	ОбластьШапка.Параметры.ОрганизацияНаименование=ДанныеОрганизации.СокращенноеНаименование;
	ОбластьШапка.Параметры.ОрганизацияИНН=ДанныеОрганизации.ИНН;
	ОбластьШапка.Параметры.ОрганизацияЮридическийАдрес=ДанныеОрганизации.ЮридическийАдрес;
	ОбластьШапка.Параметры.ОрганизацияТелефон=ДанныеОрганизации.Телефоны;
	ОбластьШапка.Параметры.ОрганизацияЕмаил=УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Организация, Справочники.ВидыКонтактнойИнформации.EmailОрганизации,АктивнаяСтрока.Счет.Дата);
	
	ДанныеКонтрагента=ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Контрагент, ТекущаяДата());
	ОбластьШапка.Параметры.КонтрагентНаименование=ДанныеКонтрагента.СокращенноеНаименование;
	ОбластьШапка.Параметры.КонтрагентИНН=ДанныеКонтрагента.ИНН;
	ОбластьШапка.Параметры.КонтрагентЮридическийАдрес=ДанныеКонтрагента.ЮридическийАдрес;
	ОбластьШапка.Параметры.КонтрагентТелефон=ДанныеКонтрагента.Телефоны;
	
	КонтрагентЕмаил=УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, Справочники.ВидыКонтактнойИнформации.EmailКонтрагента,АктивнаяСтрока.Счет.Дата);
	Если не ЗначениеЗаполнено(КонтрагентЕмаил) Тогда
		КонтрагентЕмаил=УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент.Партнер, Справочники.ВидыКонтактнойИнформации.EmailПартнера,АктивнаяСтрока.Счет.Дата);
	КонецЕсли;	
	ОбластьШапка.Параметры.КонтрагентЕмаил=КонтрагентЕмаил;
	
	ОбластьШапка.Параметры.ДатаАнализа = Формат(ДатаАнализа, "ДЛФ=Д");
	
	ТабДок.Вывести(ОбластьШапка);
	
	Если Разбивать Тогда
		МассивСтрок=Новый Массив;
		МассивСтрок.Добавить(АктивнаяСтрока);
	Иначе	
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Контрагент",АктивнаяСтрока.Контрагент);
		МассивСтрок=ТаблицаСчетов.НайтиСтроки(СтруктураОтбора);
	КонецЕсли; 
	
	СуммаСчетаИтог=0;
	СуммаОплатИтог=0;
	ЗадолженностьИтог=0;
	ПереплатаИтог = 0;
	
	Для каждого АктивнаяСтрокаТекущая Из МассивСтрок Цикл
		ОбластьСтрока.Параметры.Счет          = "№ "+ АктивнаяСтрокаТекущая.Счет.Номер + " от "+ Формат(АктивнаяСтрокаТекущая.Счет.Дата, "ДФ=dd.MM.yyyy");
		ОбластьСтрока.Параметры.ДоговорНомер  = АктивнаяСтрокаТекущая.Договор.Номер;
		Если ТипЗнч(АктивнаяСтрокаТекущая.Счет)=Тип("ДокументСсылка.ЗаказКлиента") Тогда
			Для каждого СтрокаТовары Из АктивнаяСтрокаТекущая.Счет.Товары Цикл
				Если ЗначениеЗаполнено(СтрокаТовары.пкДатаНачалаАренды) Тогда
					ОбластьСтрока.Параметры.ПериодАренды=Формат(СтрокаТовары.пкДатаНачалаАренды,"ДФ=dd.MM.yyyy")+" - "+Формат(СтрокаТовары.пкДатаОкончанияАренды,"ДФ=dd.MM.yyyy");		
					Прервать;
				КонецЕсли; 
			КонецЦикла; 
		КонецЕсли;
		
		ОбластьСтрока.Параметры.СуммаСчета    = АктивнаяСтрокаТекущая.СуммаСчета;
		СуммаСчетаИтог=СуммаСчетаИтог+АктивнаяСтрокаТекущая.СуммаСчета;
		ОбластьСтрока.Параметры.СуммаОплат    = АктивнаяСтрокаТекущая.СуммаОплат;
		СуммаОплатИтог=СуммаОплатИтог+АктивнаяСтрокаТекущая.СуммаОплат;
		ОбластьСтрока.Параметры.Задолженность = АктивнаяСтрокаТекущая.Задолженность;
		ЗадолженностьИтог=ЗадолженностьИтог+АктивнаяСтрокаТекущая.Задолженность;
//++ Рарус Лимаренко 24.10.17
		ОбластьСтрока.Параметры.Переплата = АктивнаяСтрокаТекущая.Переплата;
		ПереплатаИтог = ПереплатаИтог + АктивнаяСтрокаТекущая.Переплата;
//-- Рарус Лимаренко 24.10.17
		
		ТабДок.Вывести(ОбластьСтрока);
	КонецЦикла; 
	
	ОбластьПодвал.Параметры.СуммаСчетаИтог			= СуммаСчетаИтог;
	ОбластьПодвал.Параметры.СуммаОплатИтог			= СуммаОплатИтог;
	ОбластьПодвал.Параметры.ЗадолженностьИтог		= ЗадолженностьИтог;
//++ Рарус Лимаренко 24.10.17 	
	ОбластьПодвал.Параметры.ПереплатаИтог           = ПереплатаИтог;
//-- Рарус Лимаренко 24.10.17 	
	
	 
	УстановитьПривилегированныйРежим(Истина);
	
	ОбластьПодвал.Параметры.ДатаОжиданияПлатежа		= Формат(ДатаАнализа + 604800, "ДФ=dd.MM.yyyy");
	ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, ТекущаяДата());
	ОбластьПодвал.Параметры.Руководитель			= ?(ЗначениеЗаполнено(ОтветственныеЛица.Руководитель), ОтветственныеЛица.Руководитель.Наименование, "");
	Если ПодписиВПодвале Тогда
		ОбластьПодвал.Параметры.ГлавныйБухгалтер	= ?(ЗначениеЗаполнено(ОтветственныеЛица.ГлавныйБухгалтер), ОтветственныеЛица.ГлавныйБухгалтер.Наименование, "");
	КонецЕсли; 
	ТабДок.Вывести(ОбластьПодвал);
			
	УстановитьПривилегированныйРежим(Ложь);
	
	ТабДок.АвтоМасштаб = Истина;
	
	Возврат ТабДок;	 
	
КонецФункции // ПечатьМакет()
 