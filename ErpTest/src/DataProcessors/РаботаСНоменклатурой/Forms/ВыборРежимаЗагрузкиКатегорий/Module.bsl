
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("КоличествоКатегорийДляЗагрузки", КоличествоКатегорийДляЗагрузки);
	
	ЗаполнитьРежимСохраненияИерархии();
	
	КоличествоУровнейИерархии = Элементы.КоличествоУровнейИерархии.СписокВыбора[0].Значение;
	РежимЗагрузкиКатегорий    = Элементы.ЗагрузкаПоОтдельности.СписокВыбора[0].Значение;
	СохранятьИерархию         = РаботаСНоменклатурой.СохранятьИерархиюКатегорийПриЗагрузке();
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
	КлючСохраненияПоложенияОкна = СтрШаблон("%1", Элементы.ЗагрузкаСОбъединением.Видимость);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если РежимЗагрузкиКатегорий = Элементы.ЗагрузкаСПривязкой.СписокВыбора[0].Значение Тогда
		ПроверяемыеРеквизиты.Добавить("ВидНоменклатуры");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗагрузкаПоОтдельностиПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаСОбъединениемПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаСПривязкойПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ПараметрыЗакрытия = РаботаСНоменклатуройСлужебныйКлиентСервер.ДополнительныеПараметрыЗагрузкиКатегорий();
		
		ПараметрыЗакрытия.РежимЗагрузкиКатегорий     = РежимЗагрузкиКатегорий;
		ПараметрыЗакрытия.КоличествоУровнейИерархии  = КоличествоУровнейИерархии;
		ПараметрыЗакрытия.ВидНоменклатуры            = ВидНоменклатуры;
		
		Закрыть(ПараметрыЗакрытия);
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРежимСохраненияИерархии()
	
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(0, НСтр("ru = 'Не сохранять иерархию'"));
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(1, НСтр("ru = 'Сохранить последний уровень иерархии'"));
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(2, НСтр("ru = 'Сохранить последние 2 уровня иерархии'"));
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(3, НСтр("ru = 'Сохранить последние 3 уровня иерархии'"));
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(4, НСтр("ru = 'Сохранить последние 4 уровня иерархии'"));
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(5, НСтр("ru = 'Сохранить последние 5 уровней иерархии'"));
	Элементы.КоличествоУровнейИерархии.СписокВыбора.Добавить(1000, НСтр("ru = 'Сохранить всю иерархию'"));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.КоличествоУровнейИерархии.Доступность 
		= Форма.РежимЗагрузкиКатегорий = Элементы.ЗагрузкаПоОтдельности.СписокВыбора[0].Значение;
		
	Элементы.КоличествоУровнейИерархии.Видимость = Форма.СохранятьИерархию;
				
	Элементы.ВидНоменклатуры.Доступность 
		= Форма.РежимЗагрузкиКатегорий = Элементы.ЗагрузкаСПривязкой.СписокВыбора[0].Значение;
		
	Элементы.ВидНоменклатуры.АвтоОтметкаНезаполненного 
		= Форма.РежимЗагрузкиКатегорий = Элементы.ЗагрузкаСПривязкой.СписокВыбора[0].Значение;
		
	Элементы.ЗагрузкаСОбъединением.Видимость = Форма.КоличествоКатегорийДляЗагрузки > 1;
	
	Если Форма.КоличествоКатегорийДляЗагрузки = 1 Тогда
		Элементы.ЗагрузкаПоОтдельности.СписокВыбора[0].Представление = НСтр("ru = 'Загрузить выбранную категорию'");
		Элементы.ЗагрузкаСПривязкой.СписокВыбора[0].Представление = НСтр("ru = 'Привязать категорию к виду номенклатуры'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти