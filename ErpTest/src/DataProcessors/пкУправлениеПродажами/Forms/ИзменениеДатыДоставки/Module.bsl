
&НаСервере
Процедура ИзменитьДатыНаСервере()
    
    Для Каждого Строка ИЗ ТехникаКДоставке Цикл
        Если Строка.ДатаДоставки <> Строка.ДатаДоставкиДоИзменения ИЛИ Строка.ДатаАренды <> Строка.ДатаАрендыДоИзменения Тогда
            Если ЗначениеЗаполнено(Строка.ЗаданиеНаПеревозку) Тогда
                ЗаданиеНаПеревозку = Строка.ЗаданиеНаПеревозку.ПолучитьОбъект();
                ЗаданиеНаПеревозку.ДатаОтгрузки = Строка.ДатаДоставки;
                ЗаданиеНаПеревозку.ДатаАренды   = Строка.ДатаАренды;
                
                Попытка
                    ЗаданиеНаПеревозку.Записать(РежимЗаписиДокумента.Проведение);
                Исключение
                КонецПопытки;
            Иначе
                ОформитьЗаданиеНаПеревозку(Строка.ДатаДоставки, Строка.ДатаАренды);   
            КонецЕсли;    
        КонецЕсли;    
    КонецЦикла; 
        
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДаты(Команда)
    
    ИзменитьДатыНаСервере();
    
    ОповеститьОВыборе(Неопределено);
    
КонецПроцедуры

&НаСервере
Процедура ОформитьЗаданиеНаПеревозку(ДатаДоставки, ДатаАренды)
            
    ЗаданиеНаПеревозку = Документы.пкЗаданиеНаПеревозку.СоздатьДокумент();
    
    ЗаданиеНаПеревозку.Дата                  = ТекущаяДата();
    ЗаданиеНаПеревозку.Статус                = Перечисления.пкСтатусыЗаданийНаПеревозку.Запланировано;
    
    ЗаданиеНаПеревозку.ВидОперации           = ?(ВидДоставки = "Отгрузка", Перечисления.пкВидыОперацийЗаданийНаПеревозку.ДоставкаКлиенту, 
        Перечисления.пкВидыОперацийЗаданийНаПеревозку.ВозвратОтКлиента);
    
    ЗаданиеНаПеревозку.ДатаОтгрузки          = ДатаДоставки;
    ЗаданиеНаПеревозку.ДатаАренды            = ДатаАренды;
    ЗаданиеНаПеревозку.ЗаявкаНаАрендуТехники = ЗаявкаНаАрендуТехники;
    ЗаданиеНаПеревозку.Модель                = Модель;
    ЗаданиеНаПеревозку.АдресДоставки         = ЗаявкаНаАрендуТехники.АдресДоставки;
    ЗаданиеНаПеревозку.ВремяДоставкиС        = ЗаявкаНаАрендуТехники.ВремяДоставкиС;
    ЗаданиеНаПеревозку.ВремяДоставкиПо       = ЗаявкаНаАрендуТехники.ВремяДоставкиПо;
    ЗаданиеНаПеревозку.СпособДоставки        = ЗаявкаНаАрендуТехники.СпособДоставки;
    ЗаданиеНаПеревозку.Подразделение         = ЗаявкаНаАрендуТехники.ПодразделениеОтгрузки;
    
    Если ВидДоставки = "Возврат" Тогда
        ЗаданиеНаПеревозку.СкладПолучатель   = ЗаявкаНаАрендуТехники.ПодразделениеОтгрузки.пкСкладТехники;      
    КонецЕсли;
    
    Попытка
        ЗаданиеНаПеревозку.Записать(РежимЗаписиДокумента.Проведение);        
    Исключение
    КонецПопытки;
        
КонецПроцедуры    

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Параметры.Свойство("ЗаявкаНаАрендуТехники") Тогда
        ЗаявкаНаАрендуТехники = Параметры.ЗаявкаНаАрендуТехники;
    КонецЕсли;
    
    Если Параметры.Свойство("Модель") Тогда
        Модель = Параметры.Модель;
    КонецЕсли;
    
    Если Параметры.Свойство("Дата") Тогда
        Дата = Параметры.Дата;
    Иначе
        Дата = Неопределено;
    КонецЕсли;
    
    Если Параметры.Свойство("ДатаАренды") Тогда
        ДатаАренды = Параметры.ДатаАренды;
    Иначе
        ДатаАренды = Неопределено;
    КонецЕсли;
    
    Если Параметры.Свойство("ВидДоставки") Тогда
        ВидДоставки = Параметры.ВидДоставки;
        
        Если ВидДоставки = "Отгрузка" Тогда
            Элементы.ТехникаКДоставкеДатаДоставки.Заголовок = "Дата отгрузки";  
            Элементы.ТехникаКДоставкеДатаАренды.Заголовок   = "Начало аренды"; 
            
            Элементы.ОбщаяДатаДоставки.Заголовок = "Дата отгрузки";  
            Элементы.ОбщаяДатаАренды.Заголовок   = "Начало аренды";
        Иначе
            Элементы.ТехникаКДоставкеДатаДоставки.Заголовок = "Дата возврата";    
            Элементы.ТехникаКДоставкеДатаАренды.Заголовок   = "Последний день";
            
            Элементы.ОбщаяДатаДоставки.Заголовок = "Дата возврата";  
            Элементы.ОбщаяДатаАренды.Заголовок   = "Последний день";
        КонецЕсли;    
    КонецЕсли;
    
    Если НЕ ЗначениеЗаполнено(ЗаявкаНаАрендуТехники) ИЛИ НЕ ЗначениеЗаполнено(Модель) ИЛИ НЕ ЗначениеЗаполнено(ВидДоставки) Тогда
        Отказ = Истина;    
    КонецЕсли;  
    
    Если НЕ Отказ Тогда
        ТехникаКДоставке.Загрузить(пкУправлениеТехникойСервер.ПолучитьТехникуКДоставке(ЗаявкаНаАрендуТехники, Модель, ВидДоставки, Дата, ДатаАренды));
    КонецЕсли;    
    
КонецПроцедуры

&НаКлиенте
Процедура ТехникаКДоставкеДатаАрендыПриИзменении(Элемент)
    
    ТекущиеДанные = Элементы.ТехникаКДоставке.ТекущиеДанные;
    
    Если ВидДоставки = "Отгрузка" Тогда
        ТекущиеДанные.ДатаДоставки = ТекущиеДанные.ДатаАренды;
    Иначе
        ТекущиеДанные.ДатаДоставки = ТекущиеДанные.ДатаАренды + 3600 * 24;
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДляВсех(Команда)
    
    Для Каждого Строка ИЗ ТехникаКДоставке Цикл
        Строка.ДатаДоставки = ОбщаяДатаДоставки;
        Строка.ДатаАренды   = ОбщаяДатаАренды;
    КонецЦикла;    
        
КонецПроцедуры

&НаКлиенте
Процедура ОбщаяДатаАрендыПриИзменении(Элемент)
    
    Если ВидДоставки = "Отгрузка" Тогда
        ОбщаяДатаДоставки = ОбщаяДатаАренды;
    Иначе
        ОбщаяДатаДоставки = ОбщаяДатаАренды + 3600 * 24;
    КонецЕсли;
    
КонецПроцедуры
 
 

