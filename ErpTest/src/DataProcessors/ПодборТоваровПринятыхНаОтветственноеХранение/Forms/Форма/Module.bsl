
#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Владелец) Тогда
		ВызватьИсключение НСтр("ru='Предусмотрено открытие обработки только из документов.'");
	КонецЕсли;
	
	Объект.Владелец       = Параметры.Владелец;
	Объект.Организация    = Параметры.Организация;
	Объект.Склад          = Параметры.Склад;
	Объект.Договор        = Параметры.Договор;
	ХозяйственнаяОперация = Параметры.ХозяйственнаяОперация;
	
	ЗаполнитьСписокВыбораРежимОстатков();
	ЗаполнитьСкладыНастроитьВидимость();
	ЗаполнитьТаблицуТовары();
	
	СформироватьИнформационнуюНадписьОтборы()
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы
		И ФормаМодифицирована Тогда
		
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Данные были изменены. Все изменения будут потеряны.'");
		
		Возврат;
		
	КонецЕсли;
	
	Если ПеренестиВДокумент
		Или ВыполняетсяЗакрытие
		Или Не ФормаМодифицирована Тогда
		
		Возврат;
		
	Иначе
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если РазрешитьПереносРезультатовВДокумент() Тогда
			ПеренестиВДокумент = Истина;
			ВыполняетсяЗакрытие = Истина;
			
			Закрыть();
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ВыполняетсяЗакрытие = Истина;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиФормыНаСервере();
	
	Если ПеренестиВДокумент Тогда
		Структура = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище());
		ОповеститьОВыборе(Структура);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимОстатковПриИзменении(Элемент)
	
	РежимОстатковПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РасчитатьКоличествоВСтроке(Объект.Товары, ТекущиеДанные, ФормаМодифицирована);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПодобраноПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные.КоличествоПодобрано = 0 Тогда
		ТекущиеДанные.Пометка = Ложь;
	Иначе
		ТекущиеДанные.Пометка = Истина;
	КонецЕсли;
	
	РасчитатьКоличествоВСтроке(Объект.Товары, ТекущиеДанные, ФормаМодифицирована);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ОчиститьСообщения();
	
	Если РазрешитьПереносРезультатовВДокумент() Тогда
		ПеренестиВДокумент = Истина;
		
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКоличество(Команда)
	
	ЗаполнитьКоличествоНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	ОтметитьВсеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометку(Команда)
	
	СнятьПометкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Установка условного оформления для элемента 'КоличествоПодобрано' табличной части 'Товары'.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоПодобрано.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Пометка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Установка условного оформления для элемента 'КоличествоПодобрано' табличной части 'Товары'.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоОсталосьПодобрать.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоОсталосьПодобрать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОтрицательногоЧисла);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораРежимОстатков()
	
	РежимыОстатков = Новый СписокЗначений;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровХранящихсяНаСкладе
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПереданныхВПроизводство
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения Тогда
		
		РежимыОстатков.Добавить("ВсеПринятые", НСтр("ru = 'Все принятые'"));
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровХранящихсяНаСкладе
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровПереданныхВПроизводство Тогда
		
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровПереданныхВПроизводство Тогда
			РежимыОстатков.Добавить("ВсеПринятые", НСтр("ru = 'Переданные в производство'"));
		Иначе
			РежимыОстатков.Добавить("ВсеПринятые",НСтр("ru = 'Все принятые'"));
		КонецЕсли;
		
		РежимыОстатков.Добавить("Выбывшие", НСтр("ru = 'Выбывшие'"));
		
	КонецЕсли;
	
	Элементы.РежимОстатков.СписокВыбора.Очистить();
	
	Для Каждого Режим Из РежимыОстатков Цикл
		Элементы.РежимОстатков.СписокВыбора.Добавить(Режим.Значение, Режим.Представление);
	КонецЦикла;
	
	Если РежимыОстатков.Количество() = 1 Тогда
		Элементы.РежимОстатков.Видимость = Ложь;
		Объект.РежимОстатков = РежимыОстатков.Получить(0).Значение;
	Иначе
		ЗагрузитьНастройкиФормыНаСервере(РежимыОстатков);
	КонецЕсли;
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиФормыНаСервере(РежимыОстатков)
	
	КлючОбъекта = "Обработка_ПодборТоваровПринятыхНаОтветственноеХранилище";
	НастройкаРежимаОстатков = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, "");
	
	Если НастройкаРежимаОстатков = Неопределено Тогда
		Объект.РежимОстатков = РежимыОстатков.Получить(0).Значение;
	Иначе
		
		ЗначениеРежима = РежимыОстатков.НайтиПоЗначению(НастройкаРежимаОстатков);
		
		Если ЗначениеРежима <> Неопределено Тогда
			Объект.РежимОстатков = ЗначениеРежима.Значение;
		Иначе
			Объект.РежимОстатков = РежимыОстатков.Получить(0).Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	ЗаголовокКолонки = НСтр("ru = 'Остаток'");
	
	Если Объект.РежимОстатков = "ВсеПринятые" Тогда
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровПереданныхВПроизводство
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровПереданныхВПроизводство Тогда
			
			ЗаголовокКолонки = НСтр("ru = 'Передано в производство'");
			
		Иначе
			ЗаголовокКолонки = НСтр("ru = 'Всего принято'");
		КонецЕсли;
	ИначеЕсли Объект.РежимОстатков = "Выбывшие" Тогда
		ЗаголовокКолонки = НСтр("ru = 'Выбыло'");
	КонецЕсли;
	
	Элементы.ТоварыКоличествоОстаток.Заголовок = ЗаголовокКолонки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСкладыНастроитьВидимость()
	
	Если ТипЗнч(Объект.Склад) = Тип("СправочникСсылка.Склады")
		И Справочники.Склады.ЭтоГруппа(Объект.Склад) Тогда
		
		Элементы.ТоварыСклад.Видимость = Истина;
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Склады.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Ссылка В ИЕРАРХИИ(&Склад)
		|	И НЕ Склады.ЭтоГруппа
		|	И НЕ Склады.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("Склад", Объект.Склад);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Склады.Добавить(Выборка.Склад);
		КонецЦикла;
		
	Иначе
		Склады.Добавить(Объект.Склад);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТовары()
	
	ОбновитьПодобранныеТовары();
	
	Если Объект.РежимОстатков = "ВсеПринятые" Тогда
		// Вариант подбора товаров в документы "Выкуп товаров принятых на хранение", "Отгрузка товаров с хранения",
		// "Списание принятых на хранение товаров".
		Если Не ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыОрганизаций) Тогда
			
			Объект.Товары.Очистить();
			Возврат;
			
		Иначе
			
			ТекстЗапроса = "
			|ВЫБРАТЬ
			|	ВидыЗапасов.Ссылка КАК ВидЗапасов
			|
			|ПОМЕСТИТЬ ТаблицаЗапасов
			|ИЗ
			|	Справочник.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.ВладелецТовара = &Партнер
			|	И ВидыЗапасов.Организация  = &Организация
			|	И ВидыЗапасов.Договор      = &Договор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТоварыПринятые.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыПринятые.Организация                КАК Организация,
			|	ТоварыПринятые.ВидЗапасов                 КАК ВидЗапасов,
			|	ТоварыПринятые.НомерГТД                   КАК НомерГТД,
			|	ТоварыПринятые.КоличествоОстаток          КАК КоличествоОстаток
			|
			|ПОМЕСТИТЬ ТаблицаПринятыхТоваров
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(&Период,
			|			Организация = &Организация
			|				И ВидЗапасов В
			|					(ВЫБРАТЬ
			|						ТаблицаЗапасов.ВидЗапасов
			|					ИЗ
			|						ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыПринятые
			|
			|ГДЕ
			|	ТоварыПринятые.КоличествоОстаток > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	АналитикиУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
			|	АналитикиУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикиУчетаНоменклатуры.Назначение
			|	КОНЕЦ                                     КАК Назначение,
			|	АналитикиУчетаНоменклатуры.Серия          КАК Серия,
			|	ТоварыПринятые.НомерГТД                   КАК НомерГТД,
			|	АналитикиУчетаНоменклатуры.Склад          КАК Склад,
			|	СУММА(ТоварыПринятые.КоличествоОстаток)   КАК КоличествоОсталосьПодобрать,
			|	СУММА(ТоварыПринятые.КоличествоОстаток)   КАК КоличествоОстаток
			|ИЗ
			|	ТаблицаПринятыхТоваров КАК ТоварыПринятые
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиУчетаНоменклатуры
			|		ПО ТоварыПринятые.АналитикаУчетаНоменклатуры = АналитикиУчетаНоменклатуры.Ссылка
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
			|		ПО АналитикиУчетаНоменклатуры.Номенклатура = Товары.Ссылка
			|ГДЕ
			|	АналитикиУчетаНоменклатуры.Склад В(&Склады)
			|
			|	СГРУППИРОВАТЬ ПО
			|	АналитикиУчетаНоменклатуры.Номенклатура,
			|	АналитикиУчетаНоменклатуры.Характеристика,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикиУчетаНоменклатуры.Назначение
			|	КОНЕЦ,
			|	АналитикиУчетаНоменклатуры.Серия,
			|	ТоварыПринятые.НомерГТД,
			|	АналитикиУчетаНоменклатуры.Склад
			|
			|УПОРЯДОЧИТЬ ПО
			|	Склад,
			|	Номенклатура,
			|	Характеристика,
			|	Серия,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикиУчетаНоменклатуры.Назначение
			|	КОНЕЦ,
			|	НомерГТД";
			
		КонецЕсли;
		
	ИначеЕсли Объект.РежимОстатков = "Выбывшие" Тогда
		// Вариант подбора товаров в документ "Выкуп товаров принятых на хранение".
		Если Не ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РезервыТоваровОрганизаций) Тогда
			
			Объект.Товары.Очистить();
			Возврат;
			
		Иначе
			
			ТекстЗапроса = "
			|ВЫБРАТЬ
			|	ВидыЗапасов.Ссылка КАК ВидЗапасов
			|
			|ПОМЕСТИТЬ ТаблицаЗапасов
			|ИЗ
			|	Справочник.ВидыЗапасов КАК ВидыЗапасов
			|
			|ГДЕ
			|	ВидыЗапасов.ВладелецТовара = &Партнер
			|	И ВидыЗапасов.Организация  = &Организация
			|	И ВидыЗапасов.Договор      = &Договор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТоварыВыбывшие.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыВыбывшие.Организация                КАК Организация,
			|	ТоварыВыбывшие.ВидЗапасов                 КАК ВидЗапасов,
			|	ТоварыВыбывшие.НомерГТД                   КАК НомерГТД,
			|	-ТоварыВыбывшие.КоличествоОстаток         КАК КоличествоОстаток
			|
			|ПОМЕСТИТЬ ТаблицаВыбывшихТоваров
			|ИЗ
			|	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(&Период,
			|			Организация = &Организация
			|				И ВидЗапасов В
			|					(ВЫБРАТЬ
			|						ТаблицаЗапасов.ВидЗапасов
			|					ИЗ
			|						ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыВыбывшие
			|
			|ГДЕ
			|	-ТоварыВыбывшие.КоличествоОстаток > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	АналитикиУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
			|	АналитикиУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикиУчетаНоменклатуры.Назначение
			|	КОНЕЦ                                     КАК Назначение,
			|	АналитикиУчетаНоменклатуры.Серия          КАК Серия,
			|	ТоварыВыбывшие.НомерГТД                   КАК НомерГТД,
			|	СУММА(ТоварыВыбывшие.КоличествоОстаток)   КАК КоличествоОсталосьПодобрать,
			|	СУММА(ТоварыВыбывшие.КоличествоОстаток)   КАК КоличествоОстаток
			|ИЗ
			|	ТаблицаВыбывшихТоваров КАК ТоварыВыбывшие
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиУчетаНоменклатуры
			|		ПО ТоварыВыбывшие.АналитикаУчетаНоменклатуры = АналитикиУчетаНоменклатуры.Ссылка
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
			|		ПО АналитикиУчетаНоменклатуры.Номенклатура = Товары.Ссылка
			|ГДЕ
			|	АналитикиУчетаНоменклатуры.Склад В(&Склады)
			|
			|	СГРУППИРОВАТЬ ПО
			|	АналитикиУчетаНоменклатуры.Номенклатура,
			|	АналитикиУчетаНоменклатуры.Характеристика,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикиУчетаНоменклатуры.Назначение
			|	КОНЕЦ,
			|	АналитикиУчетаНоменклатуры.Серия,
			|	ТоварыВыбывшие.НомерГТД
			|
			|УПОРЯДОЧИТЬ ПО
			|	Номенклатура,
			|	Характеристика,
			|	Серия,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикиУчетаНоменклатуры.Назначение
			|	КОНЕЦ,
			|	НомерГТД";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	ДатаПериода = КонецДня(ТекущаяДатаСеанса());
	Период = Новый Граница(ДатаПериода, ВидГраницы.Включая);
	
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Партнер",     Объект.Владелец);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Склады",      Склады);
	Запрос.УстановитьПараметр("Договор",     Объект.Договор);
	
	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ЗаполнитьТоварыПодобраннымиТоварами();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПодобранныеТовары()
	
	Если Не (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровХранящихсяНаСкладе) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("Номенклатура, Характеристика, Назначение, Серия, НомерГТД, Склад");
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		
		Если СтрокаТоваров.Пометка
			Или СтрокаТоваров.КоличествоПодобрано > 0 Тогда
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТоваров);
			
			СтрокиПодобранныхТоваров = ПодобранныеТовары.НайтиСтроки(Отбор);
			
			Если СтрокиПодобранныхТоваров.Количество() = 0 Тогда
				НоваяСтрока = ПодобранныеТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
			Иначе
				ЗаполнитьЗначенияСвойств(СтрокиПодобранныхТоваров[0], СтрокаТоваров);
			КонецЕсли;
			
		ИначеЕсли Не СтрокаТоваров.Пометка
			И СтрокаТоваров.КоличествоПодобрано = 0 Тогда
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТоваров);
			
			СтрокиПодобранныхТоваров = ПодобранныеТовары.НайтиСтроки(Отбор);
			
			Если СтрокиПодобранныхТоваров.Количество() > 0 Тогда
				ПодобранныеТовары.Удалить(СтрокиПодобранныхТоваров[0]);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыПодобраннымиТоварами()
	
	Если Не (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровХранящихсяНаСкладе)
		Или Объект.Товары.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Отбор = Новый Структура("Номенклатура, Характеристика, Назначение, Серия, НомерГТД, Склад");
	
	Для Каждого СтрокаПодобранныхТоваров Из ПодобранныеТовары Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаПодобранныхТоваров);
		
		СтрокиТоваров = Объект.Товары.НайтиСтроки(Отбор);
		
		Если СтрокиТоваров.Количество() > 0 Тогда
			ИзменяемаяСтрока = СтрокиТоваров[0];
			ЗаполнитьЗначенияСвойств(ИзменяемаяСтрока, СтрокаПодобранныхТоваров);
			
			Если ИзменяемаяСтрока.Пометка Тогда
				
				ИзменяемаяСтрока.КоличествоОсталосьПодобрать = ИзменяемаяСтрока.КоличествоОстаток
					- ИзменяемаяСтрока.КоличествоПодобрано;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиФормыНаСервере()
	
	КлючОбъекта = "Обработка_ПодборТоваровПринятыхНаОтветственноеХранилище";
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, "", Объект.РежимОстатков);
	
КонецПроцедуры

&НаСервере
Функция АдресТоваровВХранилище()
	
	Товары = Объект.Товары.Выгрузить();
	ОтобранныеТовары = Товары.СкопироватьКолонки();
	
	Отбор = Новый Структура("Пометка", Истина);
	СтрокиТоваров = Товары.НайтиСтроки(Отбор);
	
	Для Каждого Строка Из СтрокиТоваров Цикл
		НоваяСтрока = ОтобранныеТовары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ОтобранныеТовары, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура РежимОстатковПриИзмененииНаСервере()
	
	ЗаполнитьТаблицуТовары();
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РасчитатьКоличествоВСтроке(Товары, ТекущиеДанные, ФормаМодифицирована)
	
	Если ТекущиеДанные.Пометка Тогда
		ТекущиеДанные.КоличествоОсталосьПодобрать = ТекущиеДанные.КоличествоОстаток -ТекущиеДанные.КоличествоПодобрано;
	Иначе
		ТекущиеДанные.КоличествоОсталосьПодобрать = ТекущиеДанные.КоличествоОстаток;
	КонецЕсли;
	
	Отбор = Новый Структура("Пометка", Истина);
	ПомеченныеСтроки = Товары.НайтиСтроки(Отбор);
	
	Если ПомеченныеСтроки.Количество() = 0 Тогда
		ФормаМодифицирована = Ложь;
	Иначе
		ФормаМодифицирована = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РазрешитьПереносРезультатовВДокумент()
	
	Возврат ПроверитьЗаполнение();
	
КонецФункции

&НаСервере
Процедура ЗаполнитьКоличествоНаСервере()
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.КоличествоПодобрано = СтрокаТоваров.КоличествоОстаток;
		СтрокаТоваров.Пометка = Истина;
		
		РасчитатьКоличествоВСтроке(Объект.Товары, СтрокаТоваров, ФормаМодифицирована);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьВсеНаСервере()
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.Пометка = Истина;
		
		РасчитатьКоличествоВСтроке(Объект.Товары, СтрокаТоваров, ФормаМодифицирована);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СнятьПометкуНаСервере()
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.Пометка = Ложь;
		
		РасчитатьКоличествоВСтроке(Объект.Товары, СтрокаТоваров, ФормаМодифицирована);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	ПодобранныеТовары.Очистить();
	ЗаполнитьТаблицуТовары();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИнформационнуюНадписьОтборы()
	
	Если Не ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь() Тогда
		ИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Организация%, %Партнер%, %Договор%, %Склад%'");
	Иначе
		ИнформационнаяНадписьОтборы = НСтр("ru='Отбор по: %Договор%, %Склад%'");
	КонецЕсли;
	
	Если Элементы.ТоварыСклад.Видимость Тогда
		НадписьСклад = НСтр("ru='группе складов: ""%Склад%""'");
		НадписьСклад = СтрЗаменить(НадписьСклад, "%Склад%", Объект.Склад);
	ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов")
			Или ТипЗнч(Объект.Склад) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			
		НадписьСклад = Объект.Склад;
		
	Иначе
		НадписьСклад = "";
	КонецЕсли;
	
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Организация%", Объект.Организация);
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Партнер%",     Объект.Владелец);
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Договор%",     Объект.Договор);
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Склад%",       НадписьСклад);
	
	Если Прав(ИнформационнаяНадписьОтборы, 2) = ", " Тогда
		ИнформационнаяНадписьОтборы = Лев(ИнформационнаяНадписьОтборы, СтрДлина(ИнформационнаяНадписьОтборы) - 2);
	КонецЕсли;
	
	ИнформационнаяНадписьОтборы = ИнформационнаяНадписьОтборы + ".";
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти
