&НаКлиенте
Перем ОбновитьИнтерфейс;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьСложнуюНастройкуАдресовCsmSvc = ИспользуетсяСложнаяНастройкаАдресовCsmSvc();
	
	// Обновление состояния элементов.
	УстановитьДоступность();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда 
		#Если Не ВебКлиент Тогда
		ОбновитьИнтерфейсПрограммы();
		#КонецЕсли
		
		// Обновим адрес CsmService
		КлиентскаяСтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
		ItobОперативныйМониторингКлиентСервер.ОпределитьАдресCsmSvc(КлиентскаяСтрокаСоединения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьСложнуюНастройкуАдресовCsmSvcПриИзменении(Элемент)
	УстановитьДоступность("ИспользоватьСложнуюНастройкуАдресовCsmSvc");		
КонецПроцедуры

&НаКлиенте
Процедура АдресCsmServiceПриИзменении(Элемент)
	ЗаписатьПростойАдресCsmService();	
КонецПроцедуры

&НаКлиенте
Процедура ПортCsmServiceПриИзменении(Элемент)
	ЗаписатьПростойАдресCsmService();
КонецПроцедуры

&НаКлиенте
Процедура ItobИспользоватьДляВсехIHTSПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ItobАдресIHTSПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ItobСервисОбратногоГеокодированияПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ItobКлючMapquestПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "ПараметрыСоединенияIMCS"
//
&НаКлиенте
Процедура ПараметрыСоединенияIMCS(Команда)
	ОткрытьФорму("ОбщаяФорма.ItobПараметрыСоединенияIMCS",,,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	Результат = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		#Если Не ВебКлиент Тогда
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 1, Истина);
		#КонецЕсли
	КонецЕсли;
	
	ItobРаботаС_БСП_Клиент.ПоказатьРезультатВыполнения(ЭтотОбъект, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ItobРаботаС_БСП_Клиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Вызов сервера

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	Результат = Новый Структура;
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат);
	
	УстановитьДоступность(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьПростойАдресCsmService()

	Если НЕ ИспользоватьСложнуюНастройкуАдресовCsmSvc Тогда
		// Повторно заполним регистр ItobСервисыCsmSvc
		НаборЗаписей = РегистрыСведений.ItobСервисыCsmSvc.СоздатьНаборЗаписей();
		
		НовЗапись = НаборЗаписей.Добавить();
		НовЗапись.ВидОбращения = Перечисления.ItobВидыОбращенияCsmSvc.Сервер;
		НовЗапись.АдресCsmService = АдресCsmService;
		НовЗапись.ПортCsmService = ПортCsmService;
		
		НовЗапись = НаборЗаписей.Добавить();
		НовЗапись.ВидОбращения = Перечисления.ItobВидыОбращенияCsmSvc.Клиент;
		НовЗапись.АдресCsmService = АдресCsmService;
		НовЗапись.ПортCsmService = ПортCsmService;
		
		НаборЗаписей.Записать(Истина);	
	
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаСервере
Процедура СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат;
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 14)) = НРег("НаборКонстант.") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 15);
	Иначе
		// Определение имени и запись значения реквизита в соответствующей константе из "НаборКонстант".
		// Используется для тех реквизитов формы, которые связаны с константами напрямую (в отношении один-к-одному).
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = НаборКонстант[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
		
		ItobРаботаС_БСП_КлиентСервер.РезультатВыполненияДобавитьОповещениеОткрытыхФорм(Результат, "Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступность(РеквизитПутьКДанным = "")
	
	Если РеквизитПутьКДанным = "ИспользоватьСложнуюНастройкуАдресовCsmSvc" ИЛИ РеквизитПутьКДанным = "" Тогда
		ItobРаботаС_БСП_КлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "АдресCsmService", "Видимость", НЕ ИспользоватьСложнуюНастройкуАдресовCsmSvc);
		ItobРаботаС_БСП_КлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "ПортCsmService", "Видимость", НЕ ИспользоватьСложнуюНастройкуАдресовCsmSvc);	
		ItobРаботаС_БСП_КлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "СервисыCsmSvc", "Видимость", ИспользоватьСложнуюНастройкуАдресовCsmSvc);
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "НаборКонстант.ItobИспользоватьДляВсехIHTS" ИЛИ РеквизитПутьКДанным = "" Тогда
		ItobРаботаС_БСП_КлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "ItobАдресIHTS", "Доступность", НаборКонстант.ItobИспользоватьДляВсехIHTS);			
	КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Функция ИспользуетсяСложнаяНастройкаАдресовCsmSvc()
 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ItobСервисыCsmSvc.ВидОбращения,
	               |	ItobСервисыCsmSvc.СерверОбращения,
	               |	ItobСервисыCsmSvc.АдресCsmService,
	               |	ItobСервисыCsmSvc.ПортCsmService
	               |ИЗ
	               |	РегистрСведений.ItobСервисыCsmSvc КАК ItobСервисыCsmSvc";
				   
	ТабЗаписей = Запрос.Выполнить().Выгрузить();
	
	Результат = Истина;
	Если ТабЗаписей.Количество()=0 Тогда
		Результат = Ложь;
		
	ИначеЕсли ТабЗаписей.Количество()=2 Тогда
		Если ТабЗаписей[0].СерверОбращения = ТабЗаписей[1].СерверОбращения
			И ПустаяСтрока(ТабЗаписей[0].СерверОбращения)
			И ТабЗаписей[0].АдресCsmService = ТабЗаписей[1].АдресCsmService
			И ТабЗаписей[0].ПортCsmService = ТабЗаписей[1].ПортCsmService Тогда
			
			АдресCsmService = ТабЗаписей[0].АдресCsmService;
			ПортCsmService  =  ТабЗаписей[0].ПортCsmService;
			
			Результат = Ложь;
		
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Результат;	

КонецФункции // ИспользуетсяСложнаяНастройкаАдресовCsmSvc()


#КонецОбласти