
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		АвтоТест = Истина;
	КонецЕсли;

	уатЗащищенныеФункцииСервер.уатОбработкаФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяОбработки = РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	
	//ПодключаемоеОборудование
	уатОбщегоНазначения_проф.НастроитьПодключаемоеОборудование(ЭтаФорма);
	//Конец ПодключаемоеОборудование
	
	ЗаполнитьСписокТипов();
	
	ЦветНажатойКнопки = Новый Цвет(230, 230, 210);
	ЦветФонаКнопки = ЦветаСтиля.ЦветФонаКнопки;
	
	УстановитьОграниченияДляРолиВодитель();
	
	Элементы.ШапкаАвторизации.ТекущаяСтраница = Элементы.ШапкаАвторизацииНадпись;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Отказ = Истина;
		Возврат;
	#КонецЕсли
	
	УстановитьЗаголовокФормы();
	
	//ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода, СчитывательМагнитныхКарт");
	//Конец ПодключаемоеОборудование
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбработкаОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПередЗавершениемРаботыСистемы" Тогда
		НужноЗакрытьФорму = ИСТИНА;
		Закрыть();
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование"
		//И ВводДоступен()
		Тогда
		
		Если ИмяСобытия = "ScanData" Тогда
			ОбработатьШтрихкод(Параметр);
		ИначеЕсли ИмяСобытия = "TracksData" Тогда
			ОбработатьМагнитныйКод(Параметр);
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	уатОбщегоНазначенияКлиент.ОбработкаВнешнегоСобытия(Источник, Событие, Данные);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда 
		НужноЗакрытьФорму = Истина;
		Возврат;
		
	ИначеЕсли Не АвтоТест Тогда
		Если НужноЗакрытьФорму <> Истина Тогда
			Отказ = Истина;
			СтандартнаяОбработка = Ложь;
			Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, "Терминал будет закрыт. Продолжить?", РежимДиалогаВопрос.ДаНет, 0);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда 
		Возврат;
	Иначе 
		МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутевойЛист_ДатаВыездаПриИзменении(Элемент)
	ИзмениласьДатаВыезда = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛистВремяВыездаПриИзменении(Элемент)
	ИзмениласьДатаВыезда = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛистДатаВозвращенияПриИзменении(Элемент)
	ИзмениласьДатаВозврата = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛистВремяВозвращенияПриИзменении(Элемент)
	ИзмениласьДатаВозврата = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Прицепы_Прицеп1ПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп1) Тогда
		ПриИзмененииПрицепа(1, ПутевойЛист_Прицепы_Прицеп1);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Прицепы_Прицеп2ПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп2) Тогда
		ПриИзмененииПрицепа(2, ПутевойЛист_Прицепы_Прицеп2);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Прицепы_Прицеп3ПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп3) Тогда
		ПриИзмененииПрицепа(3, ПутевойЛист_Прицепы_Прицеп3);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Прицепы_Прицеп4ПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп4) Тогда
		ПриИзмененииПрицепа(4, ПутевойЛист_Прицепы_Прицеп4);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_ВыполненоТумблерПриИзменении(Элемент)
	ПутевойЛист_Задание_Выполнено = НЕ ПутевойЛист_Задание_Выполнено;
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].Выполнено = ПутевойЛист_Задание_Выполнено;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_УсловиеРаботыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].УсловиеРаботы = ВыбранноеЗначение;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_УсловиеРаботыПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].УсловиеРаботы = ПутевойЛист_Задание_УсловиеРаботы;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_КоличествоКмГрузПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].КоличествоКмГруз = ПутевойЛист_Задание_КоличествоКмГруз;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_КоличествоКмПорожнийПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].КоличествоКмПорожний = ПутевойЛист_Задание_КоличествоКмПорожний;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_КоличествоЧасовПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].КоличествоЧасов = ПутевойЛист_Задание_КоличествоЧасов;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_КоличествоЕздокПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].КоличествоЕздок = ПутевойЛист_Задание_КоличествоЕздок;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_КоличествоОперацийПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].КоличествоОпераций = ПутевойЛист_Задание_КоличествоОпераций;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Задание_КоличествоТоннПриИзменении(Элемент)
	ПутевойЛист_Задание[ПутевойЛист_Задание_НомерСтроки-1].КоличествоТонн = ПутевойЛист_Задание_КоличествоТонн;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_Дата1ПриИзменении(Элемент)
	ПутевойЛист_Простои_ДатаПриИзменении(1);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_Дата2ПриИзменении(Элемент)
	ПутевойЛист_Простои_ДатаПриИзменении(2);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_Дата3ПриИзменении(Элемент)
	ПутевойЛист_Простои_ДатаПриИзменении(3);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_Дата4ПриИзменении(Элемент)
	ПутевойЛист_Простои_ДатаПриИзменении(4);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_Дата5ПриИзменении(Элемент)
	ПутевойЛист_Простои_ДатаПриИзменении(5);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_Дата6ПриИзменении(Элемент)
	ПутевойЛист_Простои_ДатаПриИзменении(6);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ВидЗаправкиПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].ВидЗаправки = ПутевойЛист_Заправки_ВидЗаправки;
	ПриИзмененииВидаЗаправки();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ВидЗаправкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_АЗСПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].АЗС = ПутевойЛист_Заправки_АЗС;
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_АЗСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПутевойЛист_Заправки_АЗСПриИзменении(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ТСПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].ТС = ПутевойЛист_Заправки_ТС;
	Если ЗначениеЗаполнено(ПутевойЛист_Заправки_ТС) Тогда
		Если ПутевойЛист_Заправки_ВидЗаправки <> ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
			ПутевойЛист_Заправки_ГСМ = ПолучитьЗначениеРеквизита(ПолучитьЗначениеРеквизита(ПутевойЛист_Заправки_ТС, "уатМодель"), "ОсновноеТопливо");
			ПутевойЛист_Заправки_ГСМПриИзменении(Неопределено);
						
			Если ПутевойЛист_Заправки_ВидЗаправки =	ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКарта")
				ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад") Тогда
				ПутевойЛист_Заправки_ПластиковаяКарта = уатОбщегоНазначения.уатПластиковаяКартаТСВодителя(ПутевойЛист_Заправки_ТС, Водитель);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	ПроверитьЗаполнениеЗаправкиПЛ();
	ОбновитьСписокДоступныхПластиковыхКарт();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ГСМПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].ГСМ = ПутевойЛист_Заправки_ГСМ;
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ГСМОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].ГСМ = ПутевойЛист_Заправки_ГСМ;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ГСМАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	мсвГруппДляОтбора = Новый Массив;
	Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
		мсвГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Талон"));
		мУчитыватьТЖ = Ложь;
	Иначе	
		мсвГруппДляОтбора.Добавить(ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Топливо"));
		мУчитыватьТЖ = Истина;
	КонецЕсли;
	ДопПараметры = Новый Структура("ТС, Организация, УчитыватьТЖ", ПутевойЛист_Заправки_ТС, ПутевойЛист_Организация, мУчитыватьТЖ);
	ДанныеВыбора = уатГСМ.ПолучитьСписокАвтоподбораПоляГСМ(Текст, мсвГруппДляОтбора, ДопПараметры);
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ГСМОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	ПараметрыПолученияДанных.СтрокаПоиска = "";
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ПластикКартаПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].ПластиковаяКарта = ПутевойЛист_Заправки_ПластиковаяКарта;
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ПластикКартаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].ПластиковаяКарта = ПутевойЛист_Заправки_ПластиковаяКарта;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_ТалоныПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].Талоны = ПутевойЛист_Заправки_Талоны;
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_КоличествоПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].Количество = ПутевойЛист_Заправки_Количество;
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Заправки_СуммаПриИзменении(Элемент)
	ПутевойЛист_Заправки[ПутевойЛист_Заправки_НомерСтроки-1].Сумма = ПутевойЛист_Заправки_Сумма;
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПутевогоЛистаПриСменеСтраницы(Элемент, ТекущаяСтраница)
	//Элементы.ГруппаКлавиатура.Доступность = Ложь;
	ТекЭлементФормыИмя = "";
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_СписокДокументов

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВыбратьПутевойЛистИзСписка(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПриАктивизацииСтроки(Элемент)
	ТекПЛ = Элементы.СписокДокументов.ТекущиеДанные;
	Если ТекПЛ = Неопределено Тогда
		Элементы.ВыбратьПутевойЛистИзСпискаПечать.Заголовок = "Печать";
	Иначе
		Элементы.ВыбратьПутевойЛистИзСпискаПечать.Заголовок = "Печать " + ПолучитьКраткоеИмяПФ(ТекПЛ.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПутевойЛист(Команда)
	РежимРаботыОбработки = 0;
	
	мсвПЛ = СписокПЛВодителя(Водитель);
	
	КоличОткрытыхПЛ = мсвПЛ.Количество();
	Если КоличОткрытыхПЛ = 0 Тогда
		Элементы.СтраницыСтатусВыборРежима.ТекущаяСтраница = Элементы.СтатусРежимОшибка;
		Возврат;
	КонецЕсли;
			
	Если КоличОткрытыхПЛ = 1 Тогда
		ПечатьПЛ(мсвПЛ[0]);
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСписокПЛ;
		Элементы.СтраницыКнопкаВыбратьИзСписка.ТекущаяСтраница = Элементы.СтраницыКнопкаВыбратьИзСпискаПечать;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПутевойЛист(Команда)
	РежимРаботыОбработки = 1;
	
	мсвПЛ = СписокПЛВодителя(Водитель);
	
	КоличОткрытыхПЛ = мсвПЛ.Количество();
	Если КоличОткрытыхПЛ = 0 Тогда
		Элементы.СтраницыСтатусВыборРежима.ТекущаяСтраница = Элементы.СтатусРежимОшибка;
		Возврат;
	КонецЕсли;
	
	Если КоличОткрытыхПЛ = 1 Тогда
		НачатьЗакрытиеПутевогоЛиста(мсвПЛ[0]);
	Иначе
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСписокПЛ;
		Элементы.СтраницыКнопкаВыбратьИзСписка.ТекущаяСтраница = Элементы.СтраницыКнопкаВыбратьИзСпискаВыбрать;
		Элементы.СписокДокументов.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Выход(Команда)
	ДиалогОКОтмена("Завершить работу и перейти к авторизации?", Новый ОписаниеОповещения("ВыходПродолжение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПутевойЛистИзСписка(Команда)
	ТекПЛ = Элементы.СписокДокументов.ТекущаяСтрока;
	Если ТекПЛ = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если РежимРаботыОбработки = 0 Тогда
		ПечатьПЛ(ТекПЛ);
	Иначе
		НачатьЗакрытиеПутевогоЛиста(ТекПЛ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПЛназад(Команда)
	ДиалогОКОтмена("Вернуться к выбору режима?", Новый ОписаниеОповещения("СписокПЛназадПродолжение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура НазадПоПЛ(Команда)
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗавершено Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПутевойЛист;
	Иначе
		Если Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗадание Тогда
			Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаШапка;
			Элементы.ГруппаКнопкаНазад.ТекущаяСтраница = Элементы.СтраницаКнопкаОтмена;
		ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаПростои Тогда
			Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗадание;
		ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗаправки Тогда
			Если НЕ ПроверитьЗаполнениеЗаправкиПЛ() Тогда
				Возврат;
			КонецЕсли;
			Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаПростои;
			Элементы.ГруппаКнопкаВперед.ТекущаяСтраница = Элементы.СтраницаДалее;
		КонецЕсли;
		
		ОбновитьНадписьНавигации();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВпередПоПЛ(Команда)
	Если Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаШапка Тогда
		//проверка корректности периода ПЛ
		Если ПутевойЛистДатаВремяВыезда() > ПутевойЛистДатаВремяВозвращения() Тогда
			НадписьНекорректноеЗаполнениеШапки = "*Указан некорректный период";
			Возврат;
		Иначе
			НадписьНекорректноеЗаполнениеШапки = "";
		КонецЕсли;
		Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗадание;
		Элементы.ГруппаКнопкаНазад.ТекущаяСтраница = Элементы.СтраницаКнопкаНазад;
		
	ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗадание Тогда
		Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаПростои;
		
	ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаПростои Тогда
		//проверка корректности периода простоя
		НадписьНекорректноеЗаполнениеПростоев = "";
		Для НомерПростоя = 1 По 6 Цикл
			Если НЕ Элементы["Простой" + НомерПростоя].Видимость Тогда
				Прервать;
			КонецЕсли;
			
			НачПростоя = ЭтаФорма["ПутевойЛист_Простои_Начало" + НомерПростоя];
			КонПростоя = ЭтаФорма["ПутевойЛист_Простои_Конец" + НомерПростоя];
			Если КонПростоя <> НачалоДня(КонПростоя) И НачПростоя > КонПростоя Тогда
				НадписьНекорректноеЗаполнениеПростоев = "*Указан некорректный период в строке " + НомерПростоя;
				Прервать;
			КонецЕсли;
			
			КонПростоя_ = ?(КонПростоя = НачалоДня(КонПростоя), КонецДня(КонПростоя), КонПростоя);
			
			Если ПутевойЛистДатаВремяВыезда() <> '00010101' И ПутевойЛистДатаВремяВозвращения() <> '00010101'
				И (НачПростоя < ПутевойЛистДатаВремяВыезда() ИЛИ КонПростоя_ > ПутевойЛистДатаВремяВозвращения()) Тогда
				НадписьНекорректноеЗаполнениеПростоев = "*Период простоя в строке " + НомерПростоя + " выходит за границы путевого листа";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НадписьНекорректноеЗаполнениеПростоев <> "" Тогда
			Возврат;
		КонецЕсли;
			
		Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗаправки;
		Элементы.ГруппаКнопкаВперед.ТекущаяСтраница = Элементы.СтраницаЗакрыть;
		
	КонецЕсли;
	
	ОбновитьНадписьНавигации();
КонецПроцедуры

&НаКлиенте
Процедура НеСогласенСПробегомGPS(Команда)
	Элементы.ГруппаСтраницыСпидометрВозврат.ТекущаяСтраница = Элементы.ГруппаСтраницаСпидометрВозвратGPSнеСогласен;
	ВодительНеСогласенСПробегомGPS = Истина;
	ПутевойЛист_СпидометрВозвращенияВодитель = 0;
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_СпидометрВозвращенияВодитель", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СогласенСПробегомGPS(Команда)
	Элементы.ГруппаСтраницыСпидометрВозврат.ТекущаяСтраница = Элементы.ГруппаСтраницаСпидометрВозвратGPS;
	ВодительНеСогласенСПробегомGPS = Ложь;
	ПутевойЛист_СпидометрВозвращенияВодитель = 0;
КонецПроцедуры

&НаКлиенте
Процедура НеСогласенСОстаткомТопливаGPS(Команда)
	Элементы.ГруппаСтраницыТопливоВозврат.ТекущаяСтраница = Элементы.ГруппаСтраницыТопливоВозвратGPSнеСогласен;
	ВодительНеСогласенСРасходомТопливаGPS = Истина;
	ПутевойЛист_ОстатокТопливаВозвратВодитель = 0;
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_ОстатокТопливаВозвратВодитель", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СогласенСОстаткомТопливаGPS(Команда)
	Элементы.ГруппаСтраницыТопливоВозврат.ТекущаяСтраница = Элементы.ГруппаСтраницыТопливоВозвратGPS;
	ВодительНеСогласенСРасходомТопливаGPS = Ложь;
	ПутевойЛист_ОстатокТопливаВозвратВодитель = 0;
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКВыборуРежима(Команда)
	ДиалогОКОтмена("Вернуться к выбору режима?
		|Все несохраненные данные будут утеряны!",
		Новый ОписаниеОповещения("ПерейтиКВыборуРежимаПродолжение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПрицеп(Команда)
	Для Сч = 1 По 4 Цикл
		Если Элементы["Прицеп" + Сч].Видимость Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Элементы.ГруппаПрицепыЗаголовок.Видимость Тогда
			Элементы.ГруппаПрицепыЗаголовок.Видимость = Истина;
		КонецЕсли;
		
		Элементы["Прицеп" + Сч].Видимость = Истина;
		Прервать;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПрицеп1(Команда)
	ВыбратьПрицеп(1);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПрицеп2(Команда)
	ВыбратьПрицеп(2);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПрицеп3(Команда)
	ВыбратьПрицеп(3);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПрицеп4(Команда)
	ВыбратьПрицеп(4);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПрицеп1(Команда)
	УдалитьПрицеп(1);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПрицеп2(Команда)
	УдалитьПрицеп(2);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПрицеп3(Команда)
	УдалитьПрицеп(3);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПрицеп4(Команда)
	УдалитьПрицеп(4);
КонецПроцедуры

&НаКлиенте
Процедура НеСогласенПрицеп1(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен1.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен1GPS;
	ВодительНеСогласенСоСчПрицеп1 = Истина;
	ПутевойЛист_Прицепы_СчВозврата1НеСогласен = 0;
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_СчПрицеп1", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СогласенПрицеп1(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен1.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен1GPS;
	ВодительНеСогласенСоСчПрицеп1 = Ложь;
	ПутевойЛист_Прицепы_СчВозврата1НеСогласен = 0;
КонецПроцедуры

&НаКлиенте
Процедура НеСогласенПрицеп2(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен2.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен2GPS;
	ВодительНеСогласенСоСчПрицеп2 = Истина;
	ПутевойЛист_Прицепы_СчВозврата2НеСогласен = 0;
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_СчПрицеп2", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СогласенПрицеп2(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен2.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен2GPS;
	ВодительНеСогласенСоСчПрицеп2 = Ложь;
	ПутевойЛист_Прицепы_СчВозврата2НеСогласен = 0;
КонецПроцедуры

&НаКлиенте
Процедура НеСогласенПрицеп3(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен3.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен3GPS;
	ВодительНеСогласенСоСчПрицеп3 = Истина;
	ПутевойЛист_Прицепы_СчВозврата3НеСогласен = 0;
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_СчПрицеп3", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СогласенПрицеп3(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен3.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен3GPS;
	ВодительНеСогласенСоСчПрицеп3 = Ложь;
	ПутевойЛист_Прицепы_СчВозврата3НеСогласен = 0;
КонецПроцедуры

&НаКлиенте
Процедура НеСогласенПрицеп4(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен4.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен4GPS;
	ВодительНеСогласенСоСчПрицеп4 = Истина;
	ПутевойЛист_Прицепы_СчВозврата4НеСогласен = 0;
	ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_СчПрицеп4", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура СогласенПрицеп4(Команда)
	Элементы.ГруппаСтраницыПрицепыНеСогласен4.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен4GPS;
	ВодительНеСогласенСоСчПрицеп4 = Ложь;
	ПутевойЛист_Прицепы_СчВозврата4НеСогласен = 0;
КонецПроцедуры

&НаКлиенте
Процедура Кнопка1(Команда)
	ОбработкаНажатияНаКнопку("1");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка2(Команда)
	ОбработкаНажатияНаКнопку("2");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка3(Команда)
	ОбработкаНажатияНаКнопку("3");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка4(Команда)
	ОбработкаНажатияНаКнопку("4");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка5(Команда)
	ОбработкаНажатияНаКнопку("5");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка6(Команда)
	ОбработкаНажатияНаКнопку("6");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка7(Команда)
	ОбработкаНажатияНаКнопку("7");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка8(Команда)
	ОбработкаНажатияНаКнопку("8");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка9(Команда)
	ОбработкаНажатияНаКнопку("9");
КонецПроцедуры

&НаКлиенте
Процедура Кнопка0(Команда)
	ОбработкаНажатияНаКнопку("0");
КонецПроцедуры

&НаКлиенте
Процедура КнопкаЗапятая(Команда)
	ИзменитьРежимВводДробныхЗначений();
КонецПроцедуры

&НаКлиенте
Процедура КнопкаС(Команда)
	ОбработкаНажатияНаКнопку("С");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗадание(Команда)
	НоваяСтрока = ПутевойЛист_Задание.Добавить();
	НоваяСтрока.КоличествоЕздок = 1;
	
	ОтобразитьСтрокуЗадания(ПутевойЛист_Задание.Количество());
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗадание(Команда)
	ДиалогОКОтмена("Удалить строку " + ПутевойЛист_Задание_НомерСтроки + " задания?",
		Новый ОписаниеОповещения("УдалитьЗаданиеПродолжение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущееЗадание(Команда)
	Если ПутевойЛист_Задание_НомерСтроки < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьСтрокуЗадания(ПутевойЛист_Задание_НомерСтроки - 1);
КонецПроцедуры

&НаКлиенте
Процедура СледующееЗадание(Команда)
	Если ПутевойЛист_Задание_НомерСтроки = ПутевойЛист_Задание.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьСтрокуЗадания(ПутевойЛист_Задание_НомерСтроки + 1);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьУсловиеРаботы(Команда)
	ОповещениеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораУсловиеРаботы", ЭтотОбъект);
	ОткрытьФорму("Обработка.уатТерминалПриемаИВыдачиПутевыхЛистов.Форма.ФормаВыбораСправочников",
		Новый Структура("ИмяОбъектаВыбора", "УсловиеРаботы"),
		ЭтаФорма,,,, ОповещениеВыбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПростой(Команда)
	Для Сч = 1 По 6 Цикл
		Если Элементы["Простой" + Сч].Видимость Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ Элементы.ГруппаПростоиЗаголовок.Видимость Тогда
			Элементы.ГруппаПростоиЗаголовок.Видимость = Истина;
		КонецЕсли;
		
		Элементы["Простой" + Сч].Видимость = Истина;
		
		//заполнение реквизитов по-умолчанию
		ЭтаФорма["ПутевойЛист_Простои_Начало" + Сч] = ПутевойЛистДатаВремяВыезда();
		ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч] = ПутевойЛистДатаВремяВыезда();
		
		Прервать;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой1(Команда)
	ВыбратьПростой(1);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой2(Команда)
	ВыбратьПростой(2);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой3(Команда)
	ВыбратьПростой(3);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой4(Команда)
	ВыбратьПростой(4);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой5(Команда)
	ВыбратьПростой(5);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой6(Команда)
	ВыбратьПростой(6);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой1(Команда)
	УдалитьПростой(1);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой2(Команда)
	УдалитьПростой(2);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой3(Команда)
	УдалитьПростой(3);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой4(Команда)
	УдалитьПростой(4);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой5(Команда)
	УдалитьПростой(5);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой6(Команда)
	УдалитьПростой(6);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаправку(Команда)
	Если НЕ ПроверитьЗаполнениеЗаправкиПЛ() Тогда
		Возврат;
	КонецЕсли;
	
	ПутевойЛист_Заправки.Добавить();
	ПутевойЛист_Заправки_НомерСтроки = ПутевойЛист_Заправки.Количество();
	
	//заполнение реквизитов по-умолчанию
	ПутевойЛист_Заправки_АЗС = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ПутевойЛист_Организация,
		ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнаяАЗС"));
	ПутевойЛист_Заправки_АЗСПриИзменении(Неопределено);
	ПутевойЛист_Заправки_ВидЗаправки = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ПутевойЛист_Организация,
		ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнойВидЗаправки"));
	ПутевойЛист_Заправки_ВидЗаправкиПриИзменении(Неопределено);
	ПутевойЛист_Заправки_ТС = ПутевойЛист_ТС;
	ПутевойЛист_Заправки_ТСПриИзменении(Неопределено);
		
	ОтобразитьСтрокуЗаправок(ПутевойЛист_Заправки.Количество());
	ПриИзмененииВидаЗаправки();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗаправку(Команда)
	ДиалогОКОтмена("Удалить заправку " + ПутевойЛист_Заправки_НомерСтроки + "?",
		Новый ОписаниеОповещения("УдалитьЗаправкуПродолжение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ПредыдущаяЗаправка(Команда)
	Если ПутевойЛист_Заправки_НомерСтроки < 2 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнениеЗаправкиПЛ() Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьСтрокуЗаправок(ПутевойЛист_Заправки_НомерСтроки - 1);
КонецПроцедуры

&НаКлиенте
Процедура СледующаяЗаправка(Команда)
	Если ПутевойЛист_Заправки_НомерСтроки = ПутевойЛист_Заправки.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнениеЗаправкиПЛ() Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьСтрокуЗаправок(ПутевойЛист_Заправки_НомерСтроки + 1);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидЗаправки(Команда)
	СписВидовЗаправки = Новый СписокЗначений;
	СписВидовЗаправки.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаНаличные"));
	СписВидовЗаправки.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКарта"));
	СписВидовЗаправки.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад"));
	СписВидовЗаправки.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПоставщик"));
	СписВидовЗаправки.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаСклад"));
	СписВидовЗаправки.Добавить(ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны"));
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ВыбратьВидЗаправкиЗавершение", ЭтотОбъект), СписВидовЗаправки, Элементы.ПутевойЛист_Заправки_ВидЗаправки,
		СписВидовЗаправки.НайтиПоЗначению(ПутевойЛист_Заправки_ВидЗаправки));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьАЗС(Команда)
	ЭтоАЗССклад = ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаСклад")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны");
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораАЗС", ЭтотОбъект);
	ОткрытьФорму("Обработка.уатТерминалПриемаИВыдачиПутевыхЛистов.Форма.ФормаВыбораСправочников",
		Новый Структура("ИмяОбъектаВыбора, ЭтоАЗССклад", "АЗС", ЭтоАЗССклад),
		ЭтаФорма,,,, ОповещениеВыбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТСвЗаправке(Команда)
	СписокТС = Новый СписокЗначений;
	Если ЗначениеЗаполнено(ПутевойЛист_ТС) Тогда
		СписокТС.Добавить(ПутевойЛист_ТС);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп1) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп1);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп2) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп2);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп3) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп3);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп4) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп4);
	КонецЕсли;
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ВыбратьТСЗавершение", ЭтотОбъект), СписокТС, Элементы.ПутевойЛист_Заправки_ТС);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьГСМ(Команда)
	//уатЗащищенныеФункцииКлиент.ВыбратьГСМ(ПутевойЛист_Заправки_ГСМ);
	
	Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
		мГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Талон");
	Иначе	
		мГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Топливо");
	КонецЕсли;
	
	ГСМ = Неопределено;
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ВыбратьГСМЗавершение", ЭтотОбъект), СписокГСМдляТС(ПутевойЛист_Организация, ПутевойЛист_ТС, мГруппаГСМ), Элементы.ПутевойЛист_Заправки_ГСМ);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПластиковуюКарту(Команда)
	//ОткрытьФорму("Справочник.уатПластиковыеКарты.ФормаВыбора",, Элементы.ПутевойЛист_Заправки_ПластикКарта);
	
	СписокВладельцев = Новый СписокЗначений;
	СписокВладельцев.Добавить(ПутевойЛист_ТС);
	СписокВладельцев.Добавить(Водитель);
	
	ПластикКарта = Неопределено;

	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ВыбратьПластиковуюКартуЗавершение", ЭтотОбъект), СписокПластикКарт(СписокВладельцев), Элементы.ПутевойЛист_Заправки_ПластикКарта);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьОбъяснительной(Команда)
	ТабДок = СформироватьОбъяснительную();
	//ТабДок.Показать();
	ТабДок.Напечатать();
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПЛЗавершение(Команда)
	ПечатьПЛ(ПутевойЛист_Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура Завершить(Команда)
	Если НЕ ПроверитьЗаполнениеЗаправкиПЛ() Тогда
		Возврат;
	КонецЕсли;
			
	Элементы.ПечатьОбъснительной.Видимость = (ВодительНеСогласенСПробегомGPS ИЛИ ВодительНеСогласенСРасходомТопливаGPS);
	ОбновитьИнформациюГотово();
	
	ИспользованиеGPS = уатОбщегоНазначения_проф.уатПроверитьИспользованиеGPS(ПутевойЛист_ТС);
			
	Если ВодительНеСогласенСПробегомGPS Тогда
		Элементы.ГруппаЗавершениеНеСогласенСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСтраницаНеСогл;
		Если ИспользованиеGPS Тогда
			НадписьСпидометрВозвращенияВодительНеСогласен = "Не согласен со спидометром возвр. по GPS:  ";
		Иначе
			НадписьСпидометрВозвращенияВодительНеСогласен = "Не согласен со спидометром возвр.:  ";
		КонецЕсли;
	Иначе
		Если ИспользованиеGPS Тогда
			Элементы.ГруппаЗавершениеНеСогласенСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСтраницаСоглGPS;
		Иначе
			Элементы.ГруппаЗавершениеНеСогласенСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСтраницаСогл;
		КонецЕсли;
	КонецЕсли;
	
	Если ВодительНеСогласенСРасходомТопливаGPS Тогда
		Элементы.ГруппаЗавершениеНеСогласенСРасходомСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСРасходомСтраницаНеСогл;
		Если ИспользованиеGPS Тогда
			НадписьОстатокВозвращенияВодительНеСогласен = "Не согласен с остатком топл. возвр. по GPS: ";
		Иначе
			НадписьОстатокВозвращенияВодительНеСогласен = "Не согласен с остатком топл. возвр.: ";
		КонецЕсли;
	Иначе
		Если ИспользованиеGPS Тогда
			Элементы.ГруппаЗавершениеНеСогласенСРасходомСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСРасходомСтраницаСоглGPS;
		Иначе
			Элементы.ГруппаЗавершениеНеСогласенСРасходомСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСРасходомСтраницаСогл;
		КонецЕсли;
	КонецЕсли;
	
	Если ВодительНеСогласенСоСчПрицеп1 ИЛИ ВодительНеСогласенСоСчПрицеп2 ИЛИ ВодительНеСогласенСоСчПрицеп3 ИЛИ ВодительНеСогласенСоСчПрицеп4 Тогда
		Элементы.ГруппаЗавершениеНеСогласенПрицепыСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСтраницаПрицепыНеСогл;
		Если ИспользованиеGPS Тогда
			НадписьСчПрицепыВодительНеСогласен = "Не согласен со счетчиками возвр. прицепов/оборуд. по GPS";
		Иначе
			НадписьСчПрицепыВодительНеСогласен = "Не согласен со счетчиками возвр. прицепов/оборуд.";
		КонецЕсли;
	Иначе
		Если ИспользованиеGPS Тогда
			Элементы.ГруппаЗавершениеНеСогласенПрицепыСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСтраницаПрицепыСоглGPS;
		Иначе
			Элементы.ГруппаЗавершениеНеСогласенПрицепыСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершениеНеСогласенСтраницаПрицепыСогл;
		КонецЕсли;
	КонецЕсли;
			
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗавершено;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПЛ(Команда)
	ДиалогОКОтмена("Закрыть путевой лист?", Новый ОписаниеОповещения("ЗакрытьПЛПродолжение", ЭтотОбъект));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СписокПЛВодителя(Водитель)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	уатПутевойЛист.Ссылка
	|ИЗ
	|	Документ.уатПутевойЛист КАК уатПутевойЛист
	|ГДЕ
	|	НЕ уатПутевойЛист.Рассчитан
	|	И уатПутевойЛист.Водитель1 = &Водитель
	|	И уатПутевойЛист.Проведен
	|	И НЕ уатПутевойЛист.ЗакрытВодителем");
	Запрос.УстановитьПараметр("Водитель", Водитель);
	мсвПЛ = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");					  
	
	Возврат мсвПЛ;
КонецФункции

&НаСервере
Процедура ЗагрузитьДанныеПЛвФормуСервер(ПутевойЛист)
	Если Ложь Тогда
		ПутевойЛист = Документы.уатПутевойЛист.СоздатьДокумент();
	КонецЕсли;
	
	//Шапка
	ПутевойЛист_Ссылка = ПутевойЛист;
	ПутевойЛист_Организация = ПутевойЛист.Организация;
	ПутевойЛист_ТС = ПутевойЛист.ТранспортноеСредство;
	ПутевойЛист_НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ПутевойЛист_ТС, ПутевойЛист_Организация);
	ПутевойЛист_МодельТС = ПутевойЛист_ТС.уатМодель;
	ПутевойЛист_ДатаВыезда = ПутевойЛист.ДатаВыезда;
	ПутевойЛист_ВремяВыезда = ПутевойЛист.ДатаВыезда;
	ПутевойЛист_ДатаВозвращения = ПутевойЛист.ДатаВозвращения;
	ПутевойЛист_ВремяВозвращения = ПутевойЛист.ДатаВозвращения;
	ПутевойЛист_СпидометрВыезда = ПутевойЛист.СпидометрВыезда;
	ПутевойЛист_СпидометрВозвращения = ПутевойЛист.СпидометрВозвращения;
	ПутевойЛист_СпидометрВозвращенияВодитель = 0;
	
	ПутевойЛист_ОстатокТопливаВыезд = 0;
	ПутевойЛист_ОстатокТопливаВозврат = 0;
	ПутевойЛист_ОстатокТопливаВозвратВодитель = 0;
	
	ПутевойЛист_ВыдатьКоличествоГорючего = ПутевойЛист.ВыдатьКоличествоГорючего;
	ПутевойЛист_ПробегПоGPS = ПутевойЛист.ПробегПоGPS;
	
	ОснТопливо = ПолучитьЗначениеРеквизита(ПутевойЛист_МодельТС, "ОсновноеТопливо");
	Для Каждого ТекСтрока Из ПутевойЛист.РасходГСМ Цикл
		Если ПутевойЛист_ТС = ТекСтрока.ТС И ОснТопливо = ТекСтрока.ГСМ Тогда
			ПутевойЛист_ОстатокТопливаВыезд = ТекСтрока.ОстатокПриВыезде;
			ПутевойЛист_ОстатокТопливаВозврат = ТекСтрока.ОстатокПриВозвращении;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//Прицепы
	НомСтр = 0;
	Для Каждого ТекСтрока Из ПутевойЛист.Прицепы Цикл
		НомСтр = НомСтр + 1;
		
		Если НомСтр > 4 Тогда
			Прервать;
		КонецЕсли;
		
		ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + НомСтр] = ТекСтрока.ТС;
		ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + НомСтр] = ТекСтрока.СчетчикМЧGPS;
		ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + НомСтр] = ТекСтрока.СчетчикМЧВыезда;
		ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + НомСтр] = ТекСтрока.СчетчикМЧВозвращения;
		ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + НомСтр + "НеСогласен"] = 0;
	КонецЦикла;
	//остальное чистим
	Для Сч = НомСтр+1 По 4 Цикл
		ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч] = Неопределено;
		ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + Сч] = 0;
		ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + Сч] = 0;
		ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч] = 0;
		ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч + "НеСогласен"] = 0;
	КонецЦикла;
	
	//Задание
	НомСтр = 0;
	ПутевойЛист_Задание.Очистить();
	Для Каждого ТекСтрока Из ПутевойЛист.Задание Цикл
		НомСтр = НомСтр + 1;
		
		НоваяСтрока = ПутевойЛист_Задание.Добавить();
		НоваяСтрока.Выполнено = ТекСтрока.Выполнено;
		НоваяСтрока.КоличествоЕздок = ТекСтрока.КоличествоЕздок;
		НоваяСтрока.КоличествоКмГруз = ТекСтрока.КоличествоКмГруз;
		НоваяСтрока.КоличествоКмПорожний = ТекСтрока.КоличествоКмПорожний;
		НоваяСтрока.КоличествоОпераций = ТекСтрока.КоличествоОпераций;
		НоваяСтрока.КоличествоТонн = ТекСтрока.КоличествоТонн;
		НоваяСтрока.КоличествоЧасов = ТекСтрока.КоличествоЧасов;
		НоваяСтрока.УсловиеРаботы = ТекСтрока.УсловиеРаботы;
	КонецЦикла;
		
	//Простои
	НомСтр = 0;
	Для Каждого ТекСтрока Из ПутевойЛист.Простои Цикл
		НомСтр = НомСтр + 1;
		
		Если НомСтр > 6 Тогда
			Прервать;
		КонецЕсли;
		
		ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + НомСтр] = ТекСтрока.ВидПростоя;
		ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + НомСтр] = ТекСтрока.ВключенДвигатель;
		ЭтаФорма["ПутевойЛист_Простои_Начало" + НомСтр] = ТекСтрока.НачалоПростоя;
		ЭтаФорма["ПутевойЛист_Простои_Конец" + НомСтр] = ТекСтрока.КонецПростоя;
	КонецЦикла;
	//остальное чистим
	Для Сч = НомСтр+1 По 6 Цикл
		ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + Сч] = Неопределено;
		ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + Сч] = Неопределено;
		ЭтаФорма["ПутевойЛист_Простои_Начало" + Сч] = Неопределено;
		ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч] = Неопределено;
	КонецЦикла;
	
	//Заправки
	НомСтр = 0;
	ПутевойЛист_Заправки.Очистить();
	тблЗаправки = уатОбщегоНазначения.уатПолучитьЗаправкиДляПЛ(ПутевойЛист);
	Для Каждого ТекСтрока Из тблЗаправки Цикл
		НомСтр = НомСтр + 1;
		
		НоваяСтрока = ПутевойЛист_Заправки.Добавить();
		НоваяСтрока.ТС = ТекСтрока.ТС;
		НоваяСтрока.АЗС = ТекСтрока.АЗС;
		НоваяСтрока.ВидЗаправки = ТекСтрока.ВидДвиженияГСМ;
		НоваяСтрока.ГСМ = ТекСтрока.ГСМ;
		НоваяСтрока.Количество = ТекСтрока.Количество;
		НоваяСтрока.Сумма = ТекСтрока.Сумма;
		НоваяСтрока.ПластиковаяКарта = ТекСтрока.ПластиковаяКарта;
		НоваяСтрока.Талоны = ТекСтрока.НомераТалонов;
		НоваяСтрока.Ссылка = ТекСтрока.Ссылка;
		НоваяСтрока.НомерСтроки = ТекСтрока.НомерСтроки;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция СформироватьОбъяснительную()
	ТабДок = Новый ТабличныйДокумент;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Макет = ОбработкаОбъект.ПолучитьМакет("Объяснительная");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Организация = ПутевойЛист_Организация;
	ОбластьШапка.Параметры.Водитель = Водитель;
	ОбластьШапка.Параметры.ТС = ПутевойЛист_ТС;
	//ОбластьШапка.Параметры.Директор = ;
	ОбластьШапка.Параметры.Дата = ТекущаяДата();
	
	ОбластьШапка.Параметры.Пробег = ПутевойЛист_СпидометрВозвращенияВодитель;
	ОбластьШапка.Параметры.ПробегGPS = ПутевойЛист_СпидометрВозвращения;
	
	ТабДок.Вывести(ОбластьШапка);
	
	Возврат ТабДок;
КонецФункции

&НаСервереБезКонтекста
// Возвращает список документов водителя
//Тип значения: массив, тип элемента массива: структура значений
//
Функция ДокументыВодителя(Водитель, ТекДата) Экспорт
	Рез = Новый СписокЗначений;
		
	ЗапросВодители = Новый Запрос(
	"ВЫБРАТЬ
	|	РегистрСведенийуатДокументыВодителей.ВидДокумента,
	|	РегистрСведенийуатДокументыВодителей.Серия,
	|	РегистрСведенийуатДокументыВодителей.Номер,
	|	РегистрСведенийуатДокументыВодителей.ДатаВыдачи,
	|	РегистрСведенийуатДокументыВодителей.ДатаОкончания,
	|	РегистрСведенийуатДокументыВодителей.КемВыдан,
	|	РегистрСведенийуатДокументыВодителей.Категория,
	|	ВЫБОР
	|		КОГДА РегистрСведенийуатДокументыВодителей.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(РегистрСведенийуатДокументыВодителей.ДатаОкончания, ДЕНЬ, -&ДниДоОкончанияДействияДокументовВодителей) <= &ДатаОкончания
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ДокПросрочен
	|ИЗ
	|	РегистрСведений.уатДокументыВодителей КАК РегистрСведенийуатДокументыВодителей
	|ГДЕ
	|	НЕ РегистрСведенийуатДокументыВодителей.Архив
	|	И РегистрСведенийуатДокументыВодителей.Сотрудник = &Сотрудник
	|	И НЕ РегистрСведенийуатДокументыВодителей.Архив");
	ЗапросВодители.УстановитьПараметр("ДниДоОкончанияДействияДокументовВодителей",
		уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
		ПользователиКлиентСервер.АвторизованныйПользователь(),	"ОсновнаяОрганизация"), ПредопределенноеЗначение(
		"ПланВидовХарактеристик.уатПраваИНастройки.ДниДоОкончанияДействияДокументовВодителей")));
	ЗапросВодители.УстановитьПараметр("ДатаОкончания", ТекДата);
	ЗапросВодители.УстановитьПараметр("Сотрудник", Водитель);
	Выборка = ЗапросВодители.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаПредставлениеДок = "" + Выборка.ВидДокумента;
		
		Если ЗначениеЗаполнено(Выборка.Серия) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + " " + Выборка.Серия;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Номер) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + " " + Выборка.Номер;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ДатаВыдачи) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + ", от " + Формат(Выборка.ДатаВыдачи, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ДатаОкончания) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + ", действ. до " + Формат(Выборка.ДатаОкончания, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
		Рез.Добавить(СтрокаПредставлениеДок,, Выборка.ДокПросрочен);
	КонецЦикла;
	
	Возврат Рез;
КонецФункции

&НаСервереБезКонтекста
// Возвращает список документов ТС
//Тип значения: массив, тип элемента массива: структура значений
//
Функция ДокументыТС(ТС, ТекДата) Экспорт
	Рез = Новый СписокЗначений;
	
	ЗапросТС = Новый Запрос(
	"ВЫБРАТЬ
	|	РегистрСведенийуатДокументыТС.ВидДокумента,
	|	РегистрСведенийуатДокументыТС.Серия,
	|	РегистрСведенийуатДокументыТС.Номер,
	|	РегистрСведенийуатДокументыТС.ДатаВыдачи,
	|	РегистрСведенийуатДокументыТС.ДатаОкончания,
	|	РегистрСведенийуатДокументыТС.КемВыдан,
	|	РегистрСведенийуатДокументыТС.Категория,
	|	ВЫБОР
	|		КОГДА НЕ РегистрСведенийуатДокументыТС.Архив
	|				И РегистрСведенийуатДокументыТС.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|				И ДОБАВИТЬКДАТЕ(РегистрСведенийуатДокументыТС.ДатаОкончания, ДЕНЬ, -&ДниДоОкончанияДействияДокументовТС) <= &ДатаОкончания
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ДокПросрочен
	|ИЗ
	|	РегистрСведений.уатДокументыТС КАК РегистрСведенийуатДокументыТС
	|ГДЕ
	|	НЕ РегистрСведенийуатДокументыТС.Архив
	|	И РегистрСведенийуатДокументыТС.ТС = &ТС
	|	И НЕ РегистрСведенийуатДокументыТС.Архив");
	ЗапросТС.УстановитьПараметр("ДниДоОкончанияДействияДокументовТС",
		уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
		ПользователиКлиентСервер.АвторизованныйПользователь(),	"ОсновнаяОрганизация"), ПредопределенноеЗначение(
		"ПланВидовХарактеристик.уатПраваИНастройки.ДниДоОкончанияДействияДокументовТС")));
	ЗапросТС.УстановитьПараметр("ДатаОкончания", ТекДата);
	ЗапросТС.УстановитьПараметр("ТС", ТС);
	Выборка = ЗапросТС.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаПредставлениеДок = "" + Выборка.ВидДокумента;
		
		Если ЗначениеЗаполнено(Выборка.Серия) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + " " + Выборка.Серия;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Номер) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + " " + Выборка.Номер;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ДатаВыдачи) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + ", от " + Формат(Выборка.ДатаВыдачи, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ДатаОкончания) Тогда
			СтрокаПредставлениеДок = СтрокаПредставлениеДок + ", действ. до " + Формат(Выборка.ДатаОкончания, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
		Рез.Добавить(СтрокаПредставлениеДок,, Выборка.ДокПросрочен);
	КонецЦикла;
		
	Возврат Рез;
КонецФункции

&НаСервере
Функция ПолучитьПутьКДанным(ИмяЭлементаФормы)
	Возврат Элементы[ИмяЭлементаФормы].ПутьКДанным;
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокТипов()
	Если ТипыЭлементовФормы.Количество() > 0 Тогда
		ТипыЭлементовФормы.Очистить();
	КонецЕсли;
	
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_СпидометрВозвращения"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 9; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_СпидометрВозвращенияВодитель"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 9; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_ОстатокТопливаВозврат"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 8; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_ОстатокТопливаВозвратВодитель"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 8; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_ДатаВыезда"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛистДатаВозвращения"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛистВремяВыезда"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛистВремяВозвращения"; НовТип.ТипЭлемента = "Время";
	
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_Прицеп1"; НовТип.ТипЭлемента = "СправочникСсылка.уатТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_Прицеп2"; НовТип.ТипЭлемента = "СправочникСсылка.уатТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_Прицеп3"; НовТип.ТипЭлемента = "СправочникСсылка.уатТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_Прицеп4"; НовТип.ТипЭлемента = "СправочникСсылка.уатТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВыезда1"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВыезда2"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВыезда3"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВыезда4"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата1"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата2"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата3"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата4"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 11; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата1НеСогласен"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 9; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата2НеСогласен"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 9; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата3НеСогласен"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 9; НовТип.Точность = 1;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Прицепы_СчВозврата4НеСогласен"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 9; НовТип.Точность = 1;
	
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_КоличествоКмГруз"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 7; НовТип.Точность = 3;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_КоличествоКмПорожний"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 7; НовТип.Точность = 3;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_КоличествоЧасов"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 6; НовТип.Точность = 2;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_КоличествоЕздок"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 3; НовТип.Точность = 0;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_КоличествоОпераций"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 3; НовТип.Точность = 0;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_КоличествоТонн"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 10; НовТип.Точность = 3;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Задание_УсловиеРаботы"; НовТип.ТипЭлемента = "СправочникСсылка.уатУсловияРаботы";
			
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Дата1"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Дата2"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Дата3"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Дата4"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Дата5"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Дата6"; НовТип.ТипЭлемента = "Дата";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Начало1"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Начало2"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Начало3"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Начало4"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Начало5"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Начало6"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Конец1"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Конец2"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Конец3"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Конец4"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Конец5"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_Конец6"; НовТип.ТипЭлемента = "Время";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_ВидПростоя1"; НовТип.ТипЭлемента = "СправочникСсылка.уатСостояниеТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_ВидПростоя2"; НовТип.ТипЭлемента = "СправочникСсылка.уатСостояниеТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_ВидПростоя3"; НовТип.ТипЭлемента = "СправочникСсылка.уатСостояниеТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_ВидПростоя4"; НовТип.ТипЭлемента = "СправочникСсылка.уатСостояниеТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_ВидПростоя5"; НовТип.ТипЭлемента = "СправочникСсылка.уатСостояниеТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Простои_ВидПростоя6"; НовТип.ТипЭлемента = "СправочникСсылка.уатСостояниеТС";
	
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_ВидЗаправки"; НовТип.ТипЭлемента = "ПеречислениеСсылка.уатВидыДвиженияГСМ";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_АЗС"; НовТип.ТипЭлемента = "СправочникСсылка.уатАЗС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_ТС"; НовТип.ТипЭлемента = "СправочникСсылка.уатТС";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_ГСМ"; НовТип.ТипЭлемента = "СправочникСсылка.Номенклатура";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_ПластикКарта"; НовТип.ТипЭлемента = "СправочникСсылка.уатПластиковыеКарты";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_Талоны"; НовТип.ТипЭлемента = "Строка";
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_Количество"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 10; НовТип.Точность = 3;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПутевойЛист_Заправки_Сумма"; НовТип.ТипЭлемента = "Число"; НовТип.Длина = 10; НовТип.Точность = 2;
	
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "Логин"; НовТип.ТипЭлемента = "Строка"; НовТип.Длина = 50;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "Пароль"; НовТип.ТипЭлемента = "Строка"; НовТип.Длина = 50;
	НовТип = ТипыЭлементовФормы.Добавить(); НовТип.ИмяЭлемента = "ПарольКлавиатура"; НовТип.ТипЭлемента = "Строка"; НовТип.Длина = 50;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЗначениеРеквизита(Объект, ИмяРеквизита)
	Возврат уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Объект, ИмяРеквизита);
КонецФункции

&НаСервере
Процедура УстановитьОграниченияДляРолиВодитель()
	Если НЕ РольДоступна("ПолныеПрава") И НЕ РольДоступна("уатАдминистрированиеУАТ") И НЕ РольДоступна("уатДиспетчер") Тогда
		Элементы.ГруппаПрицепыКоманды.Видимость = Ложь;
		Элементы.УдалитьПрицеп1.Видимость = Ложь;
		Элементы.УдалитьПрицеп2.Видимость = Ложь;
		Элементы.УдалитьПрицеп3.Видимость = Ложь;
		Элементы.УдалитьПрицеп4.Видимость = Ложь;
		Элементы.ВыбратьПрицеп1.Видимость = Ложь;
		Элементы.ВыбратьПрицеп2.Видимость = Ложь;
		Элементы.ВыбратьПрицеп3.Видимость = Ложь;
		Элементы.ВыбратьПрицеп4.Видимость = Ложь;
		Элементы.ПутевойЛист_Прицепы_Прицеп1.ТолькоПросмотр = Истина;
		Элементы.ПутевойЛист_Прицепы_Прицеп2.ТолькоПросмотр = Истина;
		Элементы.ПутевойЛист_Прицепы_Прицеп3.ТолькоПросмотр = Истина;
		Элементы.ПутевойЛист_Прицепы_Прицеп4.ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПутевойЛистДатаВремяВыезда()
	Рез = НачалоДня(ПутевойЛист_ДатаВыезда) + (ПутевойЛист_ВремяВыезда - НачалоДня(ПутевойЛист_ВремяВыезда));
	Возврат Рез;
КонецФункции

&НаКлиенте
Функция ПутевойЛистДатаВремяВозвращения()
	Рез = НачалоДня(ПутевойЛист_ДатаВозвращения) + (ПутевойЛист_ВремяВозвращения - НачалоДня(ПутевойЛист_ВремяВозвращения));
	Возврат Рез;
КонецФункции

&НаКлиенте
Процедура УстановитьОтборАЗС()
	Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаСклад")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ЭтоАЗССклад", Истина);
	Иначе
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ЭтоАЗССклад", Ложь);
	КонецЕсли;
	МассивПарамВыбора = Новый Массив();
	МассивПарамВыбора.Добавить(НовыйПараметр);
	Элементы.ПутевойЛист_Заправки_АЗС.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПарамВыбора);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокФормы(Очистить = Ложь)
	
	Заголовок = "Терминал приема и выдачи путевых листов";
	Если Очистить Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Водитель) Тогда
		Заголовок = "Водитель: " + Водитель;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокПутевойЛист()
	Если НЕ ЗначениеЗаполнено(ПутевойЛист_Ссылка) Тогда
		ЗаголовокПутевойЛист = "";
		Возврат;
	КонецЕсли;
	
	ЗаголовокПутевойЛист = "Закрытие путевого листа";
	
	Если ЗначениеЗаполнено(ПутевойЛист_НомерТС) Тогда
		ЗаголовокПутевойЛист = ЗаголовокПутевойЛист + ": " + СокрЛП(ПутевойЛист_НомерТС) + " (" + СокрЛП(ПутевойЛист_МодельТС) + ")";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьНавигации()
	Если Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаШапка Тогда
		НадписьНавигация = Новый ФорматированнаяСтрока(
			Новый ФорматированнаяСтрока("Основное", Новый Шрифт(Элементы.НадписьНавигация.Шрифт,,,Истина)),
			" > Задание > Простои > Заправки > Готово");
		
	ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗадание Тогда
		НадписьНавигация = Новый ФорматированнаяСтрока(
			"Основное > ",
			Новый ФорматированнаяСтрока("Задание", Новый Шрифт(Элементы.НадписьНавигация.Шрифт,,,Истина)),
			" > Простои > Заправки > Готово");
		
	ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаПростои Тогда
		НадписьНавигация = Новый ФорматированнаяСтрока(
			"Основное > Задание > ",
			Новый ФорматированнаяСтрока("Простои", Новый Шрифт(Элементы.НадписьНавигация.Шрифт,,,Истина)),
			" > Заправки > Готово");
		
	ИначеЕсли Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаЗаправки Тогда
		НадписьНавигация = Новый ФорматированнаяСтрока(
			"Основное > Задание > Простои > ",
			Новый ФорматированнаяСтрока("Заправки", Новый Шрифт(Элементы.НадписьНавигация.Шрифт,,,Истина)),
			" > Готово");
		
	КонецЕсли;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаОжидания()
	Если ТипЗнч(ТекущийЭлемент) = Тип("ПолеФормы") И ТекЭлементФормыИмя <> ТекущийЭлемент.Имя Тогда
		ПриИзмененииТекущегоЭлемента();
		ТекЭлементФормыИмя = ТекущийЭлемент.Имя;
		СброситьРежимВводДробныхЗначений();
		УстановитьЗаголовокФормы();
	КонецЕсли;
	
	//Если ТипЗнч(ТекущийЭлемент) = Тип("ПолеФормы") И НЕ ТекущийЭлемент.ТолькоПросмотр Тогда
	//	Если НЕ Элементы.ГруппаКлавиатура.Доступность Тогда
	//		Элементы.ГруппаКлавиатура.Доступность = Истина;
	//	КонецЕсли;
	//Иначе
	//	Если Элементы.ГруппаКлавиатура.Доступность Тогда
	//		Элементы.ГруппаКлавиатура.Доступность = Ложь;
	//	КонецЕсли;
	//КонецЕсли;
			
	ПодключитьОбработчикОжидания("Подключаемый_ОбработкаОжидания", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТекущегоЭлемента()
	СтарыйЭлементИмя = ТекЭлементФормыИмя;
	НовыйЭлементИмя = ТекущийЭлемент.Имя;
	
	Если (СтарыйЭлементИмя = "ПутевойЛист_ДатаВыезда" И НовыйЭлементИмя <> "ПутевойЛистВремяВыезда"
		ИЛИ СтарыйЭлементИмя = "ПутевойЛистВремяВыезда" И НовыйЭлементИмя <> "ПутевойЛист_ДатаВыезда") И ИзмениласьДатаВыезда
		ИЛИ (СтарыйЭлементИмя = "ПутевойЛистДатаВозвращения" И НовыйЭлементИмя <> "ПутевойЛистВремяВозвращения"
		ИЛИ СтарыйЭлементИмя = "ПутевойЛистВремяВозвращения" И НовыйЭлементИмя <> "ПутевойЛистДатаВозвращения") И ИзмениласьДатаВозврата Тогда
		
		Если уатОбщегоНазначения_проф.уатПроверитьИспользованиеGPS(ПутевойЛист_ТС) Тогда
			ОбновитьПоказанияGPS();
		КонецЕсли;
	КонецЕсли;
	
	Если СтарыйЭлементИмя = "ПутевойЛист_ДатаВыезда" И НовыйЭлементИмя <> "ПутевойЛистВремяВыезда"
		ИЛИ СтарыйЭлементИмя = "ПутевойЛистВремяВыезда" И НовыйЭлементИмя <> "ПутевойЛист_ДатаВыезда" Тогда
		ИзмениласьДатаВыезда = Ложь;
	КонецЕсли;
	Если СтарыйЭлементИмя = "ПутевойЛистДатаВозвращения" И НовыйЭлементИмя <> "ПутевойЛистВремяВозвращения"
		ИЛИ СтарыйЭлементИмя = "ПутевойЛистВремяВозвращения" И НовыйЭлементИмя <> "ПутевойЛистДатаВозвращения" Тогда
		ИзмениласьДатаВозврата = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ТипТекущегоЭлемента()
	//Попытка
	//	ТекТип = ТипЗнч(Вычислить(ТекЭлементФормыИмя));
	//	Если ТекТип = Тип("Число") Тогда
	//		Возврат "Число";
	//	ИначеЕсли ТекТип = Тип("Дата") Тогда
	//		Возврат "Дата";
	//	КонецЕсли;
	//Исключение
	//КонецПопытки;
	
	РезТип = Неопределено;
	
	мсвСтрокПоиск = ТипыЭлементовФормы.НайтиСтроки(Новый Структура("ИмяЭлемента", ТекЭлементФормыИмя));
	Если мсвСтрокПоиск.Количество() <> 0 Тогда
		РезТип = Новый Структура("Представление, Длина, Точность", мсвСтрокПоиск[0].ТипЭлемента, мсвСтрокПоиск[0].Длина, мсвСтрокПоиск[0].Точность);
	КонецЕсли;
		
	Возврат РезТип;
КонецФункции

&НаКлиенте
Процедура ИзменитьРежимВводДробныхЗначений()
	ТипТекЭлемента = ТипТекущегоЭлемента();
	Если ТипТекЭлемента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипТекЭлемента.Представление = "Число" И ТипТекЭлемента.Точность > 0 ИЛИ ТипТекЭлемента.Представление = "Время" Тогда
		Если РежимВводаДробныхЗначений = 0 Тогда
			РежимВводаДробныхЗначений = 1;
		Иначе
			РежимВводаДробныхЗначений = 0;
		КонецЕсли;
	ИначеЕсли ТипТекЭлемента.Представление = "Дата" Тогда
		Если РежимВводаДробныхЗначений = 0 Тогда
			РежимВводаДробныхЗначений = 1;
		ИначеЕсли РежимВводаДробныхЗначений = 1 Тогда
			РежимВводаДробныхЗначений = 2;
		Иначе
			РежимВводаДробныхЗначений = 0;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	ПриИзмененииРежимаВводаДробныхЗначений();
КонецПроцедуры

&НаКлиенте
Процедура СброситьРежимВводДробныхЗначений()
	РежимВводаДробныхЗначений = 0;
	ПриИзмененииРежимаВводаДробныхЗначений();
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРежимаВводаДробныхЗначений()
	ТипТекЭлемента = ТипТекущегоЭлемента();
	Если ТипТекЭлемента = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимВводаДробныхЗначений = 0 Тогда
		Элементы.КнопкаЗапятая.ЦветФона = ЦветФонаКнопки;
		ТекущийДробныйРазряд = 0;
	Иначе
		Элементы.КнопкаЗапятая.ЦветФона = ЦветНажатойКнопки;
		ТекущийДробныйРазряд = 1;
	КонецЕсли;
	
	Если ТипТекЭлемента.Представление = "Время" Тогда
		ПредставлениеЗапятой = ":";
	ИначеЕсли ТипТекЭлемента.Представление = "Дата" Тогда
		ПредставлениеЗапятой = ".";
	Иначе
		Если ТекЭлементФормыИмя = "ПутевойЛист_Задание_КоличествоЧасов" Тогда
			ПредставлениеЗапятой = ":";
		Иначе
			ПредставлениеЗапятой = ",";
		КонецЕсли;
	КонецЕсли;
	Если Элементы.КнопкаЗапятая.Заголовок <> ПредставлениеЗапятой Тогда
		Элементы.КнопкаЗапятая.Заголовок = ПредставлениеЗапятой;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПЛ(ПутевойЛист)
	мСписок = Новый Массив;
	мСписок.Добавить(ПутевойЛист);
	ОбъектыПечати = Новый СписокЗначений;
	
	ТабДок = СформироватьМакетПечати(мСписок, ОбъектыПечати);
	ТабДок.Напечатать();
КонецПроцедуры

&НаСервере
Функция СформироватьМакетПечати(мСписок, ОбъектыПечати)
	
	ТабДок = Документы.уатПутевойЛист.ПечатьВесьДокумент(мСписок, ОбъектыПечати);
	Возврат ТабДок;
	
КонецФункции // СформироватьМакетПечати()

&НаКлиенте
Процедура ЗагрузитьДанныеПЛвФорму(ПутевойЛист)
	ЗагрузитьДанныеПЛвФормуСервер(ПутевойЛист);
	ОбновитьОтображениеТЧПЛ();
	ЗаполнитьДокументыВодителейИТС();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеТЧПЛ()
	//прицепы
	//флВидимостьПрицепыЗаголовок = Ложь;
	Для Сч = 1 По 4 Цикл
		ЕстьПрицеп = ЗначениеЗаполнено(Вычислить("ПутевойЛист_Прицепы_Прицеп" + Сч));
		Элементы["Прицеп" + Сч].Видимость = ЕстьПрицеп;
		//Если НЕ Элементы.ГруппаПрицепыЗаголовок.Видимость И ЕстьПрицеп Тогда
		//	флВидимостьПрицепыЗаголовок = Истина;
		//КонецЕсли;
	КонецЦикла;
	//Если Элементы.ГруппаПрицепыЗаголовок.Видимость <> флВидимостьПрицепыЗаголовок Тогда
	//	Элементы.ГруппаПрицепыЗаголовок.Видимость = флВидимостьПрицепыЗаголовок;
	//КонецЕсли;
	
	//простои
	//флВидимостьПростоиЗаголовок = Ложь;
	Для Сч = 1 По 6 Цикл
		ЕстьПростой = ЗначениеЗаполнено(Вычислить("ПутевойЛист_Простои_ВидПростоя" + Сч))
			ИЛИ ЗначениеЗаполнено(Вычислить("ПутевойЛист_Простои_Начало" + Сч));
		Элементы["Простой" + Сч].Видимость = ЕстьПростой;
		//Если НЕ Элементы.ГруппаПростоиЗаголовок.Видимость И ЕстьПростой Тогда
		//	флВидимостьПростоиЗаголовок = Истина;
		//КонецЕсли;
	КонецЦикла;
	//Если Элементы.ГруппаПростоиЗаголовок.Видимость <> флВидимостьПрицепыЗаголовок Тогда
	//	Элементы.ГруппаПростоиЗаголовок.Видимость = флВидимостьПрицепыЗаголовок;
	//КонецЕсли;
	
	//задание
	ОтобразитьСтрокуЗадания(1);
	
	//заправки
	ОтобразитьСтрокуЗаправок(1);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументыВодителейИТС()
	ДокументыВодителей.Очистить();
	СписДокументовВодителя = ДокументыВодителя(Водитель, ТекущаяДата());
	Для Каждого ТекДок Из СписДокументовВодителя Цикл
		НоваяСтрока = ДокументыВодителей.Добавить();
		НоваяСтрока.Значение = ТекДок.Значение;
		НоваяСтрока.Пометка = ТекДок.Пометка;
	КонецЦикла;
	
	ДокументыТС.Очистить();
	списДокументовТС = ДокументыТС(ПутевойЛист_ТС, ТекущаяДата());
	Для Каждого ТекДок Из списДокументовТС Цикл
		НоваяСтрока = ДокументыТС.Добавить();
		НоваяСтрока.Значение = ТекДок.Значение;
		НоваяСтрока.Пометка = ТекДок.Пометка;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНавигациюЗадания()
	Элементы.УдалитьЗадание.Видимость = (ПутевойЛист_Задание.Количество() > 0);
	НавигацияЗадания = "" + ПутевойЛист_Задание_НомерСтроки + " из " + ПутевойЛист_Задание.Количество();
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНавигациюЗаправок()
	Элементы.УдалитьЗаправку.Видимость = (ПутевойЛист_Заправки.Количество() > 0);
	НавигацияЗаправки = "" + ПутевойЛист_Заправки_НомерСтроки + " из " + ПутевойЛист_Заправки.Количество();
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗакрытиеПутевогоЛиста(ТекПЛ)
	ЗагрузитьДанныеПЛвФорму(ТекПЛ);
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПутевойЛист;
	Элементы.СтраницыПутевогоЛиста.ТекущаяСтраница = Элементы.СтраницаШапка;
	Элементы.ГруппаКнопкаНазад.ТекущаяСтраница = Элементы.СтраницаКнопкаОтмена;
	Элементы.ГруппаКнопкаВперед.ТекущаяСтраница = Элементы.СтраницаДалее;
	
	Если НЕ Элементы.Прицеп1.Видимость Тогда
		Элементы.ГруппаПрицепыЗаголовок.Видимость = Ложь;
	КонецЕсли;
	Если НЕ Элементы.Простой1.Видимость Тогда
		Элементы.ГруппаПростоиЗаголовок.Видимость = Ложь;
	КонецЕсли;
	
	ВодительНеСогласенСПробегомGPS = Ложь;
	ВодительНеСогласенСРасходомТопливаGPS = Ложь;
	ВодительНеСогласенСоСчПрицеп1 = Ложь;
	ВодительНеСогласенСоСчПрицеп2 = Ложь;
	ВодительНеСогласенСоСчПрицеп3 = Ложь;
	ВодительНеСогласенСоСчПрицеп4 = Ложь;
	НадписьНекорректноеЗаполнениеШапки = "";
	НадписьНекорректноеЗаполнениеПростоев = "";
	
	УстановитьВидимостьДоступностьНеСогласен();
	
	ОбновитьНадписьНавигации();
	
	УстановитьЗаголовокПутевойЛист();
	УстановитьЗаголовокФормы();
	
	//обновление данных GPS делаем после перехода на страницу ПЛ
	ПодключитьОбработчикОжидания("ОбновитьПоказанияGPS", 1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСтрокуЗадания(НомерСтроки)
	Если ПутевойЛист_Задание.Количество() = 0 Тогда
		Элементы.ГруппаЗаданиеСтрока.Видимость = Ложь;
		
		ПутевойЛист_Задание_Выполнено = Ложь;
		ПутевойЛист_Задание_ВыполненоТумблер = 0;
		ПутевойЛист_Задание_КоличествоЕздок = 0;
		ПутевойЛист_Задание_КоличествоКмГруз = 0;
		ПутевойЛист_Задание_КоличествоКмПорожний = 0;
		ПутевойЛист_Задание_КоличествоОпераций = 0;
		ПутевойЛист_Задание_КоличествоТонн = 0;
		ПутевойЛист_Задание_КоличествоЧасов = 0;
		ПутевойЛист_Задание_УсловиеРаботы = Неопределено;
		ПутевойЛист_Задание_НомерСтроки = 0;
	Иначе
		Элементы.ГруппаЗаданиеСтрока.Видимость = Истина;
		
		ТекСтрока = ПутевойЛист_Задание[НомерСтроки-1];
		
		ПутевойЛист_Задание_Выполнено = ТекСтрока.Выполнено;
		ПутевойЛист_Задание_ВыполненоТумблер = ?(ТекСтрока.Выполнено, 1, 0);
		ПутевойЛист_Задание_КоличествоЕздок = ТекСтрока.КоличествоЕздок;
		ПутевойЛист_Задание_КоличествоКмГруз = ТекСтрока.КоличествоКмГруз;
		ПутевойЛист_Задание_КоличествоКмПорожний = ТекСтрока.КоличествоКмПорожний;
		ПутевойЛист_Задание_КоличествоОпераций = ТекСтрока.КоличествоОпераций;
		ПутевойЛист_Задание_КоличествоТонн = ТекСтрока.КоличествоТонн;
		ПутевойЛист_Задание_КоличествоЧасов = ТекСтрока.КоличествоЧасов;
		ПутевойЛист_Задание_УсловиеРаботы = ТекСтрока.УсловиеРаботы;
		ПутевойЛист_Задание_НомерСтроки = НомерСтроки;
	КонецЕсли;
	
	ОбновитьНавигациюЗадания();
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьСтрокуЗаправок(НомерСтроки)
	Если ПутевойЛист_Заправки.Количество() = 0 Тогда
		Элементы.ГруппаЗаправкиСтрока.Видимость = Ложь;
		
		ПутевойЛист_Заправки_АЗС = Неопределено;
		ПутевойЛист_Заправки_ТС = Неопределено;
		ПутевойЛист_Заправки_ВидЗаправки = Неопределено;
		ПутевойЛист_Заправки_ГСМ = Неопределено;
		ПутевойЛист_Заправки_Количество = Неопределено;
		ПутевойЛист_Заправки_Сумма = Неопределено;
		ПутевойЛист_Заправки_ПластиковаяКарта = Неопределено;
		ПутевойЛист_Заправки_Талоны = Неопределено;
		ПутевойЛист_Заправки_НомерСтроки = 0;
		ПутевойЛист_Заправки_Ссылка = Неопределено;
	Иначе
		Элементы.ГруппаЗаправкиСтрока.Видимость = Истина;
		
		ТекСтрока = ПутевойЛист_Заправки[НомерСтроки-1];
		
		ПутевойЛист_Заправки_АЗС = ТекСтрока.АЗС;
		ПутевойЛист_Заправки_ВидЗаправки = ТекСтрока.ВидЗаправки;
		ПутевойЛист_Заправки_ТС = ТекСтрока.ТС;
		ПутевойЛист_Заправки_ГСМ = ТекСтрока.ГСМ;
		ПутевойЛист_Заправки_Количество = ТекСтрока.Количество;
		ПутевойЛист_Заправки_Сумма = ТекСтрока.Сумма;
		ПутевойЛист_Заправки_ПластиковаяКарта = ТекСтрока.ПластиковаяКарта;
		ПутевойЛист_Заправки_Талоны = ТекСтрока.Талоны;
		ПутевойЛист_Заправки_НомерСтроки = НомерСтроки;
		ПутевойЛист_Заправки_Ссылка = ТекСтрока.Ссылка;
		
		ПриИзмененииВидаЗаправки();
	КонецЕсли;
	
	ОбновитьНавигациюЗаправок();
	ОбновитьСписокДоступныхПластиковыхКарт();
	УстановитьОтборАЗС();
	
	СписокТС = Новый Массив;
	Если ЗначениеЗаполнено(ПутевойЛист_ТС) Тогда
		СписокТС.Добавить(ПутевойЛист_ТС);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп1) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп1);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп2) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп2);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп3) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп3);
	КонецЕсли;
	Если ЗначениеЗаполнено(ПутевойЛист_Прицепы_Прицеп4) Тогда
		СписокТС.Добавить(ПутевойЛист_Прицепы_Прицеп4);
	КонецЕсли;
	
	Элементы.ПутевойЛист_Заправки_ТС.СписокВыбора.ЗагрузитьЗначения(СписокТС);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокДоступныхПластиковыхКарт()
	Элементы.ПутевойЛист_Заправки_ПластикКарта.СписокВыбора.Очистить();
	СписокКарт = ПолучитьСписокДоступныхПластиковыхКартСервер(Водитель, ПутевойЛист_ТС);
	Элементы.ПутевойЛист_Заправки_ПластикКарта.СписокВыбора.ЗагрузитьЗначения(СписокКарт.ВыгрузитьЗначения());
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокДоступныхПластиковыхКартСервер(Водитель, ТС)
	СписокКарт = Новый СписокЗначений;
	
	ВыборкаПластиковыеКарты = Справочники.уатТопливныеКарты.Выбрать(,, Новый Структура("КомуВыдана", Водитель));
	Пока ВыборкаПластиковыеКарты.Следующий() Цикл
		СписокКарт.Добавить(ВыборкаПластиковыеКарты.Ссылка, ВыборкаПластиковыеКарты.Код);
	КонецЦикла;
	
	ВыборкаПластиковыеКарты = Справочники.уатТопливныеКарты.Выбрать(,, Новый Структура("КомуВыдана", ТС));
	Пока ВыборкаПластиковыеКарты.Следующий() Цикл
		СписокКарт.Добавить(ВыборкаПластиковыеКарты.Ссылка, ВыборкаПластиковыеКарты.Код);
	КонецЦикла;	
	
	Возврат СписокКарт;
КонецФункции

&НаКлиенте
Функция ДиалогОКОтмена(ТекстДиалога, ОповещениеОРезультате)
	
	ОткрытьФорму(ПолноеИмяОбработки + ".Форма.Диалог",
		Новый Структура("ТекстДиалога", ТекстДиалога), 
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ДиалогОКОтменаПродолжение", ЭтотОбъект, Новый Структура("ОповещениеОРезультате", ОповещениеОРезультате)),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецФункции

&НаКлиенте
Процедура ДиалогОКОтменаПродолжение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОРезультате, (РезультатЗакрытия = Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНеСогласен()
	Элементы.ГруппаСтраницыСпидометрВозврат.ТекущаяСтраница = Элементы.ГруппаСтраницаСпидометрВозвратGPS;
	Элементы.ГруппаСтраницыТопливоВозврат.ТекущаяСтраница = Элементы.ГруппаСтраницыТопливоВозвратGPS;
	Элементы.ГруппаСтраницыПрицепыНеСогласен1.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен1GPS;
	Элементы.ГруппаСтраницыПрицепыНеСогласен2.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен2GPS;
	Элементы.ГруппаСтраницыПрицепыНеСогласен3.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен3GPS;
	Элементы.ГруппаСтраницыПрицепыНеСогласен4.ТекущаяСтраница = Элементы.ГруппаСтраницыПрицепыНеСогласен4GPS;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоказанияGPS()
	ИспользуетGPS = уатОбщегоНазначения_проф.уатПроверитьИспользованиеGPS(ПутевойЛист_ТС);
	УстановитьЗаголовокФормы();
	Если НЕ ИспользуетGPS Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектСтруктура = Новый Структура;
	ОбъектСтруктура.Вставить("ТранспортноеСредство", ПутевойЛист_ТС);
	ОбъектСтруктура.Вставить("ДатаВыезда", ПутевойЛистДатаВремяВыезда());
	ОбъектСтруктура.Вставить("ДатаВозвращения", ПутевойЛистДатаВремяВозвращения());
	ОбъектСтруктура.Вставить("СпидометрВыезда", ПутевойЛист_СпидометрВыезда);
	ОбъектСтруктура.Вставить("СпидометрВозвращения", ПутевойЛист_СпидометрВозвращения);
	ОбъектСтруктура.Вставить("Организация", ПутевойЛист_Организация);
	мсвПрицепы = Новый Массив;
	Для Сч = 1 По 4 Цикл
		Если ЗначениеЗаполнено(ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч]) Тогда
			СтруктураПрицеп = Новый Структура("ТС, СпидометрВыезда, СпидометрВозвращения",
			ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч],
			ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + Сч],
			ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч]);
			мсвПрицепы.Добавить(СтруктураПрицеп);
		КонецЕсли;
	КонецЦикла;
	ОбъектСтруктура.Вставить("Прицепы", мсвПрицепы);
	РезультатСтруктура = Новый Структура;
	
	//РАСЧЕТ
	ПараметрыФормы = Новый Структура("ОбъектСтруктура", ОбъектСтруктура);
	ДополнительныеПараметры = Новый Структура("СтарыйЭлементИмя, НовыйЭлементИмя", ТекЭлементФормыИмя, ТекущийЭлемент.Имя);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьПоказанияGPSзавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.уатТерминалПриемаИВыдачиПутевыхЛистов.Форма.ДлительнаяОперацияОбновлениеGPS", ПараметрыФормы,
		ЭтаФорма,,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоказанияGPSзавершение(РезультатСтруктура, ДополнительныеПараметры) Экспорт
	Если РезультатСтруктура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//заполнение основных реквизитов
	Если РезультатСтруктура.Свойство("СчетчикВыезда") Тогда
		ПутевойЛист_СпидометрВыезда = РезультатСтруктура.СчетчикВыезда;
	КонецЕсли;
	Если РезультатСтруктура.Свойство("СчетчикВозвращения") Тогда
		ПутевойЛист_СпидометрВозвращения = РезультатСтруктура.СчетчикВозвращения;
	КонецЕсли;
	Если РезультатСтруктура.Свойство("УровеньТопливаВыезд") Тогда
		ПутевойЛист_ОстатокТопливаВыезд = РезультатСтруктура.УровеньТопливаВыезд;
	КонецЕсли;
	Если РезультатСтруктура.Свойство("УровеньТопливаВозврат") Тогда
		ПутевойЛист_ОстатокТопливаВозврат = РезультатСтруктура.УровеньТопливаВозврат;
	КонецЕсли;
	Если РезультатСтруктура.Свойство("СчетчикПоGPS") Тогда
		ПутевойЛист_ПробегПоGPS = РезультатСтруктура.СчетчикПоGPS;
	КонецЕсли;
	
	Для Каждого ТекПрицеп Из РезультатСтруктура.Прицепы Цикл 
		Для Сч = 1 По 4 Цикл
			Если ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч] = ТекПрицеп.ТС Тогда 
				Если ТекПрицеп.Свойство("СчетчикВыезда") Тогда
					ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + Сч] = ТекПрицеп.СчетчикВыезда;
				КонецЕсли;
				Если ТекПрицеп.Свойство("СчетчикВозвращения") Тогда
					ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч] = ТекПрицеп.СчетчикВозвращения;
				КонецЕсли;
				Если ТекПрицеп.Свойство("СчетчикПоGPS") Тогда
					ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + Сч] = ТекПрицеп.СчетчикПоGPS;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	////обновляем ТЧ Расход ГСМ
	//Если РезультатСтруктура.Свойство("РасходГСМ") Тогда
	//	ОснТопливо = ПолучитьЗначениеРеквизита(ПолучитьЗначениеРеквизита(Объект.ТранспортноеСредство, "Модель"), "ОсновноеТопливо");
	//	Для Каждого ТекСтрока Из Объект.РасходГСМ Цикл
	//		Если Объект.ТранспортноеСредство = ТекСтрока.ТС И ОснТопливо = ТекСтрока.ГСМ Тогда
	//			ТекСтрока.ОстатокПриВыезде = ОстатокТопливаВыезд;
	//			ТекСтрока.ОстатокПриВозвращении = ОстатокТопливаВозврат;
	//			Если РезультатСтруктура.Свойство("РасходПоФакту") Тогда
	//				ТекСтрока.РасходПоФакту = РезультатСтруктура.РасходПоФакту;
	//			КонецЕсли;
	//			Если РезультатСтруктура.Свойство("ВыдатьКоличествоГорючего") Тогда
	//				ТекСтрока.Выдано = РезультатСтруктура.ВыдатьКоличествоГорючего;
	//			КонецЕсли;
	//			Если РезультатСтруктура.Свойство("Активен") Тогда
	//				ТекСтрока.Активен = РезультатСтруктура.Активен;
	//			КонецЕсли;
	//			
	//			Прервать;
	//		КонецЕсли;
	//	КонецЦикла;
	//КонецЕсли;
	
	Если ДополнительныеПараметры.СтарыйЭлементИмя = "ПутевойЛист_ДатаВыезда" И ДополнительныеПараметры.НовыйЭлементИмя <> "ПутевойЛистВремяВыезда"
		ИЛИ ДополнительныеПараметры.СтарыйЭлементИмя = "ПутевойЛистВремяВыезда" И ДополнительныеПараметры.НовыйЭлементИмя <> "ПутевойЛист_ДатаВыезда" Тогда
		ИзмениласьДатаВыезда = Ложь;
	КонецЕсли;
	Если ДополнительныеПараметры.СтарыйЭлементИмя = "ПутевойЛистДатаВозвращения" И ДополнительныеПараметры.НовыйЭлементИмя <> "ПутевойЛистВремяВозвращения"
		ИЛИ ДополнительныеПараметры.СтарыйЭлементИмя = "ПутевойЛистВремяВозвращения" И ДополнительныеПараметры.НовыйЭлементИмя <> "ПутевойЛистДатаВозвращения" Тогда
		ИзмениласьДатаВозврата = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЛогинНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Рез = Результат;
    Если ЗначениеЗаполнено(Рез) Тогда
        Авторизация_Логин = Рез;
        Водитель = Рез;
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Авторизован = ВодительАвторизован(Авторизация_Логин, Авторизация_Пароль, Водитель);
	
	Если Авторизован Тогда
		Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусВодитель;
		
		УстановитьЗаголовокФормы();
		ПерейтиНаСтраницуВыборРежима();
	Иначе
		Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусОшибка;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВодительАвторизован(Логин, Пароль, Водитель)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатПараметрыАвторизацииТерминала.Сотрудник
	|ИЗ
	|	РегистрСведений.уатПараметрыАвторизацииТерминала КАК уатПараметрыАвторизацииТерминала
	|ГДЕ
	|	уатПараметрыАвторизацииТерминала.Логин = &Логин
	|	И уатПараметрыАвторизацииТерминала.Пароль = &Пароль");
	
	Запрос.УстановитьПараметр("Логин", Логин);
	Запрос.УстановитьПараметр("Пароль", Пароль);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Водитель = Выборка.Сотрудник;
		Возврат Истина;
	Иначе
		Водитель = Неопределено;
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ВернутьсяКСтраницеАвторизации()
	Водитель = Неопределено;
	Авторизация_Логин = "";
	Авторизация_Пароль = "";
	Авторизация_ПарольКлавиатура = "";
	Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусВодитель;
	УстановитьЗаголовокФормы(Истина);
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАвторизация;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкод(Параметр)
	Если Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаАвторизация
		И Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаВыборРежима
		И Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаСписокПЛ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеШтрихкодов = Новый Массив();
	Если Параметр[1] = Неопределено Тогда
		ДанныеШтрихкодов.Добавить(Новый Структура("Штрихкод, Количество", Параметр[0], 1));
	Иначе
		ДанныеШтрихкодов.Добавить(Новый Структура("Штрихкод, Количество", Параметр[1][1], 1));
	КонецЕсли;
	
	//проверки и получение значения штрихкода
	ШтрихкодПЛ = "";
	Если ТипЗнч(ДанныеШтрихкодов) = Тип("Массив") И ДанныеШтрихкодов.Количество() > 0 Тогда
		СтруктураШтрихкод = ДанныеШтрихкодов[0];
		Если ТипЗнч(СтруктураШтрихкод) = Тип("Структура") И СтруктураШтрихкод.Свойство("Штрихкод") Тогда
			ШтрихкодПЛ = СтруктураШтрихкод.Штрихкод;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ШтрихкодПЛ) Тогда
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаАвторизация Тогда //авторизация по штрихкоду
			Водитель = ПолучитьВладельцаКарты(ШтрихкодПЛ, Истина);
			Если ЗначениеЗаполнено(Водитель) Тогда
				Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусВодитель;
				
				УстановитьЗаголовокФормы();
				ПерейтиНаСтраницуВыборРежима();
			Иначе
				Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусОшибкаМагнитнаяКарта;
			КонецЕсли;
			
		ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборРежима
			ИЛИ Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаСписокПЛ
			И Элементы.СтраницыКнопкаВыбратьИзСписка.ТекущаяСтраница = Элементы.СтраницыКнопкаВыбратьИзСпискаВыбрать Тогда //получение путевого листа по штрихкоду
			
			ТекПЛ = уатЗащищенныеФункцииСервер_проф.ПолучитьОбъектПоШтрихкоду(ШтрихкодПЛ);
			Если ТипЗнч(ТекПЛ) = Тип("ДокументСсылка.уатПутевойЛист")
				И ПЛдоступенДляЗакрытияВодителем(ТекПЛ) Тогда
				
				Если ЗначениеЗаполнено(ТекПЛ) И ПолучитьЗначениеРеквизита(ТекПЛ, "Водитель1") = Водитель Тогда
					УстановитьЗаголовокФормы();
					уатОбщегоНазначенияКлиент.УстановитьОтборУСписка(СписокДокументов.Отбор, "Водитель1", Водитель);
					
					//отсканирован ПЛ авторизованного водителя - переходим к его закрытию
					НачатьЗакрытиеПутевогоЛиста(ТекПЛ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПЛдоступенДляЗакрытияВодителем(ПЛссылка)
	Возврат ПЛссылка.Проведен И НЕ ПЛссылка.Рассчитан И НЕ ПЛссылка.ЗакрытВодителем;
КонецФункции

&НаКлиенте
Процедура ОбработатьМагнитныйКод(Данные)
	Если Элементы.Страницы.ТекущаяСтраница <> Элементы.СтраницаАвторизация Тогда
		Возврат;
	КонецЕсли;
	
	КодКарты = Данные[0];
	Водитель = ПолучитьВладельцаКарты(КодКарты, Ложь);
	
	Если ЗначениеЗаполнено(Водитель) Тогда
		Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусВодитель;
		
		УстановитьЗаголовокФормы();
		ПерейтиНаСтраницуВыборРежима();
	Иначе
		Элементы.СтраницыСтатус.ТекущаяСтраница = Элементы.СтатусОшибкаМагнитнаяКарта;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуВыборРежима()
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборРежима;
	Элементы.СтраницыСтатусВыборРежима.ТекущаяСтраница = Элементы.СтатусРежимОК;
	уатОбщегоНазначенияКлиент.УстановитьОтборУСписка(СписокДокументов.Отбор, "Водитель1", Водитель);
	
	мсвПЛ = СписокПЛВодителя(Водитель);
	Если мсвПЛ.Количество() = 1 Тогда
		Элементы.ПолучитьПутевойЛист.Заголовок = "Печать " + ПолучитьКраткоеИмяПФ(мсвПЛ[0]);
	Иначе
		Элементы.ПолучитьПутевойЛист.Заголовок = "Печать путевого листа";
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВладельцаКарты(КодКарты, ЭтоШтрихкод = Истина)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатКартыДоступа.ВладелецКарты
	|ИЗ
	|	Справочник.уатКартыДоступа КАК уатКартыДоступа
	|ГДЕ
	|	НЕ уатКартыДоступа.ПометкаУдаления
	|	И уатКартыДоступа.Статус = &СтатусАктивная");
	Если ЭтоШтрихкод Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И уатКартыДоступа.ШтрихКод = &КодКарты";
		Запрос.УстановитьПараметр("КодКарты", КодКарты);
	Иначе
		Запрос.Текст = Запрос.Текст + "
		|	И уатКартыДоступа.МагнитныйКод = &КодКарты";
		Запрос.УстановитьПараметр("КодКарты", КодКарты);
	КонецЕсли;
	Запрос.УстановитьПараметр("СтатусАктивная", Перечисления.уатСтатусыКарт.Действует);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВладелецКарты;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ВыходПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ВернутьсяКСтраницеАвторизации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		НужноЗакрытьФорму = Неопределено;
	Иначе
		НужноЗакрытьФорму = Истина;
		Закрыть();
	КонецЕсли;
	    
КонецПроцедуры

&НаКлиенте
Процедура СписокПЛназадПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Результат Тогда
		ПерейтиНаСтраницуВыборРежима();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_СпидометрВозвращенияВодитель()
	ТекущийЭлемент = Элементы.ПутевойЛист_СпидометрВозвращенияВодитель;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_ОстатокТопливаВозвратВодитель()
	ТекущийЭлемент = Элементы.ПутевойЛист_ОстатокТопливаВозвратВодитель;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКВыборуРежимаПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Результат Тогда
		ПерейтиНаСтраницуВыборРежима();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПрицеп(НомерПрицепа)
	ДопПараметры = Новый Структура("НомерПрицепа", НомерПрицепа);
	ОповещениеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораПрицепа", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("Обработка.уатТерминалПриемаИВыдачиПутевыхЛистов.Форма.ФормаВыбораСправочников",
		Новый Структура("ИмяОбъектаВыбора", "Прицеп"),
		ЭтаФорма,,,, ОповещениеВыбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПрицепа(Результат, ДопПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		ПриИзмененииПрицепа(ДопПараметры.НомерПрицепа, Результат);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПрицепа(НомерПрицепа, ЗначениеПрицепа)
	СчВыезда = уатОбщегоНазначения.уатТекущийСчетчикМЧ(ЗначениеПрицепа, ПутевойЛистДатаВремяВыезда());
	
	ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + НомерПрицепа] = ЗначениеПрицепа;
	ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + НомерПрицепа] = СчВыезда;
	ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + НомерПрицепа] = СчВыезда;
	ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + НомерПрицепа] = 0;
	
	ОбновитьПоказанияGPS();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПрицеп(НомерПрицепа)
	ДиалогОКОтмена("Удалить прицеп " + НомерПрицепа + " ?", 
		Новый ОписаниеОповещения("УдалитьПрицепПродолжение", ЭтотОбъект, Новый Структура("НомерПрицепа",НомерПрицепа)));
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПрицепПродолжение(Результат, ДопПараметры) Экспорт
	
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	НомерПрицепа = ДопПараметры.НомерПрицепа;
	
	//сначала считаем количество прицепов до удаления
	КоличПрицепов = 4;
	Для Сч = 1 По 4 Цикл
		Если НЕ Элементы["Прицеп" + Сч].Видимость Тогда
			КоличПрицепов = Сч-1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//скрываем неиспользуемые прицепы (их будет КоличПрицепов-1)
	Для Сч = 1 По 4 Цикл
		ВидомостьПрицепа = (Сч < КоличПрицепов);
		Если Элементы["Прицеп" + Сч].Видимость <> ВидомостьПрицепа Тогда
			Элементы["Прицеп" + Сч].Видимость = ВидомостьПрицепа;
		КонецЕсли;
	КонецЦикла;
	
	//сдвигаем значение на один, закрывая дырку, если мы удалили не последний прицеп
	Для Сч = НомерПрицепа По КоличПрицепов Цикл
		Если Сч < 4 Тогда
			ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч] = ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + Сч] = ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч] = ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + Сч] = ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч + "НеСогласен"] = ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + (Сч+1) + "НеСогласен"];
			
			ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + (Сч+1)] = Неопределено;
			ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + (Сч+1)] = 0;
			ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + (Сч+1)] = 0;
			ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + (Сч+1)] = 0;
			ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + (Сч+1) + "НеСогласен"] = 0;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличПрицепов = 1 Тогда
		Элементы.ГруппаПрицепыЗаголовок.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_СчПрицеп1()
	ТекущийЭлемент = Элементы.ПутевойЛист_Прицепы_СчВозврата1НеСогласен;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_СчПрицеп2()
	ТекущийЭлемент = Элементы.ПутевойЛист_Прицепы_СчВозврата2НеСогласен;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_СчПрицеп3()
	ТекущийЭлемент = Элементы.ПутевойЛист_Прицепы_СчВозврата3НеСогласен;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_СчПрицеп4()
	ТекущийЭлемент = Элементы.ПутевойЛист_Прицепы_СчВозврата4НеСогласен;
	УстановитьЗаголовокФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНажатияНаКнопку(Кнопка)
	Если НЕ ЗначениеЗаполнено(ТекЭлементФормыИмя) Тогда
		Возврат;
	КонецЕсли;
	
	ТипЗначенияВТекЭлементе = ТипТекущегоЭлемента();
	Если ТипЗначенияВТекЭлементе = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ТекЭлементФормы = Элементы[ТекЭлементФормыИмя];
		Если ТекЭлементФормы.ТолькоПросмотр И ТекЭлементФормыИмя <> "Логин" И ТекЭлементФормыИмя <> "ПарольКлавиатура" Тогда
			Возврат;
		КонецЕсли;
	Исключение
		Возврат;
	КонецПопытки;
	
	ПутьКДанным = ПолучитьПутьКДанным(ТекЭлементФормыИмя);
	
	Если Кнопка = "С" Тогда //стираем значение
		Если ТипЗначенияВТекЭлементе.Представление = "Время" Тогда
			ЭтаФорма[ПутьКДанным] = НачалоДня(ЭтаФорма[ПутьКДанным]);
		ИначеЕсли ТипЗначенияВТекЭлементе.Представление = "Дата" Тогда
			ЭтаФорма[ПутьКДанным] = '00010101';
			//ЭтаФорма[ПутьКДанным] = '00010101' + (ЭтаФорма[ПутьКДанным] - НачалоДня(ЭтаФорма[ПутьКДанным]));
		Иначе
			ЭтаФорма[ПутьКДанным] = Неопределено;
		КонецЕсли;
		СброситьРежимВводДробныхЗначений();
		
		Если ТекЭлементФормыИмя = "ПарольКлавиатура" Тогда
			Авторизация_Пароль = "";
		КонецЕсли;
		
	Иначе //дописываем справа цифру
		Если ТипЗначенияВТекЭлементе.Представление = "Число" Тогда
			ЦелЧастьСтрока = Формат(Цел(ЭтаФорма[ПутьКДанным]), "ЧН=; ЧГ=");
			Если ТекущийДробныйРазряд = 1 Тогда //начинаем вводить дробную часть - очищаем старые символы
				ДробЧастьСтрока = Формат(0, "ЧЦ=" + ТипЗначенияВТекЭлементе.Точность + "; ЧН=; ЧГ=; ЧВН=");	
			Иначе
				Если ЭтаФорма[ПутьКДанным] - Цел(ЭтаФорма[ПутьКДанным]) = 0 Тогда
					ДробЧастьСтрока = Формат(0, "ЧЦ=" + ТипЗначенияВТекЭлементе.Точность + "; ЧН=; ЧГ=; ЧВН=");
				Иначе
					ЗначениеСтрока = Формат(ЭтаФорма[ПутьКДанным], "ЧДЦ=" + ТипЗначенияВТекЭлементе.Точность + "; ЧГ=");
					ДробЧастьСтрока = Прав(ЗначениеСтрока, ТипЗначенияВТекЭлементе.Точность);
				КонецЕсли;
			КонецЕсли;
			
			Если РежимВводаДробныхЗначений = 0 Тогда
				ЦелЧастьСтрока = ЦелЧастьСтрока + Кнопка;
				ДлинаЦелойЧасти = ТипЗначенияВТекЭлементе.Длина - ТипЗначенияВТекЭлементе.Точность;
				Если СтрДлина(ЦелЧастьСтрока) > ДлинаЦелойЧасти Тогда
					ЦелЧастьСтрока = Прав(ЦелЧастьСтрока, ДлинаЦелойЧасти);
				КонецЕсли;
			Иначе
				Если ТекЭлементФормыИмя = "ПутевойЛист_Задание_КоличествоЧасов" Тогда
					ДробЧастьСтрока = ДробЧастьСтрока + Кнопка;
				Иначе
					Если ТекущийДробныйРазряд < ТипЗначенияВТекЭлементе.Точность Тогда
						ДробЧастьСтрока = Лев(ДробЧастьСтрока, ТекущийДробныйРазряд-1) + Кнопка + Сред(ДробЧастьСтрока, ТекущийДробныйРазряд+1);
					ИначеЕсли ТекущийДробныйРазряд = ТипЗначенияВТекЭлементе.Точность Тогда
						ДробЧастьСтрока = Лев(ДробЧастьСтрока, ТекущийДробныйРазряд-1) + Кнопка;
					Иначе
						ДробЧастьСтрока = ДробЧастьСтрока + Кнопка;
					КонецЕсли;
				КонецЕсли;
				
				Если СтрДлина(ДробЧастьСтрока) > ТипЗначенияВТекЭлементе.Точность Тогда
					ДробЧастьСтрока = Прав(ДробЧастьСтрока, ТипЗначенияВТекЭлементе.Точность);
				КонецЕсли;
				Если ТекЭлементФормыИмя = "ПутевойЛист_Задание_КоличествоЧасов" И Число(ДробЧастьСтрока) > 59 Тогда
					ДробЧастьСтрока = Формат(Число(Прав(ДробЧастьСтрока, 1)), "ЧЦ=" + ТипЗначенияВТекЭлементе.Точность + "; ЧВН=");
				КонецЕсли;
				
				ТекущийДробныйРазряд = ТекущийДробныйРазряд + 1;
			КонецЕсли;
			
			ЗначениеСтрока = ?(ЦелЧастьСтрока = "", "0", ЦелЧастьСтрока);
			Если НЕ ПустаяСтрока(ДробЧастьСтрока) Тогда
				Если СтрДлина(ДробЧастьСтрока) < ТипЗначенияВТекЭлементе.Точность Тогда
					ДробЧастьСтрока = Формат(Число(ДробЧастьСтрока), "ЧЦ=" + ТипЗначенияВТекЭлементе.Точность + "; ЧВН=");
				КонецЕсли;
				ЗначениеСтрока = ЗначениеСтрока + "." + ДробЧастьСтрока;
			КонецЕсли;
			
			ЭтаФорма[ПутьКДанным] = Число(ЗначениеСтрока);
				
		ИначеЕсли ТипЗначенияВТекЭлементе.Представление = "Время" Тогда
			Если РежимВводаДробныхЗначений = 0 Тогда //часы
				ТекДатаВремя = ЭтаФорма[ПутьКДанным];
				ЗначениеСтрока = Строка(Час(ЭтаФорма[ПутьКДанным])) + Кнопка;
				Если СтрДлина(ЗначениеСтрока) > 2 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 2);
				КонецЕсли;
				НовоеЗначение = Число(ЗначениеСтрока);
				Если НовоеЗначение > 23 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 1);
					НовоеЗначение = Число(ЗначениеСтрока);
				КонецЕсли;
				
				ЭтаФорма[ПутьКДанным] = Дата(Год(ТекДатаВремя), Месяц(ТекДатаВремя), День(ТекДатаВремя),
					НовоеЗначение, Минута(ТекДатаВремя), Секунда(ТекДатаВремя));
				
			Иначе //минуты
				ТекДатаВремя = ЭтаФорма[ПутьКДанным];
				ЗначениеСтрока = Строка(Минута(ЭтаФорма[ПутьКДанным])) + Кнопка;
				Если СтрДлина(ЗначениеСтрока) > 2 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 2);
				КонецЕсли;
				
				НовоеЗначение = Число(ЗначениеСтрока);
				Если НовоеЗначение > 59 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 1);
					НовоеЗначение = Число(ЗначениеСтрока);
				КонецЕсли;
				
				ЭтаФорма[ПутьКДанным] = Дата(Год(ТекДатаВремя), Месяц(ТекДатаВремя), День(ТекДатаВремя),
					Час(ТекДатаВремя), НовоеЗначение, Секунда(ТекДатаВремя));
					
			КонецЕсли;
			
		ИначеЕсли ТипЗначенияВТекЭлементе.Представление = "Дата" Тогда
			Если РежимВводаДробныхЗначений = 0 Тогда //дни
				ТекДатаВремя = ЭтаФорма[ПутьКДанным];
				ЗначениеСтрока = Строка(День(ЭтаФорма[ПутьКДанным])) + Кнопка;
				Если СтрДлина(ЗначениеСтрока) > 2 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 2);
				КонецЕсли;
				
				НовоеЗначение = Число(ЗначениеСтрока);
				
				КоличДнейМесяца = КоличествоДнейМесяца(Месяц(ТекДатаВремя), Год(ТекДатаВремя));
					 
				Если НовоеЗначение > КоличДнейМесяца Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 1);
					НовоеЗначение = Число(ЗначениеСтрока);
				КонецЕсли;
				
				Если НовоеЗначение = 0 Тогда
					НовоеЗначение = 1;
				КонецЕсли;
				
				ЭтаФорма[ПутьКДанным] = Дата(Год(ТекДатаВремя), Месяц(ТекДатаВремя), НовоеЗначение,
					Час(ТекДатаВремя), Минута(ТекДатаВремя), Секунда(ТекДатаВремя));
				
			ИначеЕсли РежимВводаДробныхЗначений = 1 Тогда //месяцы
				ТекДатаВремя = ЭтаФорма[ПутьКДанным];
				ЗначениеСтрока = Строка(Месяц(ЭтаФорма[ПутьКДанным])) + Кнопка;
				Если СтрДлина(ЗначениеСтрока) > 2 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 2);
				КонецЕсли;
				
				НовоеЗначение = Число(ЗначениеСтрока);
				Если НовоеЗначение > 12 Тогда
					ЗначениеСтрока = Прав(ЗначениеСтрока, 1);
					НовоеЗначение = Число(ЗначениеСтрока);
				КонецЕсли;
				
				КоличДнейМесяца = КоличествоДнейМесяца(НовоеЗначение, Год(ТекДатаВремя));
				
				Если День(ТекДатаВремя) > КоличДнейМесяца Тогда
					НовоеЗначениеДень = КоличДнейМесяца;
				Иначе
					НовоеЗначениеДень = День(ТекДатаВремя);
				КонецЕсли;
				
				Если НовоеЗначение = 0 Тогда
					НовоеЗначение = 1;
				КонецЕсли;
				
				ЭтаФорма[ПутьКДанным] = Дата(Год(ТекДатаВремя), НовоеЗначение, НовоеЗначениеДень,
					Час(ТекДатаВремя), Минута(ТекДатаВремя), Секунда(ТекДатаВремя));
				
			Иначе //годы
				ТекДатаВремя = ЭтаФорма[ПутьКДанным];
				
				ТекГод = Год(ЭтаФорма[ПутьКДанным]);
				ТекГодСтрока = Формат(ТекГод, "ЧГ=");
				
				СтрокаСтолетие = Лев(Формат(Год(ТекущаяДата()), "ЧГ="), 2);
				Если Лев(ТекГодСтрока, 2) <> СтрокаСтолетие Тогда
					ТекГодСтрока = СтрокаСтолетие + "00";
				КонецЕсли;
				
				ЗначениеСтрока = Лев(ТекГодСтрока, 2) + Прав(Прав(ТекГодСтрока, 2) + Кнопка, 2);
				НовоеЗначение = Число(ЗначениеСтрока);
				
				КоличДнейМесяца = КоличествоДнейМесяца(Месяц(ТекДатаВремя), НовоеЗначение);
				
				Если День(ТекДатаВремя) > КоличДнейМесяца Тогда
					НовоеЗначениеДень = КоличДнейМесяца;
				Иначе
					НовоеЗначениеДень = День(ТекДатаВремя);
				КонецЕсли;
				
				ЭтаФорма[ПутьКДанным] = Дата(НовоеЗначение, Месяц(ТекДатаВремя), НовоеЗначениеДень,
					Час(ТекДатаВремя), Минута(ТекДатаВремя), Секунда(ТекДатаВремя));
					
			КонецЕсли;
			
		Иначе
			Если ТекЭлементФормыИмя = "ПарольКлавиатура" Тогда
				Авторизация_Пароль = Авторизация_Пароль + Кнопка;
				СимволыПароль = "";
				Для Сч = 1 По СтрДлина(Авторизация_Пароль) Цикл
					СимволыПароль = СимволыПароль + "*";
				КонецЦикла;
				Авторизация_ПарольКлавиатура = СимволыПароль;
			Иначе
				Попытка
					Элементы[ПутьКДанным].ИзменитьСтроку();
				Исключение
				КонецПопытки;
				Попытка
					ЭтаФорма[ПутьКДанным] = ЭтаФорма[ПутьКДанным] + Кнопка;
				Исключение
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	//позиционируемся на исходное поле ввода
	ТекущийЭлемент = Элементы[ТекЭлементФормыИмя];
	
	//обработка изменения значения
	Попытка
		Выполнить(ТекЭлементФормыИмя + "ПриИзменении(Неопределено)");
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Функция КоличествоДнейМесяца(ТекМесяц, ТекГод)
	Если ТекМесяц = 1 ИЛИ ТекМесяц = 3 ИЛИ ТекМесяц = 5 ИЛИ ТекМесяц = 7
		ИЛИ ТекМесяц = 8 ИЛИ ТекМесяц = 10  ИЛИ ТекМесяц = 12 Тогда
		
		КоличДнейМесяца = 31;
	Иначе
		Если ТекМесяц = 2 Тогда
			Если ТекГод%4=0 Тогда
				КоличДнейМесяца = 29;
			Иначе
				КоличДнейМесяца = 28;
			КонецЕсли;
		Иначе
			КоличДнейМесяца = 30;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КоличДнейМесяца;
КонецФункции

&НаКлиенте
Процедура УдалитьЗаданиеПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	ПутевойЛист_Задание.Удалить(ПутевойЛист_Задание_НомерСтроки - 1);
	ОтобразитьСтрокуЗадания(ПутевойЛист_Задание.Количество());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораУсловиеРаботы(Результат, ДопПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		ПутевойЛист_Задание_УсловиеРаботы = Результат;
		ПутевойЛист_Задание_УсловиеРаботыПриИзменении(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПростой(НомерПростоя)
	ДопПараметры = Новый Структура("НомерПростоя", НомерПростоя);
	ОповещениеВыбора = Новый ОписаниеОповещения("ОбработкаВыбораПростоя", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("Обработка.уатТерминалПриемаИВыдачиПутевыхЛистов.Форма.ФормаВыбораСправочников",
		Новый Структура("ИмяОбъектаВыбора", "ВидПростоя"),
		ЭтаФорма,,,, ОповещениеВыбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПростоя(Результат, ДопПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + ДопПараметры.НомерПростоя] = Результат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛист_Простои_ДатаПриИзменении(НомерПростоя)
	ЭтаФорма["ПутевойЛист_Простои_Конец" + НомерПростоя] = НачалоДня(ЭтаФорма["ПутевойЛист_Простои_Начало" + НомерПростоя])
		+ (ЭтаФорма["ПутевойЛист_Простои_Конец" + НомерПростоя] - НачалоДня(ЭтаФорма["ПутевойЛист_Простои_Конец" + НомерПростоя]));
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростой(НомерПростоя)
	ДиалогОКОтмена("Удалить простой " + НомерПростоя + " ?",
		Новый ОписаниеОповещения("УдалитьПростойПродолжение", ЭтотОбъект, Новый Структура("НомерПростоя",НомерПростоя)));
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПростойПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	НомерПростоя = ДопПараметры.НомерПростоя;
	
	//сначала считаем количество простоев до удаления
	КоличПростоев = 6;
	Для Сч = 1 По 6 Цикл
		Если НЕ Элементы["Простой" + Сч].Видимость Тогда
			КоличПростоев = Сч-1;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//скрываем неиспользуемые простои (их будет КоличПростоев-1)
	Для Сч = 1 По 6 Цикл
		ВидомостьПростоя = (Сч < КоличПростоев);
		Если Элементы["Простой" + Сч].Видимость <> ВидомостьПростоя Тогда
			Элементы["Простой" + Сч].Видимость = ВидомостьПростоя;
		КонецЕсли;
	КонецЦикла;
	
	//сдвигаем значение на один, закрывая дырку, если мы удалили не последний простой
	Для Сч = НомерПростоя По КоличПростоев Цикл
		Если Сч < 6 Тогда
			ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + Сч] = ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Простои_Начало" + Сч] = ЭтаФорма["ПутевойЛист_Простои_Начало" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч] = ЭтаФорма["ПутевойЛист_Простои_Конец" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + Сч] = ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + (Сч+1)];
			ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + (Сч+1)] = Неопределено;
			ЭтаФорма["ПутевойЛист_Простои_Начало" + (Сч+1)] = '00010101';
			ЭтаФорма["ПутевойЛист_Простои_Конец" + (Сч+1)] = '00010101';
			ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + (Сч+1)] = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличПростоев = 1 Тогда
		Элементы.ГруппаПростоиЗаголовок.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеЗаправкиПЛ()
	Если ПутевойЛист_Заправки.Количество() = 0 Тогда
		//если ни одной заправки нет считаем, что заполнено корректно
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПутевойЛист_Заправки_ВидЗаправки) Тогда
		Проверка = Ложь;
		
	Иначе
		мсвОбязРеквизиты = Новый Массив;
		мсвОбязРеквизиты.Добавить("ПутевойЛист_Заправки_АЗС");
		мсвОбязРеквизиты.Добавить("ПутевойЛист_Заправки_ТС");
		мсвОбязРеквизиты.Добавить("ПутевойЛист_Заправки_ГСМ");
		мсвОбязРеквизиты.Добавить("ПутевойЛист_Заправки_Количество");
		Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКарта")
			ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад") Тогда
			мсвОбязРеквизиты.Добавить("ПутевойЛист_Заправки_ПластиковаяКарта");
		ИначеЕсли ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
			мсвОбязРеквизиты.Добавить("ПутевойЛист_Заправки_Талоны");
		КонецЕсли;
		
		Проверка = Истина;
		Для Каждого ТекОбязРекв Из мсвОбязРеквизиты Цикл
			Если НЕ ЗначениеЗаполнено(ЭтаФорма[ТекОбязРекв]) Тогда
				Проверка = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
	Если Проверка Тогда
		Элементы.ГруппаЗаправкиПредупрежденияСтраницы.ТекущаяСтраница = Элементы.ГруппаЗаправкиПредупрежденияСтраницаПустая;
	Иначе
		Элементы.ГруппаЗаправкиПредупрежденияСтраницы.ТекущаяСтраница = Элементы.ГруппаЗаправкиПредупрежденияСтраницаВнимание;
	КонецЕсли;
	
	Возврат Проверка;
КонецФункции

&НаКлиенте
Процедура ПриИзмененииВидаЗаправки()
	Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКарта")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад") Тогда
		Элементы.ГруппаПластиковаяКарта.Видимость = Истина;
		ПутевойЛист_Заправки_ГСМ = ПолучитьЗначениеРеквизита(ПолучитьЗначениеРеквизита(ПутевойЛист_ТС, "уатМодель"), "ОсновноеТопливо");
		ПутевойЛист_Заправки_ГСМПриИзменении(Неопределено);
		ПутевойЛист_Заправки_ПластиковаяКарта = 
			уатОбщегоНазначения.уатПластиковаяКартаТСВодителя(ПутевойЛист_ТС, Водитель);
		ПутевойЛист_Заправки_ПластикКартаПриИзменении(Неопределено);
	Иначе
		Элементы.ГруппаПластиковаяКарта.Видимость = Ложь;
		ПутевойЛист_Заправки_ПластиковаяКарта = Неопределено;
		ПутевойЛист_Заправки_ПластикКартаПриИзменении(Неопределено);
	КонецЕсли;
	
	Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
		Элементы.ГруппаЗаправкиТалоны.Видимость = Истина;
	Иначе
		Элементы.ГруппаЗаправкиТалоны.Видимость = Ложь;
		ПутевойЛист_Заправки_Талоны = "";
		ПутевойЛист_Заправки_ТалоныПриИзменении(Неопределено);
	КонецЕсли;
	
	Если ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаСклад")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад")
		ИЛИ ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
		Элементы.ГруппаЗаправкиСумма.Видимость = Ложь;
		ПутевойЛист_Заправки_Сумма = 0;
		ПутевойЛист_Заправки_СуммаПриИзменении(Неопределено);
	Иначе
		Элементы.ГруппаЗаправкиСумма.Видимость = Истина;
	КонецЕсли;
	
	мГруппаГСМ = уатОбщегоНазначения.уатПрочитатьРеквизитГСМ(ПутевойЛист_Заправки_ГСМ, "ГруппаГСМ");
	Если (ПутевойЛист_Заправки_ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") И 
		мГруппаГСМ <> ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Талон")) ИЛИ
		(ПутевойЛист_Заправки_ВидЗаправки <> ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") И 
		мГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Талон")) Тогда
		ПутевойЛист_Заправки_ГСМ = Неопределено;
	КонецЕсли;
	
	УстановитьОтборАЗС();
	ПроверитьЗаполнениеЗаправкиПЛ();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗаправкуПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Результат Тогда
		Если НЕ ПутевойЛист_Заправки_Ссылка.Пустая() Тогда
			//добавляем в кэш удаленных строк заправок, чтобы после закрытия ПЛ пометить на удаление сами документы заправок
			СтрокаЗаправкиДляУдаления = ПутевойЛист_УдаленныеЗаправки.Добавить();
			СтрокаЗаправкиДляУдаления.Ссылка = ПутевойЛист_Заправки_Ссылка;
			СтрокаЗаправкиДляУдаления.НомерСтроки = ПутевойЛист_Заправки_НомерСтроки;
		КонецЕсли;
		
		ПутевойЛист_Заправки.Удалить(ПутевойЛист_Заправки_НомерСтроки - 1);
		ОтобразитьСтрокуЗаправок(ПутевойЛист_Заправки.Количество());
		Элементы.ГруппаЗаправкиПредупрежденияСтраницы.ТекущаяСтраница = Элементы.ГруппаЗаправкиПредупрежденияСтраницаПустая;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВидЗаправкиЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    Если ВыбранныйЭлемент <> Неопределено Тогда
        ПутевойЛист_Заправки_ВидЗаправки = ВыбранныйЭлемент.Значение;
        ПутевойЛист_Заправки_ВидЗаправкиПриИзменении(Неопределено);
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораАЗС(Результат, ДопПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		ПутевойЛист_Заправки_АЗС = Результат;
		ПутевойЛист_Заправки_АЗСПриИзменении(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТСЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПутевойЛист_Заправки_ТС = ВыбранныйЭлемент.Значение;
		ПутевойЛист_Заправки_ТСПриИзменении(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьГСМЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    //мГСМ = уатЗащищенныеФункцииКлиент.СписокГСМдляТС(ПутевойЛист_Организация, ПутевойЛист_ТС, мГруппаГСМ, , Истина);
    //Если ЗначениеЗаполнено(мГСМ) Тогда
    //	ПутевойЛист_Заправки_ГСМ = мГСМ;
    //КонецЕсли;
    
    ГСМ = ВыбранныйЭлемент;
    Если ГСМ <> Неопределено Тогда
        ПутевойЛист_Заправки_ГСМ = ГСМ.Значение;
        ПутевойЛист_Заправки_ГСМПриИзменении(Неопределено);
    КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокГСМдляТС(Организация, ТС, ГруппаГСМ)
	БезАналогов = Ложь;
	УчитыватьТЖ = Истина;
	
	списокГСМ = Новый СписокЗначений;
	
	мсвСтруктур = уатЗащищенныеФункцииСервер.СписокГСМдляТССервер(Организация, ТС, ГруппаГСМ, БезАналогов);
	Для Каждого ТекЭл Из мсвСтруктур Цикл
		списокГСМ.Добавить(ТекЭл.ГСМ);
	КонецЦикла;
			
	Если УчитыватьТЖ Тогда
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	уатНоменклатураГСМ.Номенклатура
		|ИЗ
		|	РегистрСведений.уатНоменклатураГСМ КАК уатНоменклатураГСМ
		|ГДЕ
		|	уатНоменклатураГСМ.ВестиУчетОстатковТЖ");
		тблТЖ = Запрос.Выполнить().Выгрузить();
		Для Каждого ТекСтрока Из тблТЖ Цикл
			списокГСМ.Добавить(ТекСтрока.Номенклатура);
		КонецЦикла;
	КонецЕсли;
	
	Возврат списокГСМ;
КонецФункции

&НаКлиенте
Процедура ВыбратьПластиковуюКартуЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    ПластикКарта = ВыбранныйЭлемент;
    Если ПластикКарта <> Неопределено Тогда
        ПутевойЛист_Заправки_ПластиковаяКарта = ПластикКарта.Значение;
        ПутевойЛист_Заправки_ПластикКартаПриИзменении(Неопределено);
    КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокПластикКарт(ВладельцыКарт)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	уатТопливныеКарты.Ссылка
	|ИЗ
	|	Справочник.уатТопливныеКарты КАК уатТопливныеКарты
	|ГДЕ
	|	уатТопливныеКарты.КомуВыдана В(&ВладельцыКарт)
	|	И НЕ уатТопливныеКарты.ПометкаУдаления");
	Запрос.УстановитьПараметр("ВладельцыКарт", ВладельцыКарт);
	
	списокПК = Новый СписокЗначений;
	списокПК.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Возврат списокПК;
КонецФункции

&НаКлиенте
Процедура ОбновитьИнформациюГотово()
	Если ЗначениеЗаполнено(ПутевойЛист_Ссылка) И ЗначениеЗаполнено(ПутевойЛист_НомерТС) Тогда
		ЗаголовокЗавершено = "ТС: " + ПутевойЛист_НомерТС + " (" + ПутевойЛист_МодельТС + ")";
	Иначе
		ЗаголовокЗавершено = "";
	КонецЕсли;
	
	стрПрицепы = "";
	Для Сч = 1 По 4 Цикл
		Если Элементы["Прицеп" + Сч].Видимость Тогда
			стрПрицепы = стрПрицепы + ", " + уатОбщегоНазначения.уатПредставлениеТС(ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч], ПутевойЛист_Организация);
		КонецЕсли;
	КонецЦикла;
	Если стрПрицепы = "" Тогда
		Готово_Прицепы = "<нет>";
	Иначе
		Готово_Прицепы = Сред(стрПрицепы, 3);
	КонецЕсли;
	
	ВсегоПростоевСек = 0; 
	Для Сч = 1 По 6 Цикл
		Если Элементы["Простой" + Сч].Видимость Тогда
			НачалоПростояДатаВремя = ЭтаФорма["ПутевойЛист_Простои_Начало" + Сч];
			КонецПростояДатаВремя = НачалоДня(НачалоПростояДатаВремя) + (ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч] - НачалоДня(ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч]));
			Если КонецПростояДатаВремя = '00010101' Тогда
				ПродолжитПростояСек = НачалоДня(НачалоПростояДатаВремя + 24*3600) - НачалоПростояДатаВремя;
			Иначе
				ПродолжитПростояСек = КонецПростояДатаВремя - НачалоПростояДатаВремя;
			КонецЕсли;
			
			ВсегоПростоевСек = ВсегоПростоевСек + ПродолжитПростояСек;
		КонецЕсли;
	КонецЦикла;
	Если ВсегоПростоевСек = 0 Тогда
		Готово_Простои = "<нет>";
	Иначе
		Готово_Простои = "" + Формат(Цел(ВсегоПростоевСек/3600), "ЧН=0") + ":" + Формат(Цел((ВсегоПростоевСек - Цел(ВсегоПростоевСек/3600) * 3600) / 60), "ЧЦ=2; ЧН=00; ЧВН=");
	КонецЕсли;
	
	Готово_Заправки = "";
	Готово_Талоны = 0;
	Для Каждого ТекЗаправка Из ПутевойЛист_Заправки Цикл
		Если ТекЗаправка.ВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны") Тогда
			Готово_Талоны = Готово_Талоны + ТекЗаправка.Количество;
		Иначе
			Готово_Заправки = Готово_Заправки + ТекЗаправка.Количество;
		КонецЕсли;
	КонецЦикла;
	Если Готово_Талоны > 0 Тогда
		Готово_Заправки = "" + Готово_Заправки + ", талонов: " + Готово_Талоны;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Готово_Заправки) Тогда
		Готово_Заправки = "<нет>";
	КонецЕсли;
	
	СтруктураПоказателиПоДокументу = ПоказателиПоДокументу();
	Готово_РасходФакт = СтруктураПоказателиПоДокументу.РасходФакт;
	Готово_ОбщийПробег = СтруктураПоказателиПоДокументу.ПробегОбщий;
	Готово_ПробегСГрузом = СтруктураПоказателиПоДокументу.ПробегСГрузом;
	Готово_ПробегПоСпидометру = СтруктураПоказателиПоДокументу.ПробегПоСпидометру;
	Готово_ПробегПорожний = СтруктураПоказателиПоДокументу.ПробегПорожний;
	
	СтрокаЗанятость_ТО = Занятость_ТО(ПутевойЛист_ТС);
	Если ПустаяСтрока(СтрокаЗанятость_ТО) Тогда
		Элементы.ГруппаЗавершеноЗанятостьТОСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершеноЗанятостьТОСтраницаНет;
	Иначе
		Элементы.ГруппаЗавершеноЗанятостьТОСтраницы.ТекущаяСтраница = Элементы.ГруппаЗавершеноЗанятостьТОСтраницаЕсть;
	КонецЕсли;
	
	//надпись на кнопке Печать
	Элементы.ПечатьПЛ.Заголовок = "Печать " + ПолучитьКраткоеИмяПФ(ПутевойЛист_Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКраткоеИмяПФ(ПутевойЛист_Ссылка)
	Если ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._3 Тогда
		Рез = " (Форма 3)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._3спец Тогда
		Рез = " (Форма 3 спец)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._412_АПК Тогда
		Рез = " (Форма 412-АПК)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._4М Тогда
		Рез = " (Форма 4-М)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._4П Тогда
		Рез = " (Форма 4-П)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._4С Тогда
		Рез = " (Форма 4-С)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._6 Тогда
		Рез = " (Форма 6)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._6С Тогда
		Рез = " (Форма 6 спец)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._ПА1 Тогда
		Рез = " (Форма ПА-1)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._ПГ1 Тогда
		Рез = " (Форма ПГ-1)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._ПЛ1 Тогда
		Рез = " (Форма ПЛ-1)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._ЭСМ1 Тогда
		Рез = " (Форма ЭСМ-1)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._ЭСМ2 Тогда
		Рез = " (Форма ЭСМ-2)";
	ИначеЕсли ПутевойЛист_Ссылка.ВидПЛ = Перечисления.уатВидыПЛ._ЭСМ3 Тогда
		Рез = " (Форма ЭСМ-3)";
	Иначе
		Рез = "";
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

&НаСервереБезКонтекста
//Возвращает строку описания подошедших ТО
//Если все выполнены или не запланированы, то возвращается пустая строка
//
Функция Занятость_ТО(ТС)
	Рез = "";
	
	СписокТСДляТО = уатОбщегоНазначения.уатСписокТСДляТО(ТС);
	Результат = СписокТСДляТО.Выбрать();
	Если Результат.Следующий() Тогда
		Рез = "Подошел срок ТО: " + Строка(Результат.ВидРемонта);
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

&НаКлиенте
Функция ПоказателиПоДокументу()
	Рез = Новый Структура("РасходФакт, РасходНорма, ПробегПоСпидометру, ПробегОбщий, ПробегСГрузом, ПробегПорожний", 0, 0, 0, 0, 0, 0);
	Рез.ПробегПоСпидометру = уатОбщегоНазначения.уатРазностьПоказанийСчетчика(ПутевойЛист_СпидометрВыезда, ПутевойЛист_СпидометрВозвращения);
	Рез.РасходФакт = ПутевойЛист_ОстатокТопливаВыезд - ?(ПутевойЛист_ОстатокТопливаВозвратВодитель, ПутевойЛист_ОстатокТопливаВозвратВодитель, ПутевойЛист_ОстатокТопливаВозврат);
	
	Для Каждого ТекСтрока Из ПутевойЛист_Задание Цикл
		Если ТекСтрока.Выполнено Тогда
			Рез.ПробегСГрузом = Рез.ПробегСГрузом + ТекСтрока.КоличествоКмГруз * ТекСтрока.КоличествоЕздок;
			Рез.ПробегПорожний = Рез.ПробегПорожний + ТекСтрока.КоличествоКмПорожний * ТекСтрока.КоличествоЕздок;
		КонецЕсли;
	КонецЦикла;
	Рез.ПробегОбщий = Рез.ПробегСГрузом + Рез.ПробегПорожний;
	
	//todo okoe: реализовать заполнение документа-объекта ПЛ, его расчет и вычисление факта и нормы расхода ГСМ
	
	Возврат Рез;
КонецФункции

&НаКлиенте
Процедура ЗакрытьПЛПродолжение(Результат, ДопПараметры) Экспорт
	
	Если Результат Тогда
		Если ЗаписатьПЛсервер() Тогда
			ПерейтиНаСтраницуВыборРежима();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьПЛсервер()
	флМодифицирован = Ложь;
	
	ДокПЛ = ПутевойЛист_Ссылка.ПолучитьОбъект();
	
	ПутевойЛистДатаВремяВыезда = НачалоДня(ПутевойЛист_ДатаВыезда) + (ПутевойЛист_ВремяВыезда - НачалоДня(ПутевойЛист_ВремяВыезда));
	ПутевойЛистДатаВремяВозращения = НачалоДня(ПутевойЛист_ДатаВозвращения) + (ПутевойЛист_ВремяВозвращения - НачалоДня(ПутевойЛист_ВремяВозвращения));
		
	Если ДокПЛ.ДатаВыезда <> ПутевойЛистДатаВремяВыезда Тогда
		ДокПЛ.ДатаВыезда = ПутевойЛистДатаВремяВыезда;
		флМодифицирован = Истина;
	КонецЕсли;
	
	Если ДокПЛ.ДатаВозвращения <> ПутевойЛистДатаВремяВозращения Тогда
		ДокПЛ.ДатаВозвращения = ПутевойЛистДатаВремяВозращения;
		флМодифицирован = Истина;
	КонецЕсли;
	
	Если ДокПЛ.СпидометрВыезда <> ПутевойЛист_СпидометрВыезда Тогда
		ДокПЛ.СпидометрВыезда = ПутевойЛист_СпидометрВыезда;
		флМодифицирован = Истина;
	КонецЕсли;
	
	Если ВодительНеСогласенСПробегомGPS Тогда
		ДокПЛ.СпидометрВозвращения = ПутевойЛист_СпидометрВозвращенияВодитель;
		флМодифицирован = Истина;
	Иначе
		Если ДокПЛ.СпидометрВозвращения <> ПутевойЛист_СпидометрВозвращения Тогда
			ДокПЛ.СпидометрВозвращения = ПутевойЛист_СпидометрВозвращения;
			флМодифицирован = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ДокПЛ.ВыдатьКоличествоГорючего <> ПутевойЛист_ВыдатьКоличествоГорючего Тогда
		ДокПЛ.ВыдатьКоличествоГорючего = ПутевойЛист_ВыдатьКоличествоГорючего;
		флМодифицирован = Истина;
	КонецЕсли;
	
	Если ДокПЛ.ПробегПоGPS <> ПутевойЛист_ПробегПоGPS Тогда
		ДокПЛ.ПробегПоGPS = ПутевойЛист_ПробегПоGPS;
		флМодифицирован = Истина;
	КонецЕсли;
	
	ОснТопливо = ПолучитьЗначениеРеквизита(ПутевойЛист_МодельТС, "ОсновноеТопливо");
	Для Каждого ТекСтрокаРасходГСМ Из ДокПЛ.РасходГСМ Цикл
		Если ПутевойЛист_ТС = ТекСтрокаРасходГСМ.ТС И ОснТопливо = ТекСтрокаРасходГСМ.ГСМ Тогда
			Если ВодительНеСогласенСРасходомТопливаGPS Тогда
				//ТекСтрокаРасходГСМ.ОстатокПриВыезде = ПутевойЛист_ОстатокТопливаВыезд;
				ТекСтрокаРасходГСМ.ОстатокПриВозвращении = ПутевойЛист_ОстатокТопливаВозвратВодитель;
				флМодифицирован = Истина;
			Иначе
				Если ТекСтрокаРасходГСМ.ОстатокПриВозвращении <> ПутевойЛист_ОстатокТопливаВозврат Тогда
					//ТекСтрокаРасходГСМ.ОстатокПриВыезде = ПутевойЛист_ОстатокТопливаВыезд;
					ТекСтрокаРасходГСМ.ОстатокПриВозвращении = ПутевойЛист_ОстатокТопливаВозврат;
					флМодифицирован = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ СравнитьТабличныеЧасти("Прицепы")
		ИЛИ ВодительНеСогласенСоСчПрицеп1 ИЛИ ВодительНеСогласенСоСчПрицеп2
		ИЛИ ВодительНеСогласенСоСчПрицеп3 ИЛИ ВодительНеСогласенСоСчПрицеп4 Тогда
		
		ДокПЛ.Прицепы.Очистить();
		Для Сч = 1 По 4 Цикл
			ТекПрицеп = ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч];
			Если НЕ ЗначениеЗаполнено(ТекПрицеп) Тогда
				Прервать;
			КонецЕсли;
			
			НоваяСтрока = ДокПЛ.Прицепы.Добавить();
			НоваяСтрока.ТС = ТекПрицеп;
			НоваяСтрока.СчетчикМЧGPS = ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + Сч];
			НоваяСтрока.СчетчикМЧВыезда = ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + Сч];
			Если ЭтаФорма["ВодительНеСогласенСоСчПрицеп" + Сч] Тогда
				НоваяСтрока.СчетчикМЧВозвращения = ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч + "НеСогласен"];
			Иначе
				НоваяСтрока.СчетчикМЧВозвращения = ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч];
			КонецЕсли;
		КонецЦикла;
		
		флМодифицирован = Истина;
	КонецЕсли;
	
	Если НЕ СравнитьТабличныеЧасти("Простои") Тогда
		ДокПЛ.Простои.Очистить();
		Для Сч = 1 По 6 Цикл
			ТекВидПростоя = ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + Сч];
			Если НЕ ЗначениеЗаполнено(ТекВидПростоя) Тогда
				Прервать;
			КонецЕсли;
			
			НоваяСтрока = ДокПЛ.Простои.Добавить();
			НоваяСтрока.ВидПростоя = ТекВидПростоя;
			НоваяСтрока.ВключенДвигатель = ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + Сч];
			НоваяСтрока.НачалоПростоя = ЭтаФорма["ПутевойЛист_Простои_Начало" + Сч];
			НоваяСтрока.КонецПростоя = ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч];
			Если НоваяСтрока.КонецПростоя = НачалоДня(НоваяСтрока.КонецПростоя) Тогда
				НоваяСтрока.ВремяПростоя = '00010101' + ((НоваяСтрока.КонецПростоя + 3600*24) - НоваяСтрока.НачалоПростоя);
			Иначе
				НоваяСтрока.ВремяПростоя = '00010101' + (НоваяСтрока.КонецПростоя - НоваяСтрока.НачалоПростоя);
			КонецЕсли;
			
		КонецЦикла;
		
		флМодифицирован = Истина;
	КонецЕсли;
	
	Если НЕ СравнитьТабличныеЧасти("Задание") Тогда
		ДокПЛ.Задание.Очистить();
		Для Каждого ТекСтрока Из ПутевойЛист_Задание Цикл
			НоваяСтрока = ДокПЛ.Задание.Добавить();
			НоваяСтрока.Выполнено = ТекСтрока.Выполнено;
			НоваяСтрока.КоличествоЕздок = ТекСтрока.КоличествоЕздок;
			НоваяСтрока.КоличествоКмГруз = ТекСтрока.КоличествоКмГруз;
			НоваяСтрока.КоличествоКмПорожний = ТекСтрока.КоличествоКмПорожний;
			НоваяСтрока.КоличествоОпераций = ТекСтрока.КоличествоОпераций;
			НоваяСтрока.КоличествоТонн = ТекСтрока.КоличествоТонн;
			НоваяСтрока.КоличествоЧасов = ТекСтрока.КоличествоЧасов;
			НоваяСтрока.УсловиеРаботы = ТекСтрока.УсловиеРаботы;
		КонецЦикла;
		
		флМодифицирован = Истина;
	КонецЕсли;
	
	//установка статуса ПЛ - закрыт водителем
	ДокПЛ.ЗакрытВодителем = Истина;
	
	//Если флМодифицирован Тогда
		Попытка
			ДокПЛ.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			Сообщить("Возникла ошибка при закрытии путевого листа! Обратитесь к администратору", СтатусСообщения.Внимание);
			ЗаписьЖурналаРегистрации("Терминал выписки ПЛ. Ошибка при закрытии ПЛ",
				УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.уатПутевойЛист, ДокПЛ.Ссылка,
				"Водитель " + Водитель + ": " + ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	//КонецЕсли;
	
	//корректируем старые и добавляем новые заправки ГСМ
	Для Каждого ТекСтрокаЗаправки Из ПутевойЛист_Заправки Цикл
		Если ТекСтрокаЗаправки.Ссылка.Пустая() Тогда // новая заправка
			ДокЗаправка = Документы.уатЗаправкаГСМ.СоздатьДокумент();
			ДокЗаправка.Заполнить(ПутевойЛист_Ссылка);
			ДокЗаправка.ВидДвиженияГСМ = ТекСтрокаЗаправки.ВидЗаправки;
			НайденнаяСтрока = ДокЗаправка.Заправки[0];
			
		Иначе //изменение старой заправки
			ДокЗаправка = ТекСтрокаЗаправки.Ссылка.ПолучитьОбъект();
			ДокЗаправка.ВидДвиженияГСМ = ТекСтрокаЗаправки.ВидЗаправки;
			НайденнаяСтрока = ДокЗаправка.Заправки.Найти(ТекСтрокаЗаправки.НомерСтроки, "НомерСтроки");
			
		КонецЕсли;
		
		ДокЗаправка.АЗС = ТекСтрокаЗаправки.АЗС;
		
		НайденнаяСтрока.Дата 			= ПутевойЛистДатаВремяВыезда;
		НайденнаяСтрока.Водитель 		= ПутевойЛист_Ссылка.Водитель1;
		НайденнаяСтрока.ТС 				= ТекСтрокаЗаправки.ТС;
		НайденнаяСтрока.ГСМ 			= ТекСтрокаЗаправки.ГСМ;
		НайденнаяСтрока.Количество 		= ТекСтрокаЗаправки.Количество;
		НайденнаяСтрока.Сумма 			= ТекСтрокаЗаправки.Сумма;
		НайденнаяСтрока.Цена 			= ?(ТекСтрокаЗаправки.Количество = 0, 0, ТекСтрокаЗаправки.Сумма/ТекСтрокаЗаправки.Количество);
		НайденнаяСтрока.Валюта 			= Константы.ВалютаРегламентированногоУчета.Получить();
		НайденнаяСтрока.ПластиковаяКарта= ТекСтрокаЗаправки.ПластиковаяКарта;
		НайденнаяСтрока.НомераТалонов	= ТекСтрокаЗаправки.Талоны;
		Если НЕ ЗначениеЗаполнено(НайденнаяСтрока.СтавкаНДС) Тогда
			НайденнаяСтрока.СтавкаНДС = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяСтавкаНДС");
			уатОбщегоНазначенияТиповыеСервер.уатРассчитатьСуммуНДСТабЧасти(НайденнаяСтрока, ДокЗаправка);
		КонецЕсли;
		Если ЗначениеЗаполнено(НайденнаяСтрока.СтавкаНДС) Тогда
			уатОбщегоНазначенияТиповыеСервер.уатРассчитатьСуммуНДСТабЧасти(НайденнаяСтрока, ДокЗаправка);
		КонецЕсли;
		
		Попытка
			ДокЗаправка.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Сообщить("Возникла ошибка при записи заправки ГСМ! Обратитесь к администратору", СтатусСообщения.Внимание);
			ЗаписьЖурналаРегистрации("Терминал выписки ПЛ. Ошибка при закрытии ПЛ",
				УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.уатЗаправкаГСМ, ДокЗаправка.Ссылка,
				"Водитель " + Водитель + ": " + ОписаниеОшибки());
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	//помечаем на удаление заправки ГСМ, удаленные в АРМ
	Для Каждого ТекСтрокаУдалить Из ПутевойЛист_УдаленныеЗаправки Цикл
		ДокЗаправка = ТекСтрокаУдалить.Ссылка.ПолучитьОбъект();
		
		мСтроки = ДокЗаправка.Заправки.НайтиСтроки(Новый Структура("ПутевойЛист, НомерСтроки", ПутевойЛист_Ссылка, ТекСтрокаУдалить.НомерСтроки));
		Если мСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для сч = 0 По мСтроки.Количество() - 1 Цикл
			ДокЗаправка.Заправки.Удалить(мСтроки[сч]);
		КонецЦикла;	
		
		//дЗаправкаГСМ.ПакетнаяОбработкаТиповыхДокументов = Ложь;
		Если ДокЗаправка.Заправки.Количество() = 0 Тогда
			Попытка
				ДокЗаправка.Записать();
				ДокЗаправка.УстановитьПометкуУдаления(Истина);
			Исключение
				Сообщить("Возникла ошибка при записи заправки ГСМ! Обратитесь к администратору", СтатусСообщения.Внимание);
				ЗаписьЖурналаРегистрации("Терминал выписки ПЛ. Ошибка при закрытии ПЛ",
					УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.уатЗаправкаГСМ, ДокЗаправка.Ссылка,
					"Водитель " + Водитель + ": " + ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки;
		Иначе
			мРежимЗаписи = ?(ДокЗаправка.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись);
			Попытка
				ДокЗаправка.Записать();
			Исключение
				Сообщить("Возникла ошибка при записи заправки ГСМ! Обратитесь к администратору", СтатусСообщения.Внимание);
				ЗаписьЖурналаРегистрации("Терминал выписки ПЛ. Ошибка при закрытии ПЛ",
					УровеньЖурналаРегистрации.Ошибка, Метаданные.Документы.уатЗаправкаГСМ, ДокЗаправка.Ссылка,
					"Водитель " + Водитель + ": " + ОписаниеОшибки());
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	ПутевойЛист_УдаленныеЗаправки.Очистить();
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция СравнитьТабличныеЧасти(ИмяТЧ)
	Если ИмяТЧ = "Прицепы" Тогда
		//считаем количество прицепов
		КоличПрицепов = 0;
		Для Сч = 1 По 4 Цикл
			Если НЕ Элементы["Прицеп" + Сч].Видимость Тогда
				Если Сч > 0 Тогда
					КоличПрицепов = Сч-1;
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
			КоличПрицепов = Сч;
		КонецЦикла;
		
		Если КоличПрицепов <> ПутевойЛист_Ссылка.Прицепы.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
			
		Для Сч = 1 По КоличПрицепов Цикл
			Если ЭтаФорма["ПутевойЛист_Прицепы_Прицеп" + Сч] <> ПутевойЛист_Ссылка.Прицепы[Сч - 1].ТС
				ИЛИ ЭтаФорма["ПутевойЛист_Прицепы_СчGPS" + Сч] <> ПутевойЛист_Ссылка.Прицепы[Сч - 1].СчетчикМЧGPS
				ИЛИ ЭтаФорма["ПутевойЛист_Прицепы_СчВыезда" + Сч] <> ПутевойЛист_Ссылка.Прицепы[Сч - 1].СчетчикМЧВыезда
				ИЛИ ЭтаФорма["ПутевойЛист_Прицепы_СчВозврата" + Сч] <> ПутевойЛист_Ссылка.Прицепы[Сч - 1].СчетчикМЧВозвращения Тогда
				
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяТЧ = "Простои" Тогда
		//считаем количество простоев
		КоличПростоев = 0;
		Для Сч = 1 По 6 Цикл
			Если НЕ Элементы["Простой" + Сч].Видимость Тогда
				Если Сч > 0 Тогда
					КоличПростоев = Сч-1;
				КонецЕсли;
				
				Прервать;
			КонецЕсли;
			КоличПростоев = Сч;
		КонецЦикла;
		
		Если КоличПростоев <> ПутевойЛист_Ссылка.Простои.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Для Сч = 1 По КоличПростоев Цикл
			Если ЭтаФорма["ПутевойЛист_Простои_ВидПростоя" + Сч] <> ПутевойЛист_Ссылка.Простои[Сч - 1].ВидПростоя
				ИЛИ ЭтаФорма["ПутевойЛист_Простои_ВклДвиг" + Сч] <> ПутевойЛист_Ссылка.Простои[Сч - 1].ВключенДвигатель
				ИЛИ ЭтаФорма["ПутевойЛист_Простои_Начало" + Сч] <> ПутевойЛист_Ссылка.Простои[Сч - 1].НачалоПростоя
				ИЛИ ЭтаФорма["ПутевойЛист_Простои_Конец" + Сч] <> ПутевойЛист_Ссылка.Простои[Сч - 1].КонецПростоя Тогда
								
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ИмяТЧ = "Задание" Тогда
		Если ПутевойЛист_Задание.Количество() <> ПутевойЛист_Ссылка.Задание.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Для Сч = 1 По ПутевойЛист_Задание.Количество() Цикл
			Если ПутевойЛист_Задание[Сч-1].Выполнено <> ПутевойЛист_Ссылка.Задание[Сч-1].Выполнено
				ИЛИ ПутевойЛист_Задание[Сч-1].КоличествоЕздок <> ПутевойЛист_Ссылка.Задание[Сч-1].КоличествоЕздок
				ИЛИ ПутевойЛист_Задание[Сч-1].КоличествоКмГруз <> ПутевойЛист_Ссылка.Задание[Сч-1].КоличествоКмГруз
				ИЛИ ПутевойЛист_Задание[Сч-1].КоличествоКмПорожний <> ПутевойЛист_Ссылка.Задание[Сч-1].КоличествоКмПорожний
				ИЛИ ПутевойЛист_Задание[Сч-1].КоличествоОпераций <> ПутевойЛист_Ссылка.Задание[Сч-1].КоличествоОпераций
				ИЛИ ПутевойЛист_Задание[Сч-1].КоличествоТонн <> ПутевойЛист_Ссылка.Задание[Сч-1].КоличествоТонн
				ИЛИ ПутевойЛист_Задание[Сч-1].КоличествоЧасов <> ПутевойЛист_Ссылка.Задание[Сч-1].КоличествоЧасов
				ИЛИ ПутевойЛист_Задание[Сч-1].УсловиеРаботы <> ПутевойЛист_Ссылка.Задание[Сч-1].УсловиеРаботы
				Тогда
				
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура КлавиатураАвторизация(Команда)
	Элементы.ГруппаКлавиатураАвторизация.Видимость = НЕ Элементы.ГруппаКлавиатураАвторизация.Видимость;
	флКлавиатураВидимость = Элементы.ГруппаКлавиатураАвторизация.Видимость;
	Элементы.ПарольКлавиатура.Видимость = флКлавиатураВидимость;
	Элементы.Пароль.Видимость = НЕ флКлавиатураВидимость;
	Элементы.Логин.ТолькоПросмотр = флКлавиатураВидимость;
	Элементы.КлавиатураАвторизация.ЦветФона = ?(флКлавиатураВидимость, WebЦвета.СеребристоСерый, ЦветФонаКнопки);
	Если флКлавиатураВидимость Тогда
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьТекущийЭлемент_Логин", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьТекущийЭлемент_Логин()
	ТекущийЭлемент = Элементы.Логин;
КонецПроцедуры

#КонецОбласти
