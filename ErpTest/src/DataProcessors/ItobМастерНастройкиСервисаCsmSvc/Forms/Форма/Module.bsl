
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		
		ПоказатьПредупреждение(,НСтр("ru = 'Мастер настройки не работает в Web-клиенте!'"));
		Отказ = Истина;		
		
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события "ПриИзменении" флага "ИспользоватьРепликацию".
//
&НаКлиенте
Процедура ИспользоватьРепликациюПриИзменении(Элемент)
	
	УправлениеВидимостьюНастройкаРепликации();	
	
КонецПроцедуры // ИспользоватьРепликациюПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды "Далее"
//
&НаКлиенте
Процедура КомандаДалее(Команда)
	
	Если Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаСтарт" Тогда
		
		Если РасположениеСлужбы = 0 Тогда
			
			СисИнфо = Новый СистемнаяИнформация;
			
			Если СисИнфо.ТипПлатформы = ТипПлатформы.Linux_x86 ИЛИ СисИнфо.ТипПлатформы = ТипПлатформы.Linux_x86_64 Тогда
				
				ПоказатьПредупреждение(,НСтр("ru = 'Службу CsmSvc необходимо инсталлировать на операционной системе Windows'"));
				Возврат;
				
			КонецЕсли;
			
			Элементы.КнопкаДалее.Заголовок = НСтр("ru = 'Установить службу CsmSvc >>'");
			Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы["СтраницаЛокальнаяУстановка1"];			
			
			
		Иначе
			
			ЗначениеКонстантыАдресСервиса = ПолучитьАдресСервисаCsmSvc();
			АдресСервера = ЗначениеКонстантыАдресСервиса.Адрес;
			Порт = ЗначениеКонстантыАдресСервиса.Порт;
			
			Элементы.КнопкаДалее.Заголовок = "Готово";
			Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы["СтраницаСетеваяУстановка1"];		
			
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаЛокальнаяУстановка1" Тогда
		
		УстановитьСлужбуCsmSvc();
		
		#Если НЕ ВебКлиент Тогда
			
		СписокIP = Новый СписокЗначений;
			
		Попытка
			
			ИмяФайла = КаталогВременныхФайлов() + "ipconfig.txt";
			
			WshShell = ПолучитьCOMОбъект("", "WScript.Shell");
			WshShell.Run("cmd /c ipconfig /all >> "+?(Найти(ИмяФайла, " "), """"+ИмяФайла+"""", ИмяФайла), 2, Истина);
			
			// Активизируем окно 1С:Предприятия
			АктивизироватьОкноФормы(НСтр("ru = 'Сбор данных по сетевым интерфейсам...'"));	
			
			// Сбор данных по сетевым интерфейсам
			ТекстовыйДок = Новый ТекстовыйДокумент;
			ТекстовыйДок.Прочитать(ИмяФайла);
			
			Для Счетчик = 1 По ТекстовыйДок.КоличествоСтрок() Цикл
				ТекСтрока = ВРег(СокрЛП(ТекстовыйДок.ПолучитьСтроку(Счетчик)));
				Если Лев(ТекСтрока,СтрДлина("IP Address")) = "IP ADDRESS" Тогда
					ТекущийIP = СокрЛП(Сред(ТекСтрока, Найти(ТекСтрока,":")+1));
					
					Если СписокIP.НайтиПоЗначению(ТекущийIP) = Неопределено Тогда
						СписокIP.Добавить(ТекущийIP);	
						
					КонецЕсли;
					
				КонецЕсли;			
				
			КонецЦикла;				
		
		Исключение
			ItobОбщегоНазначенияСервер.ЗаписьЖурналаРегистрацииОшибка(НСтр("ru = 'Сбор данных по сетевым интерфейсам'", "ru"),ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;		
		
		СписокIP.СортироватьПоЗначению();
		
		Если СписокIP.НайтиПоЗначению("127.0.0.1") = Неопределено Тогда
			СписокIP.Вставить(0, "127.0.0.1");
			
		КонецЕсли;
		
		Для каждого ЭлементСписка Из СписокIP Цикл
			Элементы.СетевойИнтерфейс.СписокВыбора.Добавить(ЭлементСписка.Значение);
			
		КонецЦикла;
		
		СетевойИнтерфейс = "127.0.0.1";
		
		// Определим каталог установки программы
		ПутьКПрограмме = WshShell.RegRead("HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\CsmService\ImagePath");
		ПутьКПрограмме = СокрЛП(ПутьКПрограмме) + "###";
		
		ПутьIniФайла = СтрЗаменить(ПутьКПрограмме,"CsmSvc.exe###", "CsmSvc.ini");
		
		ТекстовыйДок = Новый ТекстовыйДокумент;
		ТекстовыйДок.Прочитать(ПутьIniФайла);
		
		ТекСекция = "";
		
		СоответствиеПараметров = Новый Соответствие;
		СоответствиеПараметров.Вставить("ENABLED", "ИспользоватьРепликацию");
		СоответствиеПараметров.Вставить("SERVER", "АдресСервераРепликации");
		СоответствиеПараметров.Вставить("PORT", "ПортСервераРепликации");
		СоответствиеПараметров.Вставить("LOGIN", "ЛогинРепликации");
		СоответствиеПараметров.Вставить("PASSWORD", "ПарольРепликации");		
		
		Для Счетчик = 1 По ТекстовыйДок.КоличествоСтрок() Цикл
			
			ТекСтрока = СокрЛП(ТекстовыйДок.ПолучитьСтроку(Счетчик));
			ТекСтрокаВРег = ВРЕГ(ТекСтрока);
			
			Если ТекСтрокаВРег = "[HTTP]" Тогда
				ТекСекция = "http";
				
			ИначеЕсли ТекСтрокаВРег = "[REPLICATION]" Тогда
				ТекСекция = "replication";				
				
			КонецЕсли;
			
			Если ТекСекция = "http" Тогда
				
				Если Лев(ТекСтрокаВРег,4) = "PORT" Тогда
					Порт = Сред(ТекСтрока, Найти(ТекСтрока,"=")+1);				
					
				КонецЕсли;
				
			ИначеЕсли ТекСекция = "replication" Тогда
				
				Если ПустаяСтрока(ТекСтрока) ИЛИ Лев(ТекСтрока,1)=";" ИЛИ Найти(ТекСтрока,"=")=0 Тогда
					Продолжить;
					
				КонецЕсли;
				
				Для каждого ЭлементПараметров Из СоответствиеПараметров Цикл
					Если Лев(ТекСтрокаВРег,СтрДлина(ЭлементПараметров.Ключ)) = ЭлементПараметров.Ключ Тогда
						ЭтаФорма[ЭлементПараметров.Значение] = Сред(ТекСтрока, Найти(ТекСтрока,"=")+1);
						
					КонецЕсли;
					
				КонецЦикла;			
				
			КонецЕсли;			
			
		КонецЦикла;		
		
		#КонецЕсли
		
		Элементы.КнопкаДалее.Заголовок = "Готово";
		Элементы.ГруппаСтраниц.ТекущаяСтраница = Элементы.ГруппаСтраниц.ПодчиненныеЭлементы["СтраницаЛокальнаяУстановка2"];
		УправлениеВидимостьюНастройкаРепликации();
	
		
	ИначеЕсли Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаЛокальнаяУстановка2" Тогда
		
		WshShell = ПолучитьCOMОбъект("", "WScript.Shell");	
		
		WshShell.Run("net stop CsmService", 2, Истина);
		
		// Определим каталог установки программы
		ПутьКПрограмме = WshShell.RegRead("HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\CsmService\ImagePath");
		ПутьКПрограмме = СокрЛП(ПутьКПрограмме) + "###";
		
		ПутьIniФайла = СтрЗаменить(ПутьКПрограмме,"CsmSvc.exe###", "CsmSvc.ini");
		
		ТекстовыйДок = Новый ТекстовыйДокумент;
		ТекстовыйДок.Прочитать(ПутьIniФайла);
		
		ТекСекция = "";
		
		СоответствиеПараметров = Новый Соответствие;
		СоответствиеПараметров.Вставить("ENABLED", "ИспользоватьРепликацию");
		СоответствиеПараметров.Вставить("SERVER", "АдресСервераРепликации");
		СоответствиеПараметров.Вставить("PORT", "ПортСервераРепликации");
		СоответствиеПараметров.Вставить("LOGIN", "ЛогинРепликации");
		СоответствиеПараметров.Вставить("PASSWORD", "ПарольРепликации");		
		
		Для Счетчик = 1 По ТекстовыйДок.КоличествоСтрок() Цикл
			
			ТекСтрока = СокрЛП(ТекстовыйДок.ПолучитьСтроку(Счетчик));
			ТекСтрокаВРег = ВРЕГ(ТекСтрока);
			
			Если ТекСтрокаВРег = "[HTTP]" Тогда
				
				ТекСекция = "http";
				
			ИначеЕсли ТекСтрокаВРег = "[REPLICATION]" Тогда
				ТекСекция = "replication";				
				
			КонецЕсли;
			
			Если ТекСекция = "http" Тогда
				
				Если Лев(ТекСтрокаВРег,4) = "PORT" Тогда
					ТекстовыйДок.ЗаменитьСтроку(Счетчик,"port="+Формат(Порт,"ЧГ=0")); 
				КонецЕсли;
				
			ИначеЕсли ТекСекция = "replication" Тогда
				
				Если ПустаяСтрока(ТекСтрока) ИЛИ Лев(ТекСтрока,1)=";" ИЛИ Найти(ТекСтрока,"=")=0 Тогда
					Продолжить;
					
				КонецЕсли;
				
				Для каждого ЭлементПараметров Из СоответствиеПараметров Цикл
					Если Лев(ТекСтрокаВРег,СтрДлина(ЭлементПараметров.Ключ)) = ЭлементПараметров.Ключ Тогда
						
						ЗначениеПараметра = ЭтаФорма[ЭлементПараметров.Значение];
						Если ТипЗнч(ЗначениеПараметра) = Тип("Число") Тогда
							ЗначениеПараметра = Формат(ЗначениеПараметра,"ЧГ=0");
						ИначеЕсли ТипЗнч(ЗначениеПараметра) = Тип("Булево") Тогда		
							ЗначениеПараметра = ?(ЗначениеПараметра=Истина, "1", "0");
						КонецЕсли;
						
						ТекстовыйДок.ЗаменитьСтроку(Счетчик, НРег(ЭлементПараметров.Ключ)+"="+ЗначениеПараметра); 
						
					КонецЕсли;
					
				КонецЦикла;			
				
			КонецЕсли;
		КонецЦикла;
		             
		ТекстовыйДок.Записать(ПутьIniФайла);
		
		WshShell.Run("net start CsmService", 2, Истина);
		
		АктивизироватьОкноФормы(НСтр("ru = 'Проверка доступности сервиса...'"));
		
		Если НЕ ItobОперативныйМониторингКлиент.ПроверитьДоступностьСервисаCsmSvc(СетевойИнтерфейс, Порт) Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'Сервис недоступен!'"));
			
		Иначе
			ПолныйАдресСервиса = СетевойИнтерфейс+":"+Формат(Порт,"ЧН=0; ЧГ=0");
			УстановитьАдресСервисаCsmSvc(СетевойИнтерфейс,Порт);
			ЭтаФорма.Закрыть(Истина);
			
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраниц.ТекущаяСтраница.Имя = "СтраницаСетеваяУстановка1" Тогда
		
		Если НЕ ItobОперативныйМониторингКлиент.ПроверитьДоступностьСервисаCsmSvc(АдресСервера,Порт) Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'Сервис недоступен!'"));
			
		Иначе
			ПолныйАдресСервиса = АдресСервера+":"+Формат(Порт,"ЧН=0; ЧГ=0");
			УстановитьАдресСервисаCsmSvc(АдресСервера,Порт);
			
			ДопПараметры = Новый Структура;
			ДопПараметры.Вставить("НастройкиОпределены", Истина);
			ДопПараметры.Вставить("СерверАдрес", АдресСервера);
			ДопПараметры.Вставить("СерверПорт", Порт);
			ДопПараметры.Вставить("КлиентАдрес", АдресСервера);
			ДопПараметры.Вставить("КлиентПорт", Порт);
			ItobОбщегоНазначенияСервер.УстановитьПараметрыСеанса(ДопПараметры);
			
			ЭтаФорма.Закрыть(Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьДистрибутивCsmSvc(Команда)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = НСтр("ru = 'Выберите путь сохранения файла CsmSvcSetup.exe'")+" ";
	Диалог.ПолноеИмяФайла = "CsmSvcSetup.exe";
	Диалог.ПредварительныйПросмотр = Истина;
	Диалог.Фильтр = "CsmSvcSetup.exe (CsmSvcSetup.exe)|CsmSvcSetup.exe|";
	
	Диалог.Показать(Новый ОписаниеОповещения("ООФайлПоказать", ItobОбщегоНазначенияКлиент)); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура запускает инсталлятор службы CsmSvc.
//
&НаКлиенте
Процедура УстановитьСлужбуCsmSvc()
	
	#Если НЕ ВебКлиент Тогда
	
	Адрес = ItobОбщегоНазначенияСервер.ПоместитьИнсталляторВХранилище();
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ДвоичныеДанные.Записать(КаталогВременныхФайлов() + "ItobCsmSvcSetup.exe");
	
	НачатьЗапускПриложения(Новый ОписаниеОповещения("ООПустая", ItobОбщегоНазначенияКлиент), КаталогВременныхФайлов() + "ItobCsmSvcSetup.exe", , Истина);
	
	#КонецЕсли
	
КонецПроцедуры // УстановитьСлужбуCsmSvc()

// Процедура активизирует форму инсталлятора службы CsmSvc.
//
&НаКлиенте
Процедура АктивизироватьОкноФормы(ТекстПредупреждения)
	
	#Если НЕ ВебКлиент Тогда
	
	// Взято с http://www.forum.mista.ru/topic.php?id=416922
	
	WSS=Новый COMОбъект("WScript.Shell");
	oExec=WSS.Exec("cmd /k");
	QueryResult=ПолучитьCOMОбъект("winmgmts:\\.\root\CIMV2").ExecQuery("SELECT ParentProcessId FROM Win32_Process where ProcessId = '"+Формат(oExec.ProcessID,"ЧГ=0")+"'");
	Для Каждого Item Из QueryResult Цикл 
		CurrentProcessId=Item.ParentProcessId;
	КонецЦикла;
	oExec.Terminate();
	jsName=КаталогВременныхФайлов()+(Новый УникальныйИдентификатор)+".js";
	Скрипт=Новый ТекстовыйДокумент;
	Скрипт.ДобавитьСтроку("WScript.CreateObject(""WScript.Shell"").AppActivate("+Формат(CurrentProcessId,"ЧГ=0")+");
	|(new ActiveXObject(""Scripting.FileSystemObject"")).DeleteFile("""+СтрЗаменить(jsName,"\","\\")+""");");
	Скрипт.Записать(jsName,"windows-1251");
	WSS.Run(""""+jsName+"""",0);
	
	ПоказатьПредупреждение(,ТекстПредупреждения,1);
	
	#КонецЕсли
	
КонецПроцедуры // АктивизироватьОкноФормы()

// Процедура устанавливает новые значение в регистр сведений ItobСервисыCsmSvc.
//
&НаСервере
Процедура УстановитьАдресСервисаCsmSvc(Адрес,Порт)
	
	РегистрыСведений.ItobСервисыCsmSvc.СоздатьНаборЗаписей().Записать(Истина);
	
	НовЗапись = РегистрыСведений.ItobСервисыCsmSvc.СоздатьМенеджерЗаписи();
	НовЗапись.ВидОбращения = Перечисления.ItobВидыОбращенияCsmSvc.Сервер;
	НовЗапись.АдресCsmService = Адрес;
	НовЗапись.ПортCsmService = Порт;
	НовЗапись.Записать();
	
	НовЗапись = РегистрыСведений.ItobСервисыCsmSvc.СоздатьМенеджерЗаписи();
	НовЗапись.ВидОбращения = Перечисления.ItobВидыОбращенияCsmSvc.Клиент;
	НовЗапись.АдресCsmService = Адрес;
	НовЗапись.ПортCsmService = Порт;
	НовЗапись.Записать();	
	
КонецПроцедуры // УстановитьАдресСервисаCsmSvc()

// Функция возвращает значение адреса сервиса CsmSvc.
//
&НаСервере
Функция ПолучитьАдресСервисаCsmSvc()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ItobСервисыCsmSvc.АдресCsmService,
	               |	ItobСервисыCsmSvc.ПортCsmService
	               |ИЗ
	               |	РегистрСведений.ItobСервисыCsmSvc КАК ItobСервисыCsmSvc
	               |ГДЕ
	               |	ItobСервисыCsmSvc.ВидОбращения = ЗНАЧЕНИЕ(Перечисление.ItobВидыОбращенияCsmSvc.Сервер)";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		Возврат Новый Структура("Адрес,Порт", Выборка.АдресCsmService, Выборка.ПортCsmService);
		
	Иначе		
		Возврат Новый Структура("Адрес,Порт", "127.0.0.1", 8091);
	
	КонецЕсли;	
	
КонецФункции // ПолучитьАдресСервисаCsmSvc()

// Устанавливает видимость элементов.
//
&НаКлиенте
Процедура УправлениеВидимостьюНастройкаРепликации()
	
	Элементы.АдресСервераРепликации.Доступность = ИспользоватьРепликацию;
	Элементы.ПортСервераРепликации.Доступность  = ИспользоватьРепликацию;
	Элементы.ЛогинРепликации.Доступность        = ИспользоватьРепликацию;
	Элементы.ПарольРепликации.Доступность       = ИспользоватьРепликацию; 
	
КонецПроцедуры // УправлениеВидимостьюНастройкаРепликации()

#КонецОбласти
