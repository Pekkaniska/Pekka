
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
    
    Номенклатура              = Параметры.Номенклатура;
	Упаковка 				  = Параметры.Упаковка;
	
	СтруктураНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "ЕдиницаИзмерения, ИспользоватьУпаковки");
		
	Если Упаковка.Пустая()
		И СтруктураНоменклатуры.ИспользоватьУпаковки Тогда 
		Упаковка = ПодборТоваровВызовСервера.ПолучитьУпаковкуХранения(Номенклатура);
	КонецЕсли;
	
	Если СтруктураНоменклатуры.ИспользоватьУпаковки Тогда
		Элементы.Упаковка.ПодсказкаВвода = СтруктураНоменклатуры.ЕдиницаИзмерения;
	КонецЕсли;
		
	СтараяУпаковка            = Упаковка;
		
	НаименованиеТовара = "" + Параметры.Номенклатура;
			
	Элементы.Упаковка.Видимость = ЗначениеЗаполнено(Номенклатура.НаборУпаковок);
	Элементы.ЕдиницаИзмерения.Видимость = Не ЗначениеЗаполнено(Номенклатура.НаборУпаковок);
	
	КоличествоУпаковок = 1;
    КоличествоПоМодели = 1;
			
 	УстановитьВидимостьКоличествоЕдиницХранения();

	ОбновитьКоличество();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	УпаковкаПриИзмененииНаСервере(СтараяУпаковка);
	СтараяУпаковка = Упаковка;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаОчистка(Элемент, СтандартнаяОбработка)
	
	УпаковкаПриИзмененииНаСервере(СтараяУпаковка);
	СтараяУпаковка = Упаковка;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоУпаковокПриИзменении(Элемент)
	
	ОбновитьКоличество();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	ОбновитьКоличествоУпаковок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	ОчиститьСообщения();
	    
	Если КоличествоПоМодели = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено количество моделей техники'"),,"КоличествоПоМодели",,Отказ);
    КонецЕсли;
    
    Если ДатаНачалаАренды > ДатаОкончанияАренды И ЗначениеЗаполнено(ДатаОкончанияАренды) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата начала аренды не может быть больше даты окончания'"),,,,Отказ);
    КонецЕсли;    
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПодобранныеТовары = Новый Массив;
	
	ПараметрыТовара = Новый Структура;
	ПараметрыТовара.Вставить("Номенклатура",        Номенклатура); 
	ПараметрыТовара.Вставить("ДатаНачалаАренды",    ДатаНачалаАренды);
	ПараметрыТовара.Вставить("ДатаОкончанияАренды", ДатаОкончанияАренды);
	ПараметрыТовара.Вставить("Упаковка",            Упаковка);
	ПараметрыТовара.Вставить("КоличествоУпаковок",  КоличествоУпаковок);
	ПараметрыТовара.Вставить("КоличествоПоМодели",  КоличествоПоМодели);
				
	ПодобранныеТовары.Добавить(ПараметрыТовара);
	
	Закрыть(ПодобранныеТовары);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(ЭтотОбъект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачалаАренды", "ДатаОкончанияАренды"),
        Новый ОписаниеОповещения("РедактироватьПериодЗавершение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьПериодЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт
    
    ДатаАрендыПриИзменении(Неопределено);  
    
КонецПроцедуры    

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

КонецПроцедуры

&НаСервере
Процедура УпаковкаПриИзмененииНаСервере(СтараяУпаковка)
	
	УстановитьВидимостьКоличествоЕдиницХранения();
	ОбновитьКоличество();
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличество()
	
	Количество = КоличествоУпаковок*Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоУпаковок()
	
	КоличествоУпаковок = Количество/Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКоличествоЕдиницХранения()
	
	ЕдиницаИзмеренияТипИзмеряемойВеличины = "";
	УпаковкаТипИзмеряемойВеличины = "";
	
	ЕдиницаМерная = Справочники.УпаковкиЕдиницыИзмерения.ЭтоМернаяЕдиница(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения"),
																			ЕдиницаИзмеренияТипИзмеряемойВеличины);
																			
	УпаковкаМерная = Справочники.УпаковкиЕдиницыИзмерения.ЭтоМернаяЕдиница(Упаковка,
																			УпаковкаТипИзмеряемойВеличины);
	Если ЕдиницаМерная
		И УпаковкаТипИзмеряемойВеличины <> ЕдиницаИзмеренияТипИзмеряемойВеличины
		И УпаковкаТипИзмеряемойВеличины <> "Упаковка"
		И УпаковкаТипИзмеряемойВеличины <> ""
		Или ЕдиницаИзмеренияТипИзмеряемойВеличины = "КоличествоШтук" 
		И УпаковкаМерная Тогда 
		
		Элементы.Количество.Видимость = Истина;
		
	Иначе
		
		Элементы.Количество.Видимость = Ложь;
        
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаАрендыПриИзменении(Элемент) 
    
    Если ЗначениеЗаполнено(ДатаНачалаАренды) И ЗначениеЗаполнено(ДатаОкончанияАренды) Тогда
        КоличествоУпаковок = (КонецДня(ДатаОкончанияАренды) + 1 - НачалоДня(ДатаНачалаАренды)) / (3600 * 24);
        ОбновитьКоличество();
	КонецЕсли;    
	
	Если ЗначениеЗаполнено(ДатаНачалаАренды) И ДатаНачалаАренды < НачалоДня(ТекущаяДата()) Тогда
		Элементы.ДатаНачалаАренды.ЦветФона = WebЦвета.Лосось;
	Иначе 
		Элементы.ДатаНачалаАренды.ЦветФона = WebЦвета.Белый;  	
	КонецЕсли; 
    
КонецПроцедуры

#КонецОбласти
