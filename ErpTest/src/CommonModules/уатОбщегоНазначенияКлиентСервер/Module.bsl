
// ПРОЦЕДУРЫ И ФУНКЦИИ УПРАВЛЕНИЯ ИНТЕРФЕЙСОМ ПУТЕВОГО ЛИСТА

// Установка видимость элементов формы путевого листа
//	Форма - управляемая форма ПЛ
//	СтруктураРеквизитов - структура с элементами ВидПЛ, Водитель2, Сотрудник1, Сотрудник2
//
Процедура УстановитьВидимостьПутевойЛист(Форма, СтруктураРеквизитов) Экспорт
	//международный п/л
	Если СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._4М") Тогда
		Форма.Элементы.ЗаправкиГСМСтрана.Видимость = Истина;
	Иначе
		Форма.Элементы.ЗаправкиГСМСтрана.Видимость = Ложь;
	КонецЕсли;	
	
	//легковой п/л
	Если СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._3") Тогда
		Если ЗначениеЗаполнено(СтруктураРеквизитов.Водитель2) Тогда
			СтруктураРеквизитов.Водитель2 = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		КонецЕсли;
		Если ЗначениеЗаполнено(СтруктураРеквизитов.Сотрудник1) Тогда
			СтруктураРеквизитов.Сотрудник1 = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		КонецЕсли;
		Если ЗначениеЗаполнено(СтруктураРеквизитов.Сотрудник2) Тогда
			СтруктураРеквизитов.Сотрудник2 = ПредопределенноеЗначение("Справочник.Сотрудники.ПустаяСсылка");
		КонецЕсли;
		
		Форма.Элементы.ГруппаВодитель2.Видимость = Ложь;
		Форма.Элементы.ГруппаСотрудник1.Видимость = Ложь;
		Форма.Элементы.ГруппаСотрудник2.Видимость = Ложь;
		
		Форма.Элементы.ГруппаСтажер1.Видимость = Ложь;
		Форма.Элементы.ГруппаСтажер2.Видимость = Ложь;
		Форма.Элементы.ГруппаСтажер3.Видимость = Ложь;
		Форма.Элементы.ГруппаСтажер4.Видимость = Ложь;
		
		Форма.Элементы.Сотрудник1.Заголовок = "Сопр.лицо 1";
		Форма.Элементы.Сотрудник2.Заголовок = "Сопр.лицо 2";
			
		Форма.Элементы.ЗаданиеКоличествоКмГруз.Заголовок = "Пробег с пассажирами";
		
		Форма.Элементы.ГруппаТТД.Видимость = Ложь;
	Иначе
		Форма.Элементы.ГруппаВодитель2.Видимость = Истина;
		Форма.Элементы.ГруппаСотрудник1.Видимость = Истина;
		Форма.Элементы.ГруппаСотрудник2.Видимость = Истина;
		
		Форма.Элементы.ГруппаСтажер1.Видимость = Истина;
		Форма.Элементы.ГруппаСтажер2.Видимость = Истина;
		Форма.Элементы.ГруппаСтажер3.Видимость = Истина;
		Форма.Элементы.ГруппаСтажер4.Видимость = Истина;
		
		Если СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._6")
			ИЛИ СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._6С")
			ИЛИ СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._ПА1") Тогда
			
			Форма.Элементы.Сотрудник1.Заголовок = "Кондуктор 1";
			Форма.Элементы.Сотрудник2.Заголовок = "Кондуктор 2";
			
			Форма.Элементы.ЗаданиеКоличествоКмГруз.Заголовок = "Пробег с пассажирами";
			
			Форма.Элементы.ГруппаТТД.Видимость = Ложь;
		Иначе	
			Форма.Элементы.Сотрудник1.Заголовок = "Сопр.лицо 1";
			Форма.Элементы.Сотрудник2.Заголовок = "Сопр.лицо 2";
			
			Форма.Элементы.ЗаданиеКоличествоКмГруз.Заголовок = "Пробег с грузом";
			
			Форма.Элементы.ГруппаТТД.Видимость = Истина;
		КонецЕсли;	
	КонецЕсли;
	
	//УАТ ПРОФ
	Если СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._3") Тогда
		Форма.Элементы.ГруппаВыручка.Видимость = Ложь;
		Форма.Элементы.ГруппаБилеты.Видимость = Ложь;
		Форма.Элементы.ГруппаТТД.Видимость = Ложь;
	Иначе
		Если СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._6")
			ИЛИ СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._6С")
			ИЛИ СтруктураРеквизитов.ВидПЛ = ПредопределенноеЗначение("Перечисление.уатВидыПЛ._ПА1") Тогда
			
			Если уатОбщегоНазначенияСервер.уатИспользоватьУчетБилетовИВыручки() Тогда 
				Форма.Элементы.ГруппаВыручка.Видимость = Истина;
				Форма.Элементы.ГруппаБилеты.Видимость = Истина;
			Иначе 
				Форма.Элементы.ГруппаВыручка.Видимость = Ложь;
				Форма.Элементы.ГруппаБилеты.Видимость = Ложь;
			КонецЕсли;
			
			Форма.Элементы.ГруппаТТД.Видимость = Ложь;
		Иначе
			Форма.Элементы.ГруппаВыручка.Видимость = Ложь;
			Форма.Элементы.ГруппаБилеты.Видимость = Ложь;
			Форма.Элементы.ГруппаТТД.Видимость = Истина;
		КонецЕсли;
	КонецЕсли;
	
	УчетВыработкиПоОбъектамСтроительства = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(СтруктураРеквизитов.Организация,
		ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.УчетВыработкиПоОбъектамСтроительства"));
	
	Форма.Элементы.ЗаданиеГруппаОбъектСтроительства.Видимость = УчетВыработкиПоОбъектамСтроительства;
КонецПроцедуры

// Заполняет таблицу ЗП в ПЛ. Если указан сотрудник, то заполняет таблицу только по нему,
//строки остальных сотрудников не пересчитываются
//	Форма - управляемая форма ПЛ
//	СтруктураРеквизитов - структура с элементами Водитель1, Водитель2, Сотрудник1, Сотрудник2,
// НачислятьЗПВодителю1, НачислятьЗПВодителю2, НачислятьЗПСотруднику1, НачислятьЗПСотруднику2
//
Процедура ВывестиТаблицуЗП(Форма, СтруктураРеквизитов, Сотрудник = Неопределено) Экспорт 
	Если Сотрудник = Неопределено Тогда
		Форма.ЗарплатаСводная.Очистить();
		ВывестиТаблицуЗП(Форма, СтруктураРеквизитов, СтруктураРеквизитов.Вод1);
		ВывестиТаблицуЗП(Форма, СтруктураРеквизитов, СтруктураРеквизитов.Вод2);
		ВывестиТаблицуЗП(Форма, СтруктураРеквизитов, СтруктураРеквизитов.Сотр1);
		ВывестиТаблицуЗП(Форма, СтруктураРеквизитов, СтруктураРеквизитов.Сотр2);
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
		Возврат;
	КонецЕсли;
	
	мСумма = 0;
	Для Каждого ТекСтрока Из СтруктураРеквизитов.Зарплата Цикл
		Если ТекСтрока.Сотрудник = Сотрудник Тогда
			мСумма = мСумма + ТекСтрока.Сумма;
		КонецЕсли;	
	КонецЦикла;
	
	мсвСтрок = Форма.ЗарплатаСводная.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	Если мсвСтрок.Количество() = 0 Тогда
		СтрПоиска = Форма.ЗарплатаСводная.Добавить();
		СтрПоиска.Сотрудник = Сотрудник;
	Иначе
		СтрПоиска = мсвСтрок[0];
	КонецЕсли;
	Если Сотрудник = СтруктураРеквизитов.Вод1 Тогда
		СтрПоиска.НачислятьЗарплату = СтруктураРеквизитов.ЗПВод1;
	ИначеЕсли Сотрудник = СтруктураРеквизитов.Вод2 Тогда
		СтрПоиска.НачислятьЗарплату = СтруктураРеквизитов.ЗПВод2;
	ИначеЕсли Сотрудник = СтруктураРеквизитов.Сотр1 Тогда
		СтрПоиска.НачислятьЗарплату = СтруктураРеквизитов.ЗПСотр1;
	ИначеЕсли Сотрудник = СтруктураРеквизитов.Сотр2 Тогда
		СтрПоиска.НачислятьЗарплату = СтруктураРеквизитов.ЗПСотр2;
	КонецЕсли;
	СтрПоиска.Сумма = мСумма;
КонецПроцедуры

