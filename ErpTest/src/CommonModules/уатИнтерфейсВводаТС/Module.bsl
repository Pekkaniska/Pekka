/////////////////////////////////////////////////////////////////////////////////////
// НАЗНАЧЕНИЕ:
// МОДУЛЬ ПРЕДНАЗНАЧЕН ДЛЯ ОБРАБОТКИ ВВОДА ТС В ПОЛЯ ВВОДА И ТАБЛИЧНЫЕ ЧАСТИ, А ТАКЖЕ
// ОБРАБОТКИ МЕХАНИЗМОВ ОТОБРАЖЕНИЯ ГАРАЖНОГО ИЛИ ГОС. НОМЕРА ВМЕСТО ОСНОВНОГО
// ПРЕДСТАВЛЕНИЯ ТС (ПО НАИМЕНОВАНИЮ) В ЗАВИСИМОСТИ ОТ НАСТРОЕК ПРОГРАММЫ
// ИСПОЛЬЗОВАНИЕ:
// ВСЕ ПРОЦЕДУРЫ ВСТАВЛЯЮТСЯ В СООТВЕТСТВУЮЩИЕ ОБРАБОТЧИКИ В ФОРМАХ ОБЪЕКТОВ
// ПРИМЕР:
// Документ Заправка ГСМ. Табличная часть Заправки с полем ТС
// 1) колонку ТС делаем недоступной, добавляем в табличное поле колонку НомерТС
// типа Строка.
// 2) На нее навешиваем обработчики ПриИзменении, НачалоВыбора, АвтоПодборТекста, 
// ОкончаниеВводаТекста, ОбработкаВыбора. В этих обработчиках прописываем вызовы
// соответствующих процедур данного модуля.
// 3) Аналогично указываем обработчики ПриНачалеРедактирования, ПриВыводеСтроки
// в табличном поле с ТС


/////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

//Процедура вызывается из обработчика ПриНачалеРедактирования табличного поля
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	КолонкаНомерТС - колонка табл. поля - колонка НомерТС,
//	Организация - СправочникСсылка.Организации - организация
//
Процедура ТабличноеПолеПриНачалеРедактирования(ТС, НомерТС, Организация) ЭКСПОРТ
	НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ТС, Организация);
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТА УПРАВЛЕНИЯ "НОМЕР ТС" ТАБЛИЧНОЙ ЧАСТИ

//Процедура вызывается из обработчика ПриИзменении поля ввода НомерТС табличного поля
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	Значение - значение поля ввода (номер ТС),
//	Организация - СправочникСсылка.Организации - организация
//
Процедура ТабличноеПолеНомерТСПриИзменении(ТС, Значение, Организация) ЭКСПОРТ
	Если ПустаяСтрока(Значение) Тогда
		ТС = Неопределено;
		Возврат;
	КонецЕсли;
	
	мТС = уатОбщегоНазначения.уатНайтиТСПоНомеру(Организация, Значение);
	Если мТС <> Неопределено Тогда
		ТС = мТС;
	КонецЕсли;
КонецПроцедуры

//Процедура вызывается из обработчика НачалоВыбора поля ввода НомерТС табличного поля
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	СтруктураОтбора - Структура - будет использоваться для отбора списка выбора ТС
//(по умолчанию используется только по организации документа),
//	СтандартнаяОбработка - флаг стандартной обработки выбора (здесь сбрасывается)
//
Процедура ТабличноеПолеНомерТСНачалоВыбора(Элемент, ТС, СтруктураОтбора, СтандартнаяОбработка) ЭКСПОРТ
	СтандартнаяОбработка = Ложь;
	
	Если СтруктураОтбора = Неопределено тогда
		СтруктураОтбора = Новый Структура;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура("Отбор, ТекущаяСтрока", СтруктураОтбора, ТС);
	ОткрытьФорму("Справочник.ТранспортныеСредства.ФормаВыбора", ПараметрыВыбора, Элемент);
КонецПроцедуры

//Процедура вызывается из обработчика АвтоПодборТекста поля ввода НомерТС табличного поля
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	Текст, ТекстАвтоПодбора - Строка - параметры, переданные из метода АвтоПодборТекста,
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	СтандартнаяОбработка - Булево - флаг стандартной обработки события,
//	Организация - СправочникСсылка.Организации - организация
//
Процедура ТабличноеПолеНомерТСАвтоПодборТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, Организация, Знач СтруктураДопПараметров = Неопределено) Экспорт
	Если Текст = "" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = уатЗащищенныеФункцииСервер.ПодобратьСписокТС(Организация, Текст,, СтруктураДопПараметров);
	//Если ДанныеВыбора <> Неопределено И ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	//КонецЕсли;
КонецПроцедуры

//Процедура вызывается из обработчика ОкончаниеВводаТекста поля ввода НомерТС табличного поля
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	Текст - Строка - параметр, переданный из метода ОкончаниеВводаТекста,
//	Значение - значение поля ввода (номер ТС),
//	СтандартнаяОбработка - флаг стандартной обработки (здесь сбрасывается),
//	Организация - СправочникСсылка.Организации - организация
//
Процедура ТабличноеПолеНомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, Организация, Знач СтруктураДопПараметров = Неопределено) ЭКСПОРТ
	Если Текст = "" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = уатЗащищенныеФункцииСервер.ПодобратьСписокТС(Организация, Текст,, СтруктураДопПараметров);
	//Если ДанныеВыбора <> Неопределено И ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	//КонецЕсли;
КонецПроцедуры

//Процедура вызывается из обработчика ОбработкаВыбора поля ввода НомерТС табличного поля
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	КолонкаНомерТС - колонка табл. поля - колонка НомерТС,
//	ВыбранноеЗначение - значение, выбранное из списка ТС,
//	СтандартнаяОбработка - флаг стандартной обработки (здесь сбрасывается),
//	Организация - СправочникСсылка.Организации - организация
//
Процедура ТабличноеПолеНомерТСОбработкаВыбора(ТС, НомерТС, ВыбранноеЗначение, СтандартнаяОбработка, 
		Организация) Экспорт
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ТранспортныеСредства") Тогда
		ТС = ВыбранноеЗначение;
	Иначе
		ТС = уатОбщегоНазначения.уатНайтиТСПоНомеру(Организация, СокрЛП(ВыбранноеЗначение));
	КонецЕсли;
	
	НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ТС, Организация);
КонецПроцедуры

//Процедура вызывается из обработчика Очистка поля ввода НомерТС табличного поля
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство
//
Процедура ТабличноеПолеНомерТСОчистка(ТС) ЭКСПОРТ
	ТС = Неопределено;
КонецПроцедуры

//Процедура вызывается из обработчика Открытие поля ввода НомерТС табличного поля
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	СтандартнаяОбработка - флаг стандартной обработки (здесь сбрасывается)
//
Процедура ТабличноеПолеНомерТСОткрытие(ТС, СтандартнаяОбработка) ЭКСПОРТ
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, ТС);
КонецПроцедуры


/////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТА УПРАВЛЕНИЯ "НОМЕР ТС" ШАПКИ

//Процедура вызывается из обработчика ПриИзменении поля ввода НомерТС
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	Организация - СправочникСсылка.Организации - организация
//
Процедура НомерТСПриИзменении(НомерТС, ТС, Организация) ЭКСПОРТ
	Если ПустаяСтрока(НомерТС) Тогда
		ТС = Неопределено;
		Возврат;
	КонецЕсли;
	
	мТС = уатОбщегоНазначения.уатНайтиТСПоНомеру(Организация, НомерТС);
	Если мТС = Неопределено Тогда
		ТС = Неопределено;
		НомерТС = "";
	Иначе
		ТС = мТС;
		НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ТС, Организация);
	КонецЕсли;
КонецПроцедуры

//Процедура вызывается из обработчика НачалоВыбора поля ввода НомерТС
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	СтруктураОтбора - Структура - будет использоваться для отбора списка выбора ТС
//(по умолчанию используется только по организации документа),
//	СтандартнаяОбработка - флаг стандартной обработки выбора (здесь сбрасывается)
//
Процедура НомерТСНачалоВыбора(Элемент, ТС, СтруктураОтбора, СтандартнаяОбработка) ЭКСПОРТ
	СтандартнаяОбработка = Ложь;
	
	Если СтруктураОтбора = Неопределено тогда
		СтруктураОтбора = Новый Структура;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура("Отбор, ТекущаяСтрока", СтруктураОтбора, ТС);
	ОткрытьФорму("Справочник.ТранспортныеСредства.ФормаВыбора", ПараметрыВыбора, Элемент);
КонецПроцедуры

//Процедура вызывается из обработчика ОбработкаВыбора поля ввода НомерТС
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	ВыбранноеЗначение - значение, выбранное из списка ТС,
//	СтандартнаяОбработка - флаг стандартной обработки (здесь сбрасывается),
//	Организация - СправочникСсылка.Организации - организация
//
Процедура НомерТСОбработкаВыбора(НомерТС, ТС, ВыбранноеЗначение, СтандартнаяОбработка, Организация = Неопределено) Экспорт
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ТранспортныеСредства") Тогда
		ТС = ВыбранноеЗначение;
	Иначе
		ТС = уатОбщегоНазначения.уатНайтиТСПоНомеру(Организация, СокрЛП(ВыбранноеЗначение));
	КонецЕсли;
	
	НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ТС, Организация);
КонецПроцедуры

//Процедура вызывается из обработчика АвтоПодборТекста поля ввода НомерТС
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	Текст, ТекстАвтоПодбора - Строка - параметры, переданные из метода АвтоПодборТекста,
//	СтандартнаяОбработка - Булево - флаг стандартной обработки события,
//	Организация - СправочникСсылка.Организации - организация
//
Процедура НомерТСАвтоПодборТекста(Элемент, Знач Текст, ДанныеВыбора, СтандартнаяОбработка, Организация = Неопределено, ОтборТСпоОрганизации = Истина, Знач СтруктураДопПараметров = Неопределено) ЭКСПОРТ
	
	Если Текст = "" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = уатЗащищенныеФункцииСервер.ПодобратьСписокТС(Организация, Текст, ОтборТСпоОрганизации, СтруктураДопПараметров);
	//Если ДанныеВыбора <> Неопределено И ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	//КонецЕсли;
	
КонецПроцедуры

//Процедура вызывается из обработчика ОкончаниеВводаТекста поля ввода НомерТС
//
//Параметры:
//	Элемент - элемент управления (поле НомерТС),
//	Текст - Строка - параметр, переданный из метода ОкончаниеВводаТекста,
//	Значение - значение поля ввода (номер ТС),
//	СтандартнаяОбработка - флаг стандартной обработки (здесь сбрасывается),
//	Организация - СправочникСсылка.Организации - организация
//
Процедура НомерТСОкончаниеВводаТекста(Элемент, Знач Текст, ДанныеВыбора, СтандартнаяОбработка, Организация = Неопределено, ОтборТСпоОрганизации = Истина, Знач СтруктураДопПараметров = Неопределено) ЭКСПОРТ
	
	Если Текст = "" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = уатЗащищенныеФункцииСервер.ПодобратьСписокТС(Организация, Текст, ОтборТСпоОрганизации, СтруктураДопПараметров);
	//Если ДанныеВыбора <> Неопределено И ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	//КонецЕсли;
	
КонецПроцедуры

//Процедура вызывается из обработчика Очистка поля ввода НомерТС
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство
//
Процедура НомерТСОчистка(ТС) ЭКСПОРТ
	ТС = Неопределено;
КонецПроцедуры

//Процедура вызывается из обработчика Открытие поля ввода НомерТС
//
//Параметры:
//	ТС - СправочникСсылка.уатТС - транспортное средство,
//	СтандартнаяОбработка - флаг стандартной обработки (здесь сбрасывается)
//
Процедура НомерТСОткрытие(ТС, СтандартнаяОбработка) ЭКСПОРТ
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, ТС);
КонецПроцедуры
