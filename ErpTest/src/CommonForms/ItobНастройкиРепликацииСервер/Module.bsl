
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураПараметрыРепликации = Константы.ItobПараметрыРепликации.Получить().Получить();
	Если ТипЗнч(СтруктураПараметрыРепликации) = Тип("Структура") Тогда
		
		СтруктураПараметрыРепликации.Свойство("ИспользоватьРепликацию", ИспользоватьРепликацию);
		СтруктураПараметрыРепликации.Свойство("Сервер"                , СерверРепликации);
		СтруктураПараметрыРепликации.Свойство("Порт"                  , ПортРепликации);
		СтруктураПараметрыРепликации.Свойство("Интервал"              , Интервал);		
				
	КонецЕсли;
	
	СтруктураПараметрыРепликацииРезерв = Константы.ItobПараметрыРепликацииРезервныйСервер.Получить().Получить();
	Если ТипЗнч(СтруктураПараметрыРепликацииРезерв) = Тип("Структура") Тогда
		
		СтруктураПараметрыРепликацииРезерв.Свойство("ИспользоватьРепликацию", ИспользоватьРезервныйСерверРепликации);
		СтруктураПараметрыРепликацииРезерв.Свойство("Сервер"                , СерверРепликацииРезерв);
		СтруктураПараметрыРепликацииРезерв.Свойство("Порт"                  , ПортРепликацииРезерв);
		СтруктураПараметрыРепликацииРезерв.Свойство("Логин"                 , ЛогинРепликацииРезерв);
		СтруктураПараметрыРепликацииРезерв.Свойство("Пароль"                , ПарольРепликацииРезерв);		
		СтруктураПараметрыРепликацииРезерв.Свойство("Интервал"              , ИнтервалРезерв);		
				
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
			
	Если ИспользоватьРепликацию И (Интервал=0) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задан интервал репликации!'"));
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если ИспользоватьРезервныйСерверРепликации И (ИнтервалРезерв=0) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задан интервал репликации с резервным сервером!'"));
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	Если ИспользоватьРепликацию И ИспользоватьРезервныйСерверРепликации И СерверРепликации=СерверРепликацииРезерв Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Основной сервер репликации не может совпадать с резервным!'"));
		Отказ = Истина;
		Возврат;			
	
	КонецЕсли;		
	
	Если НЕ ОбновитьСостояниеРегламентногоЗадания() Тогда	
		Отказ = Истина;
		Возврат;	
		
	КонецЕсли;
	
	СтруктураПараметрыРепликации = Новый Структура;
	СтруктураПараметрыРепликации.Вставить("ИспользоватьРепликацию", ИспользоватьРепликацию);
	СтруктураПараметрыРепликации.Вставить("Сервер"                , СерверРепликации);
	СтруктураПараметрыРепликации.Вставить("Порт"                  , ПортРепликации);
	СтруктураПараметрыРепликации.Вставить("Интервал"              , Интервал);	
	Константы.ItobПараметрыРепликации.Установить(Новый ХранилищеЗначения(СтруктураПараметрыРепликации));
	
	СтруктураПараметрыРепликацииРезерв = Новый Структура;
	СтруктураПараметрыРепликацииРезерв.Вставить("ИспользоватьРепликацию", ИспользоватьРезервныйСерверРепликации);
	СтруктураПараметрыРепликацииРезерв.Вставить("Сервер"                , СерверРепликацииРезерв);
	СтруктураПараметрыРепликацииРезерв.Вставить("Порт"                  , ПортРепликацииРезерв);
	СтруктураПараметрыРепликацииРезерв.Вставить("Логин"                 , ЛогинРепликацииРезерв);
	СтруктураПараметрыРепликацииРезерв.Вставить("Пароль"                , ПарольРепликацииРезерв);
	СтруктураПараметрыРепликацииРезерв.Вставить("Интервал"              , ИнтервалРезерв);	
	Константы.ItobПараметрыРепликацииРезервныйСервер.Установить(Новый ХранилищеЗначения(СтруктураПараметрыРепликацииРезерв));	
		
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТестРепликация(Команда)
	
	ПроверитьСоединениеССерверомРепликации(
		СерверРепликации, ПортРепликации, НаборКонстант.ItobЛогинРепликации, НаборКонстант.ItobПарольРепликации);
		
КонецПроцедуры

&НаКлиенте
Процедура ТестРепликацияРезервныйСервер(Команда)
	
	ПроверитьСоединениеССерверомРепликации(
		СерверРепликацииРезерв, ПортРепликацииРезерв, ЛогинРепликацииРезерв, ПарольРепликацииРезерв);	
	
КонецПроцедуры

#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОбновитьСостояниеРегламентногоЗадания()

	Результат = Ложь;
	
	Попытка
	
		Регламентные = РегламентныеЗадания.ПолучитьРегламентныеЗадания();
		
		НайденноеЗадание = Неопределено;
		НайденноеЗаданиеРезерв = Неопределено;
		НайденноеМетаданные = Неопределено;
		
		Для Каждого Регламентное Из Регламентные Цикл
			
			Если НЕ Регламентное.Метаданные = Метаданные.РегламентныеЗадания.ItobРепликация	Тогда
				Продолжить;
			
			КонецЕсли;
			
			Если Регламентное.Наименование = НСтр("ru = 'Репликация с сервера сбора данных IMCS'") Тогда
				НайденноеЗадание = Регламентное;
			
			ИначеЕсли Регламентное.Наименование = НСтр("ru = 'Репликация с резервным сервером сбора данных'") Тогда
				НайденноеЗаданиеРезерв = Регламентное;
			
			Иначе
				НайденноеМетаданные = Регламентное;	
				
			КонецЕсли;
									
		КонецЦикла;
		
		Если НайденноеЗадание = Неопределено И НайденноеМетаданные <> Неопределено Тогда
			НайденноеЗадание = НайденноеМетаданные;
		
		КонецЕсли;
		
		Если НайденноеЗадание = Неопределено Тогда
			НайденноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.ItobРепликация);				
		
		КонецЕсли;
		
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить("Основной");
		
		НайденноеЗадание.Наименование = НСтр("ru = 'Репликация с сервера сбора данных IMCS'");
		НайденноеЗадание.Ключ = "КлючРепликации";
		НайденноеЗадание.Использование = ИспользоватьРепликацию;
		НайденноеЗадание.ИмяПользователя = "ReplicationBot";
		НайденноеЗадание.КоличествоПовторовПриАварийномЗавершении = 3;
		НайденноеЗадание.ИнтервалПовтораПриАварийномЗавершении = 10;
		НайденноеЗадание.Параметры = ПараметрыЗадания;
		
		Расписание = Новый РасписаниеРегламентногоЗадания;
		Расписание.ПериодПовтораДней = 1;                    
		Расписание.ПериодПовтораВТечениеДня = Окр(60*?(Интервал=0,1,Интервал),0);		
		
		НайденноеЗадание.Расписание = Расписание;
		НайденноеЗадание.Записать();
		
		// Резервное задание
		Если НайденноеЗаданиеРезерв = Неопределено Тогда
			НайденноеЗаданиеРезерв = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.ItobРепликация);				
		
		КонецЕсли;
		
		ПараметрыЗаданияРезерв = Новый Массив;
		ПараметрыЗаданияРезерв.Добавить("Резервный");
		
		НайденноеЗаданиеРезерв.Наименование = НСтр("ru = 'Репликация с резервным сервером сбора данных'");
		НайденноеЗаданиеРезерв.Ключ = "КлючРепликацииРезерв";
		НайденноеЗаданиеРезерв.Использование = ИспользоватьРезервныйСерверРепликации;
		НайденноеЗаданиеРезерв.ИмяПользователя = "ReplicationBot";
		НайденноеЗаданиеРезерв.КоличествоПовторовПриАварийномЗавершении = 3;
		НайденноеЗаданиеРезерв.ИнтервалПовтораПриАварийномЗавершении = 10;
		НайденноеЗаданиеРезерв.Параметры = ПараметрыЗаданияРезерв;
		
		РасписаниеРезерв = Новый РасписаниеРегламентногоЗадания;
		РасписаниеРезерв.ПериодПовтораДней = 1;                    
		РасписаниеРезерв.ПериодПовтораВТечениеДня = Окр(60*?(ИнтервалРезерв=0,1,ИнтервалРезерв)+7,0);
		
		НайденноеЗаданиеРезерв.Расписание = РасписаниеРезерв;
		НайденноеЗаданиеРезерв.Записать();
		
		Результат = Истина;
		
	Исключение
	 	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		
	КонецПопытки;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ПроверитьСоединениеССерверомРепликации(Сервер, Порт, Логин, Пароль)

	ТекстОшибки = "";
	Если ПустаяСтрока(Сервер) Тогда
		ТекстОшибки = НСтр("ru = 'Не указан сервер'");
		
	ИначеЕсли Порт = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не указан порт'");
		
	ИначеЕсли ПустаяСтрока(Логин) Тогда	
		ТекстОшибки = НСтр("ru = 'Не указан логин'");
		
	ИначеЕсли ПустаяСтрока(Пароль) Тогда	
		ТекстОшибки = НСтр("ru = 'Не указан пароль'");		
	
	КонецЕсли;	
	
	Если ТекстОшибки <> "" Тогда
		ПоказатьПредупреждение(,ТекстОшибки);
		Возврат;
	
	КонецЕсли;
	
	ТекстОшибки = "";
	Результат = ItobВызовСервераПолныеПрава.ТестСоединенияССервером(
		Сервер, Порт, Логин, Пароль, ТекстОшибки);
	Если НЕ Результат Тогда
		ПоказатьПредупреждение(,ТекстОшибки);
		
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Соединение успешно установлено!'"));		
	
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти


