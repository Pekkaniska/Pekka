////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Возвращает набор настроек пользователя УАТ
//
Функция ПолучитьСписокНастроекПользователяУАТ()
	тблНастройки = Новый ТаблицаЗначений;
	тблНастройки.Колонки.Добавить("ИмяНастройки");
	тблНастройки.Колонки.Добавить("Представление");
	тблНастройки.Колонки.Добавить("ЗначениеПоУмолчанию");
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнаяЕдиницаПоКлассификатору";
	НоваяСтрока.Представление = "Основная единица по классификатору";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнаяОрганизация";
	НоваяСтрока.Представление = "Основная организация";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.Организации.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнаяСтавкаНДС";
	НоваяСтрока.Представление = "Основная ставка НДС";
	НоваяСтрока.ЗначениеПоУмолчанию = Перечисления.СтавкиНДС.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновноеПодразделениеОрганизации";
	НоваяСтрока.Представление = "Основное подразделение организации";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.СтруктураПредприятия.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнойОтветственный";
	НоваяСтрока.Представление = "Основной ответственный";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.Пользователи.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнойПокупатель";
	НоваяСтрока.Представление = "Основной покупатель";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.Контрагенты.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнойПоставщик";
	НоваяСтрока.Представление = "Основной поставщик";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.Контрагенты.ПустаяСсылка();
	
	НоваяСтрока = тблНастройки.Добавить();
	НоваяСтрока.ИмяНастройки = "ОсновнойСклад";
	НоваяСтрока.Представление = "Основной склад";
	НоваяСтрока.ЗначениеПоУмолчанию = Справочники.Склады.ПустаяСсылка();
	
	тблНастройки.Сортировать("Представление");
	
	Возврат тблНастройки;
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуНастроек()
	тблНастройкиПользователей = ПолучитьСписокНастроекПользователяУАТ();
	
	НастройкиПользователей.Очистить();
	
	Для Каждого ТекНастройка Из тблНастройкиПользователей Цикл
		ТекЗначениеНастройки = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекПользователь, ТекНастройка.ИмяНастройки);
		Если ТекЗначениеНастройки = Неопределено Тогда
			ТекЗначениеНастройки = ТекНастройка.ЗначениеПоУмолчанию;
		КонецЕсли;
		НоваяСтрока = НастройкиПользователей.Добавить();
		НоваяСтрока.Настройка = ТекНастройка.ИмяНастройки;
		НоваяСтрока.Представление = ТекНастройка.Представление;
		НоваяСтрока.Значение = ТекЗначениеНастройки;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки(Знач Пользователь = Неопределено)
	Если Пользователь = Неопределено Тогда
		Пользователь = ТекПользователь;
	КонецЕсли;
	
	мсвСтрокМодифицированных = НастройкиПользователей.НайтиСтроки(Новый Структура("Модифицированность", Истина));
	Для Каждого ТекСтрока Из мсвСтрокМодифицированных Цикл
		уатОбщегоНазначенияПовтИсп.УстановитьЗначениеПоУмолчанию(ТекСтрока.Настройка, ТекСтрока.Значение, Пользователь);
		ТекСтрока.Модифицированность = Ложь;
	КонецЦикла;
	
	Если мсвСтрокМодифицированных.Количество() > 0 Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция НастройкиМодифицированы()
	мсвСтрокМодифицированных = НастройкиПользователей.НайтиСтроки(Новый Структура("Модифицированность", Истина));
	Возврат (мсвСтрокМодифицированных.Количество() > 0);
КонецФункции

&НаСервере
Функция НайтиПользователяИБ(ТекПользователь)
	Возврат ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ТекПользователь, "ИдентификаторПользователяИБ")) <> Неопределено;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

&НаКлиенте
Процедура ТекПользовательПриИзменении(Элемент)
	ПоискПольз = НайтиПользователяИБ(ТекПользователь);
	Если ПоискПольз = Ложь Тогда
		Сообщить("Настройки сохраняются только для пользователей ИБ. Пользователь """ + ТекПользователь + """ не является пользователем ИБ!");
		ТекПользователь = ПредПользователь;
		Возврат;
	КонецЕсли;
	
	Если ТекПользователь = ПредПользователь Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкиМодифицированы() Тогда
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("ТекПользовательПриИзмененииЗавершение", ЭтотОбъект), "Персональные настройки пользователя """ + ПредПользователь + """ изменены! Сохранить изменения?", РежимДиалогаВопрос.ДаНетОтмена);
        Возврат;
	КонецЕсли;
	
	ТекПользовательПриИзмененииФрагмент1();
КонецПроцедуры

&НаКлиенте
Процедура ТекПользовательПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        СохранитьНастройки(ПредПользователь);
    ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
    Иначе
        ТекПользователь = ПредПользователь;
        Возврат;
    КонецЕсли;
    
    ТекПользовательПриИзмененииФрагмент1();

КонецПроцедуры

&НаКлиенте
Процедура ТекПользовательПриИзмененииФрагмент1()
    
    ТекПользовательПриИзмененииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ТекПользовательПриИзмененииФрагмент()
    
    ЗаполнитьТаблицуНастроек();
    ПредПользователь = ТекПользователь;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиПользователейЗначениеПриИзменении(Элемент)
	Элементы.НастройкиПользователей.ТекущиеДанные.Модифицированность = Истина;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОК(Команда)
	Если НастройкиМодифицированы() Тогда
		СохранитьНастройки();
	КонецЕсли;
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКСохраненным(Команда)
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("ВернутьсяКСохраненнымЗавершение", ЭтотОбъект), "Вернуться к сохраненным значениям настроек?", РежимДиалогаВопрос.ОКОтмена);
КонецПроцедуры

&НаКлиенте
Процедура ВернутьсяКСохраненнымЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.ОК Тогда
        ЗаполнитьТаблицуНастроек();
    КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	ТекПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	ПредПользователь = ТекПользователь;
	АдминистративныеПрава = ПравоДоступа("Администрирование", Метаданные);
	Если НЕ АдминистративныеПрава Тогда
		Элементы.ТекПользователь.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЗаполнитьТаблицуНастроек();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда 
		Если НастройкиМодифицированы() Тогда
			Отказ = Истина;
			ТекстПредупреждения = "Персональные настройки изменены. Все изменения будут потеряны.";
			Возврат;
		КонецЕсли;
	ИначеЕсли НастройкиМодифицированы() Тогда 
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), "Персональные настройки изменены. Отказаться от изменений и закрыть форму?", РежимДиалогаВопрос.ОКОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		Для Каждого ТекСтрока из НастройкиПользователей Цикл
			ТекСтрока.Модифицированность = Ложь;
		КонецЦикла;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры
