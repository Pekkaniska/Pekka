&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ГруппаГСМ") И Параметры.Свойство("ТС") И Параметры.Свойство("Организация") Тогда
		Если Параметры.ГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Талон") Тогда
			Заголовок = "Талоны ГСМ для авто. " + уатОбщегоНазначения.уатПредставлениеТС(Параметры.ТС, Параметры.Организация);
		ИначеЕсли Параметры.ГруппаГСМ = ПредопределенноеЗначение("Перечисление.уатГруппыГСМ.Масло") Тогда
			Заголовок = "Масла и смазочные материалы";
		Иначе //топливо
			Если Параметры.БезАналогов Тогда
				Заголовок = "Основное топливо для ТС " ;
			Иначе	
				Заголовок = "Топливо для ТС " ;
			КонецЕсли;
			Заголовок = Заголовок + уатОбщегоНазначения.уатПредставлениеТС(Параметры.ТС, Параметры.Организация);
		КонецЕсли;
		
		мсвГСМ = уатЗащищенныеФункцииСервер.СписокГСМдляТССервер(Параметры.Организация, Параметры.ТС, Параметры.ГруппаГСМ, 
			Параметры.БезАналогов);
		ТаблицаГСМ.Очистить();
		
		флЕстьТалоны = Ложь;
		
		Для Каждого ТекЭл Из мсвГСМ Цикл
			НоваяСтрока = ТаблицаГСМ.Добавить();
			НоваяСтрока.ГСМ = ТекЭл.ГСМ;
			НоваяСтрока.ГСМТалона = ТекЭл.ГСМТалона;
			НоваяСтрока.Литровый = ТекЭл.Литровый;
			НоваяСтрока.Номинал = ТекЭл.Номинал;
			
			Если ЗначениеЗаполнено(ТекЭл.ГСМТалона) Тогда
				флЕстьТалоны = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если флЕстьТалоны Тогда
			Элементы.ТаблицаГСМГСМТалона.Видимость = Истина;
			Элементы.ТаблицаГСМЛитровый.Видимость = Истина;
			Элементы.ТаблицаГСМНоминал.Видимость = Истина;
		КонецЕсли;
		
		Если Параметры.Свойство("УчитыватьТЖ") И Параметры.УчитыватьТЖ Тогда
			Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	уатНоменклатураГСМ.Номенклатура
			|ИЗ
			|	РегистрСведений.уатНоменклатураГСМ КАК уатНоменклатураГСМ
			|ГДЕ
			|	уатНоменклатураГСМ.ВестиУчетОстатковТЖ");
			тблТЖ = Запрос.Выполнить().Выгрузить();
			Для Каждого ТекСтрока Из тблТЖ Цикл
				НоваяСтрока = ТаблицаГСМ.Добавить();
				НоваяСтрока.ГСМ = ТекСтрока.Номенклатура;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаГСМВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	ТекСтрока = Элементы.ТаблицаГСМ.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Закрыть();
	Иначе
		Закрыть(ТекСтрока.ГСМ);
	КонецЕсли;
КонецПроцедуры
