
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ДокУАТ") Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	Иначе
		ДокУАТ = Параметры.ДокУАТ;
	КонецЕсли;
	
	мсвТипДок = Неопределено;
	уатТиповыеДокументы.НайтиТиповойДокументДляДокументаУАТ(ДокУАТ, мсвТипДок);
	
	Если ТипЗнч(мсвТипДок) = Тип("Массив") Тогда
		ТД = мсвТипДок[0];
		Если мсвТипДок.Количество() > 1 Тогда
			ТД2 = мсвТипДок[1];
		КонецЕсли;
		Если мсвТипДок.Количество() > 2 Тогда
			ТД3 = мсвТипДок[2];
		КонецЕсли;
		Если мсвТипДок.Количество() > 3 Тогда
			ТД4 = мсвТипДок[3];
		КонецЕсли;
	Иначе
		ТД = мсвТипДок;
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ТД) Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаТД;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаНетТД;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокУАТ) Тогда
		Если ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
			Элементы.ГруппаВидыТД.ТекущаяСтраница = Элементы.ГруппаПутевойЛист;
			//ПЛ_ТребованиеНакладная  = Истина;
			//ПЛ_ВырабСотр            = Истина;
			//ПЛ_ПрочиеДоходыРасходы  = Истина;
			//ПЛ_РегистрацияНаработок = Истина;
		ИначеЕсли ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатТТД") Тогда
			Элементы.ГруппаВидыТД.ТекущаяСтраница = Элементы.ГруппаТТД;
			//ТТД_Реализация = Истина;
			//ТТД_Счет = Ложь;
		ИначеЕсли ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатСливГСМ")
			И ДокУАТ.ВидОперации = Перечисления.уатВидыОперацийСливГСМ.НаСклад Тогда
			Элементы.ГруппаВидыТД.ТекущаяСтраница = Элементы.ГруппаСливГСМ;
			//СливГСМ_Оприходование = Истина;
			//СливГСМ_Перемещение = Ложь;
		ИначеЕсли ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатЗаказГрузоотправителя") Тогда
			Элементы.ГруппаВидыТД.ТекущаяСтраница = Элементы.ГруппаЗаказНаТС;
			//ЗаказНаТС_Реализация = Истина;
			//ЗаказНаТС_Счет = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не уатТиповыеДокументы.ЕстьПравоРедактированияРегистраТД() Тогда 
		Если мсвТипДок = Неопределено Тогда 
			Сообщить(НСтр("ru = 'Недостаточно прав для формирования типовых документов.'"));
			СтандартнаяОбработка = Ложь;
			Отказ = Истина;
			Возврат;
		Иначе 
			Элементы.ГруппаВидыТД.Видимость = Ложь;
			Элементы.ФормаСформироватьТД.Видимость = Ложь;
			Элементы.ДекорацияНетТД1.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПЛ_ТребованиеНакладнаяПриИзменении(Элемент)
	Если ПЛ_ТребованиеНакладная И ПЛ_СписаниеТоваров Тогда
		ПЛ_СписаниеТоваров = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПЛ_СписаниеТоваровПриИзменении(Элемент)
	Если ПЛ_ТребованиеНакладная И ПЛ_СписаниеТоваров Тогда
		ПЛ_ТребованиеНакладная = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СливГСМ_ОприходованиеПриИзменении(Элемент)
	Если СливГСМ_Оприходование И СливГСМ_Перемещение Тогда
		СливГСМ_Перемещение = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СливГСМ_ПеремещениеПриИзменении(Элемент)
	Если СливГСМ_Оприходование И СливГСМ_Перемещение Тогда
		СливГСМ_Оприходование = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьТД(Команда)
	
	Если Не уатТиповыеДокументы.ЕстьПравоРедактированияРегистраТД() Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураПарам = Новый Структура;
	СтруктураПарам.Вставить("Действие", "Сформировать");
	СтруктураПарам.Вставить("ДопПараметры");
	
	Если ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
		Если Не ПЛ_ВырабСотр И Не ПЛ_СписаниеТоваров И Не ПЛ_ТребованиеНакладная 
				И Не ПЛ_ПрочиеДоходыРасходы И Не ПЛ_РегистрацияНаработок Тогда
			ТекстОшибки = "Не указан ни один вид типового документа!";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		Иначе
			мсвВидыТД = Новый Массив;
			Если ПЛ_ВырабСотр Тогда
				мсвВидыТД.Добавить("ПЛ_ВырабСотр");
			КонецЕсли;
			Если ПЛ_СписаниеТоваров Тогда
				мсвВидыТД.Добавить("ПЛ_СписаниеТоваров");
			КонецЕсли;
			Если ПЛ_ТребованиеНакладная Тогда
				мсвВидыТД.Добавить("ПЛ_ТребованиеНакладная");
			КонецЕсли;
			Если ПЛ_ПрочиеДоходыРасходы Тогда 
				мсвВидыТД.Добавить("ПЛ_ПрочиеДоходыРасходы");
			КонецЕсли;
			Если ПЛ_РегистрацияНаработок Тогда 
				мсвВидыТД.Добавить("ПЛ_РегистрацияНаработок");
			КонецЕсли;
			СтруктураПарам.Вставить("ДопПараметры", Новый Структура("ВидыТД", мсвВидыТД));
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатТТД") Тогда
		Если Не ТТД_Реализация И Не ТТД_Счет Тогда
			ТекстОшибки = "Не указан ни один вид типового документа!";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		Иначе
			мсвВидыТД = Новый Массив;
			Если ТТД_Реализация Тогда
				мсвВидыТД.Добавить("ТТД_Реализация");
			КонецЕсли;
			Если ТТД_Счет Тогда
				мсвВидыТД.Добавить("ТТД_Счет");
			КонецЕсли;
			СтруктураПарам.Вставить("ДопПараметры", Новый Структура("ВидыТД", мсвВидыТД));
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатЗаказГрузоотправителя") Тогда
		Если Не ЗаказНаТС_Реализация И Не ЗаказНаТС_Счет Тогда
			ТекстОшибки = "Не указан ни один вид типового документа!";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		Иначе
			мсвВидыТД = Новый Массив;
			Если ЗаказНаТС_Реализация Тогда
				мсвВидыТД.Добавить("ЗаказНаТС_Реализация");
			КонецЕсли;
			Если ЗаказНаТС_Счет Тогда
				мсвВидыТД.Добавить("ЗаказНаТС_Счет");
			КонецЕсли;
			СтруктураПарам.Вставить("ДопПараметры", Новый Структура("ВидыТД", мсвВидыТД));
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДокУАТ) = Тип("ДокументСсылка.уатСливГСМ") Тогда
		Если Не СливГСМ_Оприходование И Не СливГСМ_Перемещение Тогда
			ТекстОшибки = "Не указан ни один вид типового документа!";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат;
		Иначе
			мсвВидыТД = Новый Массив;
			Если СливГСМ_Оприходование Тогда
				мсвВидыТД.Добавить("СливГСМ_Оприходование");
			КонецЕсли;
			Если СливГСМ_Перемещение Тогда
				мсвВидыТД.Добавить("СливГСМ_Перемещение");
			КонецЕсли;
			СтруктураПарам.Вставить("ДопПараметры", Новый Структура("ВидыТД", мсвВидыТД));
		КонецЕсли;
		
	КонецЕсли;
	
	Закрыть(СтруктураПарам);
	
КонецПроцедуры

#КонецОбласти
