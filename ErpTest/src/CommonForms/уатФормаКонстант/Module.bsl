
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//уатЗащищенныеФункцииСервер.уатСправочникФормаСпискаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.Константы.уатИспользоватьСогласованиеДокументов) Тогда 
		Элементы.уатИспользоватьСогласованиеДокументов.Доступность = Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НомерВерсииКонфигурацииУАТ = ОбновлениеИнформационнойБазы.ВерсияИБ("МодульУправлениеАвтотранспортомДля1СERP");
	
	уатИспользоватьСогласованиеДокументов = НаборКонстант.уатИспользоватьСогласованиеДокументов;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//уатЗащищенныеФункцииКлиент.уатСправочникФормаСпискаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.НастройкиМониторинга.Доступность = НаборКонстант.уатИспользоватьМониторинг;
		
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	Если Не НаборКонстант.уатИспользоватьМониторинг Тогда 
		Константы.уатИспользоватьOmnicomm.Установить(Ложь);
		Константы.уатИспользоватьDynafleet.Установить(Ложь);
		Константы.уатИспользоватьЦСМ.Установить(Ложь);
		Константы.уатИспользоватьPositionReport.Установить(Ложь);
		Константы.уатИспользоватьАвтотрекер.Установить(Ложь);
		Константы.уатИспользоватьСКАУТ.Установить(Ложь);
		Константы.уатИспользоватьАвтоГРАФ.Установить(Ложь);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура уатИспользоватьСогласованиеДокументовПриИзменении(Элемент)
	
	Если ИспользуютсяБизнесПроцессыИЗадачи() Тогда 
		НаборКонстант.уатИспользоватьСогласованиеДокументов = уатИспользоватьСогласованиеДокументов;
	Иначе 
		Если уатИспользоватьСогласованиеДокументов Тогда 
			уатИспользоватьСогласованиеДокументов               = Ложь;
			НаборКонстант.уатИспользоватьСогласованиеДокументов = Ложь;
			
			ТекстПредупреждения = "Для включения согласования по документам УАТ необходимо включить использование бизнес-процессов"
				+ " и задач (""Администрирование"" - ""Органайзер"").";
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура уатИспользоватьМониторингПриИзменении(Элемент)
	
	Элементы.НастройкиМониторинга.Доступность = НаборКонстант.уатИспользоватьМониторинг;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиМониторинга(Команда)
	
	Если Модифицированность Тогда 
		ТекстОшибки = "Перед открытием формы настроек необходимо записать изменения.";
		ПоказатьПредупреждение(, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.уатНастройкиМониторинга",, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИспользуютсяБизнесПроцессыИЗадачи()
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользуютсяБизнесПроцессыИЗадачи = Константы.ИспользоватьБизнесПроцессыИЗадачи.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИспользуютсяБизнесПроцессыИЗадачи;
	
КонецФункции // ИспользуютсяБизнесПроцессыИЗадачи()

#КонецОбласти
