
&НаСервере
Процедура ВыполнитьСервер()
	ОтборКомп = КомпНастроек.ПолучитьНастройки();
	ПустойОтбор = Новый ПолеКомпоновкиДанных("");
	Для Каждого ТекОтбор Из ОтборКомп.Отбор.Элементы Цикл
		Если ТекОтбор.Использование И ТекОтбор.ЛевоеЗначение = ПустойОтбор Тогда
			Сообщить("Некорректно настроены отборы!");
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

	Макет = КомпоновщикМакета.Выполнить(ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных),
       КомпНастроек.ПолучитьНастройки(), , ,
       Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТЗ =  Новый ТаблицаЗначений;	
	ПроцессорВывода.УстановитьОбъект(ТЗ);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
		
	СтруктураВозврата = Новый Структура("ТаблицаТС, ТолькоУчетные");
	СтруктураВозврата.ТолькоУчетные = ТолькоУчетные;
	СтруктураВозврата.ТаблицаТС = ТЗ;	
	
	АдресГотовыхДанных = ПоместитьВоВременноеХранилище(СтруктураВозврата);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапрос(Команда)
	ВыполнитьСервер();
	ОповеститьОВыборе(АдресГотовыхДанных);
КонецПроцедуры

&НаСервере
Процедура ИнициализацияКомпоновщика()
	СхемаКомпоновкиДанных = ПолучитьОбщийМакет("уатПодборТС");
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	КомпНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных));
	КомпНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Автотест = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("ТолькоУчетные") Тогда
		ТолькоУчетные = Параметры.Отбор.ТолькоУчетные;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Автотест Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если ВладелецФормы = Неопределено Тогда
		ПоказатьПредупреждение(Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект), "Непосредственное открытие для данного объекта запрещено!", 10);
        Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИнициализацияКомпоновщика();
КонецПроцедуры

