
&НаСервере
Процедура СохранитьНастройки()
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ИсторияПоискаАгрегатов", Элементы.СтрокаПоиска.СписокВыбора.ВыгрузитьЗначения());
	
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("уатПодборАгрегатов", , ПараметрыЗакрытия);

КонецПроцедуры

&НаКлиенте
Процедура СписокАгрегатовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураПодбора = Новый Структура();
	СтруктураПодбора.Вставить("Команда",      КомандаПодбора);
	СтруктураПодбора.Вставить("Агрегат", Значение);
	ОповеститьОВыборе(СтруктураПодбора);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоШинамАккумуляторам(ЗначениеОтбора)
	
	ЭлементОтбора = СписокАгрегатов.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипАгрегата");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;
	
КонецПроцедуры //

&НаСервере
Процедура УстановитьОтборПоПрочимАгрегатам(ЗначениеОтбора)
	
	ЭлементОтбора = СписокАгрегатов.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипАгрегата");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;
	
КонецПроцедуры //


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ РЕКВИЗИТОВ ФОРМЫ

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	ПрименитьПоиск();
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьПоиск()

	Использование = ЗначениеЗаполнено(СтрокаПоиска);

	ГруппаОтбора = уатИнтерфейсВзаимодействияСERPКлиентСервер.уатСоздатьГруппуЭлементовОтбора(
		СписокАгрегатов.Отбор.Элементы, "ПоискПоПодстроке",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "Наименование", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "ТипАгрегата", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);
		
	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "Номер", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);


	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатОбновитьСписокВыбора(Элементы.СтрокаПоиска.СписокВыбора, СтрокаПоиска);
	
	ТекущийЭлемент = Элементы.СписокАгрегатов;

КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатЗагрузить("ИсторияПоискаАгрегатов", Элементы.СтрокаПоиска.СписокВыбора);
	Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("уатПодборАгрегатов",);
	Если Настройки <> Неопределено Тогда
		Если Настройки.Свойство("ИсторияПоискаАгрегатов") Тогда
			Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(Настройки.ИсторияПоискаАгрегатов);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Команда") Тогда
		КомандаПодбора = Параметры.Команда;
		Если КомандаПодбора = "ПодборВТабличнуюЧастьШины" Тогда
			УстановитьОтборПоШинамАккумуляторам(Справочники.уатТипыАгрегатов.Шина);
		ИначеЕсли КомандаПодбора = "ПодборВТабличнуюЧастьАккумуляторы" Тогда
			УстановитьОтборПоШинамАккумуляторам(Справочники.уатТипыАгрегатов.Аккумулятор);
		ИначеЕсли КомандаПодбора = "ПодборВТабличнуюЧастьПрочиеАгрегаты" Тогда
			СписокТиповАгрегата = Новый Массив();
			СписокТиповАгрегата.Добавить(Справочники.уатТипыАгрегатов.Аккумулятор);
			СписокТиповАгрегата.Добавить(Справочники.уатТипыАгрегатов.Шина);
			УстановитьОтборПоПрочимАгрегатам(СписокТиповАгрегата);
		КонецЕсли;
	КонецЕсли;  
КонецПроцедуры
