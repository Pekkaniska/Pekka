
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Компоновщик = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	
	КомпоновщикПоУмолчанию = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы;
	
	НачалоПериода = Неопределено;
	КонецПериода  = Неопределено;
	
	флЗаполненаОрганизация = Ложь;
	флЗаполненКонтрагент   = Ложь;
	
	Для Каждого СтрокаНастройкиПоУмолчанию Из КомпоновщикПоУмолчанию Цикл 
		Если ТипЗнч(СтрокаНастройкиПоУмолчанию) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда 
			Если СтрокаНастройкиПоУмолчанию.Параметр = (Новый ПараметрКомпоновкиДанных("НачалоПериода")) Тогда 
				ПользовательскаяНастройка = Компоновщик.Найти(СтрокаНастройкиПоУмолчанию.ИдентификаторПользовательскойНастройки);
				Если ТипЗнч(ПользовательскаяНастройка.Значение) = Тип("Дата") Тогда
					НачалоПериода = ПользовательскаяНастройка.Значение;
				Иначе
					НачалоПериода = ПользовательскаяНастройка.Значение.Дата;
				КонецЕсли;
			КонецЕсли;
			Если СтрокаНастройкиПоУмолчанию.Параметр = (Новый ПараметрКомпоновкиДанных("КонецПериода")) Тогда 
				ПользовательскаяНастройка = Компоновщик.Найти(СтрокаНастройкиПоУмолчанию.ИдентификаторПользовательскойНастройки);
				Если ТипЗнч(ПользовательскаяНастройка.Значение) = Тип("Дата") Тогда
					КонецПериода = ПользовательскаяНастройка.Значение;
				Иначе
					КонецПериода = ПользовательскаяНастройка.Значение.Дата;
				КонецЕсли;
			КонецЕсли;
			Если СтрокаНастройкиПоУмолчанию.Параметр = (Новый ПараметрКомпоновкиДанных("Организация")) Тогда 
				ПользовательскаяНастройка = Компоновщик.Найти(СтрокаНастройкиПоУмолчанию.ИдентификаторПользовательскойНастройки);
				Попытка
					флЗаполненаОрганизация = ЗначениеЗаполнено(ПользовательскаяНастройка.Значение);
				Исключение
					флЗаполненаОрганизация = Ложь;
				КонецПопытки;
			КонецЕсли;
			Если СтрокаНастройкиПоУмолчанию.Параметр = (Новый ПараметрКомпоновкиДанных("Контрагент")) Тогда 
				ПользовательскаяНастройка = Компоновщик.Найти(СтрокаНастройкиПоУмолчанию.ИдентификаторПользовательскойНастройки);
				Попытка
					флЗаполненКонтрагент = ЗначениеЗаполнено(ПользовательскаяНастройка.Значение);
				Исключение
					флЗаполненКонтрагент = Ложь;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НачалоПериода = Неопределено Или КонецПериода = Неопределено Тогда 
		ТекстОшибки = НСтр("ru = 'Неверно указаны даты начала и окончания периода'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
	Если Не Отказ И НачалоПериода > КонецПериода Тогда
		ТекстОшибки = НСтр("ru = 'Дата начала периода не может быть больше даты конца периода'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
	Если Не флЗаполненаОрганизация Тогда 
		ТекстОшибки = НСтр("ru = 'Необходимо заполнить реквизит ""Организация""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	       
	Если Не флЗаполненКонтрагент Тогда 
		ТекстОшибки = НСтр("ru = 'Необходимо заполнить реквизит ""Контрагент""'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры
