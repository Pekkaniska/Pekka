#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает строковое представление периода отчета
//
// Параметры:
//  ДатаНач	 - Дата - Начало периода отчета
//  ДатаКон	 - Дата - Конец периода отчета
// 
// Возвращаемое значение:
//  Строковое представление периода отчета - Строка
//
Функция ПредставлениеПериодаОтчета(ДатаНач, ДатаКон) Экспорт
	// Вывод заголовка, описателя периода и фильтров и заголовка
	Если ДатаНач = '00010101' И ДатаКон = '00010101' Тогда
		
		ОписаниеПериода     = "Период: без ограничения.";
		
	Иначе
		
		Если ДатаНач = '00010101' ИЛИ ДатаКон = '00010101' Тогда
			ОписаниеПериода = "Период: " + Формат(ДатаНач, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""") 
				+ " - "      + Формат(ДатаКон, "ДФ = ""дд.ММ.гггг""; ДП = ""без ограничения""");
		Иначе
			Если ДатаНач <= ДатаКон Тогда
				ОписаниеПериода = "Период: " + ПредставлениеПериода(НачалоДня(ДатаНач), КонецДня(ДатаКон), "ФП = Истина");
			Иначе
				ОписаниеПериода = "Неправильно задан период!"
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеПериода;
КонецФункции

//Возвращает количество по параметру выбора, отформатированное:
// - если временный, то в виде 12,67 или 12:40 (в зависимости от настройки "Форматировать время в отчетах")
// - если не временный, то в виде 12,431 км
//Введена по причине того, что условное оформление не применяется для пользовательского макета
Функция КоличествоПараметраВыработки(ПараметрВыработкиВременный, Знач Количество, ФорматироватьВремя) Экспорт
	Если Количество = Null Тогда
		Количество = 0;
	КонецЕсли;
	
	Если ПараметрВыработкиВременный Тогда
		Если ФорматироватьВремя Тогда
			Рез = "" + Цел(Количество/3600) + ":" + Прав(Формат(Окр((Количество/3600 - Цел(Количество/3600))/10*6, 2), "ЧДЦ=2; ЧН=00"), 2);
		Иначе
			Рез = Формат(Количество/3600, "ЧДЦ=2");
		КонецЕсли;
	Иначе
		Рез = Формат(Количество, "ЧДЦ=3");
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	уатЗащищенныеФункцииСервер.уатОтчетОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки – Коллекция – Используется для описания настроек отчетов и вариантов
//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//   НастройкиОтчета – СтрокаДереваЗначений – Настройки размещения всех вариантов отчета.
//       См. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Вспомогательные методы:
//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет
//   поддерживает только этот режим.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Реестр оказанных транспортных услуг");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет служит для анализа оказанных транспортных услуг.'");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли