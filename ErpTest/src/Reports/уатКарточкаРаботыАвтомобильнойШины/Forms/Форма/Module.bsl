
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаКон = НачалоМесяца(ТекущаяДата())-1;
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = уатОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(
			ПользователиКлиентСервер.ТекущийПользователь(), 
			"ОсновнаяОрганизация"
		);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШинаПриИзменении(Элемент)
	
	ПриИзмененииШиныСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ШинаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("ТипАгрегата, РежимВыбора", "Шина", Истина);
	ОткрытьФорму("РегистрСведений.уатАгрегатыТС.ФормаСписка", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ШинаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("РегистрСведенийКлючЗаписи.уатАгрегатыТС") Тогда
		СтандартнаяОбработка = Ложь;
		Шина = ВыбАгрегатСервер(ВыбранноеЗначение);
		ПриИзмененииШиныСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШинаОткрытие(Элемент, СтандартнаяОбработка)
	
	КлючЗаписи = ПолучитьКлючАгрегата(Шина);
	Если КлючЗаписи <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Ключ", КлючЗаписи);
		ОткрытьФорму("РегистрСведений.уатАгрегатыТС.ФормаЗаписи", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(Шина) Тогда
		ПоказатьПредупреждение(, "Не указана шина!", 10);
		Возврат;
	КонецЕсли;
	
	ТабДок = СформироватьОтчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//  ДокументРезультат - табличный документ, формируемый отчетом
//
&НаСервере
Функция СформироватьОтчет() Экспорт
	
	ДокументРезультат = Новый ТабличныйДокумент;
	
	ЗапросУстановкаСнятие = Новый Запрос();
	ЗапросУстановкаСнятие.УстановитьПараметр("Шина", Шина);
	
	ЗапросУстановкаСнятие.Текст =
	"ВЫБРАТЬ
	|	УстановленоНаТС.Период КАК ДатаУстановкиШиныНаТС,
	|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0) КАК ДатаСнятияШиныСТС,
	|	УстановленоНаТС.УзелОбъектаЭксплуатации КАК Шины,
	|	УстановленоНаТС.ТранспортноеСредство КАК ТС,
	|	УстановленоНаТС.ТранспортноеСредство.уатМодель КАК МодельТС,
	|	УстановленоНаТС.ТранспортноеСредство.уатГаражныйНомер КАК ИнвНомерТС,
	|	УстановленоНаТС.ТранспортноеСредство.Код КАК ГосударственныйНомер,
	|	УстановленоНаТС.МестоУстановки КАК МестоУстановки
	|ИЗ
	|	РегистрСведений.уатУстановленныеАгрегаты КАК УстановленоНаТС
	|ГДЕ
	|	УстановленоНаТС.УзелОбъектаЭксплуатации = &Шина
	|	И УстановленоНаТС.СостояниеАгрегата = ЗНАЧЕНИЕ(Перечисление.уатСостоянияАгрегатов.УстановленоВРаботе)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СнятоСТС.Период КАК ДатаСнятияШиныСТС,
	|	СнятоСТС.УзелОбъектаЭксплуатации КАК Шины,
	|	СнятоСТС.ТранспортноеСредство КАК ТС,
	|	СнятоСТС.МестоУстановки КАК МестоУстановки
	|ИЗ
	|	РегистрСведений.уатУстановленныеАгрегаты КАК СнятоСТС
	|ГДЕ
	|	СнятоСТС.УзелОбъектаЭксплуатации = &Шина
	|	И СнятоСТС.СостояниеАгрегата = ЗНАЧЕНИЕ(Перечисление.уатСостоянияАгрегатов.Снято)";
	
	мсвРезультаты = ЗапросУстановкаСнятие.ВыполнитьПакет();
	
	ТабУстановки = мсвРезультаты[0].Выгрузить();
	ТабСнятия    = мсвРезультаты[1].Выгрузить();
	
	Для Каждого ТекСтрока Из ТабУстановки Цикл 
		НайдСтроки = ТабСнятия.НайтиСтроки(Новый Структура("ТС,МестоУстановки",ТекСтрока.ТС,ТекСтрока.МестоУстановки));
		Если НайдСтроки.Количество() Тогда 
			Для Каждого СтрокаСнятия Из НайдСтроки Цикл 
				Если СтрокаСнятия.ДатаСнятияШиныСТС >= ТекСтрока.ДатаУстановкиШиныНаТС Тогда 
					ТекСтрока.ДатаСнятияШиныСТС = СтрокаСнятия.ДатаСнятияШиныСТС;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Макет = РеквизитФормыВЗначение("Отчет").ПолучитьМакет("Макет");
	
	Шапка = Макет.ПолучитьОбласть("Заголовок");

	Шапка.Параметры.НазваниеОрганизации = уатОбщегоНазначенияТиповыеСервер.ОписаниеОрганизации(уатОбщегоНазначенияСервер.СведенияОЮрФизЛице(Организация, ДатаКон),
		"ПолноеНаименование, ЮридическийАдрес, Телефоны");
		
	Шапка.Параметры.Шина				= Шина;
	Шапка.Параметры.СерийныйномерШины	= Шина.СерийныйНомер;
	
	ЗапроспоШине= новый запрос;
	ЗапросПоШине.Текст = 
	"ВЫБРАТЬ
	|	уатМестонахождениеТССрезПоследних.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрСведений.уатУстановленныеАгрегаты.СрезПоследних(&Дата) КАК уатУстановленныеАгрегатыСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уатМестонахождениеТС.СрезПоследних(&Дата, ) КАК уатМестонахождениеТССрезПоследних
	|		ПО уатУстановленныеАгрегатыСрезПоследних.ТранспортноеСредство = уатМестонахождениеТССрезПоследних.ТС
	|ГДЕ
	|	уатУстановленныеАгрегатыСрезПоследних.УзелОбъектаЭксплуатации = &УзелОбъектаЭксплуатации";
	ЗапроспоШине.УстановитьПараметр("УзелОбъектаЭксплуатации", Шина);
	ЗапроспоШине.УстановитьПараметр("Дата",                    ДатаКон);
	ВыборкаПоШине=ЗапроспоШине.Выполнить().Выбрать();
	Если ВыборкаПоШине.Следующий() Тогда
		Шапка.Параметры.НомерКолонны = ВыборкаПоШине.Подразделение;
	КонецЕсли;	
	
	//запрос по началу эксплуатации
	Если ЗначениеЗаполнено(Шина) Тогда
		ЗапросДатаПоступления = Новый Запрос(
		"ВЫБРАТЬ
		|	УзелОбъектаЭксплуатацииСрезПервых.Период,
		|	уатАгрегатыТС.МодельАгрегата,
		|	УзелОбъектаЭксплуатацииСрезПервых.УзелОбъектаЭксплуатации.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.уатУстановленныеАгрегаты.СрезПервых(, УзелОбъектаЭксплуатации = &Шина) КАК УзелОбъектаЭксплуатацииСрезПервых
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уатАгрегатыТС КАК уатАгрегатыТС
		|		ПО УзелОбъектаЭксплуатацииСрезПервых.УзелОбъектаЭксплуатации = уатАгрегатыТС.УзелОбъектаЭксплуатации
		|
		|УПОРЯДОЧИТЬ ПО
		|	УзелОбъектаЭксплуатацииСрезПервых.Период");
		ЗапросДатаПоступления.Параметры.Вставить("Шина", Шина);
		ВыборкаДатаПоступления = ЗапросДатаПоступления.Выполнить().Выбрать();
		Если ВыборкаДатаПоступления.Следующий() Тогда
			Шапка.Параметры.ДатаПоступления = ВыборкаДатаПоступления.Период;
			Шапка.Параметры.МодельШины      = ВыборкаДатаПоступления.МодельАгрегата;
			Шапка.Параметры.Изготовитель    = ВыборкаДатаПоступления.МодельАгрегата.Производитель;
			Шапка.Параметры.ОбозначениеШины = ВыборкаДатаПоступления.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	ДокументРезультат.Вывести(Шапка);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблШин.ДатаУстановкиШиныНаТС,
	|	ТаблШин.ДатаСнятияШиныСТС,
	|	ТаблШин.Шины,
	|	ТаблШин.ТС,
	|	ТаблШин.МодельТС,
	|	ТаблШин.ИнвНомерТС,
	|	ТаблШин.ГосударственныйНомер,
	|	ТаблШин.МестоУстановки
	|ПОМЕСТИТЬ ВРТ
	|ИЗ
	|	&ТаблШин КАК ТаблШин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПробегЗаМесяц.ПробегОборот, 0) КАК ПробегЗаМесяц,
	|	ЕСТЬNULL(ОбщийПробег.ПробегОборот, 0) КАК ПробегОбщий,
	|	УстановленныеШины.ТС,
	|	УстановленныеШины.МодельТС,
	|	УстановленныеШины.ДатаУстановкиШиныНаТС,
	|	УстановленныеШины.ДатаСнятияШиныСТС,
	|	УстановленныеШины.ИнвНомерТС,
	|	УстановленныеШины.ГосударственныйНомер
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВРТ.ДатаУстановкиШиныНаТС КАК ДатаУстановкиШиныНаТС,
	|		ВРТ.Шины КАК Шины,
	|		ВРТ.ДатаСнятияШиныСТС КАК ДатаСнятияШиныСТС,
	|		ВРТ.ТС КАК ТС,
	|		ВРТ.МодельТС КАК МодельТС,
	|		ВРТ.ИнвНомерТС КАК ИнвНомерТС,
	|		ВРТ.ГосударственныйНомер КАК ГосударственныйНомер
	|	ИЗ
	|		ВРТ КАК ВРТ) КАК УстановленныеШины
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			уатИзносПробегШинОбороты.ТС КАК ТС,
	|			уатИзносПробегШинОбороты.УзелОбъектаЭксплуатации КАК УзелОбъектаЭксплуатации,
	|			уатИзносПробегШинОбороты.Организация КАК Организация,
	|			СУММА(уатИзносПробегШинОбороты.ПробегОборот) КАК ПробегОборот
	|		ИЗ
	|			РегистрНакопления.уатИзносПробегШин.Обороты(НАЧАЛОПЕРИОДА(&ДатаКон, МЕСЯЦ), &ДатаКон, Регистратор, УзелОбъектаЭксплуатации = &Шина) КАК уатИзносПробегШинОбороты
	|		
	|		СГРУППИРОВАТЬ ПО
	|			уатИзносПробегШинОбороты.ТС,
	|			уатИзносПробегШинОбороты.Организация,
	|			уатИзносПробегШинОбороты.УзелОбъектаЭксплуатации) КАК ПробегЗаМесяц
	|		ПО УстановленныеШины.Шины = ПробегЗаМесяц.УзелОбъектаЭксплуатации
	|			И УстановленныеШины.ТС = ПробегЗаМесяц.ТС
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			уатИзносПробегШинОбороты.ТС КАК ТС,
	|			уатИзносПробегШинОбороты.УзелОбъектаЭксплуатации КАК УзелОбъектаЭксплуатации,
	|			уатИзносПробегШинОбороты.Организация КАК Организация,
	|			СУММА(уатИзносПробегШинОбороты.ПробегОборот) КАК ПробегОборот
	|		ИЗ
	|			РегистрНакопления.уатИзносПробегШин.Обороты(, &ДатаКон, Регистратор, УзелОбъектаЭксплуатации = &Шина) КАК уатИзносПробегШинОбороты
	|		
	|		СГРУППИРОВАТЬ ПО
	|			уатИзносПробегШинОбороты.ТС,
	|			уатИзносПробегШинОбороты.Организация,
	|			уатИзносПробегШинОбороты.УзелОбъектаЭксплуатации) КАК ОбщийПробег
	|		ПО УстановленныеШины.Шины = ОбщийПробег.УзелОбъектаЭксплуатации
	|			И УстановленныеШины.ТС = ОбщийПробег.ТС");
	
	Если ДатаКон <> '00010101000000' Тогда
		Запрос.Параметры.Вставить("ДатаКон", КонецМесяца(ДатаКон));
	Иначе
		Запрос.Параметры.Вставить("ДатаКон", КонецМесяца(ТекущаяДата()));
	КонецЕсли;
	Запрос.Параметры.Вставить("Шина", Шина);
	Запрос.УстановитьПараметр("ТаблШин", ТабУстановки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЗаголовокОтчета = Макет.ПолучитьОбласть("ЗаголовокОтчета");
	
	ДокументРезультат.Вывести(ЗаголовокОтчета);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Строка = Макет.ПолучитьОбласть("Строка");
	
	Пока Выборка.Следующий() Цикл
		Строка.Параметры.Заполнить(Выборка);
		Строка.Параметры.ПредставлениеТС = Выборка.ГосударственныйНомер + ", " + Выборка.МодельТС + ", " + Выборка.ИнвНомерТС;
		Строка.Параметры.ТС = Выборка.ТС;
		Строка.Параметры.ПредставлениеПериода = Строка(Формат(ДатаКон, "ДФ=MM.yyyy")) + "г.";
		Строка.Параметры.ЗаМесяц = Выборка.ПробегЗаМесяц/1000;
		Строка.Параметры.ПробегОбщий = Выборка.ПробегОбщий/1000;
		ДокументРезультат.Вывести(Строка);
	КонецЦикла;	
	
	Подвал = Макет.ПолучитьОбласть("Подвал");
	ДокументРезультат.Вывести(Подвал);
	
	// Ориентация страницы
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСлева = 0;
	ДокументРезультат.ПолеСправа = 0;
	
	// Присвоим имя для сохранения параметров печати табличного документа
	ДокументРезультат.ИмяПараметровПечати = "Карточка работы шины";
	
	Возврат ДокументРезультат;
	
КонецФункции // СформироватьОтчет()

&НаСервереБезКонтекста
Функция ПолучитьКлючАгрегата(УзелОбъектаЭксплуатации)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("УзелОбъектаЭксплуатации", УзелОбъектаЭксплуатации);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	уатАгрегатыТС.УзелОбъектаЭксплуатации,
	|	уатАгрегатыТС.УдалитьТипАгрегата,
	|	уатАгрегатыТС.УдалитьСерияНоменклатуры,
	|	уатАгрегатыТС.УдалитьНоменклатура
	|ИЗ
	|	РегистрСведений.уатАгрегатыТС КАК уатАгрегатыТС
	|ГДЕ
	|	уатАгрегатыТС.УзелОбъектаЭксплуатации = &УзелОбъектаЭксплуатации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктураКлюча = Новый Структура(
			"УзелОбъектаЭксплуатации, УдалитьТипАгрегата, УдалитьСерияНоменклатуры, УдалитьНоменклатура",
			Выборка.УзелОбъектаЭксплуатации, 
			Выборка.УдалитьТипАгрегата, 
			Выборка.УдалитьСерияНоменклатуры,
			Выборка.УдалитьНоменклатура
		);
		
		КлючЗаписи = РегистрыСведений.уатАгрегатыТС.СоздатьКлючЗаписи(СтруктураКлюча);
		
		Возврат КлючЗаписи;
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииШиныСервер()
	
	НаименованиеШины = РегистрыСведений.уатАгрегатыТС.ПрочитатьРеквизитыАгрегата(Шина).Наименование;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыбАгрегатСервер(КлючЗаписи)
	
	Возврат КлючЗаписи.УзелОбъектаЭксплуатации;
	
КонецФункции

#КонецОбласти
