
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	об = РеквизитФормыВЗначение("Объект");
	уатЗащищенныеФункцииСервер.уатДокументФормаЭлементаПриСозданииНаСервере(об, Отказ, СтандартнаяОбработка, ЭтаФорма, ДопПараметрыОткрытие);
	ЗначениеВРеквизитФормы(Об,"Объект");
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	уатОбщегоНазначенияСервер.ЗаполнитьШапкуДокумента(
		Объект,
		,
		Параметры.ЗначениеКопирования,
		Параметры.Основание,
		,
		,
		,
		Параметры.ЗначенияЗаполнения
	);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	// Визирование
	Элементы.ГруппаСогласование.Видимость = ПолучитьФункциональнуюОпцию("уатИспользоватьСогласованиеДокументов");
	Если Объект.Ссылка.Пустая() Тогда
		СтруктураОбъектовНастроек = Новый Структура;
		СтруктураОбъектовНастроек.Вставить("Организация", Объект.Организация);
		СтруктураОбъектовНастроек.Вставить("Подразделение", Объект.Подразделение);
		Объект.ТребуетСогласования = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(СтруктураОбъектовНастроек,
			ПланыВидовХарактеристик.уатПраваИНастройки.ПеремещенияТСИОборудованияСогласованиеПоУмолчанию);
		Модифицированность = Ложь;
	КонецЕсли;
	Элементы.ТребуетСогласования.Доступность = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ПользователиКлиентСервер.ТекущийПользователь(),
		ПланыВидовХарактеристик.уатПраваИНастройки.РазрешитьИзменятьПараметрыСогласования);
		
	// Заполнение при вводе из карточки ТС
	Если Объект.Ссылка.Пустая() И Параметры.Свойство("ТС") Тогда
		НовоеТС = Объект.ТС.Добавить();
		НовоеТС.ТС = Параметры.ТС;
		НовоеТС.ДатаВвода = ТекущаяДата();
	КонецЕсли;
	
	ОбновитьСтароеМестоположениеТСсервер();
	ОбновитьНомерТСвТЧ();
	
	уатОбщегоНазначенияСервер.НастроитьПолеПодразделение(Элементы.Подразделение, "Объект.Организация");
	
	// УправлениеПредприятием.СлужебныеПодсистемы
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// Конец УправлениеПредприятием.СлужебныеПодсистемы
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатДокументФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Обновить статус согласования
	ОбновитьСтатусСогласования();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбновитьНомерТСвТЧ();
	ОбновитьСтароеМестоположениеТСсервер();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьНомерТСвТЧ();
	ОбновитьСтароеМестоположениеТСсервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если Объект.ТребуетСогласования Тогда
		СтатусСогласования = уатОбщегоНазначения_проф.СтатусСогласования(Объект.Ссылка);
		Если НЕ Элементы.ТребуетСогласования.Доступность Тогда
			Если СтатусСогласования.Код = 0 Тогда 
				//если согласования нет вообще, то введем на основании новое
				Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ПеремещениеТСИОборудованияГруппаИсполнителейСогласованияПоУмолчанию");
				уатОбщегоНазначения_проф.СоздатьБизнесПроцессСогласования(Объект.Ссылка,Настройка);
			Иначе 
				ПоказатьЗначение(Новый ОписаниеОповещения("ПослеЗаписиЗавершение", ЭтотОбъект, Новый Структура("СтатусСогласования", СтатусСогласования)), СтатусСогласования.Ссылка);
				Возврат;
			КонецЕсли;
		ИначеЕсли Элементы.ТребуетСогласования.Доступность И СтатусСогласования.Код = 0 Тогда
			СоздатьОткрытьСогласование();
		КонецЕсли;
	КонецЕсли;
	
	ПослеЗаписиФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиЗавершение(ДополнительныеПараметры) Экспорт
    
    СтатусСогласования = ДополнительныеПараметры.СтатусСогласования;
    //если согласование есть, то просто откроем его форму
    ПослеЗаписиФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиФрагмент()
    
    Перем мсвТС, ТекСтрока;
    
    мсвТС = Новый Массив;
    Для Каждого ТекСтрока Из Объект.ТС Цикл
        мсвТС.Добавить(ТекСтрока.ТС);
    КонецЦикла;
	
	Оповестить("ОбновитьФорму_МестонахождениеТС", , мсвТС);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БизнесПроцессИзменен" И уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Параметр, "Предмет") = Объект.Ссылка Тогда
		ОбновитьСтатусСогласования();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	СтруктураОбъектовНастроек = Новый Структура;
	СтруктураОбъектовНастроек.Вставить("Организация", Объект.Организация);
	СтруктураОбъектовНастроек.Вставить("Подразделение", Объект.Подразделение);
	Объект.ТребуетСогласования = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(СтруктураОбъектовНастроек,
		ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ПеремещенияТСИОборудованияСогласованиеПоУмолчанию"));
	ОбновитьСтатусСогласования();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	СтруктураОбъектовНастроек = Новый Структура;
	СтруктураОбъектовНастроек.Вставить("Организация", Объект.Организация);
	СтруктураОбъектовНастроек.Вставить("Подразделение", Объект.Подразделение);
	Объект.ТребуетСогласования = уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(СтруктураОбъектовНастроек,
		ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ПеремещенияТСИОборудованияСогласованиеПоУмолчанию"));
	ОбновитьСтатусСогласования();
КонецПроцедуры

&НаКлиенте
Процедура ТребуетСогласованияПриИзменении(Элемент)
	
	ТребуетСогласования = Объект.ТребуетСогласования;
	
	//запрет изменения флага для проведенного документа
	Если Объект.Проведен И ТребуетСогласования Тогда
		Объект.ТребуетСогласования = Ложь;	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен! Отмените проведение и установите флаг повторно!");
		Модифицированность = Ложь;
	КонецЕсли;
	
	//запрет изменения флага, если запущен бизнес-процесс согласования
	Если НЕ ТребуетСогласования И уатОбщегоНазначения_проф.СтатусСогласования(Объект.Ссылка).Код <> 0 Тогда
		Объект.ТребуетСогласования = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Невозможно отменить согласование, бизнес-процесс уже запущен!");
		Модифицированность = Ложь;
	КонецЕсли;
	
	ОбновитьСтатусСогласования();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСогласованНажатие(Элемент)
	
	флОтказ = Ложь;
	уатОбщегоНазначенияКлиент.ПроверитьЗаписьНовогоОбъектаВФорме(ЭтаФорма, Объект.Ссылка, "документ", Модифицированность, флОтказ);
	Если флОтказ Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьОткрытьСогласование();
	
КонецПроцедуры

&НаКлиенте
Процедура ТСПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекСтрока = Элементы.ТС.ТекущиеДанные;
	уатИнтерфейсВводаТС.ТабличноеПолеПриНачалеРедактирования(ТекСтрока.ТС, ТекСтрока.НомерТС, Объект.Организация);
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекСтрока.ДатаВвода = Объект.Дата;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
		СтандартнаяОбработка = Ложь;
		ОбработкаВыбораТСНаСервере(ВыбранноеЗначение);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораТСНаСервере(ВыбранноеЗначение)
	
	Для Каждого ТекЭлем Из ВыбранноеЗначение Цикл
		НовСтрока = Объект.ТС.Добавить();
		НовСтрока.ТС = ТекЭлем.Значение;
	КонецЦикла;
	
	ОбновитьНомерТСвТЧ();
	ОбновитьСтароеМестоположениеТСсервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСПриИзменении(Элемент)
	ТекСтрока = Элементы.ТС.ТекущиеДанные;
	
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСПриИзменении(ТекСтрока.ТС, ТекСтрока.НомерТС, Объект.Организация);
	ОбновитьНомерТСвТЧ(ТекСтрока);
	ОбновитьСтароеМестоположениеТС(ТекСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтруктураОтбора = Новый Структура;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		СтруктураОтбора.Вставить("уатОрганизация", Объект.Организация);
	КонецЕсли;
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСНачалоВыбора(Элемент, Элементы.ТС.ТекущиеДанные.ТС, СтруктураОтбора, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСОчистка(Элемент, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСОчистка(Элементы.ТС.ТекущиеДанные.ТС);
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСОткрытие(Элемент, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСОткрытие(Элементы.ТС.ТекущиеДанные.ТС, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ТекСтрока = Элементы.ТС.ТекущиеДанные;
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСОбработкаВыбора(ТекСтрока.ТС, ТекСтрока.НомерТС, ВыбранноеЗначение, СтандартнаяОбработка, Объект.Организация);
	ОбновитьСтароеМестоположениеТС(ТекСтрока);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСАвтоПодборТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура ТСНомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.ТабличноеПолеНомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура ТСДатаВводаПриИзменении(Элемент)
	ОбновитьСтароеМестоположениеТС(Элементы.ТС.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// УправлениеПредприятием.СлужебныеПодсистемы
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
КонецПроцедуры
// Конец УправлениеПредприятием.СлужебныеПодсистемы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ОбновитьСтатусСогласования()
	Если Объект.ТребуетСогласования Тогда
		Элементы.ГруппаНадписьСогласован.ТекущаяСтраница = Элементы.ГруппаНадписьСогласованНадпись;
		Если Объект.Ссылка.Пустая() Тогда
			Элементы.НадписьСогласован.Заголовок = "Не согласован";
			Элементы.НадписьСогласован.Гиперссылка = Истина;
		Иначе
			СтатусСогласования = уатОбщегоНазначения_проф.СтатусСогласования(Объект.Ссылка);
			Элементы.НадписьСогласован.Заголовок = СтатусСогласования.Статус;
			Элементы.НадписьСогласован.Гиперссылка = Истина;
			//Элементы.ТребуетСогласования.Доступность = (СтатусСогласования.Код = 0);
		КонецЕсли;
		Если Элементы.НадписьСогласован.Заголовок = "Согласован" Тогда
			Элементы.НадписьСогласован.ЦветТекста = WebЦвета.Зеленый;
		Иначе
			Элементы.НадписьСогласован.ЦветТекста = WebЦвета.Красный;
		КонецЕсли;
	Иначе
		Элементы.ГруппаНадписьСогласован.ТекущаяСтраница = Элементы.ГруппаНадписьСогласованПустая;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОткрытьСогласование()
	
	СтатусСогласования = уатОбщегоНазначения_проф.СтатусСогласования(Объект.Ссылка);
	Если СтатусСогласования.Код = 0 Тогда 
		//если согласования нет вообще, то введем на основании новое
		ПараметрыОткрытия = Новый Структура("Основание", Объект.Ссылка);
		ОткрытьФорму("БизнесПроцесс.уатСогласование.ФормаОбъекта", ПараметрыОткрытия);
	Иначе 
		//если согласование есть, то просто откроем его форму
		ПоказатьЗначение(,СтатусСогласования.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНомерТСвТЧ(Знач ТекСтрока = Неопределено)
	ИмяТЧ = "ТС";
	
	Если ТекСтрока <> Неопределено Тогда
		ТекСтрока = Объект[ИмяТЧ].НайтиПоИдентификатору(ТекСтрока);
	КонецЕсли;
	
	Если ТекСтрока = Неопределено Тогда
		Для Каждого ТекСтрокаТЧ Из Объект[ИмяТЧ] Цикл
			ТекСтрокаТЧ.НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ТекСтрокаТЧ.ТС, Объект.Организация);
		КонецЦикла;
	Иначе
		ТекСтрока.НомерТС = уатОбщегоНазначения.уатПредставлениеТС(ТекСтрока.ТС, Объект.Организация);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтароеМестоположениеТС(ТекСтрока)
	МестоТС = уатОбщегоНазначения.МестонахождениеТС(ТекСтрока.ТС, ТекСтрока.ДатаВвода-1);
	ТекСтрока.Организация = МестоТС.Организация;
	ТекСтрока.Подразделение = МестоТС.Подразделение;
	ТекСтрока.Колонна = МестоТС.Колонна;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтароеМестоположениеТСсервер()
	Для Каждого ТекСтрокаТЧ Из Объект.ТС Цикл
		МестоТС = уатОбщегоНазначения.МестонахождениеТС(ТекСтрокаТЧ.ТС, ТекСтрокаТЧ.ДатаВвода-1);
		ТекСтрокаТЧ.Организация = МестоТС.Организация;
		ТекСтрокаТЧ.Подразделение = МестоТС.Подразделение;
		ТекСтрокаТЧ.Колонна = МестоТС.Колонна;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
