
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	об = РеквизитФормыВЗначение("Объект");
	уатЗащищенныеФункцииСервер.уатДокументФормаЭлементаПриСозданииНаСервере(об, Отказ, СтандартнаяОбработка, ЭтаФорма, ДопПараметрыОткрытие);
	ЗначениеВРеквизитФормы(Об,"Объект");
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	//заполнение при вводе из путевого листа
	Если Параметры.Свойство("ДобавлениеПосадочнойВедомостиИзПЛ") И Параметры.ДобавлениеПосадочнойВедомостиИзПЛ = Истина Тогда
		ДобавлениеПосадочнойВедомостиИзПЛ = Истина;
		Если НЕ Параметры.Свойство("ПосадочнаяВедомостьКопирование") Тогда
			ДокОбъект = РеквизитФормыВЗначение("Объект");
			ДокОбъект.Заполнить(Параметры.ПутевойЛист);
			ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
		КонецЕсли;
	КонецЕсли;
	
	//заполнение при редактировании из путевого листа
	Если Параметры.Свойство("РедактированиеПосадочнойВедомостиИзПЛ") И Параметры.РедактированиеПосадочнойВедомостиИзПЛ = Истина Тогда
		РедактированиеПосадочнойВедомостиИзПЛ = Истина;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	уатОбщегоНазначенияСервер.ЗаполнитьШапкуДокумента(
		Объект,
		,
		Параметры.ЗначениеКопирования,
		Параметры.Основание,
		,
		,
		,
		Параметры.ЗначенияЗаполнения
	);
	
	ОбновитьНомерТС();
	
	// УправлениеПредприятием.СлужебныеПодсистемы
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// Конец УправлениеПредприятием.СлужебныеПодсистемы
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	уатЗащищенныеФункцииКлиент.уатДокументФормаЭлементаПриОткрытии(Отказ, ДопПараметрыОткрытие);
	Если Отказ тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ДобавлениеПосадочнойВедомостиИзПЛ Тогда
		Оповестить("ДобавлениеПосадочнойВедомостиИзПЛ", Объект.Ссылка);
	КонецЕсли;
	Если РедактированиеПосадочнойВедомостиИзПЛ Тогда
		Оповестить("РедактированиеПосадочнойВедомостиИзПЛ", Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	СотрудникПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	уатЗащищенныеФункцииКлиент.ДиалогВыбораСотрудника(Элемент, Объект.Сотрудник, Новый Структура("Организация", Объект.Организация), СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОткрытие(Элемент, СтандартнаяОбработка)
	уатЗащищенныеФункцииКлиент.ОткрытьФормуСотрудника(Объект.Сотрудник, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСПриИзменении(Элемент)
	уатИнтерфейсВводаТС.НомерТСПриИзменении(НомерТС, Объект.ТС, Объект.Организация);
	ОбновитьНомерТС();
	УстановитьТС(Объект.ТС);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСНачалоВыбора(Элемент, Объект.ТС, Новый Структура("уатОрганизация", Объект.Организация), СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	мТС = Объект.ТС;
	
	уатИнтерфейсВводаТС.НомерТСОбработкаВыбора(Элемент, Объект.ТС, ВыбранноеЗначение, СтандартнаяОбработка, Объект.Организация);
	ОбновитьНомерТС();
	УстановитьТС(Объект.ТС);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСАвтоПодборТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура НомерТСОткрытие(Элемент, СтандартнаяОбработка)
	уатИнтерфейсВводаТС.НомерТСОткрытие(Объект.ТС, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура БилетыСуммаПриИзменении(Элемент)
	ПересчетКомиссии(Элементы.Билеты.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура БилетыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
			
		Элемент.ТекущиеДанные.ПутевойЛист = мПутевойЛист;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	Если Не ОтменаРедактирования Тогда
		
		мПутевойЛист = Элемент.ТекущиеДанные.ПутевойЛист;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)

	СтрокаТабличнойЧасти 		= Элементы.Билеты.ТекущиеДанные;
	СтрокаТабличнойЧасти.Сумма 	= СтрокаТабличнойЧасти.Количество * уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(СтрокаТабличнойЧасти.Билет, "Цена");
	ПересчетКомиссии(СтрокаТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ПутевойЛистНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФормаВыбора = ПолучитьФорму("Документ.уатПутевойЛист.ФормаВыбора",, Элемент);
	ФормаВыбора.Список.Отбор.Элементы.Очистить();
	уатИнтерфейсВзаимодействияСERPКлиентСервер.уатУстановитьЭлементОтбораСписка(ФормаВыбора.Список, "ТранспортноеСредство", Объект.ТС);
	ФормаВыбора.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// УправлениеПредприятием.СлужебныеПодсистемы
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
КонецПроцедуры
// Конец УправлениеПредприятием.СлужебныеПодсистемы

&НаКлиенте
Процедура КоманднаяПанель2ПересчетКомиссии(Команда)
	
	Для Каждого ТекСтрока Из Объект.Билеты Цикл
		ПересчетКомиссии(ТекСтрока);
	КонецЦикла;	

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура СотрудникПриИзмененииСервер()
	мДатаНачала = ДобавитьМесяц(Объект.Дата, -1);
	мТаблицаПЛ = уатОбщегоНазначения.уатПутевыеЛистыВодителя(Объект.Сотрудник, мДатаНачала, КонецДня(Объект.Дата),,,Ложь);
	
	Попытка
		Объект.ПутевойЛист = мТаблицаПЛ[0].Ссылка;
		Объект.ТС = мПутевойЛист.ТранспортноеСредство;
		ОбновитьНомерТС();
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ПересчетКомиссии(СтрокаТабличнойЧасти)
	
	СтрокаТабличнойЧасти.КомиссияАвтостанции = СтрокаТабличнойЧасти.Сумма * уатОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(Объект.Автостанция, "Комиссия") / 100;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНомерТС()
	НомерТС = уатОбщегоНазначения.уатПредставлениеТС(Объект.ТС, Объект.Организация);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТС(мТС)
	
	Модифицированность = Истина;
	
	Если НЕ ЗначениеЗаполнено(мТС) тогда
		Возврат;	
	КонецЕсли;
	
	//заполняем водителя
	СтруктураЭкипаж = уатЗащищенныеФункцииСервер.ЭкипажТСсУчетомГрафика(мТС, Объект.Дата, Объект.Организация);
	Объект.Сотрудник = СтруктураЭкипаж.Водитель;
	
	ТекДанные = Элементы.Билеты.ТекущиеДанные;
	Если Не ТекДанные=Неопределено И Не ЗначениеЗаполнено(ТекДанные.ПутевойЛист) Тогда
		мДатаНачала = ДобавитьМесяц(Объект.Дата, -1);
		УстановитьТССервер(ТекДанные.ПутевойЛист, мТС, мДатаНачала, Объект.Дата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьТССервер(мПутевойЛист, мТС, мДатаНачала, мДата)
	ТаблицаПЛ = уатОбщегоНазначения.уатПутевыеЛисты(мТС, мДатаНачала, КонецДня(мДата),, Ложь);
	Если ТаблицаПЛ.Количество() > 0 Тогда
		мПутевойЛист = ТаблицаПЛ[0].Ссылка;
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти
