
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Процедура рассчитывает в строке табличной части суммовое значение комиссии автостанции
//
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части , требующая пересчёта
// 
Процедура ПересчетКомиссии(СтрокаТабличнойЧасти) Экспорт
	
	СтрокаТабличнойЧасти.КомиссияАвтостанции = СтрокаТабличнойЧасти.Сумма * Автостанция.Комиссия / 100;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатПосадочнаяВедомость.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение_проф.ОтразитьВыручку(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Документы.уатПосадочнаяВедомость.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры
	
Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("УправляемаяФорма") ИЛИ ТипЗнч(Основание) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
		Организация = Основание.Организация;
		Ответственный = Основание.Ответственный;
		ТС = Основание.ТранспортноеСредство;
		Сотрудник = Основание.Водитель1;
		
		Если Не ЗначениеЗаполнено(Дата) Тогда
			Дата = Основание.ДатаВыезда;
		КонецЕсли;
		
		//проверка уникальности номера
		Если ЗначениеЗаполнено(Номер) Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	уатПосадочнаяВедомость.Ссылка
			|ИЗ
			|	Документ.уатПосадочнаяВедомость КАК уатПосадочнаяВедомость
			|ГДЕ
			|	уатПосадочнаяВедомость.Номер = &Номер
			|	И уатПосадочнаяВедомость.Ссылка <> &Ссылка
			|	И уатПосадочнаяВедомость.Дата МЕЖДУ &НачПериода И &КонПериода");
			Запрос.УстановитьПараметр("Номер", Номер);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("НачПериода", НачалоГода(Дата));
			Запрос.УстановитьПараметр("КонПериода", КонецГода(Дата));
			Если НЕ Запрос.Выполнить().Пустой() Тогда
				Номер = "";
			КонецЕсли;
		КонецЕсли;
		
		//добавим основной ТС
		НоваяСтрока 				= Билеты.Добавить();
		НоваяСтрока.ПутевойЛист 	= Основание.Ссылка;
		//
		Для Каждого ТекСтрока Из Основание.Задание Цикл
			Если ЗначениеЗаполнено(ТекСтрока.Маршрут) Тогда
				НоваяСтрока.Маршрут = ТекСтрока.Маршрут;
				Прервать
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = уатОбщегоНазначенияТиповые.уатПолучитьСуммуДокументаСНДС(ЭтотОбъект);
	
КонецПроцедуры // ПередЗаписью
