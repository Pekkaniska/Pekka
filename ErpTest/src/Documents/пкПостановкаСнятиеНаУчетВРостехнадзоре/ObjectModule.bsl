#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(Номер) Тогда
		УстановитьНовыйНомер();
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		ЭтотОбъект.Организация = ДанныеЗаполнения.Организация;
		
		ЭтотОбъект.ДокументОснование=ДанныеЗаполнения;
		Если ТипЗнч(ДанныеЗаполнения)= Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
			ЭтотОбъект.СнятиеСУчета = Ложь;
			ЭтотОбъект.Регион = ДанныеЗаполнения.Местонахождение;
		Иначе	
			ЭтотОбъект.Регион = ДанныеЗаполнения.Подразделение;
			ЭтотОбъект.СнятиеСУчета = Истина;	
		КонецЕсли; 
		
		МассивОС=ДанныеЗаполнения.ОС.ВыгрузитьКолонку("ОсновноеСредство");
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	пкПостановкаНаУчетВРостехнадзореСрезПоследних.ОсновноеСредство КАК ОсновноеСредство1,
		               |	пкПостановкаНаУчетВРостехнадзореСрезПоследних.РегистрационныйНомер КАК РегистрационныйНомер,
		               |	пкПостановкаНаУчетВРостехнадзореСрезПоследних.ДатаПостановкиНаУчет КАК ДатаПостановкиНаУчет,
		               |	ОбъектыЭксплуатации.Ссылка КАК ОсновноеСредство,
		               |	ЕСТЬNULL(пкПостановкаНаУчетВРостехнадзореСрезПоследних.ПостановкаНаУчет, ЛОЖЬ) КАК ПостановкаНаУчет,
		               |	пкПостановкаНаУчетВРостехнадзореСрезПоследних.Регион
		               |ИЗ
		               |	Справочник.ОбъектыЭксплуатации КАК ОбъектыЭксплуатации
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.пкПостановкаНаУчетВРостехнадзоре.СрезПоследних(
		               |				&МоментВремениДокумента,
		               |				Организация = &Организация
		               |					И ОсновноеСредство В (&МассивОС)) КАК пкПостановкаНаУчетВРостехнадзореСрезПоследних
		               |		ПО (пкПостановкаНаУчетВРостехнадзореСрезПоследних.ОсновноеСредство = ОбъектыЭксплуатации.Ссылка)
		               |ГДЕ
		               |	ОбъектыЭксплуатации.Ссылка В(&МассивОС)";
		
		Запрос.УстановитьПараметр("МассивОС", МассивОС);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("МоментВремениДокумента", ЭтотОбъект.МоментВремени());
		
		Результат = Запрос.Выполнить();
		ТаблицаОС = Результат.Выгрузить();
		
		Для каждого СтрокаОС Из ТаблицаОС Цикл
			НоваяСтрока=ЭтотОбъект.СписокТехники.Добавить();	
			НоваяСтрока.ОсновноеСредство = СтрокаОС.ОсновноеСредство;
			//Если ЗначениеЗаполнено(СтрокаОС.Регион) Тогда
			//	НоваяСтрока.РегистрационныйНомер = СтрЗаменить(СтрокаОС.РегистрационныйНомер,СтрокаОС.Регион.Код,"");
			//Иначе	
			//	НоваяСтрока.РегистрационныйНомер = СтрокаОС.РегистрационныйНомер;
			//КонецЕсли; 
			//НоваяСтрока.ДатаПостановкиНаУчет = СтрокаОС.ДатаПостановкиНаУчет;
			Если СтрокаОС.ПостановкаНаУчет Тогда
				Если ЗначениеЗаполнено(СтрокаОС.Регион) Тогда
					НоваяСтрока.РегистрационныйНомер = СтрЗаменить(СтрокаОС.РегистрационныйНомер,СтрокаОС.Регион.Код,"");
				Иначе	
					НоваяСтрока.РегистрационныйНомер = СтрокаОС.РегистрационныйНомер;
				КонецЕсли; 
				НоваяСтрока.ДатаПостановкиНаУчет = СтрокаОС.ДатаПостановкиНаУчет;
			Иначе
				НоваяСтрока.РегистрационныйНомер = Неопределено;
				НоваяСтрока.ДатаПостановкиНаУчет = Неопределено;
			КонецЕсли;
		
		КонецЦикла; 
	КонецЕсли; 
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПроверяемыеРеквизиты.Добавить("Организация");
	//ПроверяемыеРеквизиты.Добавить("Подразделение");
	//ПроверяемыеРеквизиты.Добавить("ТопливныеКарты.Сотрудник");
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУТ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	РегистрыСведений.пкПостановкаНаУчетВРостехнадзоре.СформироватьЗаписи(Отказ, РежимПроведения, ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	СформироватьСписокРегистровДляКонтроля();
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУТ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Организация		= ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Подразделение	= ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Пользователи.ТекущийПользователь());
	Ответственный	= Пользователи.ТекущийПользователь();
	Автор 			= Пользователи.ТекущийПользователь();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура СформироватьСписокРегистровДляКонтроля()
	
	Массив = Новый Массив;
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

