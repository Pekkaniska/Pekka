
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(Основание)
	Если ТипЗнч(Основание) = Тип("УправляемаяФорма") Или ТипЗнч(Основание) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
		Организация = Основание.Организация;
		Ответственный = Основание.Ответственный;
		ТС = Основание.ТранспортноеСредство;
		Сотрудник = Основание.Водитель1;
		
		Если Не ЗначениеЗаполнено(Дата) Тогда
			Дата = Основание.ДатаВыезда;
		КонецЕсли;
		
		//проверка уникальности номера
		Если ЗначениеЗаполнено(Номер) Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	уатБилетныйЛист.Ссылка
			|ИЗ
			|	Документ.уатБилетныйЛист КАК уатБилетныйЛист
			|ГДЕ
			|	уатБилетныйЛист.Номер = &Номер
			|	И уатБилетныйЛист.Ссылка <> &Ссылка
			|	И уатБилетныйЛист.Дата МЕЖДУ &НачПериода И &КонПериода");
			Запрос.УстановитьПараметр("Номер", Номер);
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			Запрос.УстановитьПараметр("НачПериода", НачалоГода(Дата));
			Запрос.УстановитьПараметр("КонПериода", КонецГода(Дата));
			Если НЕ Запрос.Выполнить().Пустой() Тогда
				Номер = "";
			КонецЕсли;
		КонецЕсли;
		
		//добавим основной ТС
		НоваяСтрока = Билеты.Добавить();
		НоваяСтрока.ПутевойЛист = Основание.Ссылка;
		
		Если Основание.Задание.Количество() > 0 Тогда
			ТекСтрока = Основание.Задание[0];
			НоваяСтрока.Маршрут = ТекСтрока.Маршрут;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	уатПроведение.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.уатБилетныйЛист.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	уатПроведение.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	уатПроведение_проф.ОтразитьВыручку(ДополнительныеСвойства, Движения, Отказ);
	уатПроведение_проф.ОтразитьБилетыНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	уатПроведение.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль возникновения отрицательного остатка.
	Документы.уатБилетныйЛист.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
КонецПроцедуры

// Процедура вызывается перед записью документа 
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//уатОбщегоНазначения.уатПроверкаПравПередЗаписьюВДокументах(ЭтотОбъект, Отказ, , Права);
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;	
	
	СуммаДокумента = уатОбщегоНазначенияТиповые.уатПолучитьСуммуДокументаСНДС(ЭтотОбъект);
	СуммаПроданоБилетов = СуммаДокумента;
	
КонецПроцедуры // ПередЗаписью

