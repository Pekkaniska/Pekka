#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПРОВЕДЕНИЯ

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	СформироватьТаблицаВыручка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаБилетыНаСкладах(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВыручка(ДокументСсылка, СтруктураДополнительныеСвойства)
	мЗапрос = Новый Запрос;
	мЗапрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	мЗапрос.Текст =
	"ВЫБРАТЬ
	|	ТабЧастьДокумента.Ссылка.Дата КАК Период,
	|	ТабЧастьДокумента.Ссылка КАК Регистратор,
	|	ТабЧастьДокумента.Билет,
	|	ТабЧастьДокумента.Маршрут,
	|	ТабЧастьДокумента.Количество,
	|	ТабЧастьДокумента.Сумма,
	|	ТабЧастьДокумента.Ссылка.Организация КАК Организация,
	|	ТабЧастьДокумента.Ссылка.Сотрудник КАК Сотрудник,
	|	ТабЧастьДокумента.Ссылка.ТС КАК ТС,
	|	ТабЧастьДокумента.ПутевойЛист
	|ИЗ
	|	Документ.уатБилетныйЛист.Билеты КАК ТабЧастьДокумента
	|ГДЕ
	|	ТабЧастьДокумента.Ссылка = &Ссылка";
	ТаблицаДвижений = мЗапрос.Выполнить().Выгрузить(); 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыручка", ТаблицаДвижений);
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаБилетыНаСкладах(ДокументСсылка, СтруктураДополнительныеСвойства)
	мЗапрос = Новый Запрос;
	мЗапрос.УстановитьПараметр("Ссылка"          , ДокументСсылка);
	мЗапрос.УстановитьПараметр("Склад"           , ДокументСсылка.Склад);
	мЗапрос.УстановитьПараметр("МоментВремени"   , СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	мЗапрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	мЗапрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Регистратор,
	|	ТаблицаДокумента.Ссылка.Дата КАК Период,
	|	ТаблицаДокумента.Ссылка.Склад КАК Склад,
	|	ТаблицаДокумента.Билет,
	|	ТаблицаДокумента.Количество
	|ПОМЕСТИТЬ ТаблицаДокументаБилеты
	|ИЗ
	|	Документ.уатБилетныйЛист.Билеты КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокументаБилеты.Склад,
	|	ТаблицаДокументаБилеты.Билет
	|ИЗ
	|	ТаблицаДокументаБилеты КАК ТаблицаДокументаБилеты";	
	
	МассивРезультатов = мЗапрос.ВыполнитьПакет();
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.уатБилетыНаСкладах");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = МассивРезультатов[1];
	Для каждого КолонкаРезультатЗапроса Из МассивРезультатов[1].Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();

	мЗапрос.Текст = 
	"ВЫБРАТЬ
	|	ТабНоменклатуры.Склад,
	|	ТабНоменклатуры.Билет,
	|	ТабНоменклатуры.Количество,
	|	ТабНоменклатуры.Период
	|ИЗ 
	|	ТаблицаДокументаБилеты КАК ТабНоменклатуры";
	
	ТаблицаПартий = уатПроведение_проф.ПолучитьТаблицуПартийБилетовДляСписания(мЗапрос.Выполнить().Выгрузить(),
		уатПраваИНастройки.уатПолучитьПраваИНастройкиПользователя(ДокументСсылка.Организация, "СпособОценкиМПЗ"),
		СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	
	ЗапросПартии = Новый Запрос();
	ЗапросПартии.Текст = 
	"ВЫБРАТЬ
	|	ТабДанных.Склад,
	|	ТабДанных.Билет,
	|	ТабДанных.Партия,
	|	ТабДанных.Количество
	|ПОМЕСТИТЬ ВремТаблицаПартий
	|ИЗ
	|	&ТаблицаПарам КАК ТабДанных
	|;
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период,
	|	&Регистратор,
	|	ТабДанных.Склад,
	|	ТабДанных.Партия,
	|	ТабДанных.Билет,
	|	ТабДанных.Количество
	|ИЗ ВремТаблицаПартий КАК ТабДанных";
	
	ЗапросПартии.УстановитьПараметр("Период"         , ДокументСсылка.Дата);
	ЗапросПартии.УстановитьПараметр("ТаблицаПарам"   , ТаблицаПартий);
	ЗапросПартии.УстановитьПараметр("Регистратор"    , ДокументСсылка);

	МассивРезультатов = ЗапросПартии.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаБилетыНаСкладах", МассивРезультатов[1].Выгрузить());
КонецПроцедуры

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылка, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	мЗапрос = Новый Запрос;
	мЗапрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц; 
	мЗапрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабБилеты.Билет
	|ПОМЕСТИТЬ ТаблицаБилетов
	|ИЗ
	|	Документ.уатБилетныйЛист.Билеты КАК ТабБилеты
	|ГДЕ
	|	ТабБилеты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Билет,
	|	ТаблицаДокумента.Ссылка КАК Регистратор,
	|	уатБилетыНаСкладахОстатки.КоличествоОстаток
	|ИЗ
	|	Документ.уатБилетныйЛист.Билеты КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.уатБилетыНаСкладах.Остатки(
	|				&МоментВремени,
	|				Склад = &Склад
	|					И Билет В
	|						(ВЫБРАТЬ
	|							ТаблицаБилетов.Билет
	|						ИЗ
	|							ТаблицаБилетов)) КАК уатБилетыНаСкладахОстатки
	|		ПО (&Склад = уатБилетыНаСкладахОстатки.Склад)
	|			И ТаблицаДокумента.Билет = уатБилетыНаСкладахОстатки.Билет
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ЕСТЬNULL(уатБилетыНаСкладахОстатки.КоличествоОстаток, 0) < 0";
	
	мЗапрос.УстановитьПараметр("Склад"        , ДокументСсылка.Склад);
	мЗапрос.УстановитьПараметр("Ссылка"       , ДокументСсылка);
	мЗапрос.УстановитьПараметр("МоментВремени", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	
	Выборка = мЗапрос.Выполнить().Выбрать();
	Если Выборка.Количество() тогда
		Заголовок = уатОбщегоНазначенияТиповые.уатПредставлениеДокументаПриПроведении(ДокументСсылка);
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = "Для """ + Выборка.Билет + """ получены отрицательные остатки на складе """ + мЗапрос.Параметры.Склад + """. Остаток на складе: " + Выборка.КоличествоОстаток;
			уатОбщегоНазначенияТиповые.СообщитьОбОшибке(ТекстСообщения,Отказ,Заголовок);	
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Функция ПолучитьМаршрут(Ссылка)
	Маршрут = Новый Структура("Номер, Наименование");
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	уатБилетныйЛистБилеты.Маршрут.НомерМаршрута КАК Номер,
	|	уатБилетныйЛистБилеты.Маршрут.Наименование КАК Наименование
	|ИЗ
	|	Документ.уатБилетныйЛист.Билеты КАК уатБилетныйЛистБилеты
	|ГДЕ
	|	уатБилетныйЛистБилеты.Ссылка = &СсылкаНаОбъект";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда 
		Маршрут.Номер = Выборка.Номер;
		Маршрут.Наименование = Выборка.Наименование;
	КонецЕсли;
	Возврат Маршрут;
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	Заглушка = Истина;
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ ФОРМЫ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.уатБилетныйЛист";
	КомандаПечати.Идентификатор = "ПечататьЛицСторону";
	КомандаПечати.Представление = НСтр("ru = 'Печатать лиц. сторону'");
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.уатБилетныйЛист";
	КомандаПечати.Идентификатор = "ПечататьОбрСторону";
	КомандаПечати.Представление = НСтр("ru = 'Печатать обр. сторону'");
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.уатБилетныйЛист";
	КомандаПечати.Идентификатор = "ПечатьВесьДокумент";
	КомандаПечати.Представление = НСтр("ru = 'Печатать весь документ'");
	
КонецПроцедуры

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если НЕ уатОбщегоНазначенияСервер.РазрешениеПечатиНепроведенногоДокумента(МассивОбъектов) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьВесьДокумент") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПечатьВесьДокумент", "Печать весь документ", ПечатьБЛ(МассивОбъектов, ОбъектыПечати, 0));
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечататьЛицСторону") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПечататьЛицСторону", "Печатать лиц. сторону", ПечатьБЛ(МассивОбъектов, ОбъектыПечати, 1));
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечататьОбрСторону") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПечататьОбрСторону", "Печатать обр. сторону", ПечатьБЛ(МассивОбъектов, ОбъектыПечати, 2));
	КонецЕсли;
	
	уатОбщегоНазначенияСервер.УстановитьРедактированиеПечатныхФормДокумента(КоллекцияПечатныхФорм);
КонецПроцедуры // Печать

// Функция выполняет печать документа билетный лист
// Параметры:
//  нет
// 
// Возвращаемое значение:
//  табличныйДокумент - печатная форма
//
Функция ПечатьБЛ(МассивОбъектов, ОбъектыПечати, ФлагПечати="") Экспорт
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_БилетныйЛист";
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	уатБилетныйЛист.Номер КАК НомерДокумента,
		|	уатБилетныйЛист.Дата КАК ДатаДокумента,
		|	уатБилетныйЛист.Организация,
		|	уатБилетныйЛист.Сотрудник КАК Кондуктор,
		|	уатБилетныйЛист.ТС.Код КАК ГосНомерТС,
		|	уатБилетныйЛист.Сотрудник.Код КАК ТабельныйНомер
		|ИЗ
		|	Документ.уатБилетныйЛист КАК уатБилетныйЛист
		|ГДЕ
		|	уатБилетныйЛист.Ссылка = &СсылкаНаОбъект";
		
		Запрос.УстановитьПараметр("СсылкаНаОбъект", ТекущийДокумент.Ссылка);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
			ДанныеШапка = Выборка;
		Иначе
			Сообщить("Не удалось напечатать документ", СтатусСообщения.Важное);
		КонецЕсли;
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 9
		|	уатБилетныйЛистБилеты.Билет.ВидБилета КАК ВидБилета,
		|	уатБилетныйЛистБилеты.Билет.Цена КАК ЦенаБилета,
		|	уатБилетныйЛистБилеты.Билет.Серия КАК СерияБилета,
		|	уатБилетныйЛистБилеты.НомерНачальный КАК НомерБилетаНачальный,
		|	уатБилетныйЛистБилеты.НомерКонечный КАК НомерБилетаКонечный,
		|	уатБилетныйЛистБилеты.Количество КАК КоличествоБилетов,
		|	уатБилетныйЛистБилеты.Сумма КАК СуммаБилетов
		|ИЗ
		|	Документ.уатБилетныйЛист.Билеты КАК уатБилетныйЛистБилеты
		|ГДЕ
		|	уатБилетныйЛистБилеты.Ссылка = &СсылкаНаОбъект
		|
		|УПОРЯДОЧИТЬ ПО
		|	уатБилетныйЛистБилеты.НомерСтроки";
		
		ВыборкаСтрок = Запрос.Выполнить().Выбрать();
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.уатБилетныйЛист.БилетноУчетныйЛист");
		
		//печатаем лицевую сторону п/л
		Если ФлагПечати <> 2 Тогда
			Область = Макет.ПолучитьОбласть("Страница1");
			Область.Параметры.Заполнить(ДанныеШапка);
			Область.Параметры.НомерДокумента = уатОбщегоНазначенияТиповые.уатПолучитьНомерНаПечать(ТекущийДокумент.Ссылка);
			Маршрут = ПолучитьМаршрут(ТекущийДокумент.Ссылка);
			Область.Параметры.НомерМаршрута = Маршрут.Номер;
			Область.Параметры.НаименованиеМаршрута = Маршрут.Наименование;
			Счетчик = 1;
			Пока ВыборкаСтрок.Следующий() Цикл
				Область.Параметры["ВидБилета"            + Строка(Счетчик)] = ВыборкаСтрок.ВидБилета;
				Область.Параметры["ЦенаБилета"           + Строка(Счетчик)] = ВыборкаСтрок.ЦенаБилета;
				Область.Параметры["СерияБилета"          + Строка(Счетчик)] = ВыборкаСтрок.СерияБилета;
				Область.Параметры["НомерБилетаНачальный" + Строка(Счетчик)] = ВыборкаСтрок.НомерБилетаНачальный;
				Область.Параметры["НомерБилетаКонечный"  + Строка(Счетчик)] = ВыборкаСтрок.НомерБилетаКонечный;
				Область.Параметры["КоличествоБилетов"    + Строка(Счетчик)] = ВыборкаСтрок.КоличествоБилетов;
				Область.Параметры["СуммаБилетов"         + Строка(Счетчик)] = ВыборкаСтрок.СуммаБилетов;
				Счетчик = Счетчик + 1;
			КонецЦикла;
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		Если ФлагПечати = 0 Тогда	//Новая страница для печати полного пут. листа
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		//печатаем обратную сторону п/л
		Если ФлагПечати <> 1 Тогда
			
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.уатБилетныйЛист.БилетноУчетныйЛистОбратнаяСторона");
			Область = Макет.ПолучитьОбласть("Страница2");
			Область.Параметры.Заполнить(ДанныеШапка);
			ТабличныйДокумент.Вывести(Область);
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции

#КонецЕсли