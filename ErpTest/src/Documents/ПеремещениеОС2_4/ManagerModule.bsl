
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ИзменениеПараметровОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Перемещение ОС".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПеремещениеОС2_4) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПеремещениеОС2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПеремещениеОС2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

// Заполняет документ на основании инвентаризации ОС
//
// Параметры:
//  Объект				 - ДокументОбъект.ПеремещениеОС2_4	 - Документ, который надо заполнить.
//  СообщатьОбОшибках	 - Булево							 - Истина, если нужно сообщить об ошибках.
//  МассивНомеровСтрок	 - Массив							 - Строки инвентаризации.
//
Процедура ЗаполнитьНаОснованииИнвентаризацииОС(Объект, СообщатьОбОшибках, МассивНомеровСтрок = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = Документы.ИнвентаризацияОС.ДанныеЗаполненияНаОснованииИнвентаризации(Объект.ДокументОснование, "Перемещение", МассивНомеровСтрок);
	Если РезультатЗапроса.ТабличнаяЧасть = Неопределено Или РезультатЗапроса.ТабличнаяЧасть.Пустой() Тогда
		Если СообщатьОбОшибках Тогда
			ТекстОшибки = НСтр("ru='В документе %1 отсутствуют строки требующие создания перемещения'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, Объект.ДокументОснование);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Объект.ДокументОснование");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Реквизиты.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	
	Объект.ПодразделениеПолучатель = Выборка.Местонахождение;
	
	Выборка = РезультатЗапроса.ТабличнаяЧасть.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.ОС.Добавить(), Выборка);
	КонецЦикла;
	
	Если Объект.ОС.Количество() <> 0 Тогда
		МестонахождениеОС = ВнеоборотныеАктивы.МестонахождениеОС(Объект.ОС[0].ОсновноеСредство);
		Объект.Подразделение = МестонахождениеОС.Местонахождение;
	КонецЕсли; 
	
КонецПроцедуры
 
#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Вычисляет значения свойств при изменении значений реквизитов.
// Используется в форме документа и при проверке заполнения.
//
// Параметры:
//  Объект					 - ДокументОбъект.ПринятиеКУчетуОС2_4, ДанныеФормыСтруктура - Данные документа.
//  ВспомогательныеРеквизиты - Структура - Содержит значения вспомогательных реквизитов
//  ИзмененныеРеквизиты		 - Строка - Список измененных реквизитов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Содержит новые значения свойств реквизитов:
//					*ИмяРеквизита	- Строка	- Имя реквизита объекта
//					*ИмяЭлемента	- Строка	- Имя элемента формы
//					*Свойство	- Строка	- Свойство, например, "Видимость", "ТолькоПросмотр" 
//					*Значение	- Булево	- Значение свойства.
//
Функция ЗначенияСвойствЗависимыхРеквизитов(Объект, ИзмененныеРеквизиты = "") Экспорт

	СтруктураИзмененныхРеквизитов = Новый Структура(ИзмененныеРеквизиты);
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	ПараметрыРеквизитовОбъекта = Новый Массив;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") ИЛИ ОбновитьВсе Тогда
		
		ОперацияПередачи = Перечисления.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс;
		
		ОрганизацияВидима = Объект.ХозяйственнаяОперация <> ОперацияПередачи;
		ОрганизацияСдатчикВидима = Объект.ХозяйственнаяОперация = ОперацияПередачи;
		ОрганизацияПолучательВидима = Объект.ХозяйственнаяОперация = ОперацияПередачи;
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"Организация", "Организация", "ВидимостьЭлемента", ОрганизацияВидима, ПараметрыРеквизитовОбъекта);
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"Организация", "ОрганизацияСдатчик", "ВидимостьЭлемента", ОрганизацияСдатчикВидима, ПараметрыРеквизитовОбъекта);
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОрганизацияПолучатель", "ОрганизацияПолучатель", "Видимость", ОрганизацияПолучательВидима, ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("НачислениеАмортизации") ИЛИ ОбновитьВсе Тогда
		
		СтатьяРасходовДоступна = Объект.НачислениеАмортизации = 1;
		АналитикаРасходовДоступна = Объект.НачислениеАмортизации = 1;
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходов", "СтатьяРасходов", "ТолькоПросмотрБезОтметкиНезаполненного", НЕ СтатьяРасходовДоступна, ПараметрыРеквизитовОбъекта);
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаРасходов", "АналитикаРасходов", "ТолькоПросмотр", НЕ АналитикаРасходовДоступна, ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ИзменяетсяОтражениеРасходовПоНалогу") ИЛИ ОбновитьВсе Тогда
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходовНалог", "СтатьяРасходовНалог", "ТолькоПросмотрБезОтметкиНезаполненного", НЕ Объект.ИзменяетсяОтражениеРасходовПоНалогу, ПараметрыРеквизитовОбъекта);
		
		ВнеоборотныеАктивыКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаРасходовНалог", "АналитикаРасходовНалог", "ТолькоПросмотр", НЕ Объект.ИзменяетсяОтражениеРасходовПоНалогу, ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	Возврат ПараметрыРеквизитовОбъекта;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  ДокументСсылка			 - ДокументСсылка.ПеремещениеОС2_4 - Документ, для которого формируются движения
//  МенеджерВременныхТаблиц	 - МенеджерВременныхТаблиц - Содержит вспомогательные временные таблицы, которые могут использоваться для формирования движений.
//
Функция ТаблицыОтложенногоФормированияДвижений(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	// Регистры сведений для которых используется СрезПоследних
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаСчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса);
	//++ НЕ УТКА
	ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса);
	//-- НЕ УТКА
	
	ТаблицыДляДвижений = Новый Структура;
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ТаблицыДляДвижений, Истина);
	
	Возврат ТаблицыДляДвижений;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ПеремещениеОС2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие;
	
	ТекстСпособыОтраженияРасходовПоИмущественнымНалогам =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаПоследнее.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПоследнее.ВидНалога КАК ВидНалога,
	|	ТаблицаПоследнее.СтатьяРасходов КАК СтатьяРасходов,
	|	ТаблицаПоследнее.СпособОтраженияРасходовЗаданДокументом КАК СпособОтраженияРасходовЗаданДокументом,
	|	ТаблицаПоследнее.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ТаблицаПоследнее.АналитикаРасходов КАК АналитикаРасходов
	|ПОМЕСТИТЬ ВтСпособыОтраженияРасходовПоИмущественнымНалогам
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТаблицаОС.Ссылка КАК Ссылка,
	|			ТаблицаПериод.ОсновноеСредство КАК ОсновноеСредство,
	|			ТаблицаПериод.Организация КАК Организация,
	|			МАКСИМУМ(ТаблицаПериод.Период) КАК Период
	|		ИЗ
	|			Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам КАК ТаблицаПериод
	|				ПО (ТаблицаПериод.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|					И (ТаблицаПериод.Организация = ТаблицаОС.Ссылка.Организация)
	|					И (ТаблицаПериод.Период < ТаблицаОС.Ссылка.Дата)
	|					И (ТаблицаПериод.Регистратор <> ТаблицаОС.Ссылка)
	|		ГДЕ
	|			ТаблицаОС.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			И НЕ ТаблицаОС.Ссылка.ИзменяетсяОтражениеРасходовПоНалогу
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТаблицаОС.Ссылка,
	|			ТаблицаПериод.ОсновноеСредство,
	|			ТаблицаПериод.Организация) КАК ТаблицаПериод
	|		ПО (ТаблицаПериод.ОсновноеСредство = ТаблицаОС.ОсновноеСредство)
	|			И (ТаблицаПериод.Ссылка = ТаблицаОС.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам КАК ТаблицаПоследнее
	|		ПО (ТаблицаПоследнее.ОсновноеСредство = ТаблицаПериод.ОсновноеСредство)
	|			И (ТаблицаПоследнее.Организация = ТаблицаПериод.Организация)
	|			И (ТаблицаПоследнее.Период < ТаблицаПериод.Период)
	|ГДЕ
	|	ТаблицаОС.Ссылка.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И НЕ ТаблицаОС.Ссылка.ИзменяетсяОтражениеРасходовПоНалогу";
	
	ТекстыЗапросаВременныхТаблиц.Вставить("ВтСпособыОтраженияРасходовПоИмущественнымНалогам", ТекстСпособыОтраженияРасходовПоИмущественнымНалогам);
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаОС";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров,
										ТекстыЗапросаВременныхТаблиц);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСостоянияОСОрганизаций(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	//++ НЕ УТКА
	ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
	//-- НЕ УТКА
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Ссылка,
	|	ДанныеДокумента.Дата                           КАК Период,
	|	ДанныеДокумента.Номер                          КАК Номер,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.ОрганизацияПолучатель          КАК ОрганизацияПолучатель,
	|	ДанныеДокумента.МОЛПолучатель                  КАК МОЛПолучатель,
	|	ДанныеДокумента.АдресМестонахожденияПолучателя КАК АдресМестонахождения,
	|	ДанныеДокумента.Ответственный                  КАК Ответственный,
	|	ДанныеДокумента.Комментарий                    КАК Комментарий,
	|	ДанныеДокумента.Проведен                       КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления                КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете             КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете              КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.НачислениеАмортизации          КАК НачислятьАмортизацию,
	|	ДанныеДокумента.СтатьяРасходов                 КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов              КАК АналитикаРасходов,
	|	ДанныеДокумента.СтатьяРасходовНалог            КАК СтатьяРасходовНалог,
	|	ДанныеДокумента.АналитикаРасходовНалог         КАК АналитикаРасходовНалог,
	|	ДанныеДокумента.Подразделение                  КАК Подразделение,
	|	ДанныеДокумента.ПодразделениеПолучатель        КАК ПодразделениеПолучатель,
	|	ДанныеДокумента.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ДокументОснование              КАК ДокументОснование,
	|	ДанныеДокумента.ИзменяетсяОтражениеРасходовПоНалогу КАК ИзменяетсяОтражениеРасходовПоНалогу,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОсновныеСредства) КАК СтатьяАП_ОС,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК СтатьяАП_ПрибыльУбытки
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ХозяйственнаяОперацияСтоимостьОтправитель = ?(Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс,
														Перечисления.ХозяйственныеОперации.ПеремещениеСтоимостиОСвДругуюОрганизацию, 
														Перечисления.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияСтоимостьОтправитель", ХозяйственнаяОперацияСтоимостьОтправитель);
	
	ХозяйственнаяОперацияСтоимостьПолучатель = ?(Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс,
														Перечисления.ХозяйственныеОперации.ПеремещениеСтоимостиОСизДругойОрганизации, 
														Перечисления.ХозяйственныеОперации.ИзменениеПараметровСтоимостиОС);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияСтоимостьПолучатель", ХозяйственнаяОперацияСтоимостьПолучатель);
	
	ХозяйственнаяОперацияАмортизацияОтправитель = ?(Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс,
														Перечисления.ХозяйственныеОперации.ПеремещениеАмортизацииОСвДругуюОрганизацию, 
														Перечисления.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияАмортизацияОтправитель", ХозяйственнаяОперацияАмортизацияОтправитель);
	
	ХозяйственнаяОперацияАмортизацияПолучатель = ?(Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс,
														Перечисления.ХозяйственныеОперации.ПеремещениеАмортизацииОСвДругуюОрганизацию, 
														Перечисления.ХозяйственныеОперации.ИзменениеПараметровАмортизацииОС);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияАмортизацияПолучатель", ХозяйственнаяОперацияАмортизацияПолучатель);
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПеремещениеОС2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru='Перемещение ОС'"));
	
	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&Организация                     КАК Организация,
	|	&Подразделение                   КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ЛОЖЬ                             КАК ДополнительнаяЗапись,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                          КАК Ссылка,
	|	&Период                          КАК Дата,
	|	&ОрганизацияПолучатель           КАК Организация,
	|	&ПодразделениеПолучатель         КАК Подразделение,
	|	&ИдентификаторМетаданных         КАК ТипСсылки,
	|	&ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	&Проведен                        КАК Проведен,
	|	ИСТИНА                           КАК ДополнительнаяЗапись,
	|	&ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство       КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать КАК НомерПервичногоДокумента,
	|	ДанныеДокумента.Дата   КАК ДатаОтраженияВУчете
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.Номер,
	|	&ИдентификаторМетаданных,
	|	ДанныеДокумента.ОрганизацияПолучатель,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО,
	|	ДанныеДокумента.ПодразделениеПолучатель,
	|	ДанныеДокумента.Ответственный,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ИСТИНА,
	|	ДанныеДокумента.Дата,
	|	&НомерНаПечать,
	|	ДанныеДокумента.Дата
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	Текст = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&НомерНаПечать КАК НомерДокумента,
	|	0 КАК СуммаЗатратБУ,
	|	0 КАК СуммаЗатратНУ,
	|	0 КАК СуммаЗатратУСН
	|	
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаМестонахождениеОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                       КАК Регистратор,
	|	&Период                       КАК Период,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ                         КАК Организация,
	|	ТаблицаОС.ОсновноеСредство    КАК ОсновноеСредство,
	|	&МОЛПолучатель                КАК МОЛ,
	|	&ПодразделениеПолучатель      КАК Местонахождение,
	|	&АдресМестонахождения         КАК АдресМестонахождения,
	|	ТаблицаОС.ОтражатьВРеглУчете  КАК ОтражатьВРеглУчете,
	|	ТаблицаОС.ОтражатьВУпрУчете   КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"//Для организации отправителя запишем документ снятия с учета
	|ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	&Организация               КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	&Ссылка КАК ДокументСнятияСУчета,
	|	ПервоначальныеСведенияОС.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|		&Период,
	|		Регистратор <> &Ссылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПервоначальныеСведенияОС
	|	ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|		И ПервоначальныеСведенияОС.Организация = &Организация
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                    КАК Период,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ                      КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПорядокУчетаБУ КАК ПорядокУчетаБУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаНУ КАК ПорядокУчетаНУ,
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ КАК ПорядокУчетаУУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ КАК МетодНачисленияАмортизацииБУ,
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияОС.СтоимостьДо2002 КАК СтоимостьДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2002 КАК АмортизацияДо2002,
	|	ПервоначальныеСведенияОС.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимостьБУ,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ КАК ДатаВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюБУ КАК ДокументВводаВЭксплуатациюБУ,
	|	ПервоначальныеСведенияОС.ДокументВводаВЭксплуатациюУУ КАК ДокументВводаВЭксплуатациюУУ,
	|	ПервоначальныеСведенияОС.ДокументСнятияСУчета КАК ДокументСнятияСУчета,
	|	ПервоначальныеСведенияОС.ВариантПримененияЦелевогоФинансирования КАК ВариантПримененияЦелевогоФинансирования
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОС.СрезПоследних(
	|		&Период,
	|		Регистратор <> &Ссылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПервоначальныеСведенияОС
	|	ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
	|		И ПервоначальныеСведенияОС.Организация = &Организация";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСБУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ КАК НачислятьАмортизациюБУ,
	|	ЛОЖЬ КАК НачислятьАмортизациюНУ,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ПорядокУчета.СтатьяРасходовАмортизационнойПремии КАК СтатьяРасходовАмортизационнойПремии,
	|	ПорядокУчета.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
	|	ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчета.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ПорядокУчета.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ПорядокУчета.СпособОтраженияРасходовАмортизационнойПремии КАК СпособОтраженияРасходовАмортизационнойПремии,
	|	ПорядокУчета.НалогообложениеНДС КАК НалогообложениеНДС
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПорядокУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|			И (ПорядокУчета.Организация = &Организация)
	|ГДЕ
	|	&ОтражатьВРеглУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчета.Состояние КАК Состояние,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.НачислятьАмортизациюБУ
	|		КОГДА 1 ТОГДА ИСТИНА
	|		КОГДА 2 ТОГДА ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюБУ,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.НачислятьАмортизациюНУ
	|		КОГДА 1 ТОГДА ИСТИНА
	|		КОГДА 2 ТОГДА ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюНУ,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.СтатьяРасходов
	|		КОГДА 1 ТОГДА &СтатьяРасходов
	|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.АналитикаРасходов
	|		КОГДА 1 ТОГДА &АналитикаРасходов
	|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.СтатьяРасходовАмортизационнойПремии
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходовАмортизационнойПремии,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.АналитикаРасходовАмортизационнойПремии
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходовАмортизационнойПремии,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.ОрганизацияПолучательРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ОрганизацияПолучательРасходов,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.СпособОтраженияРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СпособОтраженияРасходов,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.СпособОтраженияРасходовАмортизационнойПремии
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СпособОтраженияРасходовАмортизационнойПремии,
	|	ПорядокУчета.НалогообложениеНДС КАК НалогообложениеНДС
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПорядокУчета
	|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|			И (ПорядокУчета.Организация = &Организация)
	|ГДЕ
	|	&ОтражатьВРеглУчете
	|	И (&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ИЛИ &НачислятьАмортизацию <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокУчетаОСУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ КАК НачислятьАмортизациюУУ,
	|	ЛОЖЬ КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ПорядокУчета.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчета.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПорядокУчета
	|	ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ВЫБОР КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|		ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.НачислятьАмортизациюУУ
	|		КОГДА 1 ТОГДА ИСТИНА
	|		КОГДА 2 ТОГДА ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизациюУУ,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.СтатьяРасходов
	|		КОГДА 1 ТОГДА &СтатьяРасходов
	|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР &НачислятьАмортизацию
	|		КОГДА 0 ТОГДА ПорядокУчета.АналитикаРасходов
	|		КОГДА 1 ТОГДА &АналитикаРасходов
	|		КОГДА 2 ТОГДА НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ПорядокУчета.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	ПорядокУчета.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчета.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПорядокУчета
	|	ПО ТаблицаОС.ОсновноеСредство = ПорядокУчета.ОсновноеСредство
	|		И ПорядокУчета.Организация = &Организация
	|ГДЕ
	|	&ОтражатьВУпрУчете
	|	И (&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ИЛИ &НачислятьАмортизацию <> 0)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииОСБУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&ОрганизацияПолучатель КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизации.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользованияБУ,
	|	ПараметрыАмортизации.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользованияНУ,
	|	ПараметрыАмортизации.КоэффициентАмортизацииБУ КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизации.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ПараметрыАмортизации.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизации.ПРДляВычисленияАмортизации КАК ПРДляВычисленияАмортизации,
	|	ПараметрыАмортизации.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизации.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизации.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							ВтТаблицаОС КАК Т)) КАК ПараметрыАмортизации
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыАмортизации.ОсновноеСредство
	|			И (ПараметрыАмортизации.Организация = &Организация)
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И &ОтражатьВРеглУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииОСУУ(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПараметрыАмортизацииОСУУ";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&ОрганизацияПолучатель КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизации.СрокИспользования КАК СрокИспользования,
	|	ПараметрыАмортизации.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизации.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ПараметрыАмортизации.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	ПараметрыАмортизации.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизации.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
	|	ПараметрыАмортизации.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизации.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСУУ.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							ВтТаблицаОС КАК Т)) КАК ПараметрыАмортизации
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыАмортизации.ОсновноеСредство
	|			И (ПараметрыАмортизации.Организация = &Организация)
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И &ОтражатьВУпрУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Регистры)

	ИмяРегистра = "СчетаБухгалтерскогоУчетаОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	&ОрганизацияПолучатель                  КАК Организация,
	|	ТаблицаОС.ОсновноеСредство              КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОС.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							ВтТаблицаОС КАК Т)) КАК ПорядокУчетаОС
	|		ПО ТаблицаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И &ОтражатьВРеглУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Ссылка                                     КАК Регистратор,
	|	&Период                                     КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьОтправитель  КАК ХозяйственнаяОперация,
	|	&Организация                                КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	&Подразделение                              КАК Подразделение,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ТаблицаОС.Арендатор                         КАК Арендатор,
	|	СтоимостьОС.Стоимость                       КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл                   КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                     КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                     КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                     КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                     КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                   КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                   КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                   КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремия           КАК АмортизационнаяПремия,
	|	СтоимостьОС.ЗалоговаяСтоимость              КАК ЗалоговаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимость        КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.РезервПереоценкиСтоимости       КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл   КАК РезервПереоценкиСтоимостиРегл,
	|	&ОрганизацияПолучатель                      КАК КорОрганизация,
	|	&ПодразделениеПолучатель                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	Неопределено                                КАК КорАналитикаРасходов,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                         КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                          КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Ссылка                                     КАК Регистратор,
	|	&Период                                     КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьПолучатель   КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель                      КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                  КАК ОсновноеСредство,
	|	&ПодразделениеПолучатель                    КАК Подразделение,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности           КАК НаправлениеДеятельности,
	|	ТаблицаОС.Арендатор                         КАК Арендатор,
	|	СтоимостьОС.Стоимость                       КАК Стоимость,
	|	СтоимостьОС.СтоимостьРегл                   КАК СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ                     КАК СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР                     КАК СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР                     КАК СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ                     КАК СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ                   КАК СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ                   КАК СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ                   КАК СтоимостьВРЦФ,
	|	СтоимостьОС.АмортизационнаяПремия           КАК АмортизационнаяПремия,
	|	СтоимостьОС.ЗалоговаяСтоимость              КАК ЗалоговаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимость        КАК ПредварительнаяСтоимость,
	|	СтоимостьОС.РезервПереоценкиСтоимости       КАК РезервПереоценкиСтоимости,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл   КАК РезервПереоценкиСтоимостиРегл,
	|	&Организация                                КАК КорОрганизация,
	|	&Подразделение                              КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                КАК КорСтатьяРасходов,
	|	Неопределено                                КАК КорАналитикаРасходов,
	|	ТаблицаОС.ГруппаФинансовогоУчета            КАК КорГруппаФинансовогоУчета,
	|	&ОтражатьВРеглУчете                         КАК ОтражатьВРеглУчете,
	|	&ОтражатьВУпрУчете                          КАК ОтражатьВУпрУчете
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияОС(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияОС";
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	&Период                                         КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияОтправитель    КАК ХозяйственнаяОперация,
	|	&Организация                                    КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	&Подразделение                                  КАК Подразделение,
	|	ТаблицаОС.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК Контрагент,
	|	АмортизацияОС.Амортизация                       КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл                   КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ                     КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР                     КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР                     КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ                     КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ                   КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ                   КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ                   КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|	&ОрганизацияПолучатель                          КАК КорОрганизация,
	|	&ПодразделениеПолучатель                        КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности               КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорАналитикаРасходов,
	|	ТаблицаОС.ГруппаФинансовогоУчета                КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НачислятьИзнос                    КАК НачислятьИзнос
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	&Период                                         КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияПолучатель     КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель                          КАК Организация,
	|	ТаблицаОС.ОсновноеСредство                      КАК ОсновноеСредство,
	|	&ПодразделениеПолучатель                        КАК Подразделение,
	|	ТаблицаОС.ГруппаФинансовогоУчета                КАК ГруппаФинансовогоУчета,
	|	ТаблицаОС.НаправлениеДеятельности               КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК Контрагент,
	|	АмортизацияОС.Амортизация                       КАК Амортизация,
	|	АмортизацияОС.АмортизацияРегл                   КАК АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ                     КАК АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР                     КАК АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР                     КАК АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ                     КАК АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ                   КАК АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ                   КАК АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ                   КАК АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации       КАК РезервПереоценкиАмортизации,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл   КАК РезервПереоценкиАмортизацииРегл,
	|	&Организация                                    КАК КорОрганизация,
	|	&Подразделение                                  КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности               КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                    КАК КорАналитикаРасходов,
	|	ТаблицаОС.ГруппаФинансовогоУчета                КАК КорГруппаФинансовогоУчета,
	|	АмортизацияОС.НачислятьИзнос                    КАК НачислятьИзнос
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"// Перемещение амортизации для отправителя
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияОтправитель КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&ПодразделениеПолучатель          КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&ОрганизацияПолучатель            КАК КорОрганизация,
	|
	|	АмортизацияОС.Амортизация         КАК Сумма,
	|	АмортизацияОС.Амортизация         КАК СуммаУпр,
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение амортизации для получателя
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияАмортизацияПолучатель КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель            КАК Организация,
	|
	|	&ПодразделениеПолучатель          КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	АмортизацияОС.Амортизация         КАК Сумма,
	|	АмортизацияОС.Амортизация         КАК СуммаУпр,
	|	АмортизацияОС.АмортизацияРегл + АмортизацияОС.АмортизацияЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияОС
	|		ПО АмортизацияОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И АмортизацияОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости для отправителя
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьОтправитель КАК ХозяйственнаяОперация,
	|	&Организация                      КАК Организация,
	|
	|	&Подразделение                    КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&ПодразделениеПолучатель          КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&ОрганизацияПолучатель            КАК КорОрганизация,
	|
	|	СтоимостьОС.Стоимость             КАК Сумма,
	|	СтоимостьОС.Стоимость             КАК СуммаУпр,
	|	СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Перемещение стоимости для получателя
	|ВЫБРАТЬ
	|	&Период                           КАК Период,
	|	&ХозяйственнаяОперацияСтоимостьПолучатель КАК ХозяйственнаяОперация,
	|	&ОрганизацияПолучатель            КАК Организация,
	|
	|	&ПодразделениеПолучатель          КАК Подразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК Статья,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК АналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК АналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|
	|	&Подразделение                    КАК КорПодразделение,
	|	ТаблицаОС.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	&СтатьяАП_ОС                      КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО                      КАК КорАналитикаРасходов,
	|	ТаблицаОС.ОсновноеСредство        КАК КорАналитикаАктивовПассивов,
	|	ТаблицаОС.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
	|
	|	&Организация                      КАК КорОрганизация,
	|
	|	СтоимостьОС.Стоимость             КАК Сумма,
	|	СтоимостьОС.Стоимость             КАК СуммаУпр,
	|	СтоимостьОС.СтоимостьРегл + СтоимостьОС.СтоимостьЦФ КАК СуммаРегл
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьОС
	|		ПО СтоимостьОС.ОбъектУчета = ТаблицаОС.ОсновноеСредство
	|			И СтоимостьОС.Ссылка = &Ссылка
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСостоянияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СостоянияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Период КАК ДатаСостояния,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.СнятоСУчета) КАК Состояние
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВРеглУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	&ОрганизацияПолучатель КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Период КАК ДатаСостояния,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ОтражатьВРеглУчете
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВременнаяТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество) КАК ВидНалога,
	|	&СтатьяРасходовНалог КАК СтатьяРасходов,
	|	&АналитикаРасходовНалог КАК АналитикаРасходов,
	|	ЛОЖЬ КАК СпособОтраженияРасходовЗаданДокументом,
	|	НЕОПРЕДЕЛЕНО КАК СпособОтраженияРасходов
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ИзменяетсяОтражениеРасходовПоНалогу
	|	И НЕ ТаблицаОС.ОсновноеСредство.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ,
	|	ТаблицаОС.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог),
	|	&СтатьяРасходовНалог,
	|	&АналитикаРасходовНалог,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ИзменяетсяОтражениеРасходовПоНалогу
	|	И ТаблицаОС.ОсновноеСредство.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ТранспортныеСредства), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МашиныИОборудование))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА &ОрганизацияПолучатель
	|		ИНАЧЕ &Организация
	|	КОНЕЦ,
	|	ТаблицаОС.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог),
	|	&СтатьяРасходовНалог,
	|	&АналитикаРасходовНалог,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ИзменяетсяОтражениеРасходовПоНалогу
	|	И ТаблицаОС.ОсновноеСредство.ГруппаОС = ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&ОрганизацияПолучатель,
	|	ТаблицаОС.ОсновноеСредство,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.ВидНалога,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.СтатьяРасходов,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.АналитикаРасходов,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.СпособОтраженияРасходовЗаданДокументом,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.СпособОтраженияРасходов
	|ИЗ
	|	Документ.ПеремещениеОС2_4.ОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОтраженияРасходовПоИмущественнымНалогам КАК СпособыОтраженияРасходовПоИмущественнымНалогам
	|		ПО ТаблицаОС.ОсновноеСредство = СпособыОтраженияРасходовПоИмущественнымНалогам.ОсновноеСредство
	|			И ТаблицаОС.Ссылка = СпособыОтраженияРасходовПоИмущественнымНалогам.Ссылка
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|	И НЕ &ИзменяетсяОтражениеРасходовПоНалогу";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Ссылка                      КАК Документ,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Ссылка                      КАК Документ,
	|	&ОрганизацияПолучатель       КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции
//++ НЕ УТКА
Функция ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ОтражениеДокументовВМеждународномУчете";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Документ,
	|	УчетнаяПолитикаМУ.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете) КАК Статус,
	|	УчетнаяПолитикаМУ.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаМУ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период,
	|	&Ссылка,
	|	УчетнаяПолитикаМУ.Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ),
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете),
	|	УчетнаяПолитикаМУ.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета.СрезПоследних(&Период, Организация = &ОрганизацияПолучатель) КАК УчетнаяПолитикаМУ
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТКА

Функция ПолноеИмяОбъекта()

	Возврат "Документ.ПеремещениеОС2_4";

КонецФункции

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                              КАК Регистратор,
	|	&Период                                              КАК Период,
	|	ТаблицаОС.ОсновноеСредство                           КАК ОсновноеСредство,
	|	&ОрганизацияПолучатель                               КАК Организация,
	|	ПервоначальныеСведения.ИнвентарныйНомер              КАК ИнвентарныйНомер,
	|	ПервоначальныеСведения.СпособПоступления             КАК СпособПоступления,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимость       КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведения.СпособНачисленияАмортизации   КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведения.ПоказательНаработки           КАК ПоказательНаработки,
	|	ПервоначальныеСведения.ПорядокПогашенияСтоимости     КАК ПорядокПогашенияСтоимости,
	|	ПервоначальныеСведения.ГруппаОС                      КАК ГруппаОС,
	|	ПервоначальныеСведения.КодПоОКОФ                     КАК КодПоОКОФ,
	|	ПервоначальныеСведения.ШифрПоЕНАОФ                   КАК ШифрПоЕНАОФ,
	|	ПервоначальныеСведения.АмортизационнаяГруппа         КАК АмортизационнаяГруппа
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В (ВЫБРАТЬ Т.ОсновноеСредство ИЗ ВтТаблицаОС КАК Т)
	|		) КАК ПервоначальныеСведения
	|		ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВтТаблицаОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка                                                            КАК Регистратор,
	|	&Период                                                            КАК Период,
	|	ТаблицаОС.ОсновноеСредство                                         КАК ОсновноеСредство,
	|	ПервоначальныеСведения.АмортизацияДо2002                           КАК АмортизацияДо2002,
	|	ПервоначальныеСведения.АмортизацияДо2009                           КАК АмортизацияДо2009,
	|	ПервоначальныеСведения.МетодНачисленияАмортизации                  КАК МетодНачисленияАмортизации,
	|	&ОрганизацияПолучатель                                             КАК Организация,
	|	ПервоначальныеСведения.ПервоначальнаяСтоимостьНУ                   КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведения.ПорядокВключенияСтоимостиВСоставРасходов    КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ПервоначальныеСведения.СтоимостьДо2002                             КАК СтоимостьДо2002,
	|	ПервоначальныеСведения.ФактическийСрокИспользованияДо2009          КАК ФактическийСрокИспользованияДо2009
	|ИЗ
	|	ВтТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							Т.ОсновноеСредство
	|						ИЗ
	|							ВтТаблицаОС КАК Т)) КАК ПервоначальныеСведения
	|		ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса)

	ИмяТаблицы = "ВтСпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСписокОС(ТекстыЗапроса, ПолноеИмяОбъекта());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Ссылка,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.ОсновноеСредство КАК ОсновноеСредство,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.ВидНалога КАК ВидНалога,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.СпособОтраженияРасходовЗаданДокументом КАК СпособОтраженияРасходовЗаданДокументом,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СпособыОтраженияРасходовПоИмущественнымНалогам.АналитикаРасходов КАК АналитикаРасходов
	|ПОМЕСТИТЬ ВтСпособыОтраженияРасходовПоИмущественнымНалогам
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам.СрезПоследних(
	|			&Период,
	|			Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.ОсновноеСредство
	|					ИЗ
	|						втСписокОС КАК Т)) КАК СпособыОтраженияРасходовПоИмущественнымНалогам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ОсновноеСредство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыСозданияВременныхТаблиц = Новый Массив;
	ТекстыСозданияВременныхТаблиц.Добавить(ТекстЗапросаВТАмортизацияОС());
	ТекстыСозданияВременныхТаблиц.Добавить(ТекстЗапросаВТСтоимостьОС());
	ТекстыСозданияВременныхТаблиц.Добавить(Символы.ПС);
	
	Возврат СтрСоединить(ТекстыСозданияВременныхТаблиц, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

Функция ТекстЗапросаВТАмортизацияОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица втАмортизацияОС
	|"+
	"ВЫБРАТЬ
	|	АмортизацияОС.ВидДвижения,
	|	АмортизацияОС.Период,
	|	АмортизацияОС.Регистратор,
	|	АмортизацияОС.Организация,
	|	АмортизацияОС.ОсновноеСредство,
	|	АмортизацияОС.Подразделение КАК Подразделение,
	|	АмортизацияОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	АмортизацияОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	АмортизацияОС.НомерСтроки,
	|	АмортизацияОС.Амортизация,
	|	АмортизацияОС.АмортизацияРегл,
	|	АмортизацияОС.АмортизацияНУ,
	|	АмортизацияОС.АмортизацияПР,
	|	АмортизацияОС.АмортизацияВР,
	|	АмортизацияОС.АмортизацияЦФ,
	|	АмортизацияОС.АмортизацияНУЦФ,
	|	АмортизацияОС.АмортизацияПРЦФ,
	|	АмортизацияОС.АмортизацияВРЦФ,
	|	АмортизацияОС.РезервПереоценкиАмортизации,
	|	АмортизацияОС.ХозяйственнаяОперация,
	|
	|	АмортизацияОС.КорОрганизация,
	|	АмортизацияОС.КорПодразделение,
	|	АмортизацияОС.КорСтатьяРасходов,
	|	АмортизацияОС.КорАналитикаРасходов,
	|	АмортизацияОС.НачислятьИзнос
	|
	|ПОМЕСТИТЬ втАмортизацияОС
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|		ПО ДокументыКОтражению.Ссылка = АмортизацияОС.Регистратор";
	
КонецФункции

Функция ТекстЗапросаВТСтоимостьОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица втСтоимостьОС
	|"+
	"ВЫБРАТЬ
	|	СтоимостьОС.ВидДвижения,
	|	СтоимостьОС.Период,
	|	СтоимостьОС.Регистратор,
	|	СтоимостьОС.ОсновноеСредство,
	|	СтоимостьОС.Организация,
	|	СтоимостьОС.Подразделение КАК Подразделение,
	|	СтоимостьОС.Арендатор КАК Арендатор,
	|	СтоимостьОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	СтоимостьОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СтоимостьОС.НомерСтроки,
	|	СтоимостьОС.Стоимость,
	|	СтоимостьОС.СтоимостьРегл,
	|	СтоимостьОС.СтоимостьНУ,
	|	СтоимостьОС.СтоимостьПР,
	|	СтоимостьОС.СтоимостьВР,
	|	СтоимостьОС.СтоимостьЦФ,
	|	СтоимостьОС.СтоимостьНУЦФ,
	|	СтоимостьОС.СтоимостьПРЦФ,
	|	СтоимостьОС.СтоимостьВРЦФ,
	|	СтоимостьОС.ЗалоговаяСтоимость,
	|	СтоимостьОС.ПредварительнаяСтоимость,
	|	СтоимостьОС.РезервПереоценкиСтоимости,
	|	СтоимостьОС.АмортизационнаяПремия,
	|	СтоимостьОС.ХозяйственнаяОперация,
	|
	|	СтоимостьОС.КорОрганизация,
	|	СтоимостьОС.КорПодразделение,
	|	СтоимостьОС.КорСтатьяРасходов,
	|	СтоимостьОС.КорАналитикаРасходов
	|
	|ПОМЕСТИТЬ втСтоимостьОС
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|		ПО ДокументыКОтражению.Ссылка = СтоимостьОС.Регистратор";
	
КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПеремещениеСтоимостиОС(СписокЗапросов);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПеремещениеАмортизацииОС(СписокЗапросов);
	ПередачаОСИсходящиеДвижения(СписокЗапросов);
	ПередачаОСИсходящиеДвиженияЦФ(СписокЗапросов);
	ПередачаОСВходящиеДвижения(СписокЗапросов);
	ПередачаОСВходящиеДвиженияЦФ(СписокЗапросов);
	ПередачаАмортизацииИсходящиеДвижения(СписокЗапросов);
	ПередачаАмортизацииИсходящиеДвиженияЦФ(СписокЗапросов);
	ПередачаАмортизацииВходящиеДвижения(СписокЗапросов);
	ПередачаАмортизацииВходящиеДвиженияЦФ(СписокЗапросов);
	ПеремещениеКВ(СписокЗапросов);
	
	Возврат СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

Процедура ПередачаОСИсходящиеДвижения(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача ОС исходящие проводки (Дт 79.01:: Кт СчетУчета)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетДт,
	|	Строки.КорОрганизация КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	""Передача ОС исходящие проводки"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|	
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.Организация <> Строки.КорОрганизация
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаОСИсходящиеДвиженияЦФ(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача целевых средств в стоимости ОС исходящие проводки (Дт 79.01:: Кт СчетУчета)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделение КАК ПодразделениеДт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетДт,
	|	Строки.КорОрганизация КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУЦФ КАК СуммаНУКт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРКт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРКт,
	|	""Передача ОС исходящие проводки"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|	
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.СтоимостьЦФ <> 0
	|	И Строки.Организация <> Строки.КорОрганизация
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаОСВходящиеДвижения(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача ОС входящие проводки (Дт СчетУчета:: Кт 79.01)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетКт,
	|	Строки.КорОрганизация КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУ КАК СуммаНУКт,
	|	Строки.СтоимостьПР КАК СуммаПРКт,
	|	Строки.СтоимостьВР КАК СуммаВРКт,
	|	""Передача ОС входящие проводки"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.Организация <> Строки.КорОрганизация
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаОСВходящиеДвиженияЦФ(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача целевых средств в стоиомости ОС входящие проводки (Дт СчетУчета:: Кт 79.01)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА_ЦФ) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетКт,
	|	Строки.КорОрганизация КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СтоимостьНУЦФ КАК СуммаНУКт,
	|	Строки.СтоимостьПРЦФ КАК СуммаПРКт,
	|	Строки.СтоимостьВРЦФ КАК СуммаВРКт,
	|	""Передача ОС входящие проводки"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.СтоимостьЦФ <> 0
	|	И Строки.Организация <> Строки.КорОрганизация
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаАмортизацииИсходящиеДвижения(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача амортизации ОС исходящие проводки (Дт СчетНачисленияАмортизации :: Кт 79.01)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияРегл 
	|	КОНЕЦ КАК Сумма,
	|	Строки.Амортизация КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетКт,
	|	Строки.КорОрганизация КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРКт,
	|	""Передача амортизации ОС в обособленное подразделение"" КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|	
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.Организация <> Строки.КорОрганизация
	|	И (Строки.Амортизация <> 0
	|		ИЛИ НЕ Строки.НачислятьИзнос 
	|			И (Строки.АмортизацияРегл <> 0
	|				ИЛИ Строки.АмортизацияНУ <> 0
	|				ИЛИ Строки.АмортизацияПР <> 0
	|				ИЛИ Строки.АмортизацияВР <> 0))
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаАмортизацииИсходящиеДвиженияЦФ(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Передача целевых средств в амортизации ОС исходящие проводки (Дт СчетНачисленияАмортизации :: Кт 79.01)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУДт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРДт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетКт,
	|	Строки.КорОрганизация КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРКт,
	|	""Передача амортизации ОС в обособленное подразделение"" КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|	
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.Организация <> Строки.КорОрганизация
	|	И Строки.АмортизацияЦФ <> 0
	|	И НЕ Строки.НачислятьИзнос
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаАмортизацииВходящиеДвижения(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение амортизации ОС входящие проводки (Дт 79.01 :: Кт СчетНачисленияАмортизации)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияРегл 
	|	КОНЕЦ КАК Сумма,
	|	Строки.Амортизация КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетДт,
	|	Строки.КорОрганизация КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРДт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияНУ 
	|	КОНЕЦ КАК СуммаНУКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияПР 
	|	КОНЕЦ КАК СуммаПРКт,
	|	ВЫБОР КОГДА Строки.НачислятьИзнос ТОГДА 0 
	|		ИНАЧЕ Строки.АмортизацияВР 
	|	КОНЕЦ КАК СуммаВРКт,
	|	""Принятие амортизации ОС в обособленном подразделении"" КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.Организация <> Строки.КорОрганизация
	|	И (Строки.Амортизация <> 0
	|		ИЛИ НЕ Строки.НачислятьИзнос 
	|			И (Строки.АмортизацияРегл <> 0
	|				ИЛИ Строки.АмортизацияНУ <> 0
	|				ИЛИ Строки.АмортизацияПР <> 0
	|				ИЛИ Строки.АмортизацияВР <> 0))
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПередачаАмортизацииВходящиеДвиженияЦФ(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение целевых средств в амортизации ОС входящие проводки (Дт 79.01 :: Кт СчетНачисленияАмортизации)
	|ВЫБРАТЬ
	|
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.АмортизацияЦФ КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыПоВыделенномуИмуществу) КАК СчетДт,
	|	Строки.КорОрганизация КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУДт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРДт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АмортизацияВНА_ЦФ) КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.АмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.АмортизацияВРЦФ КАК СуммаВРКт,
	|	""Принятие амортизации ОС в обособленном подразделении"" КАК Содержание
	|ИЗ
	|	втАмортизацияОС КАК Строки
	|	
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.Организация <> Строки.КорОрганизация
	|	И Строки.АмортизацияЦФ <> 0
	|	И НЕ Строки.НачислятьИзнос
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ПеремещениеКВ(СписокЗапросов)
	
	ТекстЗапроса = 
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение остатка по счету КВ (Дт КВ:: Кт )
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Регистратор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.АмортизационнаяПремия КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Перемещение амортизационной премии"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Строки.Подразделение <> Строки.КорПодразделение
	|	И Строки.АмортизационнаяПремия <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение остатка по счету КВ (Дт :: Кт КВ)
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаУУ,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	Строки.Регистратор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.АмортизационнаяПремия КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Перемещение амортизационной премии"" КАК Содержание
	|ИЗ
	|	втСтоимостьОС КАК Строки
	|
	|ГДЕ
	|	Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Строки.Подразделение <> Строки.КорПодразделение
	|	И Строки.АмортизационнаяПремия <> 0
	|";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// ОС-2 (Накладная на внутреннее перемещение ОС)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОС2";
	КомандаПечати.Представление = НСтр("ru = 'ОС-2 (Накладная на перемещение ОС)'");

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОС2") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, "ОС2",
			НСтр("ru = 'ОС-2 (Накладная на внутреннее перемещение ОС)'"),
			ПечатьОС2(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьОС2(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПеремещениеОС_ОС2";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПеремещениеОС.ПФ_MXL_ОС2");
	
	ОбластьШапка1         = Макет.ПолучитьОбласть("Шапка1");
	ОбластьСтрока         = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвалСтраницы = Макет.ПолучитьОбласть("ПодвалСтраницы");
	ОбластьПодвал         = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапка2         = Макет.ПолучитьОбласть("Шапка2");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Дата КАК Дата,
	|	Документ.Организация КАК Организация,
	|	ТабличнаяЧасть.НомерСтроки КАК НомерСтроки,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ втОбъектыДокументов
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Документ.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Дата";
	
	Запрос.Выполнить();
	
	ВнеоборотныеАктивы.ПолучитьСведенияОбОсновныхСредствахВДокументах(Запрос.МенеджерВременныхТаблиц, МассивОбъектов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Организация КАК Организация,
	|	Документ.Организация.КодПоОКПО КАК КодПоОКПО,
	|	Документ.Подразделение КАК ПодразделениеОрганизации,
	|	Документ.Подразделение.Наименование КАК ПодразделениеОрганизацииНаименование,
	|	Документ.ПодразделениеПолучатель КАК ПодразделениеПолучатель,
	|	Документ.ПодразделениеПолучатель.Наименование КАК ПодразделениеПолучательНаименование,
	|	Документ.Подразделение КАК ПодрСдатчик,
	|	Документ.ПодразделениеПолучатель КАК ПодрПолучатель,
	|	Документ.Номер КАК Номер,
	|	Документ.Дата КАК Дата,
	|	Документ.МОЛ КАК МОЛСдатчик,
	|	Документ.МОЛПолучатель КАК МОЛПолучатель,
	|	втОбъектыДокументов.НомерСтроки КАК НомерСтроки,
	|	втОбъектыДокументов.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА втОбъектыДокументов.ОсновноеСредство.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА втОбъектыДокументов.ОсновноеСредство.Наименование
	|		ИНАЧЕ втОбъектыДокументов.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК НаименованиеОС,
	|	втОбъектыДокументов.ОсновноеСредство.ДатаВыпуска КАК ГодВыпуска,
	|	втОбъектыДокументов.ОсновноеСредство.ИнвентарныйНомер КАК ИнвНомер,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СтоимостьПриход, 0) КАК СуммаПеремещения,
	|	Константы.ВалютаРегламентированногоУчета.Наименование КАК Валюта,
	|	ЕСТЬNULL(втСведенияОбъектовДокументов.СведенияАктуальны, ЛОЖЬ) КАК СведенияАктуальны
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбъектыДокументов КАК втОбъектыДокументов
	|		ПО (втОбъектыДокументов.Ссылка = Документ.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСведенияОбъектовДокументов КАК втСведенияОбъектовДокументов
	|		ПО (втОбъектыДокументов.Ссылка = втСведенияОбъектовДокументов.Ссылка)
	|			И (втОбъектыДокументов.ОсновноеСредство = втСведенияОбъектовДокументов.ОсновноеСредство)
	|ГДЕ
	|	Документ.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ 
	|	МИНИМУМ(СведенияАктуальны)
	|ПО
	|	Ссылка";
	
	ПервыйДокумент = Истина;
	
	СведенияАктуальны = Истина;
	
	Шапка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Ссылка");
	Пока Шапка.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьШапка1.Параметры.ПодрСдатчик = Шапка.ПодразделениеОрганизацииНаименование;
		ОбластьШапка1.Параметры.ПодрПолучатель = Шапка.ПодразделениеПолучательНаименование;
		ОбластьШапка1.Параметры.Заполнить(Шапка);
		ОбластьШапка1.Параметры.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер);
		
		СведенияОбОрганизации    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Шапка.Организация, Шапка.Дата);
		ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм");
		
		ОбластьШапка1.Параметры.Организация = ПредставлениеОрганизации;
		
		ТабличныйДокумент.Вывести(ОбластьШапка1);

		ИтогСумма = 0;
		
		ВыборкаПоОС = Шапка.Выбрать();
		Пока ВыборкаПоОС.Следующий() Цикл
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаПоОС);
			
			ОбластьСтрока.Параметры.НаименованиеОС = ВыборкаПоОС.НаименованиеОС;
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(ОбластьСтрока);
			СтрокаСПодвалом.Добавить(ОбластьПодвал);
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда
				ТабличныйДокумент.Вывести(ОбластьПодвалСтраницы);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапка2);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			ИтогСумма = ИтогСумма + ВыборкаПоОС.СуммаПеремещения;
			
		КонецЦикла;

		ОбластьПодвал.Параметры.ИтогСумма = ИтогСумма;
		
		Если ЗначениеЗаполнено(Шапка.МОЛСдатчик) Тогда
			
			ДанныеМОЛ = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛиц(Шапка.Организация, Шапка.МОЛСдатчик, Шапка.Дата);
			
			ОбластьПодвал.Параметры.СдатчикРасшифровкаПодписи = ДанныеМОЛ[0].Представление;
			
			СписокСотрудников = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(
				Шапка.МОЛСдатчик, 
				Истина, Шапка.Организация, 
				Шапка.Дата);
				
			СписокСотрудников = СписокСотрудников.ВыгрузитьКолонку("Сотрудник");
			
			ДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(
				Истина,
				СписокСотрудников,
				"Должность, ТабельныйНомер",
				Шапка.Дата);
			
			Если ДанныеСотрудника.Количество() > 0 Тогда
				ОбластьПодвал.Параметры.СдатчикДолжность      = ДанныеСотрудника[0].Должность;
				ОбластьПодвал.Параметры.СдатчикТабельныйНомер = ДанныеСотрудника[0].ТабельныйНомер;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Шапка.МОЛПолучатель) Тогда
			
			ДанныеМОЛ = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛиц(Шапка.Организация, Шапка.МОЛПолучатель, Шапка.Дата);
			
			ОбластьПодвал.Параметры.ПолучательРасшифровкаПодписи = ДанныеМОЛ[0].Представление;
			
			СписокСотрудников = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(
				Шапка.МОЛПолучатель, 
				Истина, 
				Шапка.Организация, 
				Шапка.Дата);
			
			СписокСотрудников = СписокСотрудников.ВыгрузитьКолонку("Сотрудник");
			
			ДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(
				Истина,
				СписокСотрудников,
				"Должность, ТабельныйНомер",
				Шапка.Дата);
			
			Если ДанныеСотрудника.Количество() > 0 Тогда
				ОбластьПодвал.Параметры.ПолучательДолжность      = ДанныеСотрудника[0].Должность;
				ОбластьПодвал.Параметры.ПолучательТабельныйНомер = ДанныеСотрудника[0].ТабельныйНомер;
			КонецЕсли;
			
		КонецЕсли;
		
		ПодразделениеОтветственныхЛиц = Шапка.ПодразделениеОрганизации;
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Шапка.Организация, Шапка.Дата, ПодразделениеОтветственныхЛиц);
		
		ОбластьПодвал.Параметры.ГлавБух = ОтветственныеЛица.ГлавныйБухгалтерПредставление;
		
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
		Если НЕ Шапка.СведенияАктуальны Тогда
			СведенияАктуальны = Ложь;
		КонецЕсли; 
		
	КонецЦикла;
	
	ВнеоборотныеАктивы.ДобавитьПредупреждениеЕслиСведенияНеАктуальны(СведенияАктуальны, ТабличныйДокумент);
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Документ.ПеремещениеОС2_4";
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Документ.ПеремещениеОС2_4");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
 			Блокировка.Заблокировать();
 			
			ДанныеОбъекта = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(Выборка.Ссылка, Выборка.ВерсияДанных, Параметры.Очередь);
			Если ДанныеОбъекта = Неопределено Тогда
				ЗафиксироватьТранзакцию();
 				Продолжить;
 			КонецЕсли;
			
			Если ДанныеОбъекта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОСпоИнвентаризации Тогда
				ДанныеОбъекта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеОС;
				ДанныеОбъекта.ДокументНаОсновании = Истина;
			КонецЕсли;
			
			Если ДанныеОбъекта.Модифицированность() Тогда
	 			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
			Иначе
	 			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДанныеОбъекта);
			КонецЕсли; 
			
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеремещениеОС2_4.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК ПеремещениеОС2_4
	|ГДЕ
	|	ПеремещениеОС2_4.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеОСпоИнвентаризации)";
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли