#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Команды

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = СтрШаблон(НСтр("ru = 'Амортизация ОС %1 за %2'"), Данные.Номер, Формат(Данные.Дата, "ДФ='ММММ гггг'"));
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет расчет амортизации в заданном месяце и записывает его с помощью проведения документа "Амортизация ОС".
//  
// Параметры:
//  ИсходныеПараметрыРасчета - Структура - Содержит параметры расчета (см. НовыеПараметрыРасчетаАмортизации()).
//
Процедура РассчитатьАмортизацию(ИсходныеПараметрыРасчета, АдресХранилища) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ЗакрытиеМесяца.НачислениеАмортизацииОС");
	
	ПараметрыРасчета = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ИсходныеПараметрыРасчета);
	ВнеоборотныеАктивыСлужебный.ДополнитьПараметрамиУчетаВнеоборотныхАктивов(ПараметрыРасчета);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИсходныеПараметрыРасчета.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Основной расчет амортизации выполняется с помощью запроса
	ТекстЗапроса = ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ВнеоборотныеАктивы.УстановитьПараметрыРасчетаАмортизации(Запрос, ПараметрыРасчета);
	
	КоличествоОбработанныхДанных = 0;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		// Расчет доначисления амортизации за прошлый месяц
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(РезультатЗапроса, Неопределено);
		
		// Расчет амортизации за текущий месяц
		Запрос.Текст = ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		АмортизацияИзменений = РассчитатьАмортизациюИзменений(
									РезультатЗапроса[РезультатЗапроса.ВГраница()], 
									РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выгрузить());
		
		Запрос.Текст = ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета);
		Запрос.УстановитьПараметр("АмортизацияИзменений_УУ", АмортизацияИзменений);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		КоличествоОбработанныхДанных = Выборка.КоличествоДанных;
		
		// Таблицы для отладки
		// ВТ_СписокОбъектовУчета
		// ВТ_СписокОССПараметрами_БУ
		// ВТ_СписокОССПараметрами_УУ
		// ВТ_СписокОССИзмененнымиПараметрами_УУ
		// ВТ_РасчетАмортизации_БУ
		// ВТ_РасчетАмортизации_УУ
		// ВТ_НачисленнаяАмортизация
		
		ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		
		Если ПараметрыРасчета.ВернутьПараметрыРасчета Тогда
			ВернутьПараметрыРасчета(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц);
		КонецЕсли;
		
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц,, "ВТ_НачисленнаяАмортизация,НомераПакетовСФилиалами");
		
		Если ПараметрыРасчета.ЗаписатьДанные Тогда
			ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц);
		КонецЕсли;
	
	Исключение
		
		ПараметрыРасчета.ЕстьОшибки = Истина;
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
			ПараметрыРасчета.ЭтапРасчета, 
			ПараметрыРасчета.Период, 
			ПараметрыРасчета.ПакетыАмортизации.ВыгрузитьКолонку("Организация"),
			ТекстОшибки);
			
	КонецПопытки; 
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоОбработанныхДанных / 100);
	
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		ПоместитьВоВременноеХранилище(ПараметрыРасчета.ЕстьОшибки, АдресХранилища);
	КонецЕсли;
	
КонецПроцедуры

// Определяет номер пакета амортизации.
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации - Организация.
// 
// Возвращаемое значение:
//  Число - Номер пакета.
//
Функция НомерПакетаПоУмолчанию(Организация) Экспорт

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ПакетыАмортизацииОС.НомерПакета) КАК НомерПакета,
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПакетыАмортизацииОС.НомерПакета), 0) КАК КоличествоНомеров
	|ИЗ
	|	РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|ГДЕ
	|	ПакетыАмортизацииОС.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПакетыАмортизацииОС.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НомерПакета = 0;
	Если Выборка.Следующий() Тогда
		Если Выборка.КоличествоНомеров = 1 Тогда
			НомерПакета = Выборка.НомерПакета;
		КонецЕсли; 
	КонецЕсли;

	Возврат НомерПакета;
	
КонецФункции

// Добавляет в список запросов формирование временной таблицы ВТ_НачисленнаяАмортизация.
//  Используется для универсального начисления амортизации.
//  Созданная временная таблица может использоваться для формирования движений по регистрам.
//
// Параметры:
//  СписокЗапросов			 - Массив	 - Список запросов.
//  ТекстПодзапроса			 - Строка	 - Текст запроса, содержащий результат расчета амортизации.
//  ОпределитьНомерПакета	 - Булеов	 - Истина, если нужно определить номер пакета.
//
Процедура ТекстЗапросаВтНачисленнаяАмортизация(СписокЗапросов, ТекстПодзапроса, ОпределитьНомерПакета) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	&НомерПакета КАК НомерПакета,
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) КАК СуммаУУ,
	|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ,
	|	СУММА(НачисленнаяАмортизация.СуммаПР) КАК СуммаПР,
	|	СУММА(НачисленнаяАмортизация.СуммаВР) КАК СуммаВР,
	|	СУММА(НачисленнаяАмортизация.СуммаЦФ) КАК СуммаЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУЦФ) КАК СуммаНУЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаПРЦФ) КАК СуммаПРЦФ,
	|	СУММА(НачисленнаяАмортизация.СуммаВРЦФ) КАК СуммаВРЦФ,
	|	НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос,
	|	НачисленнаяАмортизация.ЭтоАмортизационнаяПремия,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|	КОНЕЦ КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ВТ_НачисленнаяАмортизация
	|ИЗ
	|	(&ТекстПодзапроса) КАК НачисленнаяАмортизация
	|	//ТекстСоединения//
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяАмортизация.ПериодРасчета,
	|	НачисленнаяАмортизация.Организация,
	|	&НомерПакета,
	|	НачисленнаяАмортизация.Подразделение,
	|	НачисленнаяАмортизация.КорПодразделение,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета,
	|	НачисленнаяАмортизация.НаправлениеДеятельности,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.ОсновноеСредство,
	|	НачисленнаяАмортизация.СтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов,
	|	НачисленнаяАмортизация.ПередаватьРасходыВДругуюОрганизацию,
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос,
	|	НачисленнаяАмортизация.ЭтоАмортизационнаяПремия,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(НачисленнаяАмортизация.СуммаУУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаБУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаНУ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаПР) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаВР) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаНУЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаПРЦФ) <> 0
	|		ИЛИ СУММА(НачисленнаяАмортизация.СуммаВРЦФ) <> 0";
	
	Если ОпределитьНомерПакета Тогда
		
		ТекстСоединения = 
			"	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
			|		ПО ПакетыАмортизацииОС.Организация = НачисленнаяАмортизация.Организация
			|			И ПакетыАмортизацииОС.ОсновноеСредство = НачисленнаяАмортизация.ОсновноеСредство";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ТекстСоединения//", ТекстСоединения);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НомерПакета", "ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0)");
		
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПодзапроса", ТекстПодзапроса);
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.АмортизацияОС2_4";
	
	ВЗапросеЕстьИсточник = Истина;
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаПартииПрочихРасходов_Амортизация(Запрос, ТекстыЗапроса, Регистры);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете_Амортизация(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка           КАК Ссылка,
	|	ДанныеДокумента.Дата             КАК Период,
	|	ДанныеДокумента.Номер            КАК Номер,
	|	ДанныеДокумента.Проведен         КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления  КАК ПометкаУдаления,
	|	ДанныеДокумента.Комментарий      КАК Комментарий,
	|	ДанныеДокумента.Ответственный    КАК Ответственный,
	|	ДанныеДокумента.Организация      КАК Организация,
	|	ДанныеДокумента.НомерПакета      КАК НомерПакета
	|ИЗ
	|	Документ.АмортизацияОС2_4 КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	УниверсальныеМеханизмыПартийИСебестоимости.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.АмортизацияОС2_4"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация",  Перечисления.ХозяйственныеОперации.АмортизацияОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СтоимостьОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	НачисленнаяАмортизация.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаНУ    КАК СтоимостьНУ,
	|	-НачисленнаяАмортизация.СуммаНУ   КАК СтоимостьВР,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизационнаяПремия,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.Подразделение                 КАК КорПодразделение,
	|	НачисленнаяАмортизация.НаправлениеДеятельности       КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос
	|ИЗ
	|	ВТ_НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НачисленнаяАмортизация.ЭтоАмортизационнаяПремия
	|	И НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаАмортизацияОС(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АмортизацияОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)          КАК ВидДвижения,
	|	&Ссылка                                         КАК Регистратор,
	|	ВЫБОР 
	|		КОГДА НачисленнаяАмортизация.ОрганизацияПолучательРасходов <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОСвДругуюОрганизацию)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.АмортизацияОС)
	|	КОНЕЦ                                           КАК ХозяйственнаяОперация,
	|	НачисленнаяАмортизация.ПериодРасчета            КАК Период,
	|	НачисленнаяАмортизация.Организация              КАК Организация,
	|	НачисленнаяАмортизация.Подразделение            КАК Подразделение,
	|	НачисленнаяАмортизация.ОсновноеСредство         КАК ОсновноеСредство,
	|	НачисленнаяАмортизация.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	НачисленнаяАмортизация.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|
	|	НачисленнаяАмортизация.СуммаУУ    КАК Амортизация,
	|	НачисленнаяАмортизация.СуммаБУ    КАК АмортизацияРегл,
	|	НачисленнаяАмортизация.СуммаНУ    КАК АмортизацияНУ,
	|	НачисленнаяАмортизация.СуммаПР    КАК АмортизацияПР,
	|	НачисленнаяАмортизация.СуммаВР    КАК АмортизацияВР,
	|	НачисленнаяАмортизация.СуммаЦФ    КАК АмортизацияЦФ,
	|	НачисленнаяАмортизация.СуммаНУЦФ  КАК АмортизацияНУЦФ,
	|	НачисленнаяАмортизация.СуммаПРЦФ  КАК АмортизацияПРЦФ,
	|	НачисленнаяАмортизация.СуммаВРЦФ  КАК АмортизацияВРЦФ,
	|
	|	НачисленнаяАмортизация.ОрганизацияПолучательРасходов КАК КорОрганизация,
	|	НачисленнаяАмортизация.КорПодразделение              КАК КорПодразделение,
	|	НачисленнаяАмортизация.КорНаправлениеДеятельности    КАК КорНаправлениеДеятельности,
	|	НачисленнаяАмортизация.СтатьяРасходов                КАК КорСтатьяРасходов,
	|	НачисленнаяАмортизация.АналитикаРасходов             КАК КорАналитикаРасходов,
	|	НачисленнаяАмортизация.НачислятьИзнос                КАК НачислятьИзнос
	|ИЗ
	|	ВТ_НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|ГДЕ
	|	НЕ НачисленнаяАмортизация.ЭтоАмортизационнаяПремия
	|	И НачисленнаяАмортизация.Организация = &Организация
	|	И НачисленнаяАмортизация.НомерПакета = &НомерПакета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПорядокОтраженияПрочихОпераций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	&Период КАК Дата,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Документ,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиПрочихОпераций
	|ГДЕ
	|	СтатьиПрочихОпераций.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И СтатьиПрочихОпераций.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	И СтатьиПрочихОпераций.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.СтатьяРасходов
	|			ИЗ
	|				ВТ_НачисленнаяАмортизация КАК Т
	|			ГДЕ
	|				Т.Организация = &Организация
	|				И Т.НомерПакета = &НомерПакета)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период  КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы();
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы();
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ" + ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеНачисленнойАмортизацииВПрочихРасходах();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчете() Экспорт

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаНачислениеАмортизацииОС(СписокЗапросов);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаАмортизационнаяПремия(СписокЗапросов);
	
	Возврат СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаВТНачислениеАмортизацииОС(СписокЗапросов);
	
	СписокЗапросов.Добавить(Символы.ПС);

	Возврат СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

#КонецОбласти

#Область РасчетАмортизации

Процедура ЗаписатьРасчетАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц)

	НачатьТранзакцию();
	
	Попытка
	
		// Нужно заблокировать документы, чтобы не создать дублей
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("Документ.АмортизацияОС2_4");
		ЭлементБлокировки.УстановитьЗначение("Дата", Новый Диапазон(НачалоМесяца(ПараметрыРасчета.Период),КонецМесяца(ПараметрыРасчета.Период)));
		ЭлементБлокировки.ИсточникДанных = ПараметрыРасчета.ПакетыАмортизации;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НомерПакета", "НомерПакета");
		
		Блокировка.Заблокировать(); 
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.Проведен, НЕОПРЕДЕЛЕНО) = ЛОЖЬ
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаНеПроведен,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЕСТЬNULL(РегламентныйДокумент.ПометкаУдаления, ЛОЖЬ) = ИСТИНА
		|				ТОГДА РегламентныйДокумент.Ссылка
		|			ИНАЧЕ ЗНАЧЕНИЕ(Документ.АмортизацияОС2_4.ПустаяСсылка)
		|		КОНЕЦ) КАК СсылкаУдален
		|ИЗ
		|	НомераПакетовСФилиалами КАК НомераПакетовСФилиалами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АмортизацияОС2_4 КАК РегламентныйДокумент
		|		ПО (РегламентныйДокумент.Организация = НомераПакетовСФилиалами.Организация)
		|			И (РегламентныйДокумент.НомерПакета = НомераПакетовСФилиалами.НомерПакета)
		|			И (РегламентныйДокумент.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца)
		|ГДЕ
		|	НомераПакетовСФилиалами.Организация В(&СписокОрганизаций)
		|
		|СГРУППИРОВАТЬ ПО
		|	НомераПакетовСФилиалами.Организация,
		|	НомераПакетовСФилиалами.НомерПакета";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ПараметрыРасчета.Период));
		Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.НомерПакета = ПараметрыРасчета.НомерПакета Тогда
				
				// Расчет выполняется из формы документа.
				Если ЗначениеЗаполнено(ПараметрыРасчета.Ссылка) Тогда
					ДокументОбъект = ПараметрыРасчета.Ссылка.ПолучитьОбъект();
				Иначе 
					ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
						Выборка.СсылкаПроведен, Неопределено, Неопределено, Документы.АмортизацияОС2_4);
				КонецЕсли;
				
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Комментарий   = ПараметрыРасчета.Комментарий;
				ДокументОбъект.Ответственный = ПараметрыРасчета.Ответственный;
				
			Иначе	
				
				ДокументОбъект = ВнеоборотныеАктивыСлужебный.ПолучитьРегламентныйДокумент(
					Выборка.СсылкаПроведен, Выборка.СсылкаНеПроведен, Выборка.СсылкаУдален, Документы.АмортизацияОС2_4);
					
				ДокументОбъект.НомерПакета   = Выборка.НомерПакета;
				ДокументОбъект.Организация   = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
			КонецЕсли; 
				
			ДокументОбъект.Дата = КонецМесяца(ПараметрыРасчета.Период);
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
			ВнеоборотныеАктивыСлужебный.УничтожитьВременныеТаблицыПослеЗаписиРасчетаАмортизации(МенеджерВременныхТаблиц);
			
		КонецЦикла; 
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки; 
	
КонецПроцедуры

Функция РассчитатьАмортизациюИзменений(РезультатЗапроса, ТаблицаРазукомплектации)

	АмортизацияИзменений = РезультатЗапроса.Выгрузить();
	АмортизацияИзменений.Сортировать("ОсновноеСредство,Период,Приоритет");
	
	ТекущееОС = Неопределено;
	ОстаточнаяСтоимость = 0;
	НачисленнаяАмортизацияЗаМесяц = 0;
	РесурсНазначенный = 0;
	
	Для каждого ДанныеСтроки Из АмортизацияИзменений Цикл
		
		Если РесурсНазначенный <> ДанныеСтроки.РесурсНазначенный Тогда
			РесурсНазначенный = ДанныеСтроки.РесурсНазначенный;
		КонецЕсли;
		
		Если ДанныеСтроки.ОсновноеСредство <> ТекущееОС Тогда
			ОстаточнаяСтоимость = ДанныеСтроки.Стоимость - ДанныеСтроки.Амортизация;
			НачисленнаяАмортизацияЗаМесяц = 0;
			ТекущееОС = ДанныеСтроки.ОсновноеСредство;
		КонецЕсли; 
		
		Если ДанныеСтроки.ЭтоИзменения Тогда

			Если ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.ПереоценкаОС2_4") Тогда
				
				// Нужно расчитать остаточную стоимость так как это делает переоценка.
				
				ОстаточнаяСтоимость = ДанныеСтроки.СтоимостьИзменение;
				
				Если ДанныеСтроки.Стоимость <> 0 Тогда
					
					НоваяАмортизация = (ДанныеСтроки.Амортизация + НачисленнаяАмортизацияЗаМесяц) 
												* ДанныеСтроки.СтоимостьИзменение 
												/ ДанныеСтроки.Стоимость;
												
					ОстаточнаяСтоимость = ОстаточнаяСтоимость - Окр(НоваяАмортизация, 2); 
					
				КонецЕсли; 

			ИначеЕсли ТипЗнч(ДанныеСтроки.Регистратор) = Тип("ДокументСсылка.СписаниеОС2_4") Тогда
				
				Если ДанныеСтроки.НачислятьАмортизацию Тогда
					
					// Нужно расчитать остаточную стоимость так как это делает списание.
					
					// Уменьшение стоимости на сумму списания.
					ОстаточнаяСтоимость = ОстаточнаяСтоимость + ДанныеСтроки.СтоимостьИзменение;
					
					// Увеличение стоимости на сумму списанной амортизации.
					ДоляСписания = ?(ДанныеСтроки.Стоимость <> 0, Окр(-ДанныеСтроки.СтоимостьИзменение / ДанныеСтроки.Стоимость, 16), 1);
					ОстаточнаяСтоимость = ОстаточнаяСтоимость + (ДанныеСтроки.Амортизация + НачисленнаяАмортизацияЗаМесяц) * ДоляСписания;
					
				Иначе	
					ОстаточнаяСтоимость = 0
				КонецЕсли; 
				
			Иначе
				ОстаточнаяСтоимость = ОстаточнаяСтоимость + ДанныеСтроки.СтоимостьИзменение;
			КонецЕсли; 
			
			Если ТаблицаРазукомплектации <> Неопределено Тогда
				
				Если ДанныеСтроки.ЭтоРазукомплектация Тогда
					
					СтруктураПоиска = Новый Структура("ИсходноеОсновноеСредство", ДанныеСтроки.ОсновноеСредство);
					СписокСтрок = ТаблицаРазукомплектации.НайтиСтроки(СтруктураПоиска);
					
					СтоимостьНовыхОС = 0;
					Для каждого ПолученныйОС Из СписокСтрок Цикл
						СтоимостьНовыхОС = СтоимостьНовыхОС + ПолученныйОС.Стоимость;
					КонецЦикла; 
					Для каждого ПолученныйОС Из СписокСтрок Цикл
						ПолученныйОС.Амортизация = (ДанныеСтроки.Амортизация + ДанныеСтроки.СуммаУУ + НачисленнаяАмортизацияЗаМесяц) 
														* ПолученныйОС.Стоимость / (ДанныеСтроки.Стоимость + ДанныеСтроки.СтоимостьИзменение + СтоимостьНовыхОС);
						ПолученныйОС.Амортизация = Окр(ПолученныйОС.Амортизация, 2);
						ОстаточнаяСтоимость = ОстаточнаяСтоимость + ПолученныйОС.Амортизация; 
					КонецЦикла; 
					
				ИначеЕсли ДанныеСтроки.ПолученПриРазукомплектации Тогда
					
					СтруктураПоиска = Новый Структура("ИсходноеОсновноеСредство", ДанныеСтроки.ОсновноеСредство);
					ПолученныйОС = ТаблицаРазукомплектации.Найти(ДанныеСтроки.ОсновноеСредство, "ОсновноеСредство");
					Если ПолученныйОС <> Неопределено Тогда
						ОстаточнаяСтоимость = ОстаточнаяСтоимость - ПолученныйОС.Амортизация;
					КонецЕсли; 
					
				КонецЕсли;
				
			КонецЕсли; 
			
			ОстаточнаяСтоимостьПослеНачисленияАмортизации = 
				ОстаточнаяСтоимость
				- НачисленнаяАмортизацияЗаМесяц 
				- ДанныеСтроки.ЛиквидационнаяСтоимость;
			
			Если НЕ ДанныеСтроки.НачислятьАмортизацию 
				ИЛИ ОстаточнаяСтоимостьПослеНачисленияАмортизации <= 0 Тогда
				
				ДанныеСтроки.СуммаУУ = 0;
				
			ИначеЕсли (ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод + ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) >= ДанныеСтроки.РесурсНазначенный Тогда
				
				ДанныеСтроки.СуммаУУ = ОстаточнаяСтоимость - ДанныеСтроки.ЛиквидационнаяСтоимость;
				
			ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.Линейный
				ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции
				ИЛИ ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка
					И (ДанныеСтроки.РесурсНазначенный
						- ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод
						- ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод) <= 12 Тогда
				
				ДанныеСтроки.СуммаУУ = (ОстаточнаяСтоимость - ДанныеСтроки.ЛиквидационнаяСтоимость)
										* (ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод)
										/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод);
				
			ИначеЕсли ДанныеСтроки.МетодНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка Тогда
				
				ДанныеСтроки.СуммаУУ = ОстаточнаяСтоимость
										* ДанныеСтроки.КоэффициентУскорения
										* ДанныеСтроки.РесурсВыработанныйЗаТекущийПериод
										/ (ДанныеСтроки.РесурсНазначенный - ДанныеСтроки.КоэффициентУскорения 
												*(ДанныеСтроки.РесурсВыработанныйЗаПрошлыйПериод - ДанныеСтроки.ОтработанныхМесяцевПолныхЛет));
			КонецЕсли; 
			
			ДанныеСтроки.СуммаУУ = Окр(ДанныеСтроки.СуммаУУ, 2);
			
			Если ДанныеСтроки.СуммаУУ > ОстаточнаяСтоимостьПослеНачисленияАмортизации Тогда
				ДанныеСтроки.СуммаУУ = Макс(ОстаточнаяСтоимостьПослеНачисленияАмортизации, 0);
			КонецЕсли;
			
		КонецЕсли;
		
		НачисленнаяАмортизацияЗаМесяц = НачисленнаяАмортизацияЗаМесяц + ДанныеСтроки.СуммаУУ;
		
	КонецЦикла; 
	
	Возврат АмортизацияИзменений;

КонецФункции

Процедура ВернутьПараметрыРасчета(ИсходныеПараметрыРасчета, МенеджерВременныхТаблиц)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&КонецМесяца                                                      КАК Дата,
	|	ПараметрыАмортизации.Организация                                  КАК Организация,
	|	ПараметрыАмортизации.ОсновноеСредство                             КАК ВнеоборотныйАктив,
	
	// БУ
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюБУ 
	|			ТОГДА ПараметрыАмортизации.МетодНачисленияАмортизацииБУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК МетодНачисленияБУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюБУ 
	|			ТОГДА ПараметрыАмортизации.СрокИспользованияДляВычисленияАмортизации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СрокИспользованияБУ,
	|	ПараметрыАмортизации.КоэффициентПериода                           КАК КоэффициентПериода,
	|	ПараметрыАмортизации.СтоимостьДляВычисленияАмортизации            КАК СтоимостьБУ,
	|	ПараметрыАмортизации.ОбъемВыработки                               КАК ВыработкаЗаМесяцБУ,
	|	ПараметрыАмортизации.ОбъемПродукцииРаботДляВычисленияАмортизации  КАК ОбъемПродукцииБУ,
	|	ПараметрыАмортизации.КоэффициентАмортизацииБУ                     КАК КоэффициентАмортизацииБУ,
	|	ПараметрыАмортизации.КоэффициентУскоренияБУ                       КАК КоэффициентУскоренияБУ,
	|	ПараметрыАмортизации.СтоимостьНаНачалоГода                        КАК СтоимостьНаНачалоГодаБУ,
	|	(ПараметрыАмортизации.ЧислоЛетПользования
	|		- ПараметрыАмортизации.ЧислоПрошедшихЛет) * 12                КАК ОставшийсяСрокИспользования,
	|	ПараметрыАмортизации.ЧислоПрошедшихЛет * 12                       КАК ФактическийСрокИспользования,
	
	// НУ
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА ПараметрыАмортизации.МетодНачисленияАмортизацииНУ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК МетодНачисленияНУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА ПараметрыАмортизации.СрокПолезногоИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокИспользованияНУ,
	|	ВЫБОР
	|		КОГДА НЕ ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА 0
	|		КОГДА ПараметрыАмортизации.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) 
	|			ТОГДА ПараметрыАмортизации.СтоимостьНУНачальныйОстаток 
	|					- ПараметрыАмортизации.АмортизационнаяПремия 
	|					- ПараметрыАмортизации.АмортизацияПриПереходеКЛинейномуМетоду
	|		ИНАЧЕ ПараметрыАмортизации.СтоимостьНУНачальныйОстаток 
	|				- ПараметрыАмортизации.АмортизационнаяПремия 
	|				- ПараметрыАмортизации.АмортизацияНУНачальныйОстаток                    
	|	КОНЕЦ                                                             КАК СтоимостьНУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА ПараметрыАмортизации.СпециальныйКоэффициентНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентНУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюНУ 
	|			ТОГДА ПараметрыАмортизации.НормаАмортизации
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НормаАмортизацииНУ,
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизации.НачислятьАмортизациюБУ 
	|			ТОГДА ПараметрыАмортизации.ПРДляВычисленияАмортизации
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПРДляВычисленияАмортизации
	|ИЗ
	|	ВТ_СписокОССПараметрами_БУ КАК ПараметрыАмортизации
	|ГДЕ
	|	ПараметрыАмортизации.НачислятьАмортизациюБУ
	|		ИЛИ ПараметрыАмортизации.НачислятьАмортизациюНУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонецМесяца                                                      КАК Дата,
	|	ПараметрыАмортизации.Организация                                  КАК Организация,
	|	ПараметрыАмортизации.ОсновноеСредство                             КАК ВнеоборотныйАктив,
	
	// УУ
	|	ПараметрыАмортизации.МетодНачисленияАмортизации                   КАК МетодНачисленияУУ,
	|	ПараметрыАмортизации.ОстаточнаяСтоимость                          КАК СтоимостьУУ,
	|	ПараметрыАмортизации.ЛиквидационнаяСтоимость                      КАК ЛиквидационнаяСтоимость,
	|	ПараметрыАмортизации.РесурсНазначенный                            КАК СрокИспользованияУУ,
	|	ПараметрыАмортизации.РесурсВыработанныйЗаТекущийПериод            КАК РесурсВыработанныйЗаТекущийПериод,
	|	ПараметрыАмортизации.РесурсВыработанныйЗаПрошлыйПериод            КАК РесурсВыработанныйЗаПрошлыйПериод,
	|	ПараметрыАмортизации.ОтработанныхМесяцевПолныхЛет                 КАК ОтработанныхМесяцевПолныхЛет,
	|	ПараметрыАмортизации.КоэффициентУскоренияУУ                       КАК КоэффициентУскоренияУУ
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК ПараметрыАмортизации
	|ГДЕ
	|	ПараметрыАмортизации.НачислятьАмортизациюУУ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(ИсходныеПараметрыРасчета.Период));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ПараметрыАмортизации_Регл = Результат[0].Выгрузить();
	ПараметрыАмортизации_Упр = Результат[1].Выгрузить();

	СписокПараметровАмортизации = ПолучитьИзВременногоХранилища(ИсходныеПараметрыРасчета.АдресПараметровРасчета);
	
	Если СписокПараметровАмортизации = Неопределено Тогда
		СписокПараметровАмортизации = Новый Структура;
		СписокПараметровАмортизации.Вставить("ПараметрыАмортизации_Регл", ПараметрыАмортизации_Регл);
		СписокПараметровАмортизации.Вставить("ПараметрыАмортизации_Упр", ПараметрыАмортизации_Упр);
	Иначе
		ОбщегоНазначенияУТКлиентСервер.ДополнитьТаблицу(СписокПараметровАмортизации.ПараметрыАмортизации_Регл, ПараметрыАмортизации_Регл);
		ОбщегоНазначенияУТКлиентСервер.ДополнитьТаблицу(СписокПараметровАмортизации.ПараметрыАмортизации_Упр, ПараметрыАмортизации_Упр);
	КонецЕсли; 
	
	ПоместитьВоВременноеХранилище(СписокПараметровАмортизации, ИсходныеПараметрыРасчета.АдресПараметровРасчета);
	
КонецПроцедуры
 
Процедура ЗарегистрироватьОшибкиРасчетаАмортизации(ПараметрыРасчета, МенеджерВременныхТаблиц) Экспорт
		
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|	ПроверкаПараметровРасчета.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).Наименование КАК ОбъектУчетаПредставление,
	|	ВЫРАЗИТЬ(ПроверкаПараметровРасчета.ОбъектУчета КАК Справочник.ОбъектыЭксплуатации).ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияБУ) КАК НеУказанСрокИспользованияБУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияНУ) КАК НеУказанСрокИспользованияНУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ) КАК НеУказанСрокИспользованияУУ,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанРесурсВыработанный) КАК НеУказанРесурсВыработанный,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанаАмортизационнаяГруппа) КАК НеУказанаАмортизационнаяГруппа,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанОбъемНаработки) КАК НеУказанОбъемНаработки,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанКоэффициентАмортизации) КАК НеУказанКоэффициентАмортизации,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанКоэффициентУскорения) КАК НеУказанКоэффициентУскорения,
	|	МАКСИМУМ(ПроверкаПараметровРасчета.НеУказанаСтатьяРасходовАмортизационнойПремии) КАК НеУказанаСтатьяРасходовАмортизационнойПремии
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПроверкаПараметровРасчета.Организация КАК Организация,
	|		ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияБУ,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияНУ,
	|		ЛОЖЬ КАК НеУказанаАмортизационнаяГруппа,
	|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки,
	|		ЛОЖЬ КАК НеУказанКоэффициентАмортизации,
	|		ЛОЖЬ КАК НеУказанКоэффициентУскорения,
	|		ЛОЖЬ КАК НеУказанаСтатьяРасходовАмортизационнойПремии
	|	ИЗ
	|		ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|	ГДЕ
	|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПроверкаПараметровРасчета.Организация КАК Организация,
	|		ПроверкаПараметровРасчета.ОбъектУчета КАК ОбъектУчета,
	|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияУУ КАК НеУказанСрокИспользованияУУ,
	|		ПроверкаПараметровРасчета.НеУказанРесурсВыработанный КАК НеУказанРесурсВыработанный,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияБУ,
	|		ЛОЖЬ КАК НеУказанСрокИспользованияНУ,
	|		ЛОЖЬ КАК НеУказанаАмортизационнаяГруппа,
	|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки КАК НеУказанОбъемНаработки,
	|		ЛОЖЬ КАК НеУказанКоэффициентАмортизации,
	|		ЛОЖЬ КАК НеУказанКоэффициентУскорения,
	|		ЛОЖЬ КАК НеУказанаСтатьяРасходовАмортизационнойПремии
	|	ИЗ
	|		ВТ_ПроверкаПараметровРасчетаИзменения_УУ КАК ПроверкаПараметровРасчета
	|	ГДЕ
	|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)";
		
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПроверкаПараметровРасчета.Организация КАК Организация,
		|		ПроверкаПараметровРасчета.ОбъектУчета,
		|		ЛОЖЬ КАК НеУказанСрокИспользованияУУ,
		|		ЛОЖЬ КАК НеУказанРесурсВыработанный,
		|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияБУ,
		|		ПроверкаПараметровРасчета.НеУказанСрокИспользованияНУ,
		|		ПроверкаПараметровРасчета.НеУказанаАмортизационнаяГруппа,
		|		ПроверкаПараметровРасчета.НеУказанОбъемНаработки,
		|		ПроверкаПараметровРасчета.НеУказанКоэффициентАмортизации,
		|		ПроверкаПараметровРасчета.НеУказанКоэффициентУскорения,
		|		ПроверкаПараметровРасчета.НеУказанаСтатьяРасходовАмортизационнойПремии
		|	ИЗ
		|		ВТ_ПроверкаПараметровРасчета_БУ КАК ПроверкаПараметровРасчета
		|	ГДЕ
		|		ПроверкаПараметровРасчета.Организация В(&СписокОрганизаций)";
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + "
		|
		|) КАК ПроверкаПараметровРасчета
		|
		|СГРУППИРОВАТЬ ПО
		|	ПроверкаПараметровРасчета.ОбъектУчета,
		|	ПроверкаПараметровРасчета.Организация";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокОрганизаций", ПараметрыРасчета.СписокОрганизаций);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ШаблонАмортизационнаяГруппа  = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указана амортизационная группа.'");
	ШаблонОбъемНаработки         = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан предполагаемый объем наработки (продукции, работ).'");
	ШаблонКоэффициентАмортизации = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан процент годовой амортизации.'");
	ШаблонКоэффициентУскорения   = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан коэффициент ускорения.'");
	ШаблонСрокИспользованияБУ    = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан срок использования в бухгалтерском учете.'");
	ШаблонСрокИспользованияНУ    = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан срок использования в налоговом учете.'");
	ШаблонСрокИспользованияУУ    = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указан срок использования в управленческом учете.'");
	ШаблонРесурсВыработанный     = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не удалось определить выработанный ресурс в управленческом учете.'");
	ШаблонСтатьяРасходовАмортизационнойПремии = НСтр("ru = 'Для основного средства ""%1"" (инв. №%2) не указана статья расходов по амортизационной премии.'");
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеУказанаАмортизационнаяГруппа Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонАмортизационнаяГруппа, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли;
		
		Если Выборка.НеУказанОбъемНаработки Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонОбъемНаработки, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанРесурсВыработанный Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонРесурсВыработанный, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанКоэффициентАмортизации Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонКоэффициентАмортизации, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанКоэффициентУскорения Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонКоэффициентУскорения, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанСрокИспользованияБУ Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСрокИспользованияБУ, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанСрокИспользованияНУ Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСрокИспользованияНУ, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
		
		Если Выборка.НеУказанСрокИспользованияУУ Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСрокИспользованияУУ, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
	
		Если Выборка.НеУказанаСтатьяРасходовАмортизационнойПремии Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСтатьяРасходовАмортизационнойПремии, Выборка.ОбъектУчетаПредставление, Выборка.ИнвентарныйНомер);
			ВнеоборотныеАктивыСлужебный.ЗарегистрироватьПроблемуВыполненияРасчета(
				ПараметрыРасчета.ЭтапРасчета, 
				ПараметрыРасчета.Период,
				Выборка.Организация,
				ТекстОшибки,,
				Выборка.ОбъектУчета,
				ПараметрыРасчета.ЕстьОшибки);
				
		КонецЕсли; 
	
	КонецЦикла;
	
КонецПроцедуры

#Область ТекстыЗапросовДляРасчетаАмортизации

Функция ТекстЗапросаДляРасчетаАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запросов, в которых подготавливаются параметры для расчета
	ТекстЗапросаПакетыАмортизации(СписокЗапросов);
	ТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета);
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_БУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДляРасчетаАмортизацииСУчетомДоначисления(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	// Тексты запроса, в которых вычисляется амортизация
	ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗавершитьРасчетАмортизации(ПараметрыРасчета)

	СписокЗапросов = Новый Массив;
	
	ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета);
	ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВПакете());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаПакетыАмортизации(СписокЗапросов)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетов
	|ИЗ
	|	&ПакетыАмортизации КАК ПакетыАмортизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераПакетов.Организация КАК Организация,
	|	НомераПакетов.НомерПакета КАК НомерПакета
	|ПОМЕСТИТЬ НомераПакетовСФилиалами
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Филиалы.Ссылка,
	|	НомераПакетов.НомерПакета
	|ИЗ
	|	НомераПакетов КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Филиалы
	|		ПО (Филиалы.ГоловнаяОрганизация = НомераПакетов.Организация)
	|			И (НЕ Филиалы.ПометкаУдаления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НомерПакета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПакетыАмортизации.Организация КАК Организация,
	|	ПакетыАмортизации.ОсновноеСредство КАК ОбъектУчета
	|ПОМЕСТИТЬ ПакетыАмортизации
	|ИЗ
	|	НомераПакетовСФилиалами КАК НомераПакетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизации
	|		ПО (ПакетыАмортизации.Организация = НомераПакетов.Организация)
	|			И (ПакетыАмортизации.НомерПакета = НомераПакетов.НомерПакета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаСписокОбъектовУчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.НачалоМесяца                        КАК НачалоМесяца,
	|	СписокОС.КонецМесяца                         КАК КонецМесяца,
	|	СписокОС.Организация                         КАК Организация,
	|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
	|	СписокОС.СпособОтраженияРасходовУУ           КАК СпособОтраженияРасходов,
	|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета
	|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета
	|ИЗ
	|	ВТ_СписокОбъектовУчета_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
	|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
	|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство";
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&НачалоМесяца                                КАК НачалоМесяца,
		|	&КонецМесяца                                 КАК КонецМесяца,
		|	СписокОС.Организация                         КАК Организация,
		|	СписокОС.ОсновноеСредство                    КАК ОсновноеСредство,
		|	СписокОС.СпособОтраженияРасходовБУ           КАК СпособОтраженияРасходов,
		|	ЕСТЬNULL(ПакетыАмортизацииОС.НомерПакета, 0) КАК НомерПакета
		|ИЗ
		|	ВТ_СписокОбъектовУчета_БУ КАК СписокОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПакетыАмортизацииОС КАК ПакетыАмортизацииОС
		|		ПО ПакетыАмортизацииОС.Организация = СписокОС.Организация
		|			И ПакетыАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры
 
// Формирует тексты запросов, которые создают таблицы объектов учета, по которым требуется расчет амортизации.
//
// Параметры:
//  СписокЗапросов	 - Массив	 - Список запросов, в который нужно добавить текст запроса.
//  ПараметрыРасчета - Структура - Содержит параметры расчета амортизации.
//  ПрефиксТаблицы	 - Строка	 - Используется для добавления префикса к созданным таблицам.
//
Процедура ТекстЗапросаСписокВсехОС(СписокЗапросов, ПараметрыРасчета, ПрефиксТаблицы = "") Экспорт

	#Область ВТ_СписокОбъектовУчета_БУ
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПорядокУчетаОС.ОсновноеСредство.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
		|	ПорядокУчетаОС.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовБУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовБУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовБУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходовАмортизационнойПремии    КАК СтатьяРасходовАмортизационнойПремии,
		|	ПорядокУчетаОС.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюБУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовБУ
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_БУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
		|ГДЕ
		|	(ПорядокУчетаОС.НачислятьАмортизациюБУ
		|			ИЛИ ПорядокУчетаОС.НачислятьАмортизациюНУ)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
		
		Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_БУ", "ВТ_СписокОбъектовУчета_БУ" + ПрефиксТаблицы);
		КонецЕсли; 
		
		СписокЗапросов.Добавить(ТекстЗапроса);
	
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ВТ_СписокОбъектовУчета_Доначисление_УУ
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтоимостьОС.Организация      КАК Организация,
		|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
		|	МИНИМУМ(СтоимостьОС.Период)  КАК Период
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_Доначисление_УУ
		|ИЗ
		|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|ГДЕ
		|	СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьОС.Активность
		|	И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И СтоимостьОС.ХозяйственнаяОперация В (
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
		|	И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьОС.Организация,
		|	СтоимостьОС.ОсновноеСредство
		|
		|ИМЕЮЩИЕ
		|	СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
		
		Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_Доначисление_УУ", "ВТ_СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
		КонецЕсли; 
		
		СписокЗапросов.Добавить(ТекстЗапроса);
	КонецЕсли;
	#КонецОбласти
	
	#Область ВТ_СписокОбъектовУчета_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	&КонецМесяца                    КАК КонецМесяца,
		|	ЛОЖЬ                            КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_УУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоПредыдущегоМесяца        КАК НачалоМесяца,
		|	&КонецПредыдущегоМесяца         КАК КонецМесяца,
		|	ИСТИНА                          КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&КонецПозапрошлогоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)) КАК ПорядокУчетаОС
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	&КонецМесяца                    КАК КонецМесяца,
		|	ЛОЖЬ                            КАК ЭтоДоначисление,
		|	ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) = ТИП(Документ.РазукомплектацияОС) КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОС
		|ГДЕ
		|	ПорядокУчетаОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (
		|				ТИП(Документ.ПринятиеКУчетуОС2_4),
		|				ТИП(Документ.РазукомплектацияОС))
		|	И (ПорядокУчетаОС.Организация, ПорядокУчетаОС.ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоПредыдущегоМесяца        КАК НачалоМесяца,
		|	&КонецПредыдущегоМесяца         КАК КонецМесяца,
		|	ИСТИНА                          КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОС
		|ГДЕ
		|	ПорядокУчетаОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ТИПЗНАЧЕНИЯ(ПорядокУчетаОС.Регистратор) В (
		|				ТИП(Документ.ПринятиеКУчетуОС2_4),
		|				ТИП(Документ.РазукомплектацияОС))
		|	И (Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&КонецМесяца                    КАК ПериодНачисления,
		|	&НачалоМесяца                   КАК НачалоМесяца,
		|	&КонецМесяца                    КАК КонецМесяца,
		|	ЛОЖЬ                            КАК ЭтоДоначисление,
		|	ЛОЖЬ                            КАК ПолученПриРазукомплектации,
		|	ПорядокУчетаОС.Организация      КАК Организация,
		|	ПорядокУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ КАК НачислятьАмортизациюУУ,
		|
		|	ПорядокУчетаОС.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
		|
		|	ПорядокУчетаОС.СтатьяРасходов    КАК СтатьяРасходовУУ,
		|	ПорядокУчетаОС.АналитикаРасходов КАК АналитикаРасходовУУ,
		|
		|	ПорядокУчетаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
		|	ПорядокУчетаОС.ОрганизацияПолучательРасходов       КАК ОрганизацияПолучательРасходовУУ
		|	
		|ПОМЕСТИТЬ ВТ_СписокОбъектовУчета_УУ
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					ПакетыАмортизации.Организация,
		|					ПакетыАмортизации.ОбъектУчета
		|				ИЗ
		|					ПакетыАмортизации КАК ПакетыАмортизации)) КАК ПорядокУчетаОС
		|ГДЕ
		|	ПорядокУчетаОС.НачислятьАмортизациюУУ";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство, 
	|	Организация";
	
	Если ЗначениеЗаполнено(ПрефиксТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_УУ", "ВТ_СписокОбъектовУчета_УУ" + ПрефиксТаблицы);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_СписокОбъектовУчета_Доначисление_УУ", "ВТ_СписокОбъектовУчета_Доначисление_УУ" + ПрефиксТаблицы);
	КонецЕсли; 
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
КонецПроцедуры

// Получает все необходимые параметры для расчета.
//
Процедура ТекстЗапросаПараметрыРасчетаАмортизации(СписокЗапросов, ПараметрыРасчета)

	ОбъектУчета = "ОсновноеСредство";
	ДобавитьТекстДляДоначисления = ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"МестонахождениеОС", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПервоначальныеСведенияОС", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПорядокУчетаОС", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов);
	
	ТекстПоляДатаПоследнегоИзменения =
	"ВЫБОР 
	|	КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|		ТОГДА НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаРегистра.ДатаПоследнегоИзменения, МЕСЯЦ, 1), МЕСЯЦ)
	|	ИНАЧЕ ТаблицаРегистра.ДатаПоследнегоИзменения
	|КОНЕЦ";
	ВычислениеПолей = Новый Структура;
	ВычислениеПолей.Вставить("ДатаПоследнегоИзменения", ТекстПоляДатаПоследнегоИзменения);
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
		"ПараметрыАмортизацииОСУУ", "", ДобавитьТекстДляДоначисления, ОбъектУчета, СписокЗапросов, ВычислениеПолей);
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
			"ПараметрыАмортизацииОСБУ", "", Ложь, ОбъектУчета, СписокЗапросов);
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаПараметрыРасчетаАмортизации(
			"НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет", "СпециальныйКоэффициентНУ", Ложь, ОбъектУчета, СписокЗапросов);
		
	КонецЕсли;
	
	#Область СтоимостьОСНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(СтоимостьОС.Стоимость) КАК Стоимость,
	|	СУММА(СтоимостьОС.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(СтоимостьОС.СтоимостьНУ) КАК СтоимостьНУ,
	|	СУММА(СтоимостьОС.СтоимостьПР) КАК СтоимостьПР,
	|	СУММА(СтоимостьОС.СтоимостьВР) КАК СтоимостьВР,
	|	СУММА(СтоимостьОС.СтоимостьЦФ) КАК СтоимостьЦФ,
	|	СУММА(СтоимостьОС.СтоимостьНУЦФ) КАК СтоимостьНУЦФ,
	|	СУММА(СтоимостьОС.СтоимостьПРЦФ) КАК СтоимостьПРЦФ,
	|	СУММА(СтоимостьОС.СтоимостьВРЦФ) КАК СтоимостьВРЦФ,
	|	СУММА(СтоимостьОС.АмортизационнаяПремия) КАК АмортизационнаяПремия
	|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоМесяца
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтоимостьОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СтоимостьОС.СтоимостьОстаток КАК Стоимость,
	|		СтоимостьОС.СтоимостьРеглОстаток КАК СтоимостьРегл,
	|		СтоимостьОС.СтоимостьНУОстаток КАК СтоимостьНУ,
	|		СтоимостьОС.СтоимостьПРОстаток КАК СтоимостьПР,
	|		СтоимостьОС.СтоимостьВРОстаток КАК СтоимостьВР,
	|		СтоимостьОС.СтоимостьЦФОстаток КАК СтоимостьЦФ,
	|		СтоимостьОС.СтоимостьНУЦФОстаток КАК СтоимостьНУЦФ,
	|		СтоимостьОС.СтоимостьПРЦФОстаток КАК СтоимостьПРЦФ,
	|		СтоимостьОС.СтоимостьВРЦФОстаток КАК СтоимостьВРЦФ,
	|		СтоимостьОС.АмортизационнаяПремияОстаток КАК АмортизационнаяПремия
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС.Остатки(
	|				&КонецПредыдущегоМесяцаГраница,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						СписокОС.Организация,
	|						СписокОС.ОсновноеСредство
	|					ИЗ
	|						ВТ_СписокОбъектовУчета КАК СписокОС)) КАК СтоимостьОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтоимостьОС.ОсновноеСредство,
	|		СтоимостьОС.Стоимость,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0
	|	ИЗ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|	ГДЕ
	|		СтоимостьОС.Активность
	|		И СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|		И СтоимостьОС.Регистратор ССЫЛКА Документ.РазукомплектацияОС
	|		И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					ВТ_СписокОбъектовУчета КАК СписокОС)) КАК СтоимостьОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СтоимостьИАмортизацияНаНачалоГода
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство
	|ПОМЕСТИТЬ втСписокОСУменьшаемогоОстатка
	|ИЗ
	|	ВТ_ПервоначальныеСведенияОС КАК СписокОС
	|ГДЕ
	|	СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство
	|ИЗ
	|	ВТ_ПараметрыАмортизацииОСУУ КАК СписокОС
	|ГДЕ
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
	|	И &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если ПараметрыРасчета.ДатаНачалаУчета <= НачалоГода(ПараметрыРасчета.Период) Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&НачалоМесяца                            КАК НачалоМесяца,
		|	СтоимостьОС.ОсновноеСредство             КАК ОсновноеСредство,
		|	СтоимостьОС.СтоимостьОстаток             КАК Стоимость,
		|	СтоимостьОС.СтоимостьРеглОстаток         КАК СтоимостьРегл,
		|	СтоимостьОС.СтоимостьЦФОстаток           КАК СтоимостьЦФ
		|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоГода
		|ИЗ
		|	РегистрНакопления.СтоимостьОС.Остатки(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК СтоимостьОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СтоимостьОС.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&НачалоМесяца                          КАК НачалоМесяца,
		|	АмортизацияОС.ОсновноеСредство         КАК ОсновноеСредство,
		|	-АмортизацияОС.АмортизацияОстаток      КАК Амортизация,
		|	-АмортизацияОС.АмортизацияРеглОстаток  КАК АмортизацияРегл,
		|	-АмортизацияОС.АмортизацияЦФОстаток    КАК АмортизацияЦФ
		|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоГода
		|ИЗ
		|	РегистрНакопления.АмортизацияОС.Остатки(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК АмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АмортизацияОС.ОсновноеСредство";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СчетаУчета.СчетУчета КАК СчетУчета,
		|	СчетаУчета.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации
		|ПОМЕСТИТЬ СчетаНачалоГода
		|ИЗ
		|	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						СписокОС.ОсновноеСредство
		|					ИЗ
		|						втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК СчетаУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыЦелевогоФинансированияОС.СчетУчета КАК СчетУчета,
		|	ПараметрыЦелевогоФинансированияОС.СчетАмортизации КАК СчетАмортизации
		|ИЗ
		|	РегистрСведений.ПараметрыЦелевогоФинансированияОС.СрезПоследних(
		|			&НачалоГода,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК ПараметрыЦелевогоФинансированияОС
		|ГДЕ
		|	ПараметрыЦелевогоФинансированияОС.ПрименениеЦелевогоФинансирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&НачалоМесяца                            КАК НачалоМесяца,
		|	СтоимостьОС.Субконто1                    КАК ОсновноеСредство,
		|	0                                        КАК Стоимость,
		|	СтоимостьОС.СуммаОстатокДт               КАК СтоимостьРегл,
		|	0                                        КАК СтоимостьЦФ
		|ПОМЕСТИТЬ ВТ_СтоимостьОС_НаНачалоГода
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&НачалоГода,
		|			Счет В
		|				(ВЫБРАТЬ
		|					СчетаНачалоГода.СчетУчета
		|				ИЗ
		|					СчетаНачалоГода КАК СчетаНачалоГода),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Субконто1 В
		|					(ВЫБРАТЬ
		|						СписокОС.ОсновноеСредство
		|					ИЗ
		|						втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК СтоимостьОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&НачалоМесяца                          КАК НачалоМесяца,
		|	АмортизацияОС.Субконто1                КАК ОсновноеСредство,
		|	0                                      КАК Амортизация,
		|	АмортизацияОС.СуммаОстатокКт           КАК АмортизацияРегл,
		|	0                                      КАК АмортизацияЦФ
		|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоГода
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&НачалоГода,
		|			Счет В
		|				(ВЫБРАТЬ
		|					СчетаНачалоГода.СчетНачисленияАмортизации
		|				ИЗ
		|					СчетаНачалоГода КАК СчетаНачалоГода),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Субконто1 В
		|					(ВЫБРАТЬ
		|						СписокОС.ОсновноеСредство
		|					ИЗ
		|						втСписокОСУменьшаемогоОстатка КАК СписокОС)) КАК АмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли; 
	
	#КонецОбласти
	
	#Область ПредварительнаяСтоимостьОС
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                                КАК НачалоМесяца,
	|	СтоимостьОС.ОсновноеСредство                 КАК ОсновноеСредство,
	|	СУММА(СтоимостьОС.ПредварительнаяСтоимость)  КАК Стоимость
	|ПОМЕСТИТЬ ВТ_ПредварительнаяСтоимостьОС
	|ИЗ
	|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|ГДЕ
	|	СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И СтоимостьОС.ПредварительнаяСтоимость <> 0
	|	И СтоимостьОС.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации))
	|	И СтоимостьОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОбъектовУчета КАК СписокОС)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство";
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НачалоПредыдущегоМесяца              КАК НачалоМесяца,
		|	СтоимостьОС.ОсновноеСредство          КАК ОсновноеСредство,
		|	СУММА(СтоимостьОС.Стоимость 
		|		   - СтоимостьОС.ПредварительнаяСтоимость)  КАК Стоимость
		|ИЗ
		|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|ГДЕ
		|	СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьОС.ХозяйственнаяОперация В (
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОС), 
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоДоговоруЛизинга), 
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуОСПоИнвентаризации),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.МодернизацияОС))
		|	И СтоимостьОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтоимостьОС.ОсновноеСредство
		|
		|ИМЕЮЩИЕ СУММА(СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость) > 0";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтоимостьОС.ОсновноеСредство";
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизацияОСНаНачалоМесяца
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&НачалоМесяца                          КАК НачалоМесяца,
	|	АмортизацияОС.Организация              КАК Организация,
	|	АмортизацияОС.ОсновноеСредство         КАК ОсновноеСредство,
	|	-АмортизацияОС.АмортизацияОстаток      КАК Амортизация,
	|	-АмортизацияОС.АмортизацияРеглОстаток  КАК АмортизацияРегл,
	|	-АмортизацияОС.АмортизацияНУОстаток    КАК АмортизацияНУ,
	|	-АмортизацияОС.АмортизацияПРОстаток    КАК АмортизацияПР,
	|	-АмортизацияОС.АмортизацияВРОстаток    КАК АмортизацияВР,
	|	-АмортизацияОС.АмортизацияЦФОстаток    КАК АмортизацияЦФ,
	|	-АмортизацияОС.АмортизацияНУЦФОстаток  КАК АмортизацияНУЦФ,
	|	-АмортизацияОС.АмортизацияПРЦФОстаток  КАК АмортизацияПРЦФ,
	|	-АмортизацияОС.АмортизацияВРЦФОстаток  КАК АмортизацияВРЦФ
	|ПОМЕСТИТЬ ВТ_АмортизацияОС_НаНачалоМесяца
	|ИЗ
	|	РегистрНакопления.АмортизацияОС.Остатки(
	|			&КонецПредыдущегоМесяцаГраница,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					СписокОС.Организация,
	|					СписокОС.ОсновноеСредство
	|				ИЗ
	|					ВТ_СписокОбъектовУчета КАК СписокОС)) КАК АмортизацияОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АмортизацияОС.ОсновноеСредство,
	|	АмортизацияОС.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область АмортизацияОСПриПереходеКЛинейномуМетоду
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	АмортизацияОС.Организация,
		|	АмортизацияОС.ОсновноеСредство,
		|	АмортизацияОС.АмортизацияНУОстаток КАК АмортизацияНУ
		|ПОМЕСТИТЬ ВТ_АмортизацияОСПриПереходеКЛинейномуМетоду
		|ИЗ
		|	РегистрНакопления.АмортизацияОС.Остатки(
		|			&ДатаПереходаКЛинейномуМетоду,
		|			(Организация, ОсновноеСредство) В
		|				(ВЫБРАТЬ
		|					СписокОС.Организация,
		|					СписокОС.ОсновноеСредство
		|				ИЗ
		|					ВТ_СписокОбъектовУчета_БУ КАК СписокОС)) КАК АмортизацияОС
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АмортизацияОС.ОсновноеСредство,
		|	АмортизацияОС.Организация";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ГрафикиАмортизацииОС
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ГрафикиАмортизацииОС.Организация       КАК Организация,
		|	ГрафикиАмортизацииОС.ОсновноеСредство  КАК ОсновноеСредство,
		|	ГрафикиАмортизацииОС.ГрафикАмортизации КАК ГрафикАмортизации,
		|
		|	ВЫБОР 
		|		КОГДА ГодовыеГрафикиАмортизацииОС.Коэффициент1 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент2 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент3 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент4 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент5 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент6 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент7 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент8 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент9 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент10 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент11 
		|			+ ГодовыеГрафикиАмортизацииОС.Коэффициент12 <> 0
		|		ТОГДА 
		|			ВЫБОР МЕСЯЦ(&НачалоМесяца)
		|				КОГДА 1 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент1, 0)
		|				КОГДА 2 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент2, 0) 
		|				КОГДА 3 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент3, 0)
		|				КОГДА 4 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент4, 0) 
		|				КОГДА 5 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент5, 0)
		|				КОГДА 6 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент6, 0)
		|				КОГДА 7 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент7, 0)
		|				КОГДА 8 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент8, 0)
		|				КОГДА 9 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент9, 0)
		|				КОГДА 10 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент10, 0)
		|				КОГДА 11 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент11, 0)
		|				КОГДА 12 ТОГДА ЕСТЬNULL(ГодовыеГрафикиАмортизацииОС.Коэффициент12, 0)
		|			КОНЕЦ / (ГодовыеГрафикиАмортизацииОС.Коэффициент1 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент2 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент3 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент4 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент5 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент6 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент7 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент8 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент9 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент10 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент11 
		|						+ ГодовыеГрафикиАмортизацииОС.Коэффициент12)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоэффициентПериода
		|
		|ПОМЕСТИТЬ ВТ_ГрафикиАмортизацииОС
		|ИЗ
		|	РегистрСведений.ГрафикиАмортизацииОСБухгалтерскийУчет.СрезПоследних(
		|			&КонецПредыдущегоМесяца,
		|			(Организация, ОсновноеСредство) В
		|					(ВЫБРАТЬ
		|						СписокОС.Организация,
		|						СписокОС.ОсновноеСредство
		|					ИЗ
		|						ВТ_СписокОбъектовУчета_БУ КАК СписокОС)) КАК ГрафикиАмортизацииОС
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГодовыеГрафикиАмортизацииОС КАК ГодовыеГрафикиАмортизацииОС
		|		ПО ГодовыеГрафикиАмортизацииОС.Ссылка = ГрафикиАмортизацииОС.ГрафикАмортизации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ВыработкаОС
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВЫБОР
	|		КОГДА МЕСЯЦ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ) = МЕСЯЦ(&НачалоМесяца) - 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ ВТ_СписокОС_Выработка
	|ИЗ
	|	ВТ_СписокОбъектовУчета КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|ГДЕ
	|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ В (
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции), 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега))
	|	ИЛИ ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаНачалоМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецПредыдущегоМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					ВТ_СписокОС_Выработка КАК СписокОС)) КАК ВыработкаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки,
	|	СписокОС.ПринятоКУчетуВПрошломМесяце КАК ПринятоКУчетуВПрошломМесяце
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаКонецМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					ВТ_СписокОС_Выработка КАК СписокОС)) КАК ВыработкаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокОС_Выработка КАК СписокОС
	|		ПО (СписокОС.ОсновноеСредство = ВыработкаОС.ОбъектЭксплуатации)
	|			И (СписокОС.ПоказательНаработки = ВыработкаОС.ПоказательНаработки)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.ПоказательНаработки
	|ПОМЕСТИТЬ ВТ_СписокОС_ПринятоКУчетуВПрошломМесяце
	|ИЗ
	|	ВТ_СписокОС_Выработка КАК СписокОС
	|ГДЕ
	|	СписокОС.ПринятоКУчетуВПрошломМесяце
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаОС.ОбъектЭксплуатации КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ВыработкаОС.Значение КАК ОбъемВыработки
	|ПОМЕСТИТЬ ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|			&КонецПозапрошлогоМесяца,
	|			(ОбъектЭксплуатации, ПоказательНаработки) В
	|				(ВЫБРАТЬ
	|					СписокОС.ОсновноеСредство,
	|					СписокОС.ПоказательНаработки
	|				ИЗ
	|					ВТ_СписокОС_ПринятоКУчетуВПрошломМесяце КАК СписокОС)) КАК ВыработкаОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&НачалоМесяца КАК НачалоМесяца,
	|	ВыработкаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	СУММА(ВыработкаОС.ОбъемВыработкиБУ) КАК ОбъемВыработкиБУ,
	|	СУММА(ВыработкаОС.ОбъемВыработкиУУ) КАК ОбъемВыработкиУУ,
	|	СУММА(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца) КАК ОбъемВыработкиНаНачалоМесяца
	|ПОМЕСТИТЬ ВТ_ВыработкаОС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство) КАК ОсновноеСредство,
	|		ЕСТЬNULL(НаработкиПериодНачало.ПоказательНаработки, НаработкиПериодОкончание.ПоказательНаработки) КАК ПоказательНаработки,
	|		0 КАК ОбъемВыработкиБУ,
	|		ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0) КАК ОбъемВыработкиУУ,
	|		ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0) КАК ОбъемВыработкиНаНачалоМесяца
	|	ИЗ
	|		ВТ_ВыработкаОС_НаНачалоМесяца КАК НаработкиПериодНачало
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|			ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|				И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|	ГДЕ
	|		&ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство),
	|		ЕСТЬNULL(НаработкиПериодНачало.ПоказательНаработки, НаработкиПериодОкончание.ПоказательНаработки),
	|		ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0),
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_ВыработкаОС_НаНачалоМесяца КАК НаработкиПериодНачало
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|			ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|				И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|	ГДЕ
	|		НЕ НаработкиПериодОкончание.ПринятоКУчетуВПрошломМесяце
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЕСТЬNULL(НаработкиПериодНачало.ОсновноеСредство, НаработкиПериодОкончание.ОсновноеСредство),
	|		ЕСТЬNULL(НаработкиПериодНачало.ПоказательНаработки, НаработкиПериодОкончание.ПоказательНаработки),
	|		ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0),
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодОкончание.ОбъемВыработки, 0) - ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|				ТОГДА ЕСТЬNULL(НаработкиПериодНачало.ОбъемВыработки, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца КАК НаработкиПериодНачало
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС_НаКонецМесяца КАК НаработкиПериодОкончание
	|			ПО НаработкиПериодНачало.ОсновноеСредство = НаработкиПериодОкончание.ОсновноеСредство
	|				И НаработкиПериодНачало.ПоказательНаработки = НаработкиПериодОкончание.ПоказательНаработки
	|	ГДЕ
	|		НаработкиПериодОкончание.ПринятоКУчетуВПрошломМесяце) КАК ВыработкаОС
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыработкаОС.ОсновноеСредство,
	|	ВыработкаОС.ПоказательНаработки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПоказательНаработки";
	
	Если НЕ ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СписокОС_Выработка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыработкаОС_НаНачалоМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыработкаОС_НаКонецМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ВыработкаОС_НаНачалоПредыдущегоМесяца";
	КонецЕсли;
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область Изменения_УУ
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.Период, ДЕНЬ) КАК Период,
		|	ПараметрыАмортизацииОСУУ.Регистратор,
		|	ПараметрыАмортизацииОСУУ.ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.Организация,
		|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
		|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
		|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки,
		|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения,
		|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость
		|ПОМЕСТИТЬ ПараметрыАмортизацииОСУУИзменения
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|ГДЕ
		|	ПараметрыАмортизацииОСУУ.Активность
		|	И ПараметрыАмортизацииОСУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПараметрыАмортизацииОСУУ.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.Период, ДЕНЬ) КАК Период,
		|	ПараметрыАмортизацииОСУУ.Регистратор,
		|	ПараметрыАмортизацииОСУУ.ОсновноеСредство,
		|	ПараметрыАмортизацииОСУУ.Организация,
		|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения КАК ДатаПоследнегоИзменения,
		|	ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользования,
		|	ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемНаработки,
		|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения,
		|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО ПараметрыАмортизацииОСУУ.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	ПараметрыАмортизацииОСУУ.Активность
		|	И ПараметрыАмортизацииОСУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПараметрыАмортизацииОСУУ.Период >= Доначисление.Период
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПараметрыАмортизацииОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПараметрыАмортизацииОСУУ.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// ПорядокУчетаОС
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаОС.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаОС.Регистратор,
		|	ПорядокУчетаОС.ОсновноеСредство,
		|	РеестрДокументов.Организация,
		|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОС.НаправлениеДеятельности,
		|	ПорядокУчетаОС.ПоказательНаработки,
		|	ПорядокУчетаОС.ОбъемНаработки
		|ПОМЕСТИТЬ ПорядокУчетаОСИзменения
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО РеестрДокументов.Ссылка = ПорядокУчетаОС.Регистратор
		|			И НЕ РеестрДокументов.ДополнительнаяЗапись
		|ГДЕ
		|	ПорядокУчетаОС.Активность
		|	И ПорядокУчетаОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПорядокУчетаОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаОС.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаОС.Регистратор,
		|	ПорядокУчетаОС.ОсновноеСредство,
		|	РеестрДокументов.Организация,
		|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОС.НаправлениеДеятельности,
		|	ПорядокУчетаОС.ПоказательНаработки,
		|	ПорядокУчетаОС.ОбъемНаработки
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО ПорядокУчетаОС.ОсновноеСредство = Доначисление.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО РеестрДокументов.Ссылка = ПорядокУчетаОС.Регистратор
		|			И НЕ РеестрДокументов.ДополнительнаяЗапись
		|ГДЕ
		|	ПорядокУчетаОС.Активность
		|	И ПорядокУчетаОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПорядокУчетаОС.Период >= Доначисление.Период
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И ПорядокУчетаОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// ПорядокУчетаОСУУ
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаОСУУ.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаОСУУ.Регистратор,
		|	ПорядокУчетаОСУУ.ОсновноеСредство,
		|	ПорядокУчетаОСУУ.Организация,
		|	ПорядокУчетаОСУУ.Состояние,
		|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ,
		|	ПорядокУчетаОСУУ.СпособОтраженияРасходов,
		|	ПорядокУчетаОСУУ.СтатьяРасходов,
		|	ПорядокУчетаОСУУ.АналитикаРасходов,
		|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
		|ПОМЕСТИТЬ ПорядокУчетаОСУУИзменения
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
		|ГДЕ
		|	ПорядокУчетаОСУУ.Активность
		|	И ПорядокУчетаОСУУ.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И (ПорядокУчетаОСУУ.Организация, ПорядокУчетаОСУУ.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПорядокУчетаОСУУ.Период, ДЕНЬ) КАК Период,
		|	ПорядокУчетаОСУУ.Регистратор,
		|	ПорядокУчетаОСУУ.ОсновноеСредство,
		|	ПорядокУчетаОСУУ.Организация,
		|	ПорядокУчетаОСУУ.Состояние,
		|	ПорядокУчетаОСУУ.НачислятьАмортизациюУУ,
		|	ПорядокУчетаОСУУ.СпособОтраженияРасходов,
		|	ПорядокУчетаОСУУ.СтатьяРасходов,
		|	ПорядокУчетаОСУУ.АналитикаРасходов,
		|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов
		|ИЗ
		|	РегистрСведений.ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО ПорядокУчетаОСУУ.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	ПорядокУчетаОСУУ.Активность
		|	И ПорядокУчетаОСУУ.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И ПорядокУчетаОСУУ.Период >= Доначисление.Период
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ ПорядокУчетаОСУУ.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И (ПорядокУчетаОСУУ.Организация, ПорядокУчетаОСУУ.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// МестонахождениеОС
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(МестонахождениеОС.Период, ДЕНЬ) КАК Период,
		|	МестонахождениеОС.Регистратор,
		|	МестонахождениеОС.ОсновноеСредство,
		|	МестонахождениеОС.Организация,
		|	МестонахождениеОС.Местонахождение
		|ПОМЕСТИТЬ МестонахождениеОСИзменения
		|ИЗ
		|	РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|ГДЕ
		|	МестонахождениеОС.Активность
		|	И МестонахождениеОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И МестонахождениеОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(МестонахождениеОС.Период, ДЕНЬ) КАК Период,
		|	МестонахождениеОС.Регистратор,
		|	МестонахождениеОС.ОсновноеСредство,
		|	МестонахождениеОС.Организация,
		|	МестонахождениеОС.Местонахождение
		|ИЗ
		|	РегистрСведений.МестонахождениеОС КАК МестонахождениеОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО МестонахождениеОС.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	МестонахождениеОС.Активность
		|	И МестонахождениеОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И МестонахождениеОС.Период >= Доначисление.Период
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС2_4
		|	И НЕ МестонахождениеОС.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4
		|	И МестонахождениеОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// СтоимостьОС
		|ВЫБРАТЬ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(СтоимостьОС.Период, ДЕНЬ) КАК Период,
		|	СтоимостьОС.Регистратор,
		|	СтоимостьОС.ОсновноеСредство,
		|	СтоимостьОС.Организация,
		|	ВЫБОР 
		|		КОГДА ТИПЗНАЧЕНИЯ(СтоимостьОС.Регистратор) = ТИП(Документ.МодернизацияОС2_4)
		|			ТОГДА СтоимостьОС.ПредварительнаяСтоимость 
		|		ИНАЧЕ -СтоимостьОС.Стоимость
		|	КОНЕЦ КАК Стоимость
		|ПОМЕСТИТЬ СтоимостьОСИзменения
		|ИЗ
		|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|ГДЕ
		|	СтоимостьОС.Активность
		|	И СтоимостьОС.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И (СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
		|			ИЛИ СтоимостьОС.Регистратор ССЫЛКА Документ.РазукомплектацияОС
		|				И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
		|	И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(СписаниеОС2_4.Дата, ДЕНЬ) КАК Период,
		|	СтоимостьОС.Ссылка,
		|	СтоимостьОС.ОсновноеСредство,
		|	СписаниеОС2_4.Организация,
		|	-СтоимостьОС.СуммаСписанияУУ
		|ИЗ
		|	Документ.СписаниеОС2_4 КАК СписаниеОС2_4
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ОС КАК СтоимостьОС
		|		ПО СтоимостьОС.Ссылка = СписаниеОС2_4.Ссылка
		|ГДЕ
		|	СписаниеОС2_4.Проведен
		|	И СписаниеОС2_4.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И СписаниеОС2_4.ОтражатьВУпрУчете
		|	И СтоимостьОС.СуммаСписанияУУ <> 0
		|	И (СписаниеОС2_4.Организация, СтоимостьОС.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НачалоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(ПереоценкаОС.Дата, ДЕНЬ) КАК Период,
		|	СтоимостьОС.Ссылка,
		|	СтоимостьОС.ОсновноеСредство,
		|	ПереоценкаОС.Организация,
		|	СтоимостьОС.СтоимостьУУ
		|ИЗ
		|	Документ.ПереоценкаОС2_4 КАК ПереоценкаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаОС2_4.ОС КАК СтоимостьОС
		|		ПО СтоимостьОС.Ссылка = ПереоценкаОС.Ссылка
		|ГДЕ
		|	ПереоценкаОС.Проведен
		|	И ПереоценкаОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И ПереоценкаОС.ОтражатьВУпрУчете
		|	И СтоимостьОС.СтоимостьУУ <> 0
		|	И (ПереоценкаОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОбъектУчета
		|			ИЗ
		|				ПакетыАмортизации КАК СписокОС)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&НачалоПредыдущегоМесяца КАК НачалоМесяца,
		|	НАЧАЛОПЕРИОДА(СтоимостьОС.Период, ДЕНЬ) КАК Период,
		|	СтоимостьОС.Регистратор,
		|	СтоимостьОС.ОсновноеСредство,
		|	СтоимостьОС.Организация,
		|	СтоимостьОС.Стоимость - СтоимостьОС.ПредварительнаяСтоимость
		|ИЗ
		|	РегистрНакопления.СтоимостьОС КАК СтоимостьОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_Доначисление_УУ КАК Доначисление
		|		ПО СтоимостьОС.ОсновноеСредство = Доначисление.ОсновноеСредство
		|ГДЕ
		|	СтоимостьОС.Активность
		|	И СтоимостьОС.Период МЕЖДУ &НачалоПредыдущегоМесяца И &КонецПредыдущегоМесяца
		|	И СтоимостьОС.Период >= Доначисление.Период
		|	И СтоимостьОС.Регистратор ССЫЛКА Документ.МодернизацияОС2_4
		|	И (СтоимостьОС.Организация, СтоимостьОС.ОсновноеСредство) В
		|			(ВЫБРАТЬ
		|				СписокОС.Организация,
		|				СписокОС.ОсновноеСредство
		|			ИЗ
		|				ВТ_СписокОбъектовУчета_Доначисление_УУ КАК СписокОС)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	МАКСИМУМ(ТаблицаИзменений.Период) КАК Период,
		|	МАКСИМУМ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТаблицаИзменений.Период, МЕСЯЦ, 1), МЕСЯЦ)) КАК НачалоСледующегоМесяца,
		|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаИзменений.Организация КАК Организация,
		|	МАКСИМУМ(ЕСТЬNULL(СтоимостьОСИзменения_ПереоценкаПоследнее.Период, ТаблицаИзменений.НачалоМесяца)) КАК НачалоИзмененияСтоимости,
		|	МАКСИМУМ(ПараметрыАмортизацииОСУУПоследнее.Период) КАК ПараметрыАмортизацииОСУУПоследнееПериод,
		|	МАКСИМУМ(ПорядокУчетаОСИзмененияПоследнее.Период) КАК ПорядокУчетаОСИзмененияПоследнееПериод,
		|	МАКСИМУМ(ПорядокУчетаОСУУИзмененияПоследнее.Период) КАК ПорядокУчетаОСУУИзмененияПоследнееПериод,
		|	МАКСИМУМ(МестонахождениеОСИзмененияПоследнее.Период) КАК МестонахождениеОСИзмененияПоследнееПериод
		|ПОМЕСТИТЬ ТаблицаИзменений
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|		ТаблицаИзменений.Регистратор КАК Регистратор,
		|		ТаблицаИзменений.Период КАК Период,
		|		ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|		ТаблицаИзменений.Организация КАК Организация
		|	ИЗ
		|		ПараметрыАмортизацииОСУУИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		ПорядокУчетаОСИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		ПорядокУчетаОСУУИзменения КАК ТаблицаИзменений
		|	
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		МестонахождениеОСИзменения КАК ТаблицаИзменений
		|
		|	ОБЪЕДИНИТЬ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаИзменений.НачалоМесяца,
		|		ТаблицаИзменений.Регистратор,
		|		ТаблицаИзменений.Период,
		|		ТаблицаИзменений.ОсновноеСредство,
		|		ТаблицаИзменений.Организация
		|	ИЗ
		|		СтоимостьОСИзменения КАК ТаблицаИзменений
		|
		|	) КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУИзменения КАК ПараметрыАмортизацииОСУУПоследнее
		|		ПО (ПараметрыАмортизацииОСУУПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПараметрыАмортизацииОСУУПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСИзменения КАК ПорядокУчетаОСИзмененияПоследнее
		|		ПО (ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУИзменения КАК ПорядокУчетаОСУУИзмененияПоследнее
		|		ПО (ПорядокУчетаОСУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСИзменения КАК МестонахождениеОСИзмененияПоследнее
		|		ПО (МестонахождениеОСИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (МестонахождениеОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (МестонахождениеОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (МестонахождениеОСИзмененияПоследнее.Период <= ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОСИзменения КАК СтоимостьОСИзменения_ПереоценкаПоследнее
		|		ПО (СтоимостьОСИзменения_ПереоценкаПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (СтоимостьОСИзменения_ПереоценкаПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (СтоимостьОСИзменения_ПереоценкаПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (СтоимостьОСИзменения_ПереоценкаПоследнее.Период <= ТаблицаИзменений.Период)
		|			И (ТИПЗНАЧЕНИЯ(СтоимостьОСИзменения_ПереоценкаПоследнее.Регистратор) = ТИП(Документ.ПереоценкаОС2_4))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Регистратор,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство,
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Период,
		|	МИНИМУМ(ТаблицаИзмененийСледующее.Период) КАК ПериодСледующее
		|ПОМЕСТИТЬ ВТ_ТаблицаИзмененийСледующее
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаИзменений КАК ТаблицаИзмененийСледующее
		|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийСледующее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ТаблицаИзмененийСледующее.Период > ТаблицаИзменений.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Период,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство,
		|	МИНИМУМ(ТаблицаИзменений.Период) КАК ПериодСледующее
		|ПОМЕСТИТЬ ВТ_ТаблицаИзмененийВЭтомМесяце
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|ГДЕ
		|	(ТаблицаИзменений.НачалоМесяца = &НачалоМесяца)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.Организация КАК Организация,
		|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	ТаблицаИзменений.Период КАК Период,
		|	СУММА(СтоимостьОСИзменения.Стоимость) КАК Стоимость
		|ПОМЕСТИТЬ ВТ_СтоимостьИзмененияНаДату
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьОСИзменения КАК СтоимостьОСИзменения
		|		ПО (СтоимостьОСИзменения.Организация = ТаблицаИзменений.Организация)
		|			И (СтоимостьОСИзменения.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (СтоимостьОСИзменения.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (СтоимостьОСИзменения.Период <= ТаблицаИзменений.Период)
		|			И (СтоимостьОСИзменения.Период >= ТаблицаИзменений.НачалоИзмененияСтоимости)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.НачалоМесяца,
		|	ТаблицаИзменений.Период,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.НачалоМесяца КАК НачалоМесяца,
		|	КОНЕЦПЕРИОДА(ТаблицаИзменений.НачалоМесяца, МЕСЯЦ) КАК КонецМесяца,
		|	КОНЕЦПЕРИОДА(ТаблицаИзменений.Период, ДЕНЬ) КАК ДатаИзменений,
		|	ТаблицаИзменений.Регистратор КАК Регистратор,
		|	ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Регистратор) = ТИП(Документ.РазукомплектацияОС) КАК ЭтоРазукомплектация,
		|	ВЫБОР 
		|		КОГДА ТаблицаИзменений.НачалоМесяца = &НачалоПредыдущегоМесяца
		|			ТОГДА ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ТаблицаИзмененийВЭтомМесяце.ПериодСледующее, ДЕНЬ, -1), &КонецМесяца) 
		|		ИНАЧЕ ЕСТЬNULL(ТаблицаИзмененийСледующее.ПериодСледующее, &КонецМесяца) 
		|	КОНЕЦ КАК Период,
		|	РАЗНОСТЬДАТ(ТаблицаИзменений.Период, ЕСТЬNULL(ТаблицаИзмененийСледующее.ПериодСледующее, ТаблицаИзменений.НачалоСледующегоМесяца), ДЕНЬ) КАК СрокДействия,
		|	ТаблицаИзменений.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаИзменений.Организация КАК Организация,
		|	ЕСТЬNULL(СтоимостьИзмененияНаДату.Стоимость, 0) КАК СтоимостьИзменение, // Изменение стоимости на дату
		|	ПараметрыАмортизацииОСУУПоследнее.ДатаПоследнегоИзменения,
		|	ПараметрыАмортизацииОСУУПоследнее.СрокИспользования,
		|	ПараметрыАмортизацииОСУУПоследнее.ОбъемНаработки,
		|	ПараметрыАмортизацииОСУУПоследнее.КоэффициентУскорения,
		|	ПараметрыАмортизацииОСУУПоследнее.МетодНачисленияАмортизации,
		|	ПараметрыАмортизацииОСУУПоследнее.ЛиквидационнаяСтоимость,
		|	ПорядокУчетаОСИзмененияПоследнее.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОСИзмененияПоследнее.НаправлениеДеятельности,
		|	ПорядокУчетаОСИзмененияПоследнее.ПоказательНаработки,
		|	ПорядокУчетаОСУУИзмененияПоследнее.Состояние,
		|	ПорядокУчетаОСУУИзмененияПоследнее.НачислятьАмортизациюУУ,
		|	ПорядокУчетаОСУУИзмененияПоследнее.СпособОтраженияРасходов,
		|	ПорядокУчетаОСУУИзмененияПоследнее.СтатьяРасходов,
		|	ПорядокУчетаОСУУИзмененияПоследнее.АналитикаРасходов,
		|	ПорядокУчетаОСУУИзмененияПоследнее.ПередаватьРасходыВДругуюОрганизацию,
		|	ПорядокУчетаОСУУИзмененияПоследнее.ОрганизацияПолучательРасходов,
		|	МестонахождениеОСИзмененияПоследнее.Местонахождение
		|ПОМЕСТИТЬ ВТ_Изменения_УУ
		|ИЗ
		|	ТаблицаИзменений КАК ТаблицаИзменений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзмененийСледующее КАК ТаблицаИзмененийСледующее
		|		ПО (ТаблицаИзмененийСледующее.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийСледующее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ТаблицаИзмененийСледующее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ТаблицаИзмененийСледующее.Период = ТаблицаИзменений.Период)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзмененийВЭтомМесяце КАК ТаблицаИзмененийВЭтомМесяце
		|		ПО (ТаблицаИзмененийВЭтомМесяце.Организация = ТаблицаИзменений.Организация)
		|			И (ТаблицаИзмененийВЭтомМесяце.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметрыАмортизацииОСУУИзменения КАК ПараметрыАмортизацииОСУУПоследнее
		|		ПО (ПараметрыАмортизацииОСУУПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПараметрыАмортизацииОСУУПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПараметрыАмортизацииОСУУПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПараметрыАмортизацииОСУУПоследнее.Период = ТаблицаИзменений.ПараметрыАмортизацииОСУУПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСИзменения КАК ПорядокУчетаОСИзмененияПоследнее
		|		ПО (ПорядокУчетаОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаОСИзмененияПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУИзменения КАК ПорядокУчетаОСУУИзмененияПоследнее
		|		ПО (ПорядокУчетаОСУУИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (ПорядокУчетаОСУУИзмененияПоследнее.Период = ТаблицаИзменений.ПорядокУчетаОСУУИзмененияПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ МестонахождениеОСИзменения КАК МестонахождениеОСИзмененияПоследнее
		|		ПО (МестонахождениеОСИзмененияПоследнее.Организация = ТаблицаИзменений.Организация)
		|			И (МестонахождениеОСИзмененияПоследнее.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (МестонахождениеОСИзмененияПоследнее.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (МестонахождениеОСИзмененияПоследнее.Период = ТаблицаИзменений.МестонахождениеОСИзмененияПоследнееПериод)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьИзмененияНаДату КАК СтоимостьИзмененияНаДату
		|		ПО (СтоимостьИзмененияНаДату.Организация = ТаблицаИзменений.Организация)
		|			И (СтоимостьИзмененияНаДату.ОсновноеСредство = ТаблицаИзменений.ОсновноеСредство)
		|			И (СтоимостьИзмененияНаДату.НачалоМесяца = ТаблицаИзменений.НачалоМесяца)
		|			И (СтоимостьИзмененияНаДату.Период = ТаблицаИзменений.Период)";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК НачалоМесяца,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК КонецМесяца,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаИзменений,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	0    КАК СрокДействия,
		|	NULL КАК ОсновноеСредство,
		|	NULL КАК Организация,
		|	0    КАК СтоимостьИзменение,
		|	NULL Регистратор,
		|	ЛОЖЬ КАК ЭтоРазукомплектация,
		|	NULL КАК ДатаПоследнегоИзменения,
		|	0    КАК СрокИспользования,
		|	0    КАК ОбъемНаработки,
		|	NULL КАК КоэффициентУскорения,
		|	NULL КАК МетодНачисленияАмортизации,
		|	NULL КАК ЛиквидационнаяСтоимость,
		|	NULL КАК ГруппаФинансовогоУчета,
		|	NULL КАК НаправлениеДеятельности,
		|	NULL КАК ПоказательНаработки,
		|	NULL КАК Состояние,
		|	ЛОЖЬ КАК НачислятьАмортизациюУУ,
		|	NULL КАК СпособОтраженияРасходов,
		|	NULL КАК СтатьяРасходов,
		|	NULL КАК АналитикаРасходов,
		|	NULL КАК ПередаватьРасходыВДругуюОрганизацию,
		|	NULL КАК ОрганизацияПолучательРасходов,
		|	NULL КАК Местонахождение
		|ПОМЕСТИТЬ ВТ_Изменения_УУ
		|
		|ГДЕ
		|	ЛОЖЬ";
	
	КонецЕсли; 

	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ПериодИзменения_УУ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения_УУ.НачалоМесяца           КАК НачалоМесяца,
	|	Изменения_УУ.ОсновноеСредство       КАК ОсновноеСредство,
	|	Изменения_УУ.Организация            КАК Организация,
	|	МИНИМУМ(Изменения_УУ.ДатаИзменений) КАК Период
	|ПОМЕСТИТЬ ВТ_ПериодИзменения_УУ
	|ИЗ
	|	ВТ_Изменения_УУ КАК Изменения_УУ
	|
	|СГРУППИРОВАТЬ ПО
	|	Изменения_УУ.НачалоМесяца,
	|	Изменения_УУ.ОсновноеСредство,
	|	Изменения_УУ.Организация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОССПараметрами_БУ
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СписокОС.ОсновноеСредство,
		|	СписокОС.Организация,
		|
		|	ПорядокУчетаОС.ГруппаФинансовогоУчета,
		|	ПорядокУчетаОС.НаправлениеДеятельности,
		|	ПорядокУчетаОС.ПоказательНаработки,
		|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации,
		|
		|	СписокОС.НачислятьАмортизациюБУ
		|		И ПервоначальныеСведенияОС.ПорядокУчетаБУ В (&ПорядокУчетаПриКоторомНачисляетсяАмортизация) КАК НачислятьАмортизациюБУ,
		|
		|	СписокОС.НачислятьАмортизациюНУ
		|		И ПервоначальныеСведенияОС.ПорядокУчетаНУ В (&ПорядокУчетаПриКоторомНачисляетсяАмортизация) КАК НачислятьАмортизациюНУ,
		|
		|	СписокОС.СтатьяРасходовБУ,
		|	СписокОС.АналитикаРасходовБУ,
		|	СписокОС.СтатьяРасходовАмортизационнойПремии,
		|	СписокОС.АналитикаРасходовАмортизационнойПремии,
		|
		|	СписокОС.СпособОтраженияРасходовБУ,
		|	СписокОС.ПередаватьРасходыВДругуюОрганизациюБУ,
		|	СписокОС.ОрганизацияПолучательРасходовБУ,
		|
		|	ЕСТЬNULL(ГрафикиАмортизацииОС.ГрафикАмортизации, ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка)) КАК ГрафикАмортизации,
		|	ЕСТЬNULL(ГрафикиАмортизацииОС.КоэффициентПериода, 0) КАК КоэффициентПериода,
		|
		// ЧислоЛетПользования
		|	ВЫБОР
		|		КОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4,0))
		|	КОНЕЦ
		|		+ ВЫБОР 
		|			КОГДА (ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 
		|					- ВЫБОР
		|							КОГДА ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 < 1
		|								ТОГДА 0
		|							ИНАЧЕ ВЫРАЗИТЬ(ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации / 12 - 0.5 КАК ЧИСЛО(4,0))
		|						КОНЕЦ) > 0 
		|				ТОГДА 1 
		|			ИНАЧЕ 0 
		|		КОНЕЦ КАК ЧислоЛетПользования,
		|
		// ЧислоПрошедшихЛет
		|	ВЫБОР
		|		КОГДА (&ЧислоПрошедшихЛет / 12) < 1
		|			ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(&ЧислоПрошедшихЛет / 12 - 0.5 КАК ЧИСЛО(4,0))
		|	КОНЕЦ КАК ЧислоПрошедшихЛет,
		|
		|	СписокОС.АмортизационнаяГруппа,
		|	ЕСТЬNULL(СпециальныйКоэффициентНУ.СпециальныйКоэффициент, 1) КАК СпециальныйКоэффициентНУ,
		|	&НормаАмортизации КАК НормаАмортизации,
		|
		// ФактическийСрокПриПереходеКЛинейномуМетоду
		|	ВЫБОР
		|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
		|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009, 0)
		|		ИНАЧЕ (ГОД(&ДатаПереходаКЛинейномуМетоду) 
		|					- ГОД(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ)) * 12 
		|				+ (МЕСЯЦ(&ДатаПереходаКЛинейномуМетоду) - МЕСЯЦ(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюБУ)) - 1
		|	КОНЕЦ КАК ФактическийСрокПриПереходеКЛинейномуМетоду,
		|
		// АмортизацияПриПереходеКЛинейномуМетоду
		|	ВЫБОР
		|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
		|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияОС.АмортизацияДо2009, 0)
		|		ИНАЧЕ ЕСТЬNULL(АмортизацияОСПриПереходеКЛинейномуМетоду.АмортизацияНУ, 0)
		|	КОНЕЦ КАК АмортизацияПриПереходеКЛинейномуМетоду,
		|
		|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0)     КАК Стоимость,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) КАК СтоимостьРегл,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)   КАК СтоимостьНУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0)   КАК СтоимостьПР,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьВР, 0)   КАК СтоимостьВР,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)   КАК СтоимостьЦФ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) КАК СтоимостьНУЦФ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) КАК СтоимостьПРЦФ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьВРЦФ, 0) КАК СтоимостьВРЦФ,
		|	ЕСТЬNULL(СтоимостьОС.АмортизационнаяПремия, 0) КАК АмортизационнаяПремия,
		
		|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.СтоимостьРегл, 0) 
		|		+ ЕСТЬNULL(СтоимостьОСНаНачалоГода.СтоимостьЦФ, 0)
		|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.АмортизацияРегл, 0) 
		|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.АмортизацияЦФ, 0) КАК СтоимостьНаНачалоГода,

		|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)
		|		+ ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) КАК СтоимостьНУНачальныйОстаток,
		|
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияРегл, 0)     КАК ОстаточнаяСтоимостьРегл,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0)   - ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)       КАК ОстаточнаяСтоимостьНУ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0)   - ЕСТЬNULL(АмортизацияОС.АмортизацияПР, 0)       КАК ОстаточнаяСтоимостьПР,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)   - ЕСТЬNULL(АмортизацияОС.АмортизацияЦФ, 0)       КАК ОстаточнаяСтоимостьЦФ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0)     КАК ОстаточнаяСтоимостьНУЦФ,
		|	ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияОС.АмортизацияПРЦФ, 0)     КАК ОстаточнаяСтоимостьПРЦФ,
		
		|	ЕСТЬNULL(АмортизацияОС.АмортизацияНУ, 0)
		|		+ ЕСТЬNULL(АмортизацияОС.АмортизацияНУЦФ, 0) КАК АмортизацияНУНачальныйОстаток,
		|
		|	МестонахождениеОС.Местонахождение,
		|
		|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиБУ, 0) КАК ОбъемВыработки,
		|
		|	ПервоначальныеСведенияОС.ПорядокУчетаБУ,
		|	ПервоначальныеСведенияОС.ПорядокУчетаНУ,
		|	ПервоначальныеСведенияОС.МетодНачисленияАмортизацииБУ,
		|	ВЫБОР
		|		КОГДА СписокОС.ОсновноеСредство.АмортизационнаяГруппа В (
		|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), 
		|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), 
		|						ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа))
		|					И СписокОС.ОсновноеСредство.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения))
		|				ИЛИ СписокОС.ОсновноеСредство.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
		|		ИНАЧЕ ЕСТЬNULL(РегУчетнаяПолитика.УчетнаяПолитика.МетодНачисленияАмортизацииНУ, ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный))
		|	КОНЕЦ КАК МетодНачисленияАмортизацииНУ,
		|	ПервоначальныеСведенияОС.СтоимостьДо2002,
		|	ПервоначальныеСведенияОС.АмортизацияДо2002,
		|	ПервоначальныеСведенияОС.АмортизацияДо2009,
		|	ПервоначальныеСведенияОС.ФактическийСрокИспользованияДо2009,
		|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимостьНУ,
		|
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияБУ,
		|	ПараметрыАмортизацииОСБУ.СрокПолезногоИспользованияНУ,
		|	ПараметрыАмортизацииОСБУ.КоэффициентАмортизацииБУ,
		|	ПараметрыАмортизацииОСБУ.КоэффициентУскорения КАК КоэффициентУскоренияБУ,
		|	ПараметрыАмортизацииОСБУ.ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ПараметрыАмортизацииОСБУ.ПРДляВычисленияАмортизации,
		|	ПараметрыАмортизацииОСБУ.СрокИспользованияДляВычисленияАмортизации,
		|	ПараметрыАмортизацииОСБУ.СтоимостьДляВычисленияАмортизации,
		|	ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения,
		|
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) = 0
		|			ТОГДА 0
		|		КОГДА (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)) <> 0
		|			ТОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0) / (ЕСТЬNULL(СтоимостьОС.СтоимостьРегл, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьЦФ, 0)) 
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоэффициентЦФБУ,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) = 0
		|			ТОГДА 0
		|		КОГДА (ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0)) <> 0
		|			ТОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0) / (ЕСТЬNULL(СтоимостьОС.СтоимостьНУ, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьНУЦФ, 0)) 
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоэффициентЦФНУ,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) = 0
		|			ТОГДА 0
		|		КОГДА (ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0)) <> 0
		|			ТОГДА ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0) / (ЕСТЬNULL(СтоимостьОС.СтоимостьПР, 0) + ЕСТЬNULL(СтоимостьОС.СтоимостьПРЦФ, 0)) 
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоэффициентЦФПР
		|
		|ПОМЕСТИТЬ ВТ_СписокОССПараметрами_БУ
		|ИЗ
		|	ВТ_СписокОбъектовУчета_БУ КАК СписокОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
		|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
		|		ПО (СтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|			И (СтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоГода КАК СтоимостьОСНаНачалоГода
		|		ПО (СтоимостьОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
		|		ПО (АмортизацияОС.Организация = СписокОС.Организация)
		|			И (АмортизацияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|			И (АмортизацияОС.НачалоМесяца = СписокОС.НачалоМесяца)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоГода КАК АмортизацияОСНаНачалоГода
		|		ПО (АмортизацияОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОСПриПереходеКЛинейномуМетоду КАК АмортизацияОСПриПереходеКЛинейномуМетоду
		|		ПО (АмортизацияОСПриПереходеКЛинейномуМетоду.Организация = СписокОС.Организация)
		|			И (АмортизацияОСПриПереходеКЛинейномуМетоду.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеОС
		|		ПО (МестонахождениеОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|			И (МестонахождениеОС.НачалоМесяца = СписокОС.НачалоМесяца)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
		|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
		|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСБУ КАК ПараметрыАмортизацииОСБУ
		|		ПО (ПараметрыАмортизацииОСБУ.Организация = СписокОС.Организация)
		|			И (ПараметрыАмортизацииОСБУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпециальныйКоэффициентНУ КАК СпециальныйКоэффициентНУ
		|		ПО (СпециальныйКоэффициентНУ.Организация = СписокОС.Организация)
		|			И (СпециальныйКоэффициентНУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ГрафикиАмортизацииОС КАК ГрафикиАмортизацииОС
		|		ПО (ГрафикиАмортизацииОС.Организация = СписокОС.Организация)
		|			И (ГрафикиАмортизацииОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВыработкаОС
		|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
		|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
		|			И (ВыработкаОС.НачалоМесяца = СписокОС.НачалоМесяца)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&КонецМесяца) КАК РегУчетнаяПолитика
		|		ПО РегУчетнаяПолитика.Организация = СписокОС.Организация
		|ГДЕ
		|	(ПервоначальныеСведенияОС.ПорядокУчетаБУ В (&ПорядокУчетаПриКоторомНачисляетсяАмортизация)
		|		ИЛИ ПервоначальныеСведенияОС.ПорядокУчетаНУ В (&ПорядокУчетаПриКоторомНачисляетсяАмортизация))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СписокОС.ОсновноеСредство,
		|	СписокОС.Организация";
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса, 
			"&ЧислоПрошедшихЛет", 
			ВнеоборотныеАктивыСлужебный.ТекстПоляФактическийСрокИспользования("ПараметрыАмортизацииОСБУ.ДатаПоследнегоИзменения"));
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса, 
			"&НормаАмортизации", 
			ВнеоборотныеАктивыСлужебный.ТекстПоляНормаАмортизации("СписокОС.АмортизационнаяГруппа"));
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область СписокОССПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР 
	|		КОГДА ПериодИзмененияВТекущемМесяце.Период ЕСТЬ NULL
	|			ТОГДА СписокОС.ПериодНачисления
	|		КОГДА НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ) = НАЧАЛОПЕРИОДА(ПериодИзмененияВТекущемМесяце.Период, МЕСЯЦ)
	|			ТОГДА ПериодИзмененияВТекущемМесяце.Период
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ПериодИзмененияВТекущемМесяце.Период, ДЕНЬ, -1)
	|	КОНЕЦ КАК Период,
	|	СписокОС.ЭтоДоначисление,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.Организация,
	|
	|	ПорядокУчетаОСНаНачалоМесяца.ГруппаФинансовогоУчета,
	|	ПорядокУчетаОСНаНачалоМесяца.НаправлениеДеятельности,
	|	ПорядокУчетаОС.ПоказательНаработки,
	|
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации, 0)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	
	// СтоимостьДляВычисленияАмортизации
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамРегл)
	|			ТОГДА ПараметрыАмортизацииОСУУ.СтоимостьДляВычисленияАмортизации
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	
	|	ЕСТЬNULL(СтоимостьОСНаНачалоГода.Стоимость, 0)
	|		- ЕСТЬNULL(АмортизацияОСНаНачалоГода.Амортизация, 0) КАК СтоимостьНаНачалоГода,
	
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ВЫБОР 
	|					КОГДА НЕ ПериодИзменения_УУ.Период ЕСТЬ NULL
	|						ТОГДА ВЫБОР 
	|								КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|									ТОГДА (РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|									ИНАЧЕ (РАЗНОСТЬДАТ(СписокОС.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|							КОНЕЦ / ДЕНЬ(СписокОС.КонецМесяца)
	|					ИНАЧЕ 1
	|				КОНЕЦ * ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0)
	
			// Если расчет выполняется в месяце принятия к учету то ДеньПринятия/ВсегоДнейВМесяце
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|			ТОГДА (РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокОС.КонецМесяца)
	
			// В остальных случаях КоличествоДнейДоПервогоИзмененияПараметров/ВсегоДнейВМесяце
	|		ИНАЧЕ (РАЗНОСТЬДАТ(СписокОС.НачалоМесяца, ЕСТЬNULL(ДОБАВИТЬКДАТЕ(ПериодИзменения_УУ.Период, ДЕНЬ, -1), СписокОС.КонецМесяца), ДЕНЬ) + 1)
	|					/ ДЕНЬ(СписокОС.КонецМесяца)
	|	КОНЕЦ КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0)
	|		КОГДА НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				ИЛИ ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(СписокОС.НачалоМесяца, МЕСЯЦ, -1), МЕСЯЦ)
	|			ТОГДА (РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))
	
			// Расшифровка формулы: [кол-во отработаннных дней в мес принятия] / [всего дней в мес принятия] + [кол-во месяцев после принятия до начала месяца]
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ, 1), МЕСЯЦ), СписокОС.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	|	ВЫБОР
	|		КОГДА ГОД(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) <> ГОД(СписокОС.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)
	|				+ 12 * (ГОД(СписокОС.НачалоМесяца) - Год(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	СписокОС.НачислятьАмортизациюУУ,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|
	|	ВЫБОР 
	|		КОГДА &ПорядокУчетаВНАВУпрУчете = ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ)
	|				И НАЧАЛОПЕРИОДА(ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения, МЕСЯЦ) = СписокОС.НачалоМесяца
	|				И ТИПЗНАЧЕНИЯ(ПараметрыАмортизацииОСУУ.Регистратор) <> ТИП(Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0) - ЕСТЬNULL(АмортизацияОС.Амортизация, 0)
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|
	|	ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимость,
	|
	|	ЕСТЬNULL(СтоимостьОС.Стоимость, 0)                   КАК Стоимость,
	|	ЕСТЬNULL(АмортизацияОС.Амортизация, 0)               КАК Амортизация,
	|
	|	МестонахождениеНаНачалоМесяца.Местонахождение        КАК Местонахождение,
	|
	|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0)            КАК ОбъемВыработки,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования           КАК СрокИспользованияУУ,
	|	ПараметрыАмортизацииОСУУ.КоэффициентУскорения        КАК КоэффициентУскоренияУУ,
	|	ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации  КАК МетодНачисленияАмортизации,
	|	ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения     КАК ДатаПоследнегоИзменения
	|ПОМЕСТИТЬ ВТ_СписокОССПараметрами_УУ
	|ИЗ
	|	ВТ_СписокОбъектовУчета_УУ КАК СписокОС
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодИзменения_УУ КАК ПериодИзменения_УУ
	|		ПО (ПериодИзменения_УУ.Организация = СписокОС.Организация)
	|			И (ПериодИзменения_УУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПериодИзменения_УУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодИзменения_УУ КАК ПериодИзмененияВТекущемМесяце
	|		ПО (ПериодИзмененияВТекущемМесяце.Организация = СписокОС.Организация)
	|			И (ПериодИзмененияВТекущемМесяце.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПериодИзмененияВТекущемМесяце.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОСНаНачалоМесяца
	|		ПО (ПорядокУчетаОСНаНачалоМесяца.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПорядокУчетаОСНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоГода КАК СтоимостьОСНаНачалоГода
	|		ПО (СтоимостьОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоГода КАК АмортизацияОСНаНачалоГода
	|		ПО (АмортизацияОСНаНачалоГода.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
	|		ПО (ПредварительнаяСтоимостьОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПредварительнаяСтоимостьОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = СписокОС.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (МестонахождениеОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеНаНачалоМесяца
	|		ПО (МестонахождениеНаНачалоМесяца.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (МестонахождениеНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = СписокОС.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = СписокОС.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = СписокОС.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|ГДЕ
	|	(НЕ СписокОС.ЭтоДоначисление
	|		ИЛИ СписокОС.НачалоМесяца = ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ)
	|	И ПервоначальныеСведенияОС.ПорядокУчетаУУ В (&ПорядокУчетаПриКоторомНачисляетсяАмортизация)
	|	И НЕ СписокОС.ПолученПриРазукомплектации";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписокОССИзмененнымиПараметрами_УУ
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Изменения_УУ.Период,
	|	Изменения_УУ.Регистратор,
	|	Изменения_УУ.ОсновноеСредство КАК ОсновноеСредство,
	|	Изменения_УУ.Организация,
	|	(Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца) КАК ЭтоДоначисление,
	|
	|	Изменения_УУ.ЭтоРазукомплектация И НЕ ЕСТЬNULL(СписокОС.ПолученПриРазукомплектации, ЛОЖЬ) КАК ЭтоРазукомплектация,
	|	ЕСТЬNULL(СписокОС.ПолученПриРазукомплектации, ЛОЖЬ) КАК ПолученПриРазукомплектации, 
	|
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(ПервоначальныеСведенияОС.ДатаВводаВЭксплуатациюУУ, МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА ЕСТЬNULL(ПредварительнаяСтоимостьОС.Стоимость, 0)
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(СтоимостьОС.Стоимость, 0)
	|	КОНЕЦ КАК Стоимость,
	|
	|	ЕСТЬNULL(АмортизацияОС.Амортизация, 0) КАК Амортизация,
	|
	|	Изменения_УУ.СтоимостьИзменение КАК СтоимостьИзменение,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА ПорядокУчетаОСНаНачалоМесяца.ГруппаФинансовогоУчета
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.ГруппаФинансовогоУчета, ПорядокУчетаОС.ГруппаФинансовогоУчета)
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ЕСТЬNULL(Изменения_УУ.НаправлениеДеятельности, ПорядокУчетаОС.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(Изменения_УУ.ПоказательНаработки,     ПорядокУчетаОС.ПоказательНаработки)     КАК ПоказательНаработки,
	|
	// РесурсНазначенный
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(Изменения_УУ.ОбъемНаработки, ПараметрыАмортизацииОСУУ.ОбъемПродукцииРаботДляВычисленияАмортизации)
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.СрокИспользования, ПараметрыАмортизацииОСУУ.СрокИспользованияДляВычисленияАмортизации)
	|	КОНЕЦ КАК РесурсНазначенный,
	|
	// РесурсВыработанныйЗаТекущийПериод
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0)
	|		ИНАЧЕ 1
	|	КОНЕЦ * Изменения_УУ.СрокДействия / ДЕНЬ(Изменения_УУ.КонецМесяца) КАК РесурсВыработанныйЗаТекущийПериод,
	|	  
	// РесурсВыработанныйЗаПрошлыйПериод
	|	ВЫБОР 
	|		КОГДА ПараметрыАмортизацииОСУУ.ОсновноеСредство ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			ТОГДА ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиНаНачалоМесяца, 0)
	|
	|		КОГДА НАЧАЛОПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ) = Изменения_УУ.НачалоМесяца
	|			ТОГДА 0
	
	// 		Расшифровка формулы: [Кол-во отработаннных дней в мес. принятия] / [всего дней в мес.принятия] + [кол-во мес. после принятия до начала месяца расчета]
	|		ИНАЧЕ
	|			(РАЗНОСТЬДАТ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), КОНЕЦПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ), ДЕНЬ) + 1) / ДЕНЬ(КОНЕЦПЕРИОДА(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ))
	|			+ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения), МЕСЯЦ, 1), МЕСЯЦ), Изменения_УУ.КонецМесяца, МЕСЯЦ)
	|	КОНЕЦ КАК РесурсВыработанныйЗаПрошлыйПериод,
	|
	// ОтработанныхМесяцевПолныхЛет
	|	ВЫБОР
	|		КОГДА ГОД(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)) <> ГОД(Изменения_УУ.НачалоМесяца) ТОГДА
	|			13 - МЕСЯЦ(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения))
	|				+ 12 * (ГОД(Изменения_УУ.НачалоМесяца) - Год(ЕСТЬNULL(Изменения_УУ.ДатаПоследнегоИзменения, ПараметрыАмортизацииОСУУ.ДатаПоследнегоИзменения)) - 1)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтработанныхМесяцевПолныхЛет,
	|
	|	ЕСТЬNULL(Изменения_УУ.НачислятьАмортизациюУУ, ЕСТЬNULL(СписокОС.НачислятьАмортизациюУУ, ЛОЖЬ)) КАК НачислятьАмортизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.СтатьяРасходов, СписокОС.СтатьяРасходовУУ) КАК СтатьяРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.АналитикаРасходов, СписокОС.АналитикаРасходовУУ) КАК АналитикаРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.СпособОтраженияРасходов, СписокОС.СпособОтраженияРасходовУУ) КАК СпособОтраженияРасходовУУ,
	|	ЕСТЬNULL(Изменения_УУ.ПередаватьРасходыВДругуюОрганизацию, ЕСТЬNULL(СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ, ЛОЖЬ)) КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ЕСТЬNULL(Изменения_УУ.ОрганизацияПолучательРасходов, СписокОС.ОрганизацияПолучательРасходовУУ) КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ЕСТЬNULL(Изменения_УУ.ЛиквидационнаяСтоимость, ЕСТЬNULL(ПараметрыАмортизацииОСУУ.ЛиквидационнаяСтоимость, 0)) КАК ЛиквидационнаяСтоимость,
	|
	|	ВЫБОР
	|		КОГДА Изменения_УУ.НачалоМесяца = &НачалоПредыдущегоМесяца
	|			ТОГДА МестонахождениеНаНачалоМесяца.Местонахождение
	|		ИНАЧЕ ЕСТЬNULL(Изменения_УУ.Местонахождение, МестонахождениеОС.Местонахождение) 
	|	КОНЕЦ КАК Местонахождение,
	|
	|	ЕСТЬNULL(ВыработкаОС.ОбъемВыработкиУУ, 0) КАК ОбъемВыработки,
	|
	|	ПараметрыАмортизацииОСУУ.СрокИспользования КАК СрокИспользованияУУ,
	|	ЕСТЬNULL(Изменения_УУ.КоэффициентУскорения, ПараметрыАмортизацииОСУУ.КоэффициентУскорения) КАК КоэффициентУскоренияУУ,
	|	ЕСТЬNULL(Изменения_УУ.МетодНачисленияАмортизации, ПараметрыАмортизацииОСУУ.МетодНачисленияАмортизации) КАК МетодНачисленияАмортизации
	|ПОМЕСТИТЬ ВТ_СписокОССИзмененнымиПараметрами_УУ
	|ИЗ
	|	ВТ_Изменения_УУ КАК Изменения_УУ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокОбъектовУчета_УУ КАК СписокОС
	|		ПО (СписокОС.Организация = Изменения_УУ.Организация)
	|			И (СписокОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (СписокОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО (ПорядокУчетаОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПорядокУчетаОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПорядокУчетаОС КАК ПорядокУчетаОСНаНачалоМесяца
	|		ПО (ПорядокУчетаОСНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПорядокУчетаОСНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьОС_НаНачалоМесяца КАК СтоимостьОС
	|		ПО (СтоимостьОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (СтоимостьОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредварительнаяСтоимостьОС КАК ПредварительнаяСтоимостьОС
	|		ПО (ПредварительнаяСтоимостьОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПредварительнаяСтоимостьОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС_НаНачалоМесяца КАК АмортизацияОС
	|		ПО (АмортизацияОС.Организация = Изменения_УУ.Организация)
	|			И (АмортизацияОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (АмортизацияОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеОС
	|		ПО (МестонахождениеОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (МестонахождениеОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МестонахождениеОС КАК МестонахождениеНаНачалоМесяца
	|		ПО (МестонахождениеНаНачалоМесяца.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (МестонахождениеНаНачалоМесяца.НачалоМесяца = &НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
	|		ПО (ПервоначальныеСведенияОС.Организация = Изменения_УУ.Организация)
	|			И (ПервоначальныеСведенияОС.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПервоначальныеСведенияОС.НачалоМесяца = СписокОС.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыАмортизацииОСУУ КАК ПараметрыАмортизацииОСУУ
	|		ПО (ПараметрыАмортизацииОСУУ.Организация = Изменения_УУ.Организация)
	|			И (ПараметрыАмортизацииОСУУ.ОсновноеСредство = Изменения_УУ.ОсновноеСредство)
	|			И (ПараметрыАмортизацииОСУУ.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыработкаОС КАК ВыработкаОС
	|		ПО (ВыработкаОС.ОсновноеСредство = ПорядокУчетаОС.ОсновноеСредство)
	|			И (ВыработкаОС.ПоказательНаработки = ПорядокУчетаОС.ПоказательНаработки)
	|			И (ВыработкаОС.НачалоМесяца = Изменения_УУ.НачалоМесяца)
	|ГДЕ
	|	ПервоначальныеСведенияОС.ПорядокУчетаУУ В (&ПорядокУчетаПриКоторомНачисляетсяАмортизация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтоДоначисление, 
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СпособыОтраженияРасходов
	
	ТекстЗапроса =
	"ВЫБРАТЬ 
	|	СписокВсехОС.СпособОтраженияРасходов КАК Ссылка
	|ПОМЕСТИТЬ втСпособыОтраженияРасходов
	|ИЗ 
	|	ВТ_СписокОбъектовУчета КАК СписокВсехОС
	|ГДЕ 
	|	СписокВсехОС.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ 
	|	СписокВсехОС.СпособОтраженияРасходов
	|ИЗ 
	|	ВТ_Изменения_УУ КАК СписокВсехОС
	|ГДЕ 
	|	СписокВсехОС.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
	|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
	|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
	|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизация.Коэффициент                          КАК База,
	|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
	|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизации КАК СуммаБазы,
	|	ЛОЖЬ                                                    КАК ЭтоАмортизационнаяПремия
	|ПОМЕСТИТЬ ВТ_СпособыОтраженияРасходов
	|ИЗ
	|	Документ.ИзменениеПараметровОС2_4.Амортизация КАК ТаблицаАмортизация
	|ГДЕ
	|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходов КАК СпособыОтраженияРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаАмортизация.Ссылка                               КАК СпособОтраженияРасходов,
	|	ТаблицаАмортизация.Подразделение                        КАК Подразделение,
	|	ТаблицаАмортизация.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ТаблицаАмортизация.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ТаблицаАмортизация.АналитикаРасходов                    КАК АналитикаРасходов,
	|	ТаблицаАмортизация.ПередаватьРасходыВДругуюОрганизацию  КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаАмортизация.ОрганизацияПолучательРасходов        КАК ОрганизацияПолучательРасходов,
	|	ТаблицаАмортизация.Коэффициент                          КАК База,
	|	ТаблицаАмортизация.СуммаКоэффициентовПредыдущихСтрок    КАК БазаНакопленная,
	|	ТаблицаАмортизация.Ссылка.СуммаКоэффициентовАмортизационнойПремии КАК СуммаБазы,
	|	ИСТИНА                                                  КАК ЭтоАмортизационнаяПремия
	|ИЗ
	|	Документ.ИзменениеПараметровОС2_4.АмортизационнаяПремия КАК ТаблицаАмортизация
	|ГДЕ
	|	ТаблицаАмортизация.Ссылка В (ВЫБРАТЬ СпособыОтраженияРасходов.Ссылка ИЗ втСпособыОтраженияРасходов КАК СпособыОтраженияРасходов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов,
	|	ЭтоАмортизационнаяПремия";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
		
КонецПроцедуры

Процедура ТекстЗапросаПроверкаПараметровРасчета(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
	// НеУказанСрокИспользованияУУ
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|		И СписокОС.РесурсНазначенный = 0 КАК НеУказанСрокИспользованияУУ,
	|
	// НеУказанОбъемНаработки
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И СписокОС.РесурсНазначенный = 0 КАК НеУказанОбъемНаработки
	|
	|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчета_УУ
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.НачислятьАмортизациюУУ 
	|	И СписокОС.ОстаточнаяСтоимость > 0
	|	И (СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|			И СписокОС.РесурсНазначенный = 0
	|		ИЛИ СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И СписокОС.РесурсНазначенный = 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	//
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
	|
	// НеУказанСрокИспользованияУУ
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|		И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0 КАК НеУказанСрокИспользованияУУ,
	|
	// НеУказанОбъемНаработки
	|	СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0 КАК НеУказанОбъемНаработки,
	|
	// НеУказанРесурсВыработанный
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод ЕСТЬ NULL
	|		ИЛИ СписокОС.РесурсВыработанныйЗаПрошлыйПериод ЕСТЬ NULL КАК НеУказанРесурсВыработанный
	|
	|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчетаИзменения_УУ
	|ИЗ
	|	ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.НачислятьАмортизациюУУ 
	|	И (СписокОС.Стоимость - СписокОС.Амортизация) > 0
	|	И (СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный)
	|			И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0
	|		ИЛИ СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|			И ЕСТЬNULL(СписокОС.РесурсНазначенный, 0) = 0
	|		ИЛИ СписокОС.РесурсВыработанныйЗаТекущийПериод ЕСТЬ NULL
	|		ИЛИ СписокОС.РесурсВыработанныйЗаПрошлыйПериод ЕСТЬ NULL)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СписокОС.Организация КАК Организация,
		|	СписокОС.ОсновноеСредство КАК ОбъектУчета,
		|
		// НеУказанСрокИспользованияБУ
		|	СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ В (
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоСуммеЧиселЛетИспользования))
		|		И СписокОС.СрокИспользованияДляВычисленияАмортизации = 0 КАК НеУказанСрокИспользованияБУ,
		|
		// НеУказанСрокИспользованияНУ
		|	СписокОС.НачислятьАмортизациюНУ
		|		И СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
		|		И СписокОС.СрокПолезногоИспользованияНУ = 0 КАК НеУказанСрокИспользованияНУ,
		|
		// НеУказанаАмортизационнаяГруппа
		|	СписокОС.НачислятьАмортизациюНУ
		|		И СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)
		|		И СписокОС.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка) КАК НеУказанаАмортизационнаяГруппа,
		|
		// НеУказанОбъемНаработки
		|	СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
		|		И СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации = 0 КАК НеУказанОбъемНаработки,
		|
		// НеУказанКоэффициентАмортизации
		|	СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФ)
		|		И СписокОС.КоэффициентАмортизацииБУ = 0 КАК НеУказанКоэффициентАмортизации,
		|
		// НеУказанКоэффициентУскорения
		|	СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
		|		И СписокОС.КоэффициентУскоренияБУ = 0 КАК НеУказанКоэффициентУскорения,
		|
		|	СписокОС.СтатьяРасходовАмортизационнойПремии = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		И СписокОС.АмортизационнаяПремия <> 0 КАК НеУказанаСтатьяРасходовАмортизационнойПремии
		|
		|ПОМЕСТИТЬ ВТ_ПроверкаПараметровРасчета_БУ
		|ИЗ
		|	ВТ_СписокОССПараметрами_БУ КАК СписокОС
		|ГДЕ
		|	СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ В (
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоСуммеЧиселЛетИспользования))
		|		И СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
		|
		|	ИЛИ СписокОС.НачислятьАмортизациюНУ
		|		И СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
		|		И СписокОС.СрокПолезногоИспользованияНУ = 0
		|
		|	ИЛИ СписокОС.НачислятьАмортизациюНУ
		|		И СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный)
		|		И СписокОС.АмортизационнаяГруппа = ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПустаяСсылка)
		|
		|	ИЛИ СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
		|		И СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации = 0
		|
		|	ИЛИ СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФ)
		|		И СписокОС.КоэффициентАмортизацииБУ = 0
		|
		|	ИЛИ СписокОС.НачислятьАмортизациюБУ
		|		И СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка)
		|		И СписокОС.КоэффициентУскоренияБУ = 0
		|
		|	ИЛИ СписокОС.СтатьяРасходовАмортизационнойПремии = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|		И СписокОС.АмортизационнаяПремия <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектУчета";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_БУ(СписокЗапросов, ПараметрыРасчета)

	Если НЕ ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.Подразделение,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов)          КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов)    КАК АналитикаРасходов,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаБУ
	|		ИНАЧЕ Амортизация.СуммаБУ
	|	КОНЕЦ КАК СуммаБУ,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаНУ_регл
	|		ИНАЧЕ Амортизация.СуммаНУ
	|	КОНЕЦ КАК СуммаНУ,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаПР_регл
	|		ИНАЧЕ Амортизация.СуммаПР
	|	КОНЕЦ КАК СуммаПР,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаВР_регл
	|		ИНАЧЕ Амортизация.СуммаВР
	|	КОНЕЦ КАК СуммаВР,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаЦФ
	|		ИНАЧЕ Амортизация.СуммаЦФ
	|	КОНЕЦ КАК СуммаЦФ,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаНУЦФ_регл
	|		ИНАЧЕ Амортизация.СуммаНУЦФ
	|	КОНЕЦ КАК СуммаНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаПРЦФ_регл
	|		ИНАЧЕ Амортизация.СуммаПРЦФ
	|	КОНЕЦ КАК СуммаПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка) ТОГДА
	|			&СуммаВРЦФ_регл
	|		ИНАЧЕ Амортизация.СуммаВРЦФ
	|	КОНЕЦ КАК СуммаВРЦФ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов,
	|	Амортизация.НачислятьИзнос,
	|	Амортизация.ЭтоАмортизационнаяПремия
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_БУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.ПериодРасчета              КАК ПериодРасчета,
	|		СписокОС.Организация                КАК Организация,
	|		СписокОС.Подразделение              КАК Подразделение,
	|		СписокОС.ОсновноеСредство           КАК ОсновноеСредство,
	|		СписокОС.ГруппаФинансовогоУчета     КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		СписокОС.СтатьяРасходов             КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов          КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов    КАК СпособОтраженияРасходов,
	|
			// СуммаБУ
	|		ВЫБОР 
	|			КОГДА СУММА(СписокОС.СуммаБУ - ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьРегл
	|				ТОГДА СУММА(СписокОС.СуммаБУ - ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьРегл
	|		КОНЕЦ КАК СуммаБУ,
	|
			// СуммаНУ
	|		ВЫБОР 
	|			КОГДА СУММА(СписокОС.СуммаНУ - ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьНУ
	|				ТОГДА СУММА(СписокОС.СуммаНУ - ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьНУ
	|		КОНЕЦ КАК СуммаНУ,
	|
			// СуммаПР
	|		ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос ИЛИ СписокОС.ОстаточнаяСтоимостьПР = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ОстаточнаяСтоимостьПР > 0
	|				ТОГДА 
	|					ВЫБОР КОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьПР
	|						ТОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПР
	|					КОНЕЦ
	|			ИНАЧЕ 
	|					ВЫБОР КОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) > СписокОС.ОстаточнаяСтоимостьПР
	|						ТОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПР
	|					КОНЕЦ
	|		КОНЕЦ КАК СуммаПР,
	|
			// СуммаВР
	|		ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос
	|				ТОГДА 0
	|			КОГДА СУММА(СписокОС.СуммаБУ - ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьРегл
	|				ТОГДА СУММА(СписокОС.СуммаБУ - ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьРегл
	|		КОНЕЦ
	|		- ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос
	|				ТОГДА 0
	|			КОГДА СУММА(СписокОС.СуммаНУ - ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьНУ
	|				ТОГДА СУММА(СписокОС.СуммаНУ - ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьНУ
	|		КОНЕЦ
	|		- ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос ИЛИ СписокОС.ОстаточнаяСтоимостьПР = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ОстаточнаяСтоимостьПР > 0
	|				ТОГДА 
	|					ВЫБОР КОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьПР
	|						ТОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПР
	|					КОНЕЦ
	|			ИНАЧЕ 
	|					ВЫБОР КОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) > СписокОС.ОстаточнаяСтоимостьПР
	|						ТОГДА СУММА(СписокОС.СуммаПР - ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПР
	|					КОНЕЦ
	|		КОНЕЦ КАК СуммаВР,
	|
			// СуммаЦФ
	|		ВЫБОР 
	|			КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьЦФ
	|				ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьЦФ
	|		КОНЕЦ КАК СуммаЦФ,
	|
			// СуммаНУЦФ
	|		ВЫБОР 
	|			КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьНУЦФ
	|				ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьНУЦФ
	|		КОНЕЦ КАК СуммаНУЦФ,
	|
			// СуммаПРЦФ
	|		ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос ИЛИ СписокОС.ОстаточнаяСтоимостьПРЦФ = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ОстаточнаяСтоимостьПРЦФ > 0
	|				ТОГДА
	|					ВЫБОР КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьПРЦФ
	|						ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПРЦФ
	|					КОНЕЦ
	|			ИНАЧЕ
	|					ВЫБОР КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) > СписокОС.ОстаточнаяСтоимостьПРЦФ
	|						ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПРЦФ
	|					КОНЕЦ
	|		КОНЕЦ КАК СуммаПРЦФ,
	|
			// СуммаВРЦФ
	|		ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос
	|				ТОГДА 0
	|			КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьЦФ
	|				ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаБУ * СписокОС.КоэффициентЦФБУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьЦФ
	|		КОНЕЦ 
	|		- ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос
	|				ТОГДА 0
	|			КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьНУЦФ
	|				ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаНУ * СписокОС.КоэффициентЦФНУ КАК ЧИСЛО (15,2)))
	|			ИНАЧЕ СписокОС.ОстаточнаяСтоимостьНУЦФ
	|		КОНЕЦ
	|		- ВЫБОР 
	|			КОГДА СписокОС.НачислятьИзнос ИЛИ СписокОС.ОстаточнаяСтоимостьПРЦФ = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ОстаточнаяСтоимостьПРЦФ > 0
	|				ТОГДА
	|					ВЫБОР КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) < СписокОС.ОстаточнаяСтоимостьПРЦФ
	|						ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПРЦФ
	|					КОНЕЦ
	|			ИНАЧЕ
	|					ВЫБОР КОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2))) > СписокОС.ОстаточнаяСтоимостьПРЦФ
	|						ТОГДА СУММА(ВЫРАЗИТЬ(СписокОС.СуммаПР * СписокОС.КоэффициентЦФПР КАК ЧИСЛО (15,2)))
	|						ИНАЧЕ СписокОС.ОстаточнаяСтоимостьПРЦФ
	|					КОНЕЦ
	|		КОНЕЦ КАК СуммаВРЦФ,
	|
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		СписокОС.НачислятьИзнос КАК НачислятьИзнос,
	|		СписокОС.ЭтоАмортизационнаяПремия КАК ЭтоАмортизационнаяПремия
	|	ИЗ
	|		(ВЫБРАТЬ
	|			&КонецМесяца                          КАК ПериодРасчета,
	|			СписокОС.Организация                  КАК Организация,
	|			СписокОС.Местонахождение              КАК Подразделение,
	|			СписокОС.ОсновноеСредство             КАК ОсновноеСредство,
	|			СписокОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|			СписокОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|			СписокОС.СтатьяРасходовБУ             КАК СтатьяРасходов,
	|			СписокОС.АналитикаРасходовБУ          КАК АналитикаРасходов,
	|			СписокОС.СпособОтраженияРасходовБУ    КАК СпособОтраженияРасходов,
	|
	|			&СуммаАмортизацииБУ КАК СуммаБУ,
	|			&СуммаАмортизацииНУ КАК СуммаНУ,
	|			ВЫБОР
	|				КОГДА СписокОС.СтоимостьДляВычисленияАмортизации <> 0
	|					ТОГДА ВЫРАЗИТЬ(СписокОС.ПРДляВычисленияАмортизации 
	|									* (&СуммаАмортизацииБУ) 
	|									/ СписокОС.СтоимостьДляВычисленияАмортизации КАК ЧИСЛО (15,2))
	|				ИНАЧЕ 0
	|			КОНЕЦ КАК СуммаПР,
	|
	|			ЛОЖЬ КАК ЭтоАмортизационнаяПремия,
	|			СписокОС.КоэффициентЦФБУ КАК КоэффициентЦФБУ,
	|			СписокОС.КоэффициентЦФНУ КАК КоэффициентЦФНУ,
	|			СписокОС.КоэффициентЦФПР КАК КоэффициентЦФПР,
	|			СписокОС.ПередаватьРасходыВДругуюОрганизациюБУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|			СписокОС.ОрганизацияПолучательРасходовБУ КАК ОрганизацияПолучательРасходов,
	|			СписокОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) КАК НачислятьИзнос,
	|
	|			СписокОС.ОстаточнаяСтоимостьРегл     КАК ОстаточнаяСтоимостьРегл,
	|			СписокОС.ОстаточнаяСтоимостьНУ       КАК ОстаточнаяСтоимостьНУ,
	|			СписокОС.ОстаточнаяСтоимостьПР       КАК ОстаточнаяСтоимостьПР,
	|			СписокОС.ОстаточнаяСтоимостьЦФ       КАК ОстаточнаяСтоимостьЦФ,
	|			СписокОС.ОстаточнаяСтоимостьНУЦФ     КАК ОстаточнаяСтоимостьНУЦФ,
	|			СписокОС.ОстаточнаяСтоимостьПРЦФ     КАК ОстаточнаяСтоимостьПРЦФ
	|		ИЗ
	|			ВТ_СписокОССПараметрами_БУ КАК СписокОС
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_БУ КАК ПроверкаПараметровРасчета
	|				ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|					И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|		ГДЕ
	|			ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	// Амортизационная премия
	|		ВЫБРАТЬ
	|			&КонецМесяца                          КАК ПериодРасчета,
	|			СписокОС.Организация                  КАК Организация,
	|			СписокОС.Местонахождение              КАК Подразделение,
	|			СписокОС.ОсновноеСредство             КАК ОсновноеСредство,
	|			СписокОС.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|			СписокОС.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|			СписокОС.СтатьяРасходовАмортизационнойПремии    КАК СтатьяРасходов,
	|			СписокОС.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходов,
	|			СписокОС.СпособОтраженияРасходовБУ    КАК СпособОтраженияРасходов,
	|
	|			0 КАК СуммаБУ,
	|			СписокОС.АмортизационнаяПремия КАК СуммаНУ,
	|			0 КАК СуммаПР,
	|
	|			ИСТИНА КАК ЭтоАмортизационнаяПремия,
	|			СписокОС.КоэффициентЦФБУ КАК КоэффициентЦФБУ,
	|			СписокОС.КоэффициентЦФНУ КАК КоэффициентЦФНУ,
	|			СписокОС.КоэффициентЦФПР КАК КоэффициентЦФПР,
	|			СписокОС.ПередаватьРасходыВДругуюОрганизациюБУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|			СписокОС.ОрганизацияПолучательРасходовБУ КАК ОрганизацияПолучательРасходов,
	|			СписокОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) КАК НачислятьИзнос,
	|
	|			СписокОС.ОстаточнаяСтоимостьРегл    КАК ОстаточнаяСтоимостьРегл,
	|			СписокОС.ОстаточнаяСтоимостьНУ      КАК ОстаточнаяСтоимостьНУ,
	|			СписокОС.ОстаточнаяСтоимостьПР      КАК ОстаточнаяСтоимостьПР,
	|			0                                   КАК ОстаточнаяСтоимостьЦФ,
	|			0                                   КАК ОстаточнаяСтоимостьНУЦФ,
	|			0                                   КАК ОстаточнаяСтоимостьПРЦФ
	|		ИЗ
	|			ВТ_СписокОССПараметрами_БУ КАК СписокОС
	|				ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_БУ КАК ПроверкаПараметровРасчета
	|				ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|					И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|		ГДЕ
	|			ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL
	|			И СписокОС.АмортизационнаяПремия <> 0
	|			И (&СуммаАмортизацииНУ) > 0
	|
	|		) КАК СписокОС
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СписокОС.ПериодРасчета,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.ЭтоАмортизационнаяПремия,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов,
	|		СписокОС.НачислятьИзнос,
	|		СписокОС.ОстаточнаяСтоимостьРегл,
	|		СписокОС.ОстаточнаяСтоимостьНУ,
	|		СписокОС.ОстаточнаяСтоимостьПР,
	|		СписокОС.ОстаточнаяСтоимостьЦФ,
	|		СписокОС.ОстаточнаяСтоимостьНУЦФ,
	|		СписокОС.ОстаточнаяСтоимостьПРЦФ
	|
	|	) КАК Амортизация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)
	|			И (СпособыОтраженияРасходов.ЭтоАмортизационнаяПремия = Амортизация.ЭтоАмортизационнаяПремия)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииБУ", ТекстЗапросаСуммаАмортизацииБУ());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииНУ", ТекстЗапросаСуммаАмортизацииНУ());
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаБУ",        ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаБУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНУ_регл",   ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаНУ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаПР_регл",   ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаПР"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаВР_регл",   ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаВР"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаЦФ",        ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаЦФ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаНУЦФ_регл", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаНУЦФ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаПРЦФ_регл", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаПРЦФ"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаВРЦФ_регл", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаВРЦФ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Местонахождение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	ВЫБОР 
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - (&СуммаАмортизацииУУ)) > СписокОС.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - СписокОС.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА СписокОС.ОстаточнаяСтоимость - СписокОС.ЛиквидационнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокОС.Стоимость,
	|	СписокОС.Амортизация,
	|	СписокОС.ОстаточнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.КоэффициентУскоренияУУ КАК КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстЗапросаСуммаАмортизацииУУ(Ложь, ПараметрыРасчета));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_ДоначислениеСгруппированная(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(СписокОС.СуммаУУ) КАК СуммаУУ
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Доначисление КАК СписокОС
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокОС.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Предварительный(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Местонахождение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходовУУ КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходовУУ КАК СпособОтраженияРасходов,
	|	ВЫБОР 
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - (&СуммаАмортизацииУУ)) > СписокОС.ЛиквидационнаяСтоимость
	|			ТОГДА &СуммаАмортизацииУУ
	|		КОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость) > 0
	|			ТОГДА (СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) - СписокОС.ЛиквидационнаяСтоимость)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходовУУ КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
	|	СписокОС.Стоимость,
	|	СписокОС.Амортизация,
	|	СписокОС.ОстаточнаяСтоимость - ЕСТЬNULL(Доначисление.СуммаУУ, 0) КАК ОстаточнаяСтоимость,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.КоэффициентУскоренияУУ КАК КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Предварительный
	|ИЗ
	|	ВТ_СписокОССПараметрами_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчета_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	НЕ СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаАмортизацииУУ", ТекстЗапросаСуммаАмортизацииУУ(Истина, ПараметрыРасчета));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РазукомплектацияОС.ОсновноеСредство КАК ИсходноеОсновноеСредство,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ТабличнаяЧасть.СтоимостьУУ КАК Стоимость,
	|	0 КАК Амортизация
	|ИЗ
	|	Документ.РазукомплектацияОС КАК РазукомплектацияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РазукомплектацияОС.ОС КАК ТабличнаяЧасть
	|		ПО ТабличнаяЧасть.Ссылка = РазукомплектацияОС.Ссылка
	|ГДЕ
	|	РазукомплектацияОС.Дата МЕЖДУ &НачалоМесяца И &КонецМесяца
	|	И РазукомплектацияОС.Проведен
	|	И РазукомплектацияОС.ОтражатьВУпрУчете
	|	И (РазукомплектацияОС.Организация, ТабличнаяЧасть.ОсновноеСредство) В
	|			(ВЫБРАТЬ
	|				СписокОС.Организация,
	|				СписокОС.ОбъектУчета
	|			ИЗ
	|				ПакетыАмортизации КАК СписокОС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	1 КАК Коэффициент,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ КАК ЭтоРазукомплектация,
	|	ЛОЖЬ КАК ПолученПриРазукомплектации, 
	|	СписокОС.Стоимость КАК Стоимость,
	|	0 КАК СтоимостьИзменение,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ ) = &НачалоМесяца
	|			ТОГДА СписокОС.Амортизация + ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокОС.Амортизация
	|	КОНЕЦ КАК Амортизация,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Предварительный КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|ГДЕ
	|	СписокОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СписокОС.ПолученПриРазукомплектации
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Приоритет,
	|	СписокОС.Период,
	|	СписокОС.Регистратор,
	|	СписокОС.Организация,
	|	СписокОС.Местонахождение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	0,
	|	1,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.КоэффициентУскоренияУУ,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ,
	|	СписокОС.ЭтоРазукомплектация КАК ЭтоРазукомплектация,
	|	СписокОС.ПолученПриРазукомплектации КАК ПолученПриРазукомплектации, 
	|	СписокОС.Стоимость КАК Стоимость,
	|	СписокОС.СтоимостьИзменение КАК СтоимостьИзменение,
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СписокОС.Период, МЕСЯЦ ) = &НачалоМесяца
	|			ТОГДА СписокОС.Амортизация + ЕСТЬNULL(Доначисление.СуммаУУ, 0) 
	|		ИНАЧЕ СписокОС.Амортизация
	|	КОНЕЦ,
	|	ИСТИНА
	|ИЗ
	|	ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетАмортизации_УУ_ДоначислениеСгруппированная КАК Доначисление
	|		ПО Доначисление.ОсновноеСредство = СписокОС.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПроверкаПараметровРасчетаИзменения_УУ КАК ПроверкаПараметровРасчета
	|		ПО ПроверкаПараметровРасчета.ОбъектУчета = СписокОС.ОсновноеСредство
	|			И ПроверкаПараметровРасчета.Организация = СписокОС.Организация
	|ГДЕ
	|	НЕ СписокОС.ЭтоДоначисление
	|	И ПроверкаПараметровРасчета.ОбъектУчета ЕСТЬ NULL";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаВыборкаРасчетаАмортизацииИзменений_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	&НачалоМесяца КАК Период,
	|	НЕОПРЕДЕЛЕНО КАК Регистратор,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	1 КАК Коэффициент,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.КоэффициентУскорения,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизацию,
	|	ЛОЖЬ КАК ЭтоРазукомплектация,
	|	ЛОЖЬ КАК ПолученПриРазукомплектации, 
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьИзменение,
	|	0 КАК Амортизация,
	|	ЛОЖЬ КАК ЭтоИзменения
	|ИЗ
	|	ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокОС
	|ГДЕ
	|	СписокОС.ОсновноеСредство В
	|			(ВЫБРАТЬ
	|				СписокОС.ОсновноеСредство
	|			ИЗ
	|				ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1 КАК Приоритет,
	|	СписокОС.Период,
	|	СписокОС.Регистратор,
	|	СписокОС.Организация,
	|	СписокОС.Местонахождение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство,
	|	СписокОС.СтатьяРасходовУУ,
	|	СписокОС.АналитикаРасходовУУ,
	|	СписокОС.СпособОтраженияРасходовУУ,
	|	0,
	|	1,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизациюУУ,
	|	СписокОС.ОрганизацияПолучательРасходовУУ,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный,
	|	СписокОС.МетодНачисленияАмортизации,
	|	СписокОС.ЛиквидационнаяСтоимость,
	|	СписокОС.КоэффициентУскоренияУУ,
	|	СписокОС.ОтработанныхМесяцевПолныхЛет,
	|	СписокОС.НачислятьАмортизациюУУ,
	|	ЛОЖЬ,
	|	ЛОЖЬ, 
	|	СписокОС.Стоимость,
	|	СписокОС.СтоимостьИзменение,
	|	СписокОС.Амортизация,
	|	ИСТИНА
	|ИЗ
	|	ВТ_СписокОССИзмененнымиПараметрами_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоДоначисление";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ_Доначисление(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный
	|ПОМЕСТИТЬ ВТ_АмортизацияИзменений_УУ_Доначисление
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК Подразделение,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ_Доначисление
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Период КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Предварительный_Доначисление КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.Период,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов
	|	ИЗ
	|		ВТ_АмортизацияИзменений_УУ_Доначисление КАК СписокОС
	|
	|	) КАК Амортизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаРасчетАмортизации_УУ(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СписокОС.Период КАК Период,
	|	СписокОС.Организация КАК Организация,
	|	СписокОС.Подразделение КАК Подразделение,
	|	СписокОС.ГруппаФинансовогоУчета,
	|	СписокОС.НаправлениеДеятельности,
	|	СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|	СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|	СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СписокОС.СуммаУУ КАК СуммаУУ,
	|	СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	СписокОС.РесурсВыработанныйЗаПрошлыйПериод,
	|	СписокОС.РесурсВыработанныйЗаТекущийПериод,
	|	СписокОС.РесурсНазначенный
	|ПОМЕСТИТЬ ВТ_АмортизацияИзменений_УУ
	|ИЗ
	|	&АмортизацияИзменений_УУ КАК СписокОС
	|ГДЕ
	|	СписокОС.ЭтоИзменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Амортизация.ПериодРасчета,
	|	Амортизация.Организация,
	|	Амортизация.Подразделение КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Амортизация.Подразделение
	|		ИНАЧЕ ЕСТЬNULL(СпособыОтраженияРасходов.Подразделение, Амортизация.Подразделение) 
	|	КОНЕЦ КАК КорПодразделение,
	|	Амортизация.ОсновноеСредство,
	|	Амортизация.ГруппаФинансовогоУчета,
	|	Амортизация.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.НаправлениеДеятельности, Амортизация.НаправлениеДеятельности) КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, Амортизация.СтатьяРасходов) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, Амортизация.АналитикаРасходов) КАК АналитикаРасходов,
	|	Амортизация.СпособОтраженияРасходов,
	|	ВЫБОР
	|		КОГДА Амортизация.СпособОтраженияРасходов <> ЗНАЧЕНИЕ(Документ.ИзменениеПараметровОС2_4.ПустаяСсылка)
	|			ТОГДА &РаспределениеСуммыУУ
	|		ИНАЧЕ Амортизация.СуммаУУ
	|	КОНЕЦ КАК СуммаУУ,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию, Амортизация.ПередаватьРасходыВДругуюОрганизацию) КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.ОрганизацияПолучательРасходов, Амортизация.ОрганизацияПолучательРасходов) КАК ОрганизацияПолучательРасходов
	|ПОМЕСТИТЬ ВТ_РасчетАмортизации_УУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СписокОС.Период КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Предварительный КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.ПериодРасчета КАК ПериодРасчета,
	|		СписокОС.Организация КАК Организация,
	|		СписокОС.Подразделение КАК Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство КАК ОсновноеСредство,
	|		СписокОС.СтатьяРасходов КАК СтатьяРасходов,
	|		СписокОС.АналитикаРасходов КАК АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ КАК СуммаУУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ_Доначисление КАК СписокОС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписокОС.Период,
	|		СписокОС.Организация,
	|		СписокОС.Подразделение,
	|		СписокОС.ГруппаФинансовогоУчета,
	|		СписокОС.НаправлениеДеятельности,
	|		СписокОС.ОсновноеСредство,
	|		СписокОС.СтатьяРасходов,
	|		СписокОС.АналитикаРасходов,
	|		СписокОС.СпособОтраженияРасходов,
	|		СписокОС.СуммаУУ,
	|		СписокОС.ПередаватьРасходыВДругуюОрганизацию,
	|		СписокОС.ОрганизацияПолучательРасходов
	|	ИЗ
	|		ВТ_АмортизацияИзменений_УУ КАК СписокОС
	|
	|	) КАК Амортизация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО (СпособыОтраженияРасходов.СпособОтраженияРасходов = Амортизация.СпособОтраженияРасходов)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РаспределениеСуммыУУ", ВнеоборотныеАктивыСлужебный.ТекстРаспределенияСуммы("Амортизация.СуммаУУ"));
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Процедура ТекстЗапросаНачисленнаяАмортизация(СписокЗапросов, ПараметрыРасчета)

	ТекстПодзапроса = "";
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		ТекстПодзапроса = ТекстПодзапроса + 
		"ВЫБРАТЬ
		|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
		|		РасчетАмортизации.Организация КАК Организация,
		|		РасчетАмортизации.Подразделение КАК Подразделение,
		|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
		|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство,
		|		РасчетАмортизации.ГруппаФинансовогоУчета,
		|		РасчетАмортизации.НаправлениеДеятельности,
		|		РасчетАмортизации.КорНаправлениеДеятельности,
		|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
		|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
		|		0 КАК СуммаУУ,
		|		РасчетАмортизации.СуммаБУ КАК СуммаБУ,
		|		РасчетАмортизации.СуммаНУ КАК СуммаНУ,
		|		РасчетАмортизации.СуммаПР КАК СуммаПР,
		|		РасчетАмортизации.СуммаВР КАК СуммаВР,
		|		РасчетАмортизации.СуммаЦФ КАК СуммаЦФ,
		|		РасчетАмортизации.СуммаНУЦФ КАК СуммаНУЦФ,
		|		РасчетАмортизации.СуммаПРЦФ КАК СуммаПРЦФ,
		|		РасчетАмортизации.СуммаВРЦФ КАК СуммаВРЦФ,
		|		РасчетАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
		|		РасчетАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
		|		РасчетАмортизации.НачислятьИзнос КАК НачислятьИзнос,
		|		РасчетАмортизации.ЭтоАмортизационнаяПремия КАК ЭтоАмортизационнаяПремия
		|	ИЗ
		|		ВТ_РасчетАмортизации_БУ КАК РасчетАмортизации
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	";
	КонецЕсли;
	
	ТекстПодзапроса = ТекстПодзапроса + 
	"	ВЫБРАТЬ
	|		РасчетАмортизации.ПериодРасчета КАК ПериодРасчета,
	|		РасчетАмортизации.Организация КАК Организация,
	|		РасчетАмортизации.Подразделение КАК Подразделение,
	|		РасчетАмортизации.КорПодразделение КАК КорПодразделение,
	|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство,
	|		РасчетАмортизации.ГруппаФинансовогоУчета,
	|		РасчетАмортизации.НаправлениеДеятельности,
	|		РасчетАмортизации.КорНаправлениеДеятельности,
	|		РасчетАмортизации.СтатьяРасходов КАК СтатьяРасходов,
	|		РасчетАмортизации.АналитикаРасходов КАК АналитикаРасходов,
	|		РасчетАмортизации.СуммаУУ КАК СуммаУУ,
	|		0 КАК СуммаБУ,
	|		0 КАК СуммаНУ,
	|		0 КАК СуммаПР,
	|		0 КАК СуммаВР,
	|		0 КАК СуммаЦФ,
	|		0 КАК СуммаНУЦФ,
	|		0 КАК СуммаПРЦФ,
	|		0 КАК СуммаВРЦФ,
	|		РасчетАмортизации.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|		РасчетАмортизации.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|		ЛОЖЬ КАК НачислятьИзнос,
	|		ЛОЖЬ КАК ЭтоАмортизационнаяПремия
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ КАК РасчетАмортизации";
	
	
	ТекстЗапросаВтНачисленнаяАмортизация(СписокЗапросов, ТекстПодзапроса, Истина);
	
КонецПроцедуры

Процедура ТекстЗапросаКоличествоОбработанныхДанных(СписокЗапросов, ПараметрыРасчета)

	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КОЛИЧЕСТВО(НачисленнаяАмортизация.ОсновноеСредство), 0) КАК КоличествоДанных
	|ИЗ
	|	(";
	
	Если ПараметрыРасчета.ИспользоватьРеглУчет Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство
		|	ИЗ
		|		ВТ_РасчетАмортизации_БУ КАК РасчетАмортизации
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|		РасчетАмортизации.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		ВТ_РасчетАмортизации_УУ КАК РасчетАмортизации
	|
	|	) КАК НачисленнаяАмортизация";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

Функция ТекстЗапросаСуммаАмортизацииУУ(УчестьДоначисление, ПараметрыРасчета)
	
	Если ПараметрыРасчета.ПорядокУчетаВНАВУпрУчете = Перечисления.ПорядокУчетаВНАВУпрУчете.ПоСтандартамМУ Тогда
		
		ТекстЗапроса = 
		"ВЫБОР 
		|	КОГДА НЕ СписокОС.НачислятьАмортизациюУУ ИЛИ (СписокОС.ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 ТОГДА
		|		0
		|	КОГДА (СписокОС.РесурсВыработанныйЗаПрошлыйПериод + СписокОС.РесурсВыработанныйЗаТекущийПериод) >= СписокОС.РесурсНазначенный ТОГДА
		|		СписокОС.ОстаточнаяСтоимость - &СуммаДоначисления - СписокОС.ЛиквидационнаяСтоимость
		// Линейный или ПропорциональноОбъемуПродукции
		|	КОГДА СписокОС.МетодНачисленияАмортизации В (
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный),
		|				ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)) 
		|			ИЛИ (СписокОС.РесурсНазначенный
		|					- СписокОС.РесурсВыработанныйЗаПрошлыйПериод
		|					- СписокОС.РесурсВыработанныйЗаТекущийПериод) <= 12 
		|				И СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
		|
		|		ВЫБОР 
		|			КОГДА СписокОС.ЭтоДоначисление
		|				ТОГДА ВЫРАЗИТЬ((СписокОС.СтоимостьДляВычисленияАмортизации - &СуммаДоначисления)
		|								* ((СписокОС.РесурсВыработанныйЗаТекущийПериод) / (СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод)) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ ВЫРАЗИТЬ((СписокОС.СтоимостьДляВычисленияАмортизации - &СуммаДоначисления - СписокОС.ЛиквидационнаяСтоимость) 
		|								* ((СписокОС.РесурсВыработанныйЗаТекущийПериод) / (СписокОС.РесурсНазначенный - СписокОС.РесурсВыработанныйЗаПрошлыйПериод)) КАК ЧИСЛО(31,2))
		|		КОНЕЦ
		|
			// УменьшаемогоОстатка
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
		|   
		|		ВЫРАЗИТЬ((СписокОС.СтоимостьДляВычисленияАмортизации - &СуммаДоначисления)
		|					* СписокОС.КоэффициентУскоренияУУ
		|					* СписокОС.РесурсВыработанныйЗаТекущийПериод
		|					/ (СписокОС.РесурсНазначенный 
		|						- СписокОС.КоэффициентУскоренияУУ * (СписокОС.РесурсВыработанныйЗаПрошлыйПериод - СписокОС.ОтработанныхМесяцевПолныхЛет)) КАК ЧИСЛО(31,2))
		|КОНЕЦ"; 
			
	Иначе
		
		ТекстЗапроса = 
		"ВЫБОР 
		|	КОГДА НЕ СписокОС.НачислятьАмортизациюУУ ИЛИ (СписокОС.ОстаточнаяСтоимость - &СуммаДоначисления) <= 0 ТОГДА
		|		0
		// Линейный
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокОС.РесурсНазначенный = 0
		|				ТОГДА 0
		|			ИНАЧЕ
		|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации / СписокОС.РесурсНазначенный КАК ЧИСЛО(31,2))
		|		КОНЕЦ
		|   
		// ПропорциональноОбъемуПродукции
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокОС.РесурсВыработанныйЗаТекущийПериод <> 0 И СписокОС.РесурсНазначенный <> 0 
		|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
		|									* СписокОС.РесурсВыработанныйЗаТекущийПериод 
		|									/ СписокОС.РесурсНазначенный КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	
		// УменьшаемогоОстатка
		|	КОГДА СписокОС.МетодНачисленияАмортизации = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
		|		ВЫБОР 
		|			КОГДА СписокОС.РесурсНазначенный = 0
		|				ТОГДА 0
		|			ИНАЧЕ
		|				ВЫБОР
		|					КОГДА ГОД(СписокОС.ДатаПоследнегоИзменения) = ГОД(&КонецМесяца) 
		|						ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
		|										/ СписокОС.РесурсНазначенный 
		|										* СписокОС.КоэффициентУскоренияУУ КАК ЧИСЛО(31,2))
		|					ИНАЧЕ
		|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
		|									/ СписокОС.РесурсНазначенный 
		|									* СписокОС.КоэффициентУскоренияУУ КАК ЧИСЛО(31,2))
		|				КОНЕЦ
		|		КОНЕЦ 
		|КОНЕЦ"; 
				
	КонецЕсли;
	
	Если УчестьДоначисление Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаДоначисления", "ЕСТЬNULL(Доначисление.СуммаУУ, 0)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаДоначисления", "0");
	КонецЕсли; 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммаАмортизацииБУ()
	
	ТекстЗапроса =
	"ВЫБОР 
	|	КОГДА НЕ СписокОС.НачислятьАмортизациюБУ ТОГДА
	|		0
	// Линейный
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.Линейный) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации / СписокОС.СрокИспользованияДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * 12 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации 
	|							* СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|   
	// ПропорциональноОбъемуПродукции
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.ОбъемВыработки <> 0 И СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации <> 0 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|									* СписокОС.ОбъемВыработки 
	|									/ СписокОС.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|
	// ПоЕНАОФ
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФ) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.КоэффициентАмортизацииБУ <> 0 ТОГДА
	|				ВЫБОР 
	|					КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) 
	|						ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * СписокОС.КоэффициентАмортизацииБУ / 100 / 12 КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * СписокОС.КоэффициентАмортизацииБУ / 100 * СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|
	// ПоЕНАОФНа1000кмПробега
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.ОбъемВыработки <> 0 И СписокОС.КоэффициентАмортизацииБУ <> 0 
	|				ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * СписокОС.ОбъемВыработки / 1000 * СписокОС.КоэффициентАмортизацииБУ / 100 КАК ЧИСЛО(31,2))
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|
	// УменьшаемогоОстатка
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.СрокИспользованияДляВычисленияАмортизации = 0
	|				ТОГДА 0
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) ТОГДА
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаПоследнегоИзменения) = ГОД(&КонецМесяца) 
	|						ТОГДА ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|										/ СписокОС.СрокИспользованияДляВычисленияАмортизации 
	|										* СписокОС.КоэффициентУскоренияБУ КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
	|									/ СписокОС.СрокИспользованияДляВычисленияАмортизации 
	|									* СписокОС.КоэффициентУскоренияБУ КАК ЧИСЛО(31,2))
	|				КОНЕЦ 
	|			ИНАЧЕ
	|				ВЫБОР
	|					КОГДА ГОД(СписокОС.ДатаПоследнегоИзменения) = ГОД(&КонецМесяца) ТОГДА
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации * 12 
	|							* СписокОС.КоэффициентПериода 
	|							* СписокОС.КоэффициентУскоренияБУ КАК ЧИСЛО(31,2))
	|					ИНАЧЕ
	|						ВЫРАЗИТЬ(СписокОС.СтоимостьНаНачалоГода 
	|							/ СписокОС.СрокИспользованияДляВычисленияАмортизации * 12 
	|							* СписокОС.КоэффициентПериода 
	|							* СписокОС.КоэффициентУскоренияБУ КАК ЧИСЛО(31,2))
	|				КОНЕЦ
	|		КОНЕЦ 
	|
	// ПоСуммеЧиселЛетИспользования
	|	КОГДА СписокОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоСуммеЧиселЛетИспользования) ТОГДА
	|		ВЫБОР 
	|			КОГДА СписокОС.ГрафикАмортизации = ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка) ТОГДА
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * &ЧислоЛетОставшихся / &СуммаЧиселЛет / 12 КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				ВЫРАЗИТЬ(СписокОС.СтоимостьДляВычисленияАмортизации * &ЧислоЛетОставшихся / &СуммаЧиселЛет * СписокОС.КоэффициентПериода КАК ЧИСЛО(31,2))
	|		КОНЕЦ
	|КОНЕЦ";				
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЧислоЛетОставшихся", "(СписокОС.ЧислоЛетПользования - СписокОС.ЧислоПрошедшихЛет)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаЧиселЛет", "((СписокОС.ЧислоЛетПользования + 1) * СписокОС.ЧислоЛетПользования / 2)");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСуммаАмортизацииНУ()

	ТекстЗапроса =
	"ВЫБОР 
	|	КОГДА НЕ СписокОС.НачислятьАмортизациюНУ ТОГДА
	|		0
	|	КОГДА СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) ТОГДА
	|		ВЫБОР 
	|			КОГДА (СписокОС.СрокПолезногоИспользованияНУ - СписокОС.ФактическийСрокПриПереходеКЛинейномуМетоду) > 0 ТОГДА
	|				ВЫРАЗИТЬ((СписокОС.СтоимостьНУНачальныйОстаток - СписокОС.АмортизационнаяПремия - СписокОС.АмортизацияПриПереходеКЛинейномуМетоду)
	|					 / (СписокОС.СрокПолезногоИспользованияНУ - СписокОС.ФактическийСрокПриПереходеКЛинейномуМетоду) * СписокОС.СпециальныйКоэффициентНУ КАК ЧИСЛО(31,2))
	|			ИНАЧЕ
	|				0
	|		КОНЕЦ
	|	КОГДА СписокОС.МетодНачисленияАмортизацииНУ = ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Нелинейный) ТОГДА
	|		ВЫРАЗИТЬ((СписокОС.СтоимостьНУНачальныйОстаток - СписокОС.АмортизационнаяПремия - СписокОС.АмортизацияНУНачальныйОстаток)
	|			* СписокОС.НормаАмортизации / 100
	|			* СписокОС.СпециальныйКоэффициентНУ КАК ЧИСЛО(31,2))
	|КОНЕЦ";				
	
	Возврат ТекстЗапроса;

КонецФункции
 
#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
