#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значание:
//	Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ТипРасходов");
	Результат.Добавить("ВариантРаспределенияРасходовРегл");
	Результат.Добавить("ВариантРаспределенияРасходовУпр; ВариантРаспределенияРасходовУпр");
	Результат.Добавить("ТипЗначения; ТипЗначения");
	Результат.Добавить("ВариантРаздельногоУчетаНДС");
	Результат.Добавить("ВидАктива");
	Результат.Добавить("ПринятиеКналоговомуУчету");
	Результат.Добавить("ВидРасходов");
	Результат.Добавить("ВидПрочихРасходов");
	Результат.Добавить("КосвенныеЗатратыНУ; ПрямыеКосвенныеНУ");
	Результат.Добавить("ВидРБП");
	Результат.Добавить("ПризнаватьРасходамиПриУСН");
	Результат.Добавить("ВидДеятельностиРасходов");
	Результат.Добавить("ВидДеятельностиДляНалоговогоУчетаЗатрат");
	
	Возврат Результат;
	
КонецФункции

// Функция определяет реквизиты выбранной статьи расходов.
//
// Параметры:
//  СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - Ссылка на статью расходов.
//
// Возвращаемое значение:
//	Структура - реквизиты статьи расходов.
//
Функция ПолучитьРеквизитыСтатьиРасходов(Знач СтатьяРасходов) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СтатьиРасходов.СпособРаспределенияПоНаправлениямДеятельности КАК СпособРаспределения,
	|	СтатьиРасходов.ТипЗначения КАК ТипЗначения,
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|");
	
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СпособРаспределения = Выборка.СпособРаспределения;
		ТребуетсяСпособРаспределения = Не Выборка.ТипЗначения.СодержитТип(Тип("СправочникСсылка.НаправленияДеятельности"));
		ТипЗначения = Выборка.ТипЗначения;
		СтатьяРасходовРаспределение = Выборка.СтатьяРасходов;
	Иначе
		СпособРаспределения = Справочники.СпособыРаспределенияПоНаправлениямДеятельности.ПустаяСсылка();
		ТребуетсяСпособРаспределения = Ложь;
		ТипЗначения = Неопределено;
		СтатьяРасходовРаспределение = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("СпособРаспределения, ТребуетсяСпособРаспределения, ТипЗначения",
		СпособРаспределения,
		ТребуетсяСпособРаспределения,
		ТипЗначения);
	СтруктураРеквизитов.Вставить("СтатьяРасходов", СтатьяРасходовРаспределение);
	
	Возврат СтруктураРеквизитов;

КонецФункции

// Функция определяет правило распределения на себестоимость товаров для статьи расходов.
//
// Параметры:
//  СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - Ссылка на статью расходов.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ПравилаРаспределенияНаСебестоимостьТоваров - правило распределения статьи расходов.
//
Функция ПолучитьПравилоРаспределения(СтатьяРасходов) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СтатьиРасходов.ПравилоРаспределенияНаСебестоимость КАК ПравилоРаспределения
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|	И (СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|");
	
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ПравилоРаспределения = Выборка.ПравилоРаспределения;
	Иначе
		ПравилоРаспределения = Неопределено;
	КонецЕсли;
	
	Возврат ПравилоРаспределения;

КонецФункции

// Функция определяет аналитику расходов для подстановки в документ по статье расходов.
//
// Параметры:
//  СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - Ссылка на статью расходов
//	Объект - ДанныеФормыСтруктура - Текущий объект.
//
// Возвращаемое значение:
//	СправочникСсылка, ДокументСсылка - значение аналитики расходов.
//
Функция ПолучитьАналитикуРасходовПоУмолчанию(СтатьяРасходов, Объект) Экспорт
	
	ОписаниеТипов = Новый ОписаниеТипов(СтатьяРасходов.ТипЗначения);
	АналитикаРасходов = ОписаниеТипов.ПривестиЗначение();
	
	Если СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Склады")
	   И Объект.Свойство("Склад") Тогда
	   
		АналитикаРасходов = Объект.Склад;
	   
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("ДокументСсылка.ЗаказПоставщику")
		И Объект.Свойство("ЗаказПоставщику") Тогда
		
		АналитикаРасходов = Объект.ЗаказПоставщику;
		
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("ДокументСсылка.ПриобретениеТоваровУслуг")
		И ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		АналитикаРасходов = Объект.Ссылка;
		
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Партнеры")
		И Объект.Свойство("Партнер") Тогда
		
		АналитикаРасходов = Объект.Партнер;	
		
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Организации")
	   И Объект.Свойство("Организация") Тогда
	   
		АналитикаРасходов = Объект.Организация;
		
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия")
	   И Объект.Свойство("Подразделение") Тогда
	   
		АналитикаРасходов = Объект.Подразделение;
		
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица")
		И Объект.Свойство("ПодотчетноеЛицо") Тогда
		
		АналитикаРасходов = Объект.ПодотчетноеЛицо;
		
	ИначеЕсли СтатьяРасходов.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Кассы")
		И Объект.Свойство("Касса") Тогда
		
		АналитикаРасходов = Объект.Касса;
		
	ИначеЕсли СтатьяРасходов.АналитикаРасходовЗаказРеализация Тогда
		АналитикаРасходов =  Документы.РеализацияТоваровУслуг.ПустаяСсылка();
		
	КонецЕсли;
	
	Возврат АналитикаРасходов;
	
КонецФункции

// Процедура заполняет список хозяйственных операций, для которых используется
// ограничение отбора статьей расходов.
//
// Параметры:
//	СписокОпераций - СписокЗначений - Список хозяйственных операций и их представлений.
//
Процедура ЗаполнитьСписокХозяйственныхОпераций(СписокОпераций, ВариантРаспределенияРасходовУпр = Неопределено, ВариантРаспределенияРасходовРегл = Неопределено) Экспорт
	
	СписокОпераций.Очистить();
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.АвансовыйОтчет, НСтр("ru = 'Прочие расходы подотчетного лица'"));
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика, НСтр("ru = 'Услуги сторонних организаций'"));
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПрочиеРасходы, НСтр("ru = 'Выдача денежных средств на прочие расходы'"));
	
	Если ВариантРаспределенияРасходовУпр <> Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров
	 И ВариантРаспределенияРасходовРегл <> Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьВнутреннееПотребление") Тогда
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию, НСтр("ru = 'Внутреннее потребление товаров'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКачествоТоваров") Тогда
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПорчаТоваров, НСтр("ru = 'Изменение стоимости испорченного товара'"));
		КонецЕсли;
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПересортицаТоваров, НСтр("ru = 'Изменение стоимости при пересортице'"));
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеТоваров, НСтр("ru = 'Списания недостач товаров'"));
	КонецЕсли;
	
	Если ВариантРаспределенияРасходовУпр = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности
	 ИЛИ ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности Тогда
		СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности, НСтр("ru = 'Списание задолженности партнеров'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПлатежнымиКартами") Тогда
			СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ОтчетБанкаПоОперациямЭквайринга, НСтр("ru = 'Эквайринговая комиссия'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Производит проверку заполнения реквизитов аналитик статей расходов в переданном объекте.
//
// Параметры:
// 		Объект - СправочникОбъект, ДокументОбъект, ДанныеФормыСтруктура - Объект ИБ предназначенный для проверки
// 		Реквизиты - Строка, Структура, Массив - Реквизиты статей расходов для проверки
// 			<Строка> Перечисление пар реквизитов для проверки в формате "СтатьяРаходов1, АналитикаРасходов1, СтатьяРаходов2, АналитикаРасходов2, ..."
// 				Пустая строка трактуется как "СтатьяРасходов, АналитикаРасходов"
// 			<Структура> Ключ: Строка с именем табличной части; Значение - Строка в нотации как у параметра типа <Строка>
// 			<Массив> Массив, элементы которого либо структуры в нотации как у параметра с типа <Структура>, либо строки в нотации <Строка>
// 		НепроверяемыеРеквизиты - Массив - Массив для накопления не проверяемых реквизитов переданного объекта
// 		Отказ - Булево - Признак наличия ошибок заполнения аналитик расходов переданного объекта
// 		ДополнительныеПараметры - Структура - При наличии свойства "ПрограммнаяПроверка", ошибки записываются в эту структуру, пользователю не выводятся.
//
Процедура ПроверитьЗаполнениеАналитик(Объект, Реквизиты = "", НепроверяемыеРеквизиты = Неопределено, Отказ = Ложь,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	Ошибки = Новый Структура;
	Ошибки.Вставить("СписокОшибок", Новый Массив);
	Ошибки.Вставить("ГруппыОшибок", Новый Соответствие);
	Ошибки.Вставить("ПрефиксОбъекта", ?(ТипЗнч(Объект)=Тип("УправляемаяФорма"), "", "Объект."));
	
	МассивОбработки = Новый Массив;
	Если ТипЗнч(Реквизиты) = Тип("Массив") Тогда
		МассивОбработки = Реквизиты;
	Иначе
		МассивОбработки.Добавить(Реквизиты);
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из МассивОбработки Цикл
		
		Если ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда
			ПроверкаЗаполненияАналитикТЧОбъекта(Объект, ЭлементМассива, НепроверяемыеРеквизиты, Ошибки);
		Иначе
			ПроверкаЗаполненияАналитикОбъекта(Объект, ЭлементМассива, НепроверяемыеРеквизиты, Ошибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ПрограммнаяПроверка") Тогда
		ДополнительныеПараметры.Вставить("Ошибки", Ошибки);
	Иначе
		Если Ошибки.СписокОшибок.Количество() <> 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Производит заполнение условного оформления формы
//
// Параметры:
// 		УсловноеОФормление - УсловноеОформлениеКомпоновкиДанных - Условное оформление формы объекта
// 		Реквизиты - Строка, Структура, Массив - Реквизиты статей расходов и их аналитик для оформления
// 			<Строка> Перечисление пар реквизитов в формате "СтатьяРаходов1, АналитикаРасходов1, СтатьяРаходов2, АналитикаРасходов2, ..."
// 				Пустая строка трактуется как "СтатьяРасходов, АналитикаРасходов"
// 			<Структура> Ключ: Строка с именем табличной части; Значение - Строка в нотации как у параметра типа <Строка>
// 			<Массив> Массив, элементы которого либо структуры в нотации как у параметра с типа <Структура>, либо строки в нотации <Строка>
// 		ФормаОбъекта - Булево - Признак формы объекта ИБ.
//
Процедура УстановитьУсловноеОформлениеАналитик(УсловноеОФормление, Реквизиты = "", ФормаОбъекта = Истина, ПрефиксКонтроля = "") Экспорт
	
	МассивОбработки = Новый Массив;
	Если ТипЗнч(Реквизиты) = Тип("Массив") Тогда
		МассивОбработки = Реквизиты;
	Иначе
		МассивОбработки.Добавить(Реквизиты);
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из МассивОбработки Цикл
		
		Если ТипЗнч(ЭлементМассива) = Тип("Структура") Тогда
			
			Для Каждого КлючИЗначение Из ЭлементМассива Цикл
				
				СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(КлючИЗначение.Значение);
				УстановитьУсловноеОформление(УсловноеОформление, СтруктураРеквизитов, КлючИЗначение.Ключ, ФормаОбъекта, ПрефиксКонтроля)
				
			КонецЦикла;
			
		Иначе
			
			СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(ЭлементМассива);
			УстановитьУсловноеОформление(УсловноеОформление, СтруктураРеквизитов, , ФормаОбъекта, ПрефиксКонтроля)
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполнения колонок "АналитикаРасходовЗаказРеализация" в формах.
// Параметры:
// 		ТаблицаФормы - ДанныеФормыКоллекция
// 		Реквизиты - Строка, Структура - описание реквизитов для заполнения
// 			Перечисление пар реквизитов в формате "СтатьяРаходов1, АналитикаРасходов1, СтатьяРаходов2, АналитикаРасходов2, ..."
// 			Пустая строка трактуется как "СтатьяРасходов, АналитикаРасходов".
//
Процедура ЗаполнитьПризнакАналитикаРасходовЗаказРеализация(ТаблицаФормы, Реквизиты = "") Экспорт
	
	Если ТаблицаФормы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки%ПоляСтатей%
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таблица
	|;
	|//////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки%ПоляФлагов%
	|ИЗ
	|	Таблица КАК Таблица%ПоляСоединений%
	|");
	
	ШаблонСтатьи = ",
	|	Таблица.%ИмяСтатьи% КАК %ИмяСтатьи%";
	ШаблонФлага = ",
	|	ЕСТЬNULL(ПВХ%ИмяСтатьи%.АналитикаРасходовЗаказРеализация, ЛОЖЬ) КАК %ИмяАналитики%";
	ШаблонСоединения = "
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХ%ИмяСтатьи%
	|		ПО Таблица.%ИмяСтатьи% = ПВХ%ИмяСтатьи%.Ссылка";
	
	ПоляСтатей = "";
	ПоляФлагов = "";
	ПоляСоединений ="";
	
	СтруктураРеквизитов = РеквизитыКзаполнению(Реквизиты);
	
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		ПоляСтатей = ПоляСтатей + СтрЗаменить(ШаблонСтатьи, "%ИмяСтатьи%", КлючИЗначение.Ключ);
		ПоляФлагов = ПоляФлагов + СтрЗаменить(СтрЗаменить(ШаблонФлага, "%ИмяАналитики%", КлючИЗначение.Значение), "%ИмяСтатьи%", КлючИЗначение.Ключ);
		ПоляСоединений = ПоляСоединений + СтрЗаменить(ШаблонСоединения, "%ИмяСтатьи%", КлючИЗначение.Ключ);
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляСтатей%", ПоляСтатей);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляФлагов%", ПоляФлагов);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляСоединений%", ПоляСоединений);
	
	Запрос.УстановитьПараметр(
		"Таблица",
		ТаблицаФормы.Выгрузить(,"НомерСтроки, " + ?(ПустаяСтрока(Реквизиты), "СтатьяРасходов", Реквизиты)));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаФормы[Выборка.НомерСтроки-1], Выборка,, "НомерСтроки");
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполнения колонок "АналитикаРасходовОбязательна" в формах.
// Параметры:
// 		ТаблицаФормы - ДанныеФормыКоллекция
// 		Реквизиты - Строка, Структура - описание реквизитов для заполнения
// 			Перечисление пар реквизитов в формате "СтатьяРаходов1, АналитикаРасходов1, СтатьяРаходов2, АналитикаРасходов2, ..."
// 			Пустая строка трактуется как "СтатьяРасходов, АналитикаРасходов".
//
Процедура ЗаполнитьПризнакАналитикаРасходовОбязательна(ТаблицаФормы, Реквизиты = "") Экспорт
	
	Если ТаблицаФормы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Таблица.НомерСтроки КАК ЧИСЛО) КАК НомерСтроки%ПоляСтатей%
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таблица;
	|ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки%ПоляФлагов%
	|ИЗ
	|	Таблица КАК Таблица%ПоляСоединений%";
	
	ШаблонСтатьи = ",
	|	Таблица.%ИмяСтатьи% КАК %ИмяСтатьи%";
	ШаблонФлага = ",
	|	ЕСТЬNULL(ПВХ%ИмяСтатьи%.КонтролироватьЗаполнениеАналитики, ЛОЖЬ) КАК %ИмяАналитики%Обязательна";
	ШаблонСоединения = "
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХ%ИмяСтатьи%
	|		ПО Таблица.%ИмяСтатьи% = ПВХ%ИмяСтатьи%.Ссылка";
	
	ПоляСтатей = "";
	ПоляФлагов = "";
	ПоляСоединений ="";
	
	СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(Реквизиты);
	
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		ПоляСтатей = ПоляСтатей + СтрЗаменить(ШаблонСтатьи, "%ИмяСтатьи%", КлючИЗначение.Ключ);
		ПоляФлагов = ПоляФлагов + СтрЗаменить(СтрЗаменить(ШаблонФлага, "%ИмяАналитики%", КлючИЗначение.Значение), "%ИмяСтатьи%", КлючИЗначение.Ключ);
		ПоляСоединений = ПоляСоединений + СтрЗаменить(ШаблонСоединения, "%ИмяСтатьи%", КлючИЗначение.Ключ);
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляСтатей%", ПоляСтатей);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляФлагов%", ПоляФлагов);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ПоляСоединений%", ПоляСоединений);
	
	Запрос.УстановитьПараметр(
		"Таблица",
		ТаблицаФормы.Выгрузить(,"НомерСтроки, " + ?(ПустаяСтрока(Реквизиты), "СтатьяРасходов, АналитикаРасходов", Реквизиты)));
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаФормы[Выборка.НомерСтроки-1], Выборка,, "НомерСтроки");
	КонецЦикла;
	
КонецПроцедуры

// Возвращает статьи расходов, использование которых запрещено
//
// Возвращаемое значение:
// 	ЗаблокированныеСтатьи - СписокЗначений - Список заблокированных статей расходов.
//
Функция ЗаблокированныеСтатьиРасходов() Экспорт
	
	ЗаблокированныеСтатьи = Новый СписокЗначений;
	ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.СебестоимостьПродаж);
	Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.НачисленныйНДСПриВыкупеМногооборотнойТары);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ФормированиеРезервовПоСомнительнымДолгам);
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента);
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		ЗаблокированныеСтатьи.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации);
	КонецЕсли; 
	
	Возврат ЗаблокированныеСтатьи;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

//++ НЕ УТ

// Возвращает признак использования реквизита ВидРасходов
//
// Параметры:
// 	Объект - ПланВидовХарактеристикОбъект.СтатьиРасходов - Статья расходов.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак использования.
//
Функция ВидРасходовИспользуется(Объект) Экспорт
	
	НаВнеоборотныеАктивы = (Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы);
	НаНаправленияДеятельности = (Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	НеРаспределять = (Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	НаПроизводственныеЗатраты = (Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты);
	
	РасходыПоОсновнойДеятельности = (Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность);
	РасходыПоОсновнойИПрочейДеятельности = (Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность);
	
	Возврат (НаНаправленияДеятельности ИЛИ НеРаспределять ИЛИ НаПроизводственныеЗатраты ИЛИ НаВнеоборотныеАктивы) 
	        И (РасходыПоОсновнойДеятельности ИЛИ РасходыПоОсновнойИПрочейДеятельности)
	
КонецФункции

// Возвращает признак использования реквизита ВидПрочихРасходов
//
// Параметры:
// 	Объект - ПланВидовХарактеристикОбъект.СтатьиРасходов - Статья расходов.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак использования.
//
Функция ВидПрочихРасходовИспользуется(Объект) Экспорт
	
	НаНаправленияДеятельности = (Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности);
	НеРаспределять = (Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НеРаспределять);
	
	РасходыПоПрочейДеятельности = (Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность);
	РасходыПоОсновнойИПрочейДеятельности = (Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность);
	
	Возврат (НаНаправленияДеятельности ИЛИ НеРаспределять) И (РасходыПоПрочейДеятельности ИЛИ РасходыПоОсновнойИПрочейДеятельности)
	
КонецФункции

// Заполняет отбор в параметрах при обработке получения данных выбора и открытии формы выбора.
//
// Параметры:
// 		Параметры - Структура - Структура параметров в формате параметров формы выбора или процедуры "ОбработкаПолученияДанныхВыбора".
//
Процедура ОбработкаПараметровВыбора(Параметры) Экспорт
	
	ПорядокУчетаНУ = Неопределено;
	
	Параметры.Свойство("ПорядокНалоговогоУчетаВОА", ПорядокУчетаНУ);
		
	МассивВариантовРаспределения = ВнеоборотныеАктивыКлиентСервер.ВариантыРаспределенияРасходовПоАмортизацииРегл(ПорядокУчетаНУ);
		
	Если МассивВариантовРаспределения <> Неопределено Тогда
		Параметры.Отбор.Вставить("ВариантРаспределенияРасходовРегл", Новый ФиксированныйМассив(МассивВариантовРаспределения));
	КонецЕсли; 
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ОбщегоНазначенияУТВызовСервера.ОбработкаПолученияДанныхВыбораПВХСтатьиРасходов(ДанныеВыбора, Параметры, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункцииЗаполненияАналитик

Функция ОбязательныеСтатьи(МассивСтатей)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статьи.Ссылка КАК Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|ГДЕ
	|	Статьи.Ссылка В (&МассивСтатей)
	|	И Статьи.КонтролироватьЗаполнениеАналитики");
	
	Запрос.УстановитьПараметр("МассивСтатей", МассивСтатей);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция ИменаРеквизитовСтатьиИАналитики(СтрокаРеквизитов, НепроверяемыеРеквизиты = Неопределено, ПрефиксТабличнойЧасти = "")
	
	Если ПустаяСтрока(СтрокаРеквизитов) Тогда
		Если НепроверяемыеРеквизиты <> Неопределено Тогда
			НепроверяемыеРеквизиты.Добавить(ПрефиксТабличнойЧасти + "АналитикаРасходов");
		КонецЕсли;
		Возврат Новый Структура("СтатьяРасходов", "АналитикаРасходов");
	КонецЕсли;
	
	СтруктураОбработки = Новый Структура(СтрокаРеквизитов);
	СтруктураВозврата = Новый Структура;
	ПредыдущийКлюч = Неопределено;
	Для Каждого КлючИЗначение Из СтруктураОбработки Цикл
		Если ПредыдущийКлюч = Неопределено Тогда
			ПредыдущийКлюч = КлючИЗначение.Ключ;
		Иначе
			СтруктураВозврата.Вставить(ПредыдущийКлюч, КлючИЗначение.Ключ);
			ПредыдущийКлюч = Неопределено;
			Если НепроверяемыеРеквизиты <> Неопределено Тогда
				НепроверяемыеРеквизиты.Добавить(ПрефиксТабличнойЧасти + КлючИЗначение.Ключ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция РеквизитыКзаполнению(СтрокаРеквизитов, НепроверяемыеРеквизиты = Неопределено, ПрефиксТабличнойЧасти = "")
	
	Если ПустаяСтрока(СтрокаРеквизитов) Тогда
		Если НепроверяемыеРеквизиты <> Неопределено Тогда
			НепроверяемыеРеквизиты.Добавить(ПрефиксТабличнойЧасти + "АналитикаРасходовЗаказРеализация");
		КонецЕсли;
		Возврат Новый Структура("СтатьяРасходов", "АналитикаРасходовЗаказРеализация");
	КонецЕсли;
	
	СтруктураОбработки = Новый Структура(СтрокаРеквизитов);
	СтруктураВозврата = Новый Структура;
	ПредыдущийКлюч = Неопределено;
	Для Каждого КлючИЗначение Из СтруктураОбработки Цикл
		Если ПредыдущийКлюч = Неопределено Тогда
			ПредыдущийКлюч = КлючИЗначение.Ключ;
		Иначе
			СтруктураВозврата.Вставить(ПредыдущийКлюч, КлючИЗначение.Ключ);
			ПредыдущийКлюч = Неопределено;
			Если НепроверяемыеРеквизиты <> Неопределено Тогда
				НепроверяемыеРеквизиты.Добавить(ПрефиксТабличнойЧасти + КлючИЗначение.Ключ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ПроверкаЗаполненияАналитикОбъекта(Объект, Реквизиты, НепроверяемыеРеквизиты, Ошибки)
	
	СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(Реквизиты, НепроверяемыеРеквизиты);
	МассивСтатей = Новый Массив;
	
	// Определим список статей для контроля
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		
		Статья = Объект[КлючИЗначение.Ключ];
		
		Если ЗначениеЗаполнено(Статья) Тогда
			МассивСтатей.Добавить(Статья);
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверим заполнение аналитики
	ОбязательныеСтатьи = ОбязательныеСтатьи(МассивСтатей);
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		
		Статья = Объект[КлючИЗначение.Ключ];
		Аналитика = Объект[КлючИЗначение.Значение];
		
		Если Не (ОбязательныеСтатьи.Найти(Статья) = Неопределено Или ЗначениеЗаполнено(Аналитика)) Тогда
			
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки,
				Ошибки.ПрефиксОбъекта + КлючИЗначение.Значение,
				НСтр("ru='Аналитика расходов не заполнена'"), "");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверкаЗаполненияАналитикТЧОбъекта(Объект, Реквизиты, НепроверяемыеРеквизиты, Ошибки)
	
	// Определим список статей для контроля
	ОбщийМассивСтатей = Новый Массив;
	Для Каждого ОписаниеТЧ Из Реквизиты Цикл
		
		ИмяТЧ = ОписаниеТЧ.Ключ;
		
		СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(ОписаниеТЧ.Значение, НепроверяемыеРеквизиты, ИмяТЧ + ".");
		
		Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
			
			МассивСтатей = Объект[ИмяТЧ].ВыгрузитьКолонку(КлючИЗначение.Ключ);
			Для Каждого Статья Из МассивСтатей Цикл
				ОбщийМассивСтатей.Добавить(Статья);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Проверим заполнение аналитики
	ОбязательныеСтатьи = ОбязательныеСтатьи(ОбщийМассивСтатей);
	Для Каждого ОписаниеТЧ Из Реквизиты Цикл // Табличные части
		
		ИмяТЧ = ОписаниеТЧ.Ключ;
		ТЧ = Объект[ИмяТЧ];
		
		СтруктураРеквизитов = ИменаРеквизитовСтатьиИАналитики(ОписаниеТЧ.Значение, Неопределено, ИмяТЧ + ".");
		
		Для Индекс = 0 По ТЧ.Количество() - 1 Цикл // Строки табличной части
			
			СтрокаТЧ = ТЧ[Индекс];
			
			Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
				
				Статья = СтрокаТЧ[КлючИЗначение.Ключ];
				Аналитика = СтрокаТЧ[КлючИЗначение.Значение];
				
				Если Не (ОбязательныеСтатьи.Найти(Статья) = Неопределено Или ЗначениеЗаполнено(Аналитика)) Тогда
					
					ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
						Ошибки,
						Ошибки.ПрефиксОбъекта + ИмяТЧ + "[%1]." + КлючИЗначение.Значение,
						НСтр("ru='Не заполнена аналитика расходов'"),
						ИмяТЧ,
						Индекс,
						НСтр("ru='Не заполнена аналитика расходов в строке %1'"));
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьУсловноеОформление(УсловноеОформление, СтруктураРеквизитов, ИмяТабличнойЧасти = "", ФормаОбъекта, ПрефиксКонтроля)
	
	ПревиксТЧ = ?(ПустаяСтрока(ИмяТабличнойЧасти), "", ИмяТабличнойЧасти + ".");
	ПрефиксАналитики = ?(ФормаОбъекта, "Объект.", "") + ПревиксТЧ;
	Если ПустаяСтрока(ПрефиксКонтроля) Тогда
		ПрефиксКонтроля  = ?(ФормаОбъекта И Не ПустаяСтрока(ПревиксТЧ), "Объект.", "") + ПревиксТЧ;
	Иначе
		ПрефиксКонтроля = ПрефиксКонтроля + ".";
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СтруктураРеквизитов Цикл
		
		ИмяАналитики = КлючИЗначение.Значение;
		ИмяКонтроля = ИмяАналитики + "Обязательна";
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ИмяТабличнойЧасти + ИмяАналитики);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПрефиксКонтроля + ИмяКонтроля);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
		
		Если ПустаяСтрока(ИмяТабличнойЧасти) Тогда
			
			Элемент = УсловноеОформление.Элементы.Добавить();
			Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ИмяАналитики);
			
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПрефиксКонтроля + ИмяКонтроля);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Истина;
			
			ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПрефиксАналитики + ИмяАналитики);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗаполнитьПредопределенныеСтатьиРасходов() Экспорт
	
	МассивСтатей = Новый Массив;
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.СебестоимостьПродаж);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.НачисленныйНДСПриВыкупеМногооборотнойТары);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ФормированиеРезервовПоСомнительнымДолгам);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ТорговыйСбор);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.ВыбытияТоваровВПрошлыхПериодах);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыНаПлатон);
	МассивСтатей.Добавить(ПланыВидовХарактеристик.СтатьиРасходов.РасходыЛизинговыеПлатежи);
	
	Для Каждого СтатьяРасходов Из МассивСтатей Цикл
		
		Если Не ЗначениеЗаполнено(СтатьяРасходов.ВариантРаспределенияРасходовРегл) Тогда
			
			СтатьяРасходовОбъект = СтатьяРасходов.ПолучитьОбъект();
			Если СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("ПеречислениеСсылка.АналитикаКурсовыхРазниц");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.КурсовыеРазницы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("ПеречислениеСсылка.АналитикаКурсовыхРазниц");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы;
				СтатьяРасходовОбъект.СчетУчета = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.НачисленныйНДСПриВыкупеМногооборотнойТары Тогда
				СтатьяРасходовОбъект.ТипРасходов = Перечисления.ТипыРасходов.ПродажаТоваров;
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Партнеры");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ФормированиеРезервовПоСомнительнымДолгам Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Организации");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
					СтатьяРасходовОбъект.Наименование = НСтр("ru = 'Прочие расходы'");;
				КонецЕсли;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.НДСНалоговогоАгента Тогда
				СтатьяРасходовОбъект.ТипРасходов = Перечисления.ТипыРасходов.ЗакупкаТоваров;
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("ДокументСсылка.ПриобретениеТоваровУслуг");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров;
				СтатьяРасходовОбъект.ПравилоРаспределенияНаСебестоимость = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноСебестоимости;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ТорговыйСбор Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Склады");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения;
				СтатьяРасходовОбъект.СчетУчета = ПланыСчетов.Хозрасчетный.ИздержкиОбращения;
				//-- НЕ УТ
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрибыльУбытокПрошлыхЛет;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ПрочиеРасходы");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				СтатьяРасходовОбъект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ВыбытияТоваровВПрошлыхПериодах Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.МатериальныеРасходы;
				СтатьяРасходовОбъект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.РасходыНаПлатон Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				//-- НЕ УТ
			ИначеЕсли СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.РасходыЛизинговыеПлатежи Тогда
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				//-- НЕ УТ
			Иначе
				СтатьяРасходовОбъект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности");
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
				СтатьяРасходовОбъект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
				//++ НЕ УТ
				СтатьяРасходовОбъект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
				//-- НЕ УТ
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтатьяРасходовОбъект.ТипРасходов) Тогда
				СтатьяРасходовОбъект.ТипРасходов = Перечисления.ТипыРасходов.ПрочиеРасходы;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтатьяРасходовОбъект.ВариантРаспределенияРасходовУпр) Тогда
				СтатьяРасходовОбъект.ВариантРаспределенияРасходовУпр = СтатьяРасходовОбъект.ВариантРаспределенияРасходовРегл;
			КонецЕсли;
			СтатьяРасходовОбъект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
			СтатьяРасходовОбъект.ДополнительныеСвойства.Вставить("ИзменениеКлючевыхРеквизитов", Истина);
			//++ НЕ УТ
			Если СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ТорговыйСбор Тогда
				СтатьяРасходовОбъект.ПринятиеКналоговомуУчету = Ложь;
			Иначе
				СтатьяРасходовОбъект.ПринятиеКналоговомуУчету = Истина;
			КонецЕсли;
			СтатьяРасходовОбъект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			//-- НЕ УТ

			СтатьяРасходовОбъект.Записать();

		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления УТ 11.4.1.
// Заполняет новые реквизиты ТипРасходов, ВариантРаспределенияРасходовУпр, ХарактерПроизводственныхЗатрат. 
// 
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию() Экспорт
	
	МетаданныеОбъекта = Метаданные.ПланыВидовХарактеристик.СтатьиРасходов;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	//++ НЕ УТ
	|	СтатьиРасходов.УдалитьСпособРаспределенияНаПроизводственныеЗатраты 			КАК СпособРаспределенияНаПроизводственныеЗатраты,
	|	ЕСТЬNULL(ПоПодразделениям.БазаРаспределения, 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)) 		КАК БазаРаспределенияПоПодразделениям,
	|	ЕСТЬNULL(ПоПартиям.БазаРаспределения, 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)) 		КАК БазаРаспределенияПоПартиям,
	|	ЕСТЬNULL(ПоПодразделениям.УдалитьНаправлениеРаспределения, 
	|		ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияСтатейРасходов.ПустаяСсылка)) КАК НаправлениеРаспределения,
	|	ЕСТЬNULL(ПоПартиям.СтатьяКалькуляции, 
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка))					КАК СтатьяКалькуляции,
	|	ПоПодразделениям.Ссылка 													КАК Показатель,
	|	ЕСТЬNULL(ПоПартиям.Наименование, """") КАК НаименованиеПравилаПоПартиям,
	|	ЕСТЬNULL(ПоПодразделениям.Наименование, """") КАК НаименованиеПравилаПоПодразделениям,
	|	ПоПодразделениям.ОтборПоПодразделениям.(
	|		Подразделение КАК Подразделение
	|	) КАК ОтборПоПодразделениям,
	|	ПоПартиям.ОтборПоМатериалам.(
	|		Материал КАК Материал
	|	) КАК ОтборПоМатериалам,
	|	ПоПартиям.ОтборПоВидамРабот.(
	|		ВидРабот КАК ВидРабот
	|	) КАК ОтборПоВидамРабот,
	|	ПоПартиям.ОтборПоГруппамПродукции.(
	|		ГруппаПродукции КАК ГруппаПродукции
	|	) КАК ОтборПоГруппамПродукции,
	|	ВЫБОР КОГДА (СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|		И НЕ СтатьиРасходов.УдалитьСпособРаспределенияНаПроизводственныеЗатраты = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияСтатейРасходов.ПустаяСсылка)
	|		И СтатьиРасходов.ПравилоРаспределенияРасходов = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка)
	|	ТОГДА ИСТИНА
	|	ИНАЧЕ ЛОЖЬ 
	|	КОНЕЦ КАК ЗаполнитьПравилоРаспределения,
	//-- НЕ УТ
	|	СтатьиРасходов.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПоПодразделениям
	|		ПО (СтатьиРасходов.УдалитьПравилоРаспределенияПоПодразделениям = ПоПодразделениям.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПоПартиям
	|		ПО (СтатьиРасходов.УдалитьПравилоРаспределенияПоЭтапамПроизводства = ПоПартиям.Ссылка)
	//-- НЕ УТ
	|ГДЕ
	|	СтатьиРасходов.ЭтоГруппа = ЛОЖЬ
	|	И (СтатьиРасходов.ТипРасходов = ЗНАЧЕНИЕ(Перечисление.ТипыРасходов.ПустаяСсылка)
	|		ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовУпр
	|			= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
	//++ НЕ УТ
	|		ИЛИ СтатьиРасходов.ХарактерПроизводственныхЗатрат
	|			= ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.ПустаяСсылка)
	|		ИЛИ СтатьиРасходов.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации)
	|			И (СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
	|				ИЛИ СтатьиРасходов.ВидПрочихРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыПрочихДоходовИРасходов.ПустаяСсылка))
	//-- НЕ УТ
	|		ИЛИ СтатьиРасходов.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ВыбытияТоваровВПрошлыхПериодах)
	|			И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
	|		ИЛИ СтатьиРасходов.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты)
	|			И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
	|		ИЛИ СтатьиРасходов.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РасходыЛизинговыеПлатежи)
	|			И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)
	|	)
	//++ НЕ УТ
	|	ИЛИ ((СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			ИЛИ СтатьиРасходов.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|	И НЕ СтатьиРасходов.УдалитьСпособРаспределенияНаПроизводственныеЗатраты = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияСтатейРасходов.ПустаяСсылка)
	|	И СтатьиРасходов.ПравилоРаспределенияРасходов = ЗНАЧЕНИЕ(Справочник.ПравилаРаспределенияРасходов.ПустаяСсылка))
	|	
	|	ИЛИ СтатьиРасходов.ЭтоГруппа = ЛОЖЬ 
	|		И СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ПустаяСсылка)
	//-- НЕ УТ
	|");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если Объект.Ссылка = ПланыВидовХарактеристик.СтатьиРасходов.ДоначислениеИмущественныхНалоговИАмортизации Тогда
			Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ПрочиеРасходы");
			Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
			Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность;
			//++ НЕ УТ
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
			Объект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы;
			//-- НЕ УТ
			Объект.ВидЦенностиНДС = Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги;
			Объект.ПравилоРаспределенияНаСебестоимость = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноКоличеству;
			Объект.ПрочиеРасходы = Истина;
			Объект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
			Объект.ПризнаватьРасходамиПриУСН = Истина;
			Объект.ПринятиеКналоговомуУчету = Истина;
			
		ИначеЕсли Объект.Ссылка = ПланыВидовХарактеристик.СтатьиРасходов.ВыбытияТоваровВПрошлыхПериодах Тогда
			Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
			Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
			Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяИПрочаяДеятельность;
			//++ НЕ УТ
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ВидРасходов = Перечисления.ВидыРасходовНУ.МатериальныеРасходы;
			Объект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы;
			//-- НЕ УТ
			Объект.ВидЦенностиНДС = Перечисления.ВидыЦенностей.Товары;
			Объект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
			Объект.ПризнаватьРасходамиПриУСН = Истина;
			Объект.ПринятиеКналоговомуУчету = Истина;
			
		ИначеЕсли Объект.Ссылка = ПланыВидовХарактеристик.СтатьиРасходов.РасходыНаПлатон Тогда
			
			Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ПрочиеРасходы");
			Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НеРаспределять;
			Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
			//++ НЕ УТ
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
			//-- НЕ УТ
			Объект.ВидЦенностиНДС = Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги;
			Объект.ПрочиеРасходы = Истина;
			Объект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
			Объект.ПризнаватьРасходамиПриУСН = Истина;
			Объект.ПринятиеКналоговомуУчету = Истина;
			
		ИначеЕсли Объект.Ссылка = ПланыВидовХарактеристик.СтатьиРасходов.РасходыПриКонвертацииВалюты Тогда
			
			Объект.ТипЗначения = Новый ОписаниеТипов("ПеречислениеСсылка.АналитикаКурсовыхРазниц");
			Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
			Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ПрочаяДеятельность;
			Объект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
			Объект.ПринятиеКналоговомуУчету = Истина;
			Объект.ВидЦенностиНДС = Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги;
			//++ НЕ УТ
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ВидПрочихРасходов = Перечисления.ВидыПрочихДоходовИРасходов.ПрочиеВнереализационныеДоходыРасходы;
			Объект.СчетУчета = ПланыСчетов.Хозрасчетный.ПрочиеРасходы;
			//-- НЕ УТ
				
		ИначеЕсли Объект.Ссылка = ПланыВидовХарактеристик.СтатьиРасходов.РасходыЛизинговыеПлатежи Тогда
			
			Объект.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ПрочиеРасходы");
			Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаНаправленияДеятельности;
			Объект.ВидДеятельностиРасходов = Перечисления.ВидыДеятельностиРасходов.ОсновнаяДеятельность;
			//++ НЕ УТ
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
			Объект.ВидРасходов = Перечисления.ВидыРасходовНУ.ПрочиеРасходы;
			//-- НЕ УТ
			Объект.ВидЦенностиНДС = Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги;
			Объект.ПрочиеРасходы = Истина;
			Объект.ВариантРаздельногоУчетаНДС = Перечисления.ВариантыРаздельногоУчетаНДС.ИзДокумента;
			Объект.ПризнаватьРасходамиПриУСН = Истина;
			Объект.ПринятиеКналоговомуУчету = Истина;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.ВариантРаспределенияРасходовУпр) Тогда
			Объект.ВариантРаспределенияРасходовУпр = Объект.ВариантРаспределенияРасходовРегл;
		КонецЕсли;
		
		//++ НЕ УТ
		Если Не ЗначениеЗаполнено(Объект.ХарактерПроизводственныхЗатрат) Тогда
			Объект.ХарактерПроизводственныхЗатрат = Перечисления.ХарактерПроизводственныхЗатрат.Постоянные;
		КонецЕсли;
		//-- НЕ УТ
		
		Если Не ЗначениеЗаполнено(Объект.ТипРасходов) Тогда
			Если Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ВводОстатков"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ЗаказПоставщику"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")) Тогда
				Объект.ТипРасходов = Перечисления.ТипыРасходов.ЗакупкаТоваров;
				
			ИначеЕсли Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Склады"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ЗаказНаПеремещение"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ПеремещениеТоваров"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ЗаказНаСборку"))
			 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.СборкаТоваров")) Тогда
				Объект.ТипРасходов = Перечисления.ТипыРасходов.СкладскоеХранение;
				
			ИначеЕсли Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты Тогда
				Объект.ТипРасходов = Перечисления.ТипыРасходов.Производственные;
			ИначеЕсли Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы
			 ИЛИ Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПрочиеАктивы Тогда
				Объект.ТипРасходов = Перечисления.ТипыРасходов.ФормированиеСтоимостиВНА;	
			Иначе
				Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Партнеры"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.СделкиСКлиентами"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ЗаказКлиента"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.РеализацияТоваровУслуг")) Тогда
					Объект.ТипРасходов = Перечисления.ТипыРасходов.ПродажаТоваров;
				Иначе
					Объект.ТипРасходов = Перечисления.ТипыРасходов.ПрочиеРасходы;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Объект.ВариантРаспределенияРасходовУпр) Тогда
			Объект.ВариантРаспределенияРасходовУпр = Объект.ВариантРаспределенияРасходовРегл;
		КонецЕсли;
		
		//++ НЕ УТ
		Если Не ЗначениеЗаполнено(Объект.ХарактерПроизводственныхЗатрат) Тогда
			Объект.ХарактерПроизводственныхЗатрат = Перечисления.ХарактерПроизводственныхЗатрат.Постоянные;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат) Тогда
			Объект.ВидДеятельностиДляНалоговогоУчетаЗатрат = Перечисления.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения;
		КонецЕсли;
		//-- НЕ УТ
		
		Если Не ЗначениеЗаполнено(Объект.ТипРасходов) Тогда
			Если Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров Тогда
				Если Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ВводОстатков"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ЗаказПоставщику"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")) Тогда
					Объект.ТипРасходов = Перечисления.ТипыРасходов.ЗакупкаТоваров;
				Иначе
					Объект.ТипРасходов = Перечисления.ТипыРасходов.СкладскоеХранение;
				КонецЕсли;
			ИначеЕсли Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты Тогда
				Объект.ТипРасходов = Перечисления.ТипыРасходов.Производственные;
			ИначеЕсли Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы
			 ИЛИ Объект.ВариантРаспределенияРасходовРегл = Перечисления.ВариантыРаспределенияРасходов.НаПрочиеАктивы Тогда
				Объект.ТипРасходов = Перечисления.ТипыРасходов.ФормированиеСтоимостиВНА;	
			Иначе
				Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Партнеры"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.СделкиСКлиентами"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.ЗаказКлиента"))
				 ИЛИ Объект.ТипЗначения.СодержитТип(Тип("ДокументСсылка.РеализацияТоваровУслуг")) Тогда
					Объект.ТипРасходов = Перечисления.ТипыРасходов.ПродажаТоваров;
				Иначе
					Объект.ТипРасходов = Перечисления.ТипыРасходов.ПрочиеРасходы;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//++ НЕ УТ
		Если Не Выборка.ЗаполнитьПравилоРаспределения Тогда
		//-- НЕ УТ
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		//++ НЕ УТ
			Продолжить;
			
		КонецЕсли;
		
		НастройкиСтруктурой = НастройкиПравила();
		
		НастройкиСтруктурой.РаспределятьПоПартиям = Истина;
		НастройкиСтруктурой.БазаРаспределенияПоПартиям = Выборка.БазаРаспределенияПоПартиям;
		
		Если Выборка.СпособРаспределенияНаПроизводственныеЗатраты = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямИЭтапамПоПравилам Тогда
			
			НаименованиеПравила = НСтр("ru = '%1/%2'");
			НаименованиеПравила = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеПравила,
				Выборка.НаименованиеПравилаПоПодразделениям,
				Выборка.НаименованиеПравилаПоПартиям);
				
			НастройкиСтруктурой.БазаРаспределенияПоПодразделениям = Выборка.БазаРаспределенияПоПодразделениям;
			НастройкиСтруктурой.РаспределятьПоПодразделениям = Истина;
			
			Если Выборка.БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно
				Или Выборка.БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении Тогда
				НастройкиСтруктурой.Показатель = Выборка.Показатель;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Выборка.НаправлениеРаспределения) Тогда
				Если Выборка.НаправлениеРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПоказателюНаВышестоящееПодразделение Тогда
					НастройкиСтруктурой.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Вышестоящее;
				ИначеЕсли Выборка.НаправлениеРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПоказателюНаУказанныеПодразделения Тогда
					НастройкиСтруктурой.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Указанные;
				ИначеЕсли Выборка.НаправлениеРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПоказателюНаНижестоящиеПодразделения Тогда
					НастройкиСтруктурой.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Нижестоящие;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли Выборка.СпособРаспределенияНаПроизводственныеЗатраты = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу Тогда
			
			НаименованиеПравила = НСтр("ru = 'По подразделениям вручную по партиям по %1'");
			НаименованиеПравила = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеПравила,
				Выборка.НаименованиеПравилаПоПартиям);
			НастройкиСтруктурой.РаспределятьПоПодразделениям = Истина;
			НастройкиСтруктурой.ПодразделенияУказаныВручную = Истина;
			
		ИначеЕсли Выборка.СпособРаспределенияНаПроизводственныеЗатраты = Перечисления.СпособыРаспределенияСтатейРасходов.НаДругиеСтатьиРасходов Тогда
			
			НаименованиеПравила = НСтр("ru = 'На другие статьи расходов'");
			НастройкиСтруктурой.РаспределятьНаСтатьи = Истина;
			НастройкиСтруктурой.РаспределятьПоПартиям = Ложь;
			НастройкиСтруктурой.БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПустаяСсылка();
			
		ИначеЕсли Выборка.СпособРаспределенияНаПроизводственныеЗатраты = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамВручнуюПоВсемПодразделениям Тогда
			
			НаименованиеПравила = НСтр("ru = 'Партии уточняются в документе'");
			НастройкиСтруктурой.УточнятьПартииВДокументе = Истина;
			НастройкиСтруктурой.БазаРаспределенияПоПартиям = Перечисления.ТипыБазыРаспределенияРасходов.ПустаяСсылка();
			
		ИначеЕсли Выборка.СпособРаспределенияНаПроизводственныеЗатраты = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуВДанномПодразделении Тогда
			
			НаименованиеПравила = НСтр("ru = 'По партиям по правилу %1 в подразделении расхода'");
			НаименованиеПравила = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеПравила,
				Выборка.НаименованиеПравилаПоПартиям);
			НастройкиСтруктурой.НаправлениеРаспределения = Перечисления.НаправлениеРаспределенияПоПодразделениям.Текущее;
			
		ИначеЕсли Выборка.СпособРаспределенияНаПроизводственныеЗатраты = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуПоВсемПодразделениям Тогда
			
			НаименованиеПравила = НСтр("ru = 'По партиям по правилу %1 по всем подразделениям'");
			НаименованиеПравила = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеПравила,
				Выборка.НаименованиеПравилаПоПартиям);
			
		КонецЕсли;
		
		НастройкиСтруктурой.ОтборПоМатериалам = Выборка.ОтборПоМатериалам.Выгрузить().ВыгрузитьКолонку("Материал");
		ОбщегоНазначенияУТКлиентСервер.СортироватьМассив(НастройкиСтруктурой.ОтборПоМатериалам);
		
		НастройкиСтруктурой.ОтборПоВидамРабот = Выборка.ОтборПоВидамРабот.Выгрузить().ВыгрузитьКолонку("ВидРабот");
		ОбщегоНазначенияУТКлиентСервер.СортироватьМассив(НастройкиСтруктурой.ОтборПоВидамРабот);
		
		НастройкиСтруктурой.ОтборПоПодразделениям = Выборка.ОтборПоПодразделениям.Выгрузить().ВыгрузитьКолонку("Подразделение");
		ОбщегоНазначенияУТКлиентСервер.СортироватьМассив(НастройкиСтруктурой.ОтборПоПодразделениям);
		
		НастройкиСтруктурой.ОтборПоГруппамПродукции = Выборка.ОтборПоГруппамПродукции.Выгрузить().ВыгрузитьКолонку("ГруппаПродукции");
		ОбщегоНазначенияУТКлиентСервер.СортироватьМассив(НастройкиСтруктурой.ОтборПоГруппамПродукции);
		
		СериализованныеНастройки = ОбщегоНазначения.ЗначениеВСтрокуXML(НастройкиСтруктурой);
		ХешСумма = ОбщегоНазначенияУТ.ХешСуммаСтроки(СериализованныеНастройки, ХешФункция.MD5);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПравилаРаспределенияРасходов.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределенияРасходов
			|ГДЕ
			|	ПравилаРаспределенияРасходов.ХешСумма = &ХешСумма";
		
		Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
		
		Результат = Запрос.Выполнить();
		ВыборкаНастроек = Результат.Выбрать();
		
		Если ВыборкаНастроек.Следующий() Тогда
			
			Объект.ПравилоРаспределенияРасходов = ВыборкаНастроек.Ссылка;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
			Продолжить;
			
		КонецЕсли;
		
		НовоеПравило = Справочники.ПравилаРаспределенияРасходов.СоздатьЭлемент();
		НовоеПравило.НазначениеПравила = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства;
		НовоеПравило.ХешСумма = ХешСумма;
		НовоеПравило.Наименование = НаименованиеПравила;
			
		ЗаполнитьЗначенияСвойств(НовоеПравило, НастройкиСтруктурой, , 
			"ОтборПоГруппамПродукции, ОтборПоМатериалам, ОтборПоВидамРабот, ОтборПоПодразделениям");
		
		Для Каждого СтрокаОтбора Из НастройкиСтруктурой.ОтборПоВидамРабот Цикл
			НовоеПравило.ОтборПоВидамРабот.Добавить().ВидРабот = СтрокаОтбора;
		КонецЦикла;
		
		Для Каждого СтрокаОтбора Из НастройкиСтруктурой.ОтборПоМатериалам Цикл
			НовоеПравило.ОтборПоМатериалам.Добавить().Материал = СтрокаОтбора;
		КонецЦикла;
		
		Для Каждого СтрокаОтбора Из НастройкиСтруктурой.ОтборПоПодразделениям Цикл
			НовоеПравило.ОтборПоПодразделениям.Добавить().Подразделение = СтрокаОтбора;
		КонецЦикла;
		
		Для Каждого СтрокаОтбора Из НастройкиСтруктурой.ОтборПоГруппамПродукции Цикл
			НовоеПравило.ОтборПоГруппамПродукции.Добавить().ГруппаПродукции = СтрокаОтбора;
		КонецЦикла;
		
		НовоеПравило.ПредставлениеПравила = Справочники.ПравилаРаспределенияРасходов.ПолучитьПредставлениеПравила(
			НовоеПравило.ОтборПоМатериалам, НовоеПравило.ОтборПоВидамРабот, НовоеПравило.ОтборПоПродукции, НовоеПравило.БазаРаспределенияПоПартиям);
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НовоеПравило);
		
		Объект.ПравилоРаспределенияРасходов = НовоеПравило.Ссылка;
		Если Не ЗначениеЗаполнено(Объект.СтатьяКалькуляции) Тогда
			Объект.СтатьяКалькуляции = Выборка.СтатьяКалькуляции;
		КонецЕсли;
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(Объект);
		
		//-- НЕ УТ
		
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТ

Функция НастройкиПравила()
	
	Настройки = Новый Структура("НаправлениеРаспределения, БазаРаспределенияПоПодразделениям, БазаРаспределенияПоПартиям, 
	|УточнятьПартииВДокументе, РаспределятьНаСтатьи, РаспределятьПоПартиям, Показатель, РаспределятьПоПодразделениям, 
	|ПодразделенияУказаныВручную, ОтборПоГруппамПродукции, ОтборПоМатериалам, ОтборПоВидамРабот, ОтборПоПодразделениям");
	
	Настройки.Вставить("НаправлениеРаспределения", Перечисления.НаправлениеРаспределенияПоПодразделениям.ПустаяСсылка());
	Настройки.Вставить("БазаРаспределенияПоПодразделениям", Перечисления.ТипыБазыРаспределенияРасходов.ПустаяСсылка());
	Настройки.Вставить("БазаРаспределенияПоПартиям", Перечисления.ТипыБазыРаспределенияРасходов.ПустаяСсылка());
	Настройки.Вставить("Показатель", Справочники.ПравилаРаспределенияРасходов.ПустаяСсылка());
	Настройки.Вставить("УточнятьПартииВДокументе", Ложь);
	Настройки.Вставить("РаспределятьНаСтатьи", Ложь);
	Настройки.Вставить("РаспределятьПоПартиям", Ложь);
	Настройки.Вставить("РаспределятьПоПодразделениям", Ложь);
	Настройки.Вставить("ПодразделенияУказаныВручную", Ложь);
	Настройки.Вставить("ОтборПоГруппамПродукции", Новый Массив);
	Настройки.Вставить("ОтборПоМатериалам", Новый Массив);
	Настройки.Вставить("ОтборПоВидамРабот", Новый Массив);
	Настройки.Вставить("ОтборПоПодразделениям", Новый Массив);
	
	Возврат Настройки;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#КонецЕсли
